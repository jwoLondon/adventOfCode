---
follows: data/d03_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2017): Spiral Memory

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/3):

{( part1|}

You come across an experimental new kind of memory stored on an infinite two-dimensional grid.

Each square on the grid is allocated in a spiral pattern starting at a location marked 1 and then counting up while spiraling outward. For example, the first few squares are allocated like this:

    17  16  15  14  13
    18   5   4   3  12
    19   6   1   2  11
    20   7   8   9  10
    21  22  23---> ...

While this is very space-efficient (no squares are skipped), requested data must be carried back to square 1 (the location of the only access port for this memory system) by programs that can only move up, down, left, or right. They always take the shortest path: the Manhattan Distance between the location of the data and square 1.

For example:

- Data from square 1 is carried 0 steps, since it's at the access port.
- Data from square 12 is carried 3 steps, such as: down, left, left.
- Data from square 23 is carried only 2 steps: up twice.
- Data from square 1024 must be carried 31 steps.

**How many steps** are required to carry the data from the square identified in your puzzle input all the way to the access port?

{| part1 )}

## Approach

There would appear to be two main approaches to this problem (a) 'Walk the spiral' until we get to the desired position; (b) create an algebraic solution that computes the location given an index value.

Approach (b) would seem the more efficient for large index values, so let's consider a slightly larger spiral to see if we an detect a computable pattern:

         -4  -3  -2  -1   0   1   2   3   4
       ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
    -4 ┃ 65  64  63  62  61  60  59  58  57
    -3 ┃ 66  37  36  35  34  33  32  31  56
    -2 ┃ 67  38  17  16  15  14  13  30  55
    -1 ┃ 68  39  18   5   4   3  12  29  54
     0 ┃ 69  40  19   6   1   2  11  28  53
     1 ┃ 70  41  20   7   8   9  10  27  52
     2 ┃ 71  42  21  22  23  24  25  26  51
     3 ┃ 72  43  44  45  46  47  48  49  50
     4 ┃ 73  74  75  76  77  78  79  80  81 —>

Note the positions of the perfect squares of the odd numbers (1, 3, 9, 25, 49, 81...) form the bottom-right corner of each spiral ring. We can use these positions as 'anchors' and find the locations relative to the anchor accordingly.

```elm {l}
spiralToLocation : Int -> ( Int, Int )
spiralToLocation n =
    let
        ring =
            ceiling ((sqrt (toFloat n) - 1) / 2)

        corner =
            (2 * ring + 1) * (2 * ring + 1)
    in
    if n >= corner - 2 * ring then
        ( n - corner + ring, ring )

    else if n >= corner - 4 * ring then
        ( -ring, n - corner + 3 * ring )

    else if n >= corner - 6 * ring then
        ( corner - 5 * ring - n, -ring )

    else
        ( ring, corner - 7 * ring - n )
```

Computing the Manhattan distance for any given location is simple:

```elm {l}
distanceFromCentre : ( Int, Int ) -> Int
distanceFromCentre ( x, y ) =
    abs x + abs y
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> spiralToLocation
        |> distanceFromCentre
```

{( part2 |}

As a stress test on the system, the programs here clear the grid and then store the value 1 in square 1. Then, in the same allocation order as shown above, they store the sum of the values in all adjacent squares, including diagonals.

So, the first few squares' values are chosen as follows:

- Square 1 starts with the value 1.
- Square 2 has only one adjacent filled square (with value 1), so it also stores 1.
- Square 3 has both of the above squares as neighbors and stores the sum of their values, 2.
- Square 4 has all three of the aforementioned squares as neighbors and stores the sum of their values, 4.
- Square 5 only has the first and fourth squares as neighbors, so it gets the value 5.

Once a square is written, its value does not change. Therefore, the first few squares would receive the following values:

    147 142 133 122  59
    304   5   4   2  57
    330  10   1   1  54
    351  11  23  25  26
    362 747 806---> ...

What is the **first value written** that is **larger** than your puzzle input?

{| part2 )}

## Approach

We are now forced to keep track of of written memory values in order to use them to accumulate totals of neighbours. Rather than use a grid to store these values, we can use a more efficient (for Elm at least) Dictionary to do so.

```elm {l}
type alias Memory =
    Dict Int Int


highestAddress : Memory -> Int
highestAddress memory =
    Dict.keys memory |> List.maximum |> Maybe.withDefault 1


valueAtHighestAddress : Memory -> Int
valueAtHighestAddress memory =
    Dict.get (highestAddress memory) memory |> Maybe.withDefault 0
```

Because we need to find the spiral index of a neighbouring locations, we also need be able to convert from a 2d Cartesian position to its spiral index (i.e. the opposite of the conversion required in part 1):

```elm {l}
locationToSpiral : ( Int, Int ) -> Int
locationToSpiral ( x, y ) =
    let
        ring =
            max (abs x) (abs y)

        corner =
            (2 * ring + 1) * (2 * ring + 1)
    in
    if y == ring then
        corner - ring + x

    else if x == -ring then
        corner - 3 * ring + y

    else if y == -ring then
        corner - 5 * ring - x

    else
        corner - 7 * ring - y
```

The stress test can then proceed by accumulating the neighbouring memory values in spiral sequence:

```elm {l}
stressTest : Memory -> Int -> Int
stressTest memory threshold =
    if valueAtHighestAddress memory > threshold then
        valueAtHighestAddress memory

    else
        stressTest (memory |> accumNeighbours (highestAddress memory + 1)) threshold


accumNeighbours : Int -> Memory -> Memory
accumNeighbours n memory =
    let
        valueAt addr =
            Dict.get addr memory |> Maybe.withDefault 0

        ( x0, y0 ) =
            spiralToLocation n

        accum =
            (valueAt <| locationToSpiral ( x0 + 1, y0 ))
                + (valueAt <| locationToSpiral ( x0 + 1, y0 - 1 ))
                + (valueAt <| locationToSpiral ( x0, y0 - 1 ))
                + (valueAt <| locationToSpiral ( x0 - 1, y0 - 1 ))
                + (valueAt <| locationToSpiral ( x0 - 1, y0 ))
                + (valueAt <| locationToSpiral ( x0 - 1, y0 + 1 ))
                + (valueAt <| locationToSpiral ( x0, y0 + 1 ))
                + (valueAt <| locationToSpiral ( x0 + 1, y0 + 1 ))
    in
    Dict.insert n accum memory
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> stressTest (Dict.singleton 1 1)
```

## Reflection

One of those puzzles where an optimisation for part 1 (algebraic determination of spiral index for any given 2d position) is not applicable to the modification in part 1. Or rather I cannot think of a way of calculating the accumulations without walking the spiral.

I note this problem uses a [Ulam-spiral](https://en.wikipedia.org/wiki/Ulam_spiral), but without registering primes.
