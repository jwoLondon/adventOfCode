---
follows: data/d07_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2017): Recursive Circus

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/7).

{( part1|}

Before you're ready to help them, you need to make sure your information is correct. **What is the name of the bottom program?**

{| part1 )}

To extract the names of the programs in the tower we can (for the moment) ignore the weights and just extract a list of program names, separating the parent (head) from the children (tail).

```elm {l}
parseForChildren : String -> ( String, List String )
parseForChildren =
    let
        toProg tokens =
            case tokens of
                parent :: children ->
                    ( parent, children )

                _ ->
                    ( String.concat tokens, [] ) |> Debug.log "Bad input"
    in
    AOC.submatches "(\\w+) \\((?:\\d+)\\)|(?: -> )?(\\w+)"
        >> List.filterMap identity
        >> toProg
```

The root will be the only name that does not feature in any of the lists of children:

```elm {l r}
part1 : String
part1 =
    let
        ( parents, children ) =
            puzzleInput
                |> List.map parseForChildren
                |> List.unzip
    in
    parents
        |> List.filter (\p -> List.member p (List.concat children) |> not)
        |> List.head
        |> Maybe.withDefault ""
```

{( part2 |}

Given that exactly one program is the wrong weight, **what would its weight need to be** to balance the entire tower?

{| part2 )}

## Parsing

We need to alter the input parsing so we also extract the weights associated with each parent:

```elm {l}
parseForWeight : String -> ( String, Int )
parseForWeight =
    let
        toProg tokens =
            case tokens of
                [ prog, weight ] ->
                    ( prog, AOC.toInt weight )

                _ ->
                    ( String.concat tokens, 0 ) |> Debug.log "Bad input"
    in
    AOC.submatches "(\\w+) \\((\\d+)\\)"
        >> List.filterMap identity
        >> toProg
```

We will need to represent the tree of programs in order to parse them efficiently. We can do this with a tree type representing each program, its weight and its children:

```elm{l}
type Prog
    = Node String Int (List Prog)


buildTree : (String -> Int) -> (String -> List String) -> String -> Prog
buildTree att children name =
    Node name (att name) (List.map (buildTree att children) (children name))
```

We can find the unbalanced program by navigating the tree and comparing weights of its children:

```elm {l}
unbalancedProg : Prog -> Maybe ( List Int, List Int )
unbalancedProg prog =
    let
        (Node _ _ children) =
            prog

        totalWeight (Node _ weight c) =
            weight + (List.map totalWeight c |> List.sum)

        cWeights =
            List.map totalWeight children

        identical ws =
            case ws of
                hd :: tl ->
                    List.all (\w -> w == hd) tl

                _ ->
                    True
    in
    if identical cWeights then
        Nothing

    else if List.filterMap unbalancedProg children == [] then
        Just ( cWeights, List.map (\(Node _ w _) -> w) children )

    else
        List.filterMap unbalancedProg children
            |> List.head
```

Once we have the unbalanced pathway though the tree, we can find the weight correction necessary:

```elm{l}
correctWeight : Maybe ( List Int, List Int ) -> Int
correctWeight unbalanced =
    let
        outlierDiff x xs =
            if (List.filter (\n -> n == x) xs |> List.length) == 1 then
                ((List.sum xs - x) // (List.length xs - 1)) - x

            else
                0
    in
    case unbalanced of
        Just wsPair ->
            AOC.zip (Tuple.first wsPair) (Tuple.second wsPair)
                |> List.map (\( w0, w1 ) -> ( outlierDiff w0 (Tuple.first wsPair), w1 ))
                |> List.filter (\( a, _ ) -> a /= 0)
                |> List.map (\( a, b ) -> a + b)
                |> List.head
                |> Maybe.withDefault 0

        Nothing ->
            0
```

```elm {l r}
part2 : Int
part2 =
    let
        weightLookup =
            puzzleInput
                |> List.map parseForWeight
                |> Dict.fromList

        childrenLookup =
            puzzleInput
                |> List.map parseForChildren
                |> Dict.fromList

        getWeight name =
            Dict.get name weightLookup |> Maybe.withDefault 0

        getChildren name =
            Dict.get name childrenLookup |> Maybe.withDefault []
    in
    part1
        |> buildTree getWeight getChildren
        |> unbalancedProg
        |> correctWeight
```

---

## Reflection

The first tree navigation puzzle of the year. Will there be others, as the featured heavily in 2016? Functional languages like Elm can represent trees very compactly with a recursive type, but I still struggle sometimes to work with them intuitively.
