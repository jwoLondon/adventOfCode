---
follows: data/d01_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2017): Inverse Captcha

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/1)

{( part1|}

What is the solution to your captcha?

{| part1 )}

First, lets convert the puzzle input from a string of digits into a list of numbers as we will need the numeric values when summing them:

```elm {l}
toDigits : String -> List Int
toDigits =
    String.toList
        >> List.map (String.fromChar >> String.toInt >> Maybe.withDefault 0)
```

To compare adjacent values, we can convert the list of numbers into a list of tuples combining the original list with a rotated version of it shifted by one position (zipping):

```elm {l}
pairs : Int -> List a -> List ( a, a )
pairs offset xs =
    List.map2 Tuple.pair xs (List.drop offset xs ++ List.take offset xs)
```

And now we can simply filter only those tuples that match the first and second value, and sum the first items in the filtered list:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> toDigits
        |> pairs 1
        |> List.filter (\( a, b ) -> a == b)
        |> List.map Tuple.first
        |> List.sum
```

{( part2 |}

What is the solution to your new captcha?

{| part2 )}

The process for this variation is the same as the previous one except that instead of offsetting by 1, we offset by half the list length:

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> toDigits
        |> pairs (String.length puzzleInput // 2)
        |> List.filter (\( a, b ) -> a == b)
        |> List.map Tuple.first
        |> List.sum
```

---

## Reflection

A nice warmup puzzle that is good practice for thinking functionally. List mapping, zipping and filtering are all basic processes that are likely to be used extensively in later puzzles. My first attempt at part one hard-coded the shift-by-one zipping, but this was easily adaptable once it became apparent in part 2 that we needed to use a different offset. I also note the rotation used in `pairs` was also required for [d08_2016](d08_2016.md).
