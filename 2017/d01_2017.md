---
follows: data/d01_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2017): Inverse Captcha

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/1):

{( part1|}

You're standing in a room with "digitization quarantine" written in LEDs along one wall. The only door is locked, but it includes a small interface. "Restricted Area - Strictly No Digitized Users Allowed."

It goes on to explain that you may only leave by solving a captcha to prove you're not a human. Apparently, you only get one millisecond to solve the captcha: too fast for a normal human, but it feels like hours to you.

The captcha requires you to review a sequence of digits (your puzzle input) and find the sum of all digits that match the next digit in the list. The list is circular, so the digit after the last digit is the first digit in the list.

For example:

- 1122 produces a sum of 3 (1 + 2) because the first digit (1) matches the second digit and the third digit (2) matches the fourth digit.
- 1111 produces 4 because each digit (all 1) matches the next.
- 1234 produces 0 because no digit matches the next.
- 91212129 produces 9 because the only digit that matches the next one is the last digit, 9.

What is the solution to your captcha?

{| part1 )}

## Approach

First, lets convert the puzzle input from a string of digits into a list of numbers as we will need the numeric values when summing them:

```elm {l}
toDigits : String -> List Int
toDigits =
    String.toList
        >> List.map (String.fromChar >> String.toInt >> Maybe.withDefault 0)
```

To compare adjacent values, we can convert the list of numbers into a list of tuples combining the original list with a rotated version of it shifted by one position (zipping):

```elm {l}
pairs : Int -> List a -> List ( a, a )
pairs offset xs =
    List.map2 Tuple.pair xs (List.drop offset xs ++ List.take offset xs)
```

And now we can simply filter only those tuples that match the first and second value, and sum the first items in the filtered list:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> toDigits
        |> pairs 1
        |> List.filter (\( a, b ) -> a == b)
        |> List.map Tuple.first
        |> List.sum
```

{( part2 |}

You notice a progress bar that jumps to 50% completion. Apparently, the door isn't yet satisfied, but it did emit a star as encouragement. The instructions change:

Now, instead of considering the next digit, it wants you to consider the digit halfway around the circular list. That is, if your list contains 10 items, only include a digit in your sum if the digit 10/2 = 5 steps forward matches it. Fortunately, your list has an even number of elements.

For example:

- 1212 produces 6: the list contains 4 items, and all four digits match the digit 2 items ahead.
- 1221 produces 0, because every comparison is between a 1 and a 2.
- 123425 produces 4, because both 2s match each other, but no other digit has a match.
- 123123 produces 12.
- 12131415 produces 4.

What is the solution to your new captcha?

{| part2 )}

The process for this variation is the same as the previous one except that instead of offsetting by 1, we offset by half the list length:

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> toDigits
        |> pairs (String.length puzzleInput // 2)
        |> List.filter (\( a, b ) -> a == b)
        |> List.map Tuple.first
        |> List.sum
```

## Reflection

A nice warmup puzzle that is good practice for thinking functionally. List mapping, zipping and filtering are all basic processes that are likely to be used extensively in later puzzles. My first attempt at part one hard-coded the shift-by-one zipping, but this was easily adaptable once it became apparent in part 2 that we needed to use a different offset. I also note the rotation used in `pairs` was also required for [d08_2016](d08_2016.md).
