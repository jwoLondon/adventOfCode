---
follows: data/d05_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2017): A Maze of Twisty Trampolines, All Alike

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/5):

{( part1|}

An urgent interrupt arrives from the CPU: it's trapped in a maze of jump instructions, and it would like assistance from any programs with spare cycles to help find the exit.

The message includes a list of the offsets for each jump. Jumps are relative: -1 moves to the previous instruction, and 2 skips the next one. Start at the first instruction in the list. The goal is to follow the jumps until one leads **outside** the list.

In addition, these instructions are a little strange; after each jump, the offset of that instruction increases by 1. So, if you come across an offset of 3, you would move three instructions forward, but change it to a 4 for the next time it is encountered.

For example, consider the following list of jump offsets:

    0
    3
    0
    1
    -3

Positive jumps ("forward") move downward; negative jumps move upward. For legibility in this example, these offset values will be written all on one line, with the current instruction marked in parentheses. The following steps would be taken before an exit is found:

- (0) 3 0 1 -3 - **before** we have taken any steps.
- (1) 3 0 1 -3 - jump with offset 0 (that is, don't jump at all). Fortunately, the instruction is then incremented to 1.
- 2 (3) 0 1 -3 - step forward because of the instruction we just modified. The first instruction is incremented again, now to 2.
- 2 4 0 1 (-3) - jump all the way to the end; leave a 4 behind.
- 2 (4) 0 1 -2 - go back to where we just were; increment -3 to -2.
- 2 5 0 1 -2 - jump 4 steps forward, escaping the maze.

In this example, the exit is reached in 5 steps.

**How many steps** does it take to reach the exit?

{| part1 )}

## Approach

This puzzle requires moving to different positions in a sequence of values keeping track of the position and the number of jumps made. So let's start by storing those three elements in a record, using an array to represent the sequence of instructions:

```elm {l}
type alias InstructionSet =
    { instructions : Array Int
    , position : Int
    , numSteps : Int
    }
```

To jump we take a record representing the current instructions, position and number of steps and update it according to the instruction at the position. We keep jumping until we jump out of bounds:

```elm {l}
jump : (Int -> Int) -> InstructionSet -> InstructionSet
jump fn js =
    case Array.get js.position js.instructions of
        Nothing ->
            js

        Just jmp ->
            jump fn
                (InstructionSet (Array.set js.position (fn jmp) js.instructions)
                    (js.position + jmp)
                    (js.numSteps + 1)
                )
```

```elm {l r}
part1 : Int
part1 =
    let
        update x =
            x + 1
    in
    jump update (InstructionSet (Array.fromList puzzleInput) 0 0)
        |> .numSteps
```

{( part2 |}

Now, the jumps are even stranger: after each jump, if the offset was **three or more**, instead **decrease** it by 1. Otherwise, increase it by 1 as before.

Using this rule with the above example, the process now takes 10 steps, and the offset values after finding the exit are left as 2 3 2 3 -1.

**How many steps** does it now take to reach the exit?

{| part2 )}

All we need to do is replace the updating function to reflect the new rule:

```elm {l r}
part2 : Int
part2 =
    let
        update x =
            if x >= 3 then
                x - 1

            else
                x + 1
    in
    jump update (InstructionSet (Array.fromList puzzleInput) 0 0)
        |> .numSteps
```

## Reflection

In the first iteration of the code for part 1, I didn't abstract the updating function. But once it became clear in part 2 that the same process was being applied but with a different update rule, providing an update function involved the least repetition of code.

I thought initially that because array manipulation in Elm is quite slow that creating a deque to keep track of the moving pointer position would be faster. But because we never slice the sequence, simply updating the value at the pointer, arrays proved the faster option.
