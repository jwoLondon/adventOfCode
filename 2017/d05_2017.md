---
follows: data/d05_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2017): A Maze of Twisty Trampolines, All Alike

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/5).

{( part1|}

**How many steps** does it take to reach the exit?

{| part1 )}

This puzzle requires moving to different positions in a sequence of values keeping track of the position and the number of jumps made. So let's start by storing those three elements in a record, using an array to represent the sequence of instructions:

```elm {l}
type alias InstructionSet =
    { instructions : Array Int
    , position : Int
    , numSteps : Int
    }
```

To jump we take a record representing the current instructions, position and number of steps and update it according to the instruction at the position. We keep jumping until we jump out of bounds:

```elm {l}
jump : (Int -> Int) -> InstructionSet -> InstructionSet
jump fn js =
    case Array.get js.position js.instructions of
        Nothing ->
            js

        Just jmp ->
            jump fn
                (InstructionSet (Array.set js.position (fn jmp) js.instructions)
                    (js.position + jmp)
                    (js.numSteps + 1)
                )
```

```elm {l r}
part1 : Int
part1 =
    let
        update x =
            x + 1
    in
    jump update (InstructionSet (Array.fromList puzzleInput) 0 0)
        |> .numSteps
```

{( part2 |}

**How many steps** does it now take to reach the exit?

{| part2 )}

All we need to do is replace the updating function to reflect the new rule:

```elm {l r}
part2 : Int
part2 =
    let
        update x =
            if x >= 3 then
                x - 1

            else
                x + 1
    in
    jump update (InstructionSet (Array.fromList puzzleInput) 0 0)
        |> .numSteps
```

---

## Reflection

In the first iteration of the code for part 1, I didn't abstract the updating function. But once it became clear in part 2 that the same process was being applied but with a different update rule, providing an update function involved the least repetition of code.

I thought initially that because array manipulation in Elm is quite slow that creating a deque to keep track of the moving pointer position would be faster. But because we never slice the sequence, simply updating the value at the pointer, arrays proved the faster option.
