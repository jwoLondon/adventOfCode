---
follows: data/d16_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 16 (2017): Permutation Promenade

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/16).

{( part1|}

You watch the dance for a while and record their dance moves (your puzzle input). **In what order are the programs standing** after their dance?

{| part1 )}

We can represent each dance move along with its parameters:

```elm {l}
type Move
    = Spin Int
    | Exchange Int Int
    | Partner Char Char
```

And then convert the text input into a list of dance moves.

```elm {l}
parse : String -> List Move
parse =
    let
        toMove : List Move -> List (Maybe String) -> List Move
        toMove moves tokens =
            case List.take 3 tokens of
                [] ->
                    List.reverse moves

                [ Just "s", Just n, Nothing ] ->
                    toMove (Spin (AOC.toInt n) :: moves) (List.drop 3 tokens)

                [ Just "x", Just n1, Just n2 ] ->
                    toMove (Exchange (AOC.toInt n1) (AOC.toInt n2) :: moves) (List.drop 3 tokens)

                [ Just "p", Just p1, Just p2 ] ->
                    toMove
                        (Partner (p1 |> String.toList |> List.head |> Maybe.withDefault 'x')
                            (p2 |> String.toList |> List.head |> Maybe.withDefault 'x')
                            :: moves
                        )
                        (List.drop 3 tokens)

                _ ->
                    List.reverse moves |> Debug.log "Bad input"
    in
    AOC.submatches "(s|x|p)(\\d+|[a-p])(?:/(\\d+|[a-p]))?"
        >> toMove []
```

We can represent the order of programs as a list of characters.

```elm {l}
type alias Progs =
    List Char


start : Progs
start =
    "abcdefghijklmnop" |> String.toList
```

The three dance moves are easily computable from a list of characters:

```elm {l}
partner : Char -> Char -> Progs -> Progs
partner p1 p2 prog =
    let
        swap p =
            if p == p1 then
                p2

            else if p == p2 then
                p1

            else
                p
    in
    List.map swap prog


exchange : Int -> Int -> Progs -> Progs
exchange n1 n2 prog =
    let
        arr =
            Array.fromList prog

        p1 =
            Array.get n1 arr |> Maybe.withDefault 'x'

        p2 =
            Array.get n2 arr |> Maybe.withDefault 'x'
    in
    arr |> Array.set n1 p2 |> Array.set n2 p1 |> Array.toList


spin : Int -> Progs -> Progs
spin n prog =
    let
        pivot =
            List.length prog - modBy (List.length prog) n
    in
    List.drop pivot prog ++ List.take pivot prog
```

Then to start the dance, we just fold over the list of dance moves rearranging the sequence of programs on each iteration.

```elm {l}
dance : List Move -> Progs -> Progs
dance moves progs =
    let
        danceMove move =
            case move of
                Spin n ->
                    spin n

                Exchange n1 n2 ->
                    exchange n1 n2

                Partner p1 p2 ->
                    partner p1 p2
    in
    List.foldl danceMove progs moves
```

```elm {l r}
part1 : String
part1 =
    start
        |> dance (parse puzzleInput)
        |> String.fromList
```

{( part2 |}

In **what order are the programs standing** after their billion dances?

{| part2 )}

The large number of total iterations requested by the question suggests there should be a quicker way of finding the eventual sequence. Looking for repeated cycles reveals the sequence repeats after every 30 runs, so we just need to find the state after 1000000000 modulo 30 times.

```elm {l r}
part2 : String
part2 =
    let
        moves =
            parse puzzleInput

        cycle =
            AOC.sequenceCycle 0 start (dance moves)
                |> Maybe.withDefault ( 1, 0 )
                |> Tuple.first
    in
    AOC.iterate (modBy cycle 1000000000) start (dance moves)
        |> String.fromList
```

---

## Reflection

A classic AoC problem with a part one that encourages an iterative solution and a part two that requires heuristics or optimisations to be found. Initially I created a function for cycle detection that assumed cycles started from the first element in the sequence, but later adapted it to use the more general one in [aoc.elm](../src/Aoc.elm) that can detect cycles that don't start at the first iteration (not strictly necessary for this example, but it keeps the code clean).
