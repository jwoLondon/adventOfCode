---
follows: data/d14_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 14 (2017): Disk Defragmentation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/14):

{( part1|}

Suddenly, a scheduled job activates the system's disk defragmenter. Were the situation different, you might sit and watch it for a while, but today, you just don't have that kind of time. It's soaking up valuable system resources that are needed elsewhere, and so the only option is to help it finish its task as soon as possible.

The disk in question consists of a 128x128 grid; each square of the grid is either **free** or **used**. On this disk, the state of the grid is tracked by the bits in a sequence of knot hashes.

A total of 128 knot hashes are calculated, each corresponding to a single row in the grid; each hash contains 128 bits which correspond to individual grid squares. Each bit of a hash indicates whether that square is **free** (0) or **used** (1).

The hash inputs are a key string (your puzzle input), a dash, and a number from 0 to 127 corresponding to the row. For example, if your key string were flqrgnkx, then the first row would be given by the bits of the knot hash of flqrgnkx-0, the second row from the bits of the knot hash of flqrgnkx-1, and so on until the last row, flqrgnkx-127.

The output of a knot hash is traditionally represented by 32 hexadecimal digits; each of these digits correspond to 4 bits, for a total of 4 \* 32 = 128 bits. To convert to bits, turn each hexadecimal digit to its equivalent binary value, high-bit first: 0 becomes 0000, 1 becomes 0001, e becomes 1110, f becomes 1111, and so on; a hash that begins with a0c2017... in hexadecimal would begin with 10100000110000100000000101110000... in binary.

Continuing this process, the **first 8 rows and columns** for key flqrgnkx appear as follows, using # to denote used squares, and . to denote free ones:

    ##.#.#..-->
    .#.#.#.#
    ....#.#.
    #.#.##.#
    .##.#...
    ##..#..#
    .#...#..
    ##.#.##.-->
    | |
    V V

In this example, 8108 squares are used across the entire 128x128 grid.

Given your actual key string, **how many squares are used?**

{| part1 )}

## Approach

The question assumes a grid size of 128x128, but let's store those dimensions separately in case we need to change the size at a future point.

```elm {l}
gridSize : Int
gridSize =
    128
```

We can reuse the knot hashing from [day 10](d10_2017.md), which has been stored in [knotHash.elm](../src/dependencies/knotHash.elm) and which this document follows. The only additional step is to convert the hex hash into binary digits and sum them.

```elm {l}
toRowBits : String -> Int -> List Int
toRowBits salt row =
    KH.knotHashBinary (salt ++ "-" ++ String.fromInt row)
```

s

```elm {l r}
part1 : Int
part1 =
    let
        sumBits row =
            (+) (toRowBits puzzleInput row |> List.sum)
    in
    List.foldl sumBits 0 (List.range 0 (gridSize - 1))
```

{( part2 |}

Now, all the defragmenter needs to know is the number of **regions**. A region is a group of **used** squares that are all **adjacent**, not including diagonals. Every used square is in exactly one region: lone used squares form their own isolated regions, while several adjacent squares all count as a single region.

In the example above, the following nine regions are visible, each marked with a distinct digit:

    11.2.3..-->
    .1.2.3.4
    ....5.6.
    7.8.55.9
    .88.5...
    88..5..8
    .8...8..
    88.8.88.-->
    |      |
    V      V

Of particular interest is the region marked 8; while it does not appear contiguous in this small view, all of the squares marked 8 are connected when considering the whole 128x128 grid. In total, in this example, 1242 regions are present.

**How many regions** are present given your key string?

{| part2 )}

## Approach

We will need to be able to find connected groups, which we can do recursively by searching outwards until the boundary of the grid is found.

```elm {l}
connected : Int -> ( Int, Int ) -> AOC.Grid Int -> AOC.Grid Int
connected groupID ( r, c ) grid =
    if AOC.gGet ( r, c ) grid == Just 1 then
        AOC.gSet ( r, c ) groupID grid
            |> connected groupID ( r - 1, c )
            |> connected groupID ( r + 1, c )
            |> connected groupID ( r, c - 1 )
            |> connected groupID ( r, c + 1 )

    else
        grid
```

We can use the connected cells identify the contiguous groups.

```elm {l}
toGroups : AOC.Grid Int -> AOC.Grid Int
toGroups grid =
    let
        toGroup location groupID g =
            case AOC.gGet location g of
                Just 1 ->
                    connected groupID location g

                _ ->
                    g
    in
    List.foldl (\loc ( id, g ) -> ( id + 1, toGroup loc id g ))
        ( 2, grid )
        (AOC.gridLocations ( 0, 0 ) ( gridSize - 1, gridSize - 1 ))
        |> Tuple.second
```

And to count them we simply find the size of the set of group IDs, excluding zeros from the group count (i.e. one less than the set size).

```elm {l}
numGroups : AOC.Grid Int -> Int
numGroups =
    AOC.gToList
        >> Set.fromList
        >> Set.size
        >> AOC.flip (-) 1
```

```elm {l r}
part2 : Int
part2 =
    let
        grid =
            AOC.gInit gridSize gridSize 0
    in
    List.foldl (\r g -> AOC.gSetRow r (toRowBits puzzleInput r) g) grid (List.range 0 (gridSize - 1))
        |> toGroups
        |> numGroups
```

### Reflection

Not my favourite kind of puzzle, although good to be able to see how knot hashing may be applied.
