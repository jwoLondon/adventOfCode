---
follows: data/d14_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 14 (2017): Disk Defragmentation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/14).

{( part1|}

Given your actual key string, **how many squares are used?**

{| part1 )}

The question assumes a grid size of 128x128, but let's store those dimensions separately in case we need to change the size at a future point.

```elm {l}
gridSize : Int
gridSize =
    128
```

We can reuse the knot hashing from [day 10](d10_2017.md), which has been stored in [knotHash.elm](../src/dependencies/knotHash.elm) and which this document follows. The only additional step is to convert the hex hash into binary digits and sum them.

```elm {l}
toRowBits : String -> Int -> List Int
toRowBits salt row =
    KH.knotHashBinary (salt ++ "-" ++ String.fromInt row)
```

s

```elm {l r}
part1 : Int
part1 =
    let
        sumBits row =
            (+) (toRowBits puzzleInput row |> List.sum)
    in
    List.foldl sumBits 0 (List.range 0 (gridSize - 1))
```

{( part2 |}

**How many regions** are present given your key string?

{| part2 )}

We will need to be able to find connected groups, which we can do recursively by searching outwards until the boundary of the grid is found.

```elm {l}
connected : Int -> ( Int, Int ) -> AOC.Grid Int -> AOC.Grid Int
connected groupID ( r, c ) grid =
    if AOC.gGet ( r, c ) grid == Just 1 then
        AOC.gSet ( r, c ) groupID grid
            |> connected groupID ( r - 1, c )
            |> connected groupID ( r + 1, c )
            |> connected groupID ( r, c - 1 )
            |> connected groupID ( r, c + 1 )

    else
        grid
```

We can use the connected cells identify the contiguous groups.

```elm {l}
toGroups : AOC.Grid Int -> AOC.Grid Int
toGroups grid =
    let
        toGroup location groupID g =
            case AOC.gGet location g of
                Just 1 ->
                    connected groupID location g

                _ ->
                    g
    in
    List.foldl (\loc ( id, g ) -> ( id + 1, toGroup loc id g ))
        ( 2, grid )
        (AOC.gridLocations ( 0, 0 ) ( gridSize - 1, gridSize - 1 ))
        |> Tuple.second
```

And to count them we simply find the size of the set of group IDs, excluding zeros from the group count (i.e. one less than the set size).

```elm {l}
numGroups : AOC.Grid Int -> Int
numGroups =
    AOC.gToList
        >> Set.fromList
        >> Set.size
        >> AOC.flip (-) 1
```

```elm {l r}
part2 : Int
part2 =
    let
        grid =
            AOC.gInit gridSize gridSize 0
    in
    List.foldl (\r g -> AOC.gSetRow r (toRowBits puzzleInput r) g) grid (List.range 0 (gridSize - 1))
        |> toGroups
        |> numGroups
```

---

## Reflection

Not my favourite kind of puzzle, although good to be able to see how knot hashing may be applied.
