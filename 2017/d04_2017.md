---
follows: data/d04_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 4 (2017): High-Entropy Passphrases

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/4).

{( part1|}

The system's full passphrase list is available as your puzzle input. **How many passphrases are valid?**

{| part1 )}

For any given passphrase, we need to know the frequency of each of its words and then exclude any with frequencies greater than 1.

To create a function that determines if a phrase is valid we could compare the size of _set_ of its words (unique values) with the list of words (which can contain repeats). It will only be valid if both sizes are equal.

```elm {l}
isValid : List String -> Bool
isValid ws =
    List.length ws == Set.size (Set.fromList ws)
```

Then we simply count the number of valid passphrases after filtering for validity:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> List.map String.words
        |> List.filter isValid
        |> List.length
```

{( part2 |}

Under this new system policy, **how many passphrases are valid**?

{| part2 )}

We don't need to find the anagrams of each word if we simply sort all words's characters before comparing. Any anagrams, once sorted, will appear as duplicates:

```elm {l}
words2 : String -> List String
words2 =
    String.words
        >> List.map (String.toList >> List.sort >> String.fromList)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.map words2
        |> List.filter isValid
        |> List.length
```

---

## Reflection

I liked the form of the puzzle. Part 2 contains a distractor encouraging us to calculate word anagrams, when this was not necessary to solve the puzzle. A useful reminder to consider what is required for the answer rather than the process by which it could be derived. An approach that is encouraged by functional declarative languages.
