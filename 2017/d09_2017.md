---
follows: data/d09_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2017): Stream Processing

{( part1|}

A large stream blocks your path. According to the locals, it's not safe to cross the stream at the moment because it's full of garbage. You look down at the stream; rather than water, you discover that it's a stream of characters.

You sit for a while and record part of the stream (your puzzle input). The characters represent groups - sequences that begin with `{` and end with `}`. Within a group, there are zero or more other things, separated by commas: either another group or garbage. Since groups can contain other groups, a `}` only closes the most-recently-opened unclosed group - that is, they are nestable. Your puzzle input represents a single, large group which itself contains many smaller ones.

Sometimes, instead of a group, you will find garbage. Garbage begins with `<` and ends with `>`. Between those angle brackets, almost any character can appear, including `{` and `}`. Within garbage, `<` has no special meaning.

In a futile attempt to clean up the garbage, some program has canceled some of the characters within it using `!`: inside garbage, any character that comes after `!` should be ignored, including `<`, `>`, and even another `!`.

You don't see any characters that deviate from these rules. Outside garbage, you only find well-formed groups, and garbage always terminates according to the rules above.

Here are some self-contained pieces of garbage:

- `<>`, empty garbage.
- `<random characters>`, garbage containing random characters.
- `<<<<>`, because the extra `<` are ignored.
- `<{!>}>`, because the first `>` is canceled.
- `<!!>`, because the second `!` is canceled, allowing the `>` to terminate the garbage.
- `<!!!>>`, because the second `!` and the first `>` are canceled.
- `<{o"i!a,<{i<a>`, which ends at the first `>`.

Here are some examples of whole streams and the number of groups they contain:

- `{}`, 1 group.
- `{{{}}}`, 3 groups.
- `{{},{}}`, also 3 groups.
- `{{{},{},{{}}}}`, 6 groups.
- `{<{},{},{{}}>}`, 1 group (which itself contains garbage).
- `{<a>,<a>,<a>,<a>}`, 1 group.
- `{{<a>},{<a>},{<a>},{<a>}}`, 5 groups.
- `{{<!>},{<!>},{<!>},{<a>}}`, 2 groups (since all but the last `>` are canceled).

Your goal is to find the total score for all groups in your input. Each group is assigned a score which is one more than the score of the group that immediately contains it. (The outermost group gets a score of 1.)

- `{}`, score of 1.
- `{{{}}}`, score of 1 + 2 + 3 = 6.
- `{{},{}}`, score of 1 + 2 + 2 = 5.
- `{{{},{},{{}}}}`, score of 1 + 2 + 3 + 3 + 3 + 4 = 16.
- `{<a>,<a>,<a>,<a>}`, score of 1.
- `{{<ab>},{<ab>},{<ab>},{<ab>}}`, score of 1 + 2 + 2 + 2 + 2 = 9.
- `{{<!!>},{<!!>},{<!!>},{<!!>}}`, score of 1 + 2 + 2 + 2 + 2 = 9.
- `{{<a!>},{<a!>},{<a!>},{<ab>}}`, score of 1 + 2 = 3.

What is the total score for all groups in your input?

{| part1 )}

This is a good problem to represent as a finite state machine.
As we parse characters in the stream we can be in one of three states: in a group; in some garbarge; or following the escape character.
So let's represent those states with a custom type and a finite state machine that keeps track of the score and the depth of nested groups.

```elm {l}
type Mode
    = Escape
    | Garbage
    | Group


type alias FSM =
    { state : Mode
    , groupLevel : Int
    , groupScore : Int
    }
```

As we parse the stream depending on which state the FSM is in and the character we are parsing, we can transition to another state and accumulate the score:

```elm {l}
transition : Char -> FSM -> FSM
transition char fsm =
    case fsm.state of
        Escape ->
            { fsm | state = Garbage }

        Garbage ->
            case char of
                '!' ->
                    { fsm | state = Escape }

                '>' ->
                    { fsm | state = Group }

                _ ->
                    fsm

        Group ->
            case char of
                '<' ->
                    { fsm | state = Garbage }

                '{' ->
                    { fsm
                        | groupLevel = fsm.groupLevel + 1
                        , groupScore = fsm.groupScore + fsm.groupLevel + 1
                    }

                '}' ->
                    { fsm | groupLevel = fsm.groupLevel - 1 }

                _ ->
                    fsm
```

Now we simply send the stream to our FSM parser and report the score:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> String.toList
        |> List.foldl transition (FSM Group 0 0)
        |> .groupScore
```

{( part2 |}

Now, you're ready to remove the garbage.

To prove you've removed it, you need to count all of the characters within the garbage. The leading and trailing `<` and `>` don't count, nor do any canceled characters or the `!` doing the canceling.

- `<>`, 0 characters.
- `<random characters>`, 17 characters.
- `<<<<>`, 3 characters.
- `<{!>}>`, 2 characters.
- `<!!>`, 0 characters.
- `<!!!>>`, 0 characters.
- `<{o"i!a,<{i<a>`, 10 characters.

How many non-canceled characters are within the garbage in your puzzle input?

{| part2 )}

We only need a minor modification to our FSM that keeps track of the garbage count as we parse each character and transition between states:

```elm {l}
type alias FSM2 =
    { state : Mode
    , garbageCount : Int
    , groupLevel : Int
    , groupScore : Int
    }


transition2 : Char -> FSM2 -> FSM2
transition2 char fsm =
    case fsm.state of
        Escape ->
            { fsm | state = Garbage }

        Garbage ->
            case char of
                '!' ->
                    { fsm | state = Escape }

                '>' ->
                    { fsm | state = Group }

                _ ->
                    { fsm | garbageCount = fsm.garbageCount + 1 }

        Group ->
            case char of
                '<' ->
                    { fsm | state = Garbage }

                '{' ->
                    { fsm
                        | groupLevel = fsm.groupLevel + 1
                        , groupScore = fsm.groupScore + fsm.groupLevel + 1
                    }

                '}' ->
                    { fsm | groupLevel = fsm.groupLevel - 1 }

                _ ->
                    fsm
```

Note also that this version of the FSM could also be used to solve the first part as we still keep track of the group score.

```elm {l r }
part2 : Int
part2 =
    puzzleInput
        |> String.toList
        |> List.foldl transition2 (FSM2 Group 0 0 0)
        |> .garbageCount
```
