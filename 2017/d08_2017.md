---
follows: data/d08_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2017): I Heard You Like Registers

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/8).

{( part1|}

**What is the largest value in any register** after completing the instructions in your puzzle input?

{| part1 )}

We can abstract all expressions into the same form. They all involve potentially changing a named register (`reg`), by some value (`delta`) conditional on some predicate (`cOp`) being applied to the value (`cVal`) in another register (`cReg`).

```elm {l}
type alias Expression =
    { reg : String
    , delta : Int
    , cReg : String
    , cOp : Int -> Int -> Bool
    , cVal : Int
    }
```

We can store all register names and their associated value in a dictionary:

```elm {l}
type alias Registers =
    Dict String Int
```

Syntactically, all expressions have a similar form, so we can parse them into the `Expression` record:

```elm {l}
parseLine : String -> Expression
parseLine =
    let
        toExpression tokens =
            case tokens of
                [ reg, deltaOp, delta, cReg, compOp, compVal ] ->
                    let
                        deltaVal =
                            if deltaOp == "inc" then
                                AOC.toInt delta

                            else
                                -1 * AOC.toInt delta

                        cOp =
                            case compOp of
                                "==" ->
                                    (==)

                                "!=" ->
                                    (/=)

                                "<" ->
                                    (<)

                                ">" ->
                                    (>)

                                "<=" ->
                                    (<=)

                                ">=" ->
                                    (>=)

                                _ ->
                                    (==) |> Debug.log "Bad comparison op"
                    in
                    Expression reg deltaVal cReg cOp (AOC.toInt compVal)

                _ ->
                    Expression "" 0 "" (==) 0 |> Debug.log "Bad input"
    in
    AOC.submatches "(\\w+) (inc|dec) (-?\\d+) if (\\w+) ([<>=!]+) (-?\\d+)"
        >> List.filterMap identity
        >> toExpression
```

We can process each expression updating the value of the registers and then simply find the largest register value after running all instructions.
(note the use of `MAX_REG` is for part 2).

```elm {l}
applyInstruction : Expression -> Registers -> Registers
applyInstruction op registers =
    if op.cOp (Dict.get op.cReg registers |> Maybe.withDefault 0) op.cVal then
        let
            newVal =
                (Dict.get op.reg registers |> Maybe.withDefault 0) + op.delta
        in
        Dict.insert "MAX_REG" (max newVal (Dict.get "MAX_REG" registers |> Maybe.withDefault 0)) registers
            |> Dict.insert op.reg newVal

    else
        registers
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> List.map parseLine
        |> List.foldl applyInstruction Dict.empty
        |> Dict.remove "MAX_REG"
        |> Dict.values
        |> List.maximum
        |> Maybe.withDefault 0
```

{( part2 |}

To be safe, the CPU also needs to know **the highest value held in any register** during this process so that it can decide how much memory to allocate to these operations. For example, in the above instructions, the highest value ever held was 10 (in register c after the third instruction was evaluated).

{| part2 )}

For convenience we create an extra register that keeps track of the maximum register value at any one time. Querying that register after running the instructions gives us this maximum.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.map parseLine
        |> List.foldl applyInstruction Dict.empty
        |> Dict.get "MAX_REG"
        |> Maybe.withDefault -1
```

---

## Reflection

I enjoyed this one as it works well in a functional language that is able to store operators that may be applied to register values. Using an extra register to keep track of the maximum value for part two felt like a nice way of doing things given we had already built the register infrastructure.
