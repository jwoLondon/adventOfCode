---
follows: data/d06_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2017): Memory Reallocation

{( part1|}

A debugger program here is having an issue: it is trying to repair a memory reallocation routine, but it keeps getting stuck in an infinite loop.

In this area, there are sixteen memory banks; each memory bank can hold any number of **blocks**. The goal of the reallocation routine is to balance the blocks between the memory banks.

The reallocation routine operates in cycles. In each cycle, it finds the memory bank with the most blocks (ties won by the lowest-numbered memory bank) and redistributes those blocks among the banks. To do this, it removes all of the blocks from the selected bank, then moves to the next (by index) memory bank and inserts one of the blocks. It continues doing this until it runs out of blocks; if it reaches the last memory bank, it wraps around to the first one.

The debugger would like to know how many redistributions can be done before a blocks-in-banks configuration is produced that **has been seen before**.

For example, imagine a scenario with only four memory banks:

- The banks start with 0, 2, 7, and 0 blocks. The third bank has the most blocks, so it is chosen for redistribution.
- Starting with the next bank (the fourth bank) and then continuing to the first bank, the second bank, and so on, the 7 blocks are spread out over the memory banks. The fourth, first, and second banks get two blocks each, and the third bank gets one back. The final result looks like this: 2 4 1 2.
- Next, the second bank is chosen because it contains the most blocks (four). Because there are four memory banks, each gets one block. The result is: 3 1 2 3.
- Now, there is a tie between the first and fourth memory banks, both of which have three blocks. The first bank wins the tie, and its three blocks are distributed evenly over the other three banks, leaving it with none: 0 2 3 4.
- The fourth bank is chosen, and its four blocks are distributed such that each of the four banks receives one: 1 3 4 1.
- The third bank is chosen, and the same thing happens: 2 4 1 2.

At this point, we've reached a state we've seen before: 2 4 1 2 was already seen. The infinite loop is detected after the fifth block redistribution cycle, and so the answer in this example is 5.

Given the initial block counts in your puzzle input, **how many redistribution cycles** must be completed before a configuration is produced that has been seen before?

{| part1 )}

## Approach

We have two tasks to code here: the redistribution of a bank's contents as evenly as possible to other banks; and the checking of whether a given set of banks is a repeat of an earlier set.

Representing the banks as a list of numbers, we will need to find the highest value in a bank along with its position in the list:

```elm {l}
indexedMax : List Int -> ( Int, Int )
indexedMax banks =
    let
        findMax ( i, n ) ( iMax, nMax ) =
            if n > nMax then
                ( i, n )

            else
                ( iMax, nMax )
    in
    banks
        |> List.indexedMap Tuple.pair
        |> List.foldl findMax ( -1, 0 )
```

Once we have found the bank with the highest value, we can redistribute it:

```elm {l}
distribute : List Int -> List Int
distribute banks =
    let
        len =
            List.length banks

        iMax =
            indexedMax banks

        maxVal =
            Tuple.second iMax

        divi =
            ceiling (toFloat maxVal / toFloat len)

        remains =
            if modBy divi maxVal > 0 then
                [ modBy divi maxVal ] ++ List.repeat (len - maxVal // divi - 1) 0

            else
                List.repeat (len - maxVal // divi) 0

        rotate n xs =
            List.drop (len - modBy len n) xs ++ List.take (len - modBy len n) xs

        addition =
            List.repeat (maxVal // divi) divi
                ++ remains
                |> rotate (Tuple.first iMax + 1)
    in
    List.map2 (+) (setListAt (Tuple.first iMax) 0 banks) addition
```

We now have to check to see if the redistributed banks match any that have been seen previously. We can do this by storing a representation of any set of banks, effectively a hash, in a set and checking to see if the hash of any newly distributed banks is contained in this set of previous hashes. Because the list of banks is small, we can create a simple hash as the concatenated string representation of the banks' numeric values.

```elm {l}
hash : List Int -> String
hash banks =
    banks
        |> List.map (\n -> String.fromInt n ++ " ")
        |> String.concat
```

We can combine the repeat checking with the redistribution:

```elm {l}
findRepeat : Set String -> Int -> List Int -> ( List Int, Int )
findRepeat states counter banks =
    let
        state =
            hash banks
    in
    if Set.member state states then
        ( banks, counter )

    else
        findRepeat (Set.insert state states) (counter + 1) (distribute banks)
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> findRepeat Set.empty 0
        |> Tuple.second
```

{( part2 |}

Out of curiosity, the debugger would also like to know the size of the loop: starting from a state that has already been seen, how many block redistribution cycles must be performed before that same state is seen again?

In the example above, 2 4 1 2 is seen again after four cycles, and so the answer in that example would be 4.

**How many cycles** are in the infinite loop that arises from the configuration in your puzzle input?

{| part2 )}

Although, in the example given in the question, the first repeat (after 4 redistributions) matches the initial state of the banks, this may not necessarily be the case. Our first task therefore is to get to the state of the first repeat (i.e. the state found in part 1), then continue until that state is found again, taking the difference in counter values between the two cases.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> findRepeat Set.empty 0
        |> Tuple.first
        |> findRepeat Set.empty 0
        |> Tuple.second
```

## Reflection

A reasonably straightforward solution, even if not the most efficient. Discussion on the AoC subreddit suggests the solution can be found in O(n) time with O(1) space using [Floyd's cycle-finding algorithm](https://en.wikipedia.org/wiki/Cycle_detection#Floyd.27s_Tortoise_and_Hare).
