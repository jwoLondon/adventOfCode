---
follows: data/d06_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2017): Memory Reallocation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/6).

{( part1|}

Given the initial block counts in your puzzle input, **how many redistribution cycles** must be completed before a configuration is produced that has been seen before?

{| part1 )}

We have two tasks to code here: the redistribution of a bank's contents as evenly as possible to other banks; and the checking of whether a given set of banks is a repeat of an earlier set.

Representing the banks as a list of numbers, we will need to find the highest value in a bank along with its position in the list:

```elm {l}
indexedMax : List Int -> ( Int, Int )
indexedMax banks =
    let
        findMax ( i, n ) ( iMax, nMax ) =
            if n > nMax then
                ( i, n )

            else
                ( iMax, nMax )
    in
    banks
        |> List.indexedMap Tuple.pair
        |> List.foldl findMax ( -1, 0 )
```

Once we have found the bank with the highest value, we can redistribute it:

```elm {l}
distribute : List Int -> List Int
distribute banks =
    let
        len =
            List.length banks

        iMax =
            indexedMax banks

        maxVal =
            Tuple.second iMax

        divi =
            ceiling (toFloat maxVal / toFloat len)

        remains =
            if modBy divi maxVal > 0 then
                [ modBy divi maxVal ] ++ List.repeat (len - maxVal // divi - 1) 0

            else
                List.repeat (len - maxVal // divi) 0

        rotate n xs =
            List.drop (len - modBy len n) xs ++ List.take (len - modBy len n) xs

        addition =
            List.repeat (maxVal // divi) divi
                ++ remains
                |> rotate (Tuple.first iMax + 1)
    in
    List.map2 (+) (AOC.setListAt (Tuple.first iMax) 0 banks) addition
```

We now have to check to see if the redistributed banks match any that have been seen previously. We can do this by storing a representation of any set of banks, effectively a hash, in a set and checking to see if the hash of any newly distributed banks is contained in this set of previous hashes. Because the list of banks is small, we can create a simple hash as the concatenated string representation of the banks' numeric values.

```elm {l}
hash : List Int -> String
hash banks =
    banks
        |> List.map (\n -> String.fromInt n ++ " ")
        |> String.concat
```

We can combine the repeat checking with the redistribution:

```elm {l}
findRepeat : Set String -> Int -> List Int -> ( List Int, Int )
findRepeat states counter banks =
    let
        state =
            hash banks
    in
    if Set.member state states then
        ( banks, counter )

    else
        findRepeat (Set.insert state states) (counter + 1) (distribute banks)
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> findRepeat Set.empty 0
        |> Tuple.second
```

{( part2 |}

**How many cycles** are in the infinite loop that arises from the configuration in your puzzle input?

{| part2 )}

Although, in the example given in the question, the first repeat (after 4 redistributions) matches the initial state of the banks, this may not necessarily be the case. Our first task therefore is to get to the state of the first repeat (i.e. the state found in part 1), then continue until that state is found again, taking the difference in counter values between the two cases.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> findRepeat Set.empty 0
        |> Tuple.first
        |> findRepeat Set.empty 0
        |> Tuple.second
```

---

## Reflection

A reasonably straightforward solution, even if not the most efficient. Discussion on the AoC subreddit suggests the solution can be found in O(n) time with O(1) space using [Floyd's cycle-finding algorithm](https://en.wikipedia.org/wiki/Cycle_detection#Floyd.27s_Tortoise_and_Hare).
