---
follows: data/d02_2017.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2017): Corruption Checksum

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2017/day/2).

{( part1|}

What is the checksum for the spreadsheet in your puzzle input?

{| part1 )}

Let's first convert the multi-line, tab-separated string into a list of list of integers:

```elm {l}
spreadsheet : List (List Int)
spreadsheet =
    puzzleInput
        |> String.split "\n"
        |> List.map (List.filterMap String.toInt << String.split "\t")
```

This then becomes a straightforward set of list operations.

```elm {l}
maxMinDiff : List Int -> Int
maxMinDiff numbers =
    Maybe.map2 (-) (List.maximum numbers) (List.minimum numbers)
        |> Maybe.withDefault 0
```

```elm {l r}
part1 : Int
part1 =
    spreadsheet
        |> List.map maxMinDiff
        |> List.sum
```

{( part2 |}

What is the sum of each row's result in your puzzle input?

{| part2 )}

We need to replace the `maxMinDiff` function with one that identifies the dividend and divisor.

We can use the `selectLargest` function from [aoc.md](aoc.md) to separate each list of numbers into tuples of each element paired with those elements in the list that are smaller than it. We can then text each of the items in the list component of the tuple to see if it divides into the larger number.

```elm {l}
divis : List Int -> Int
divis nums =
    let
        divPairs ( n, ns ) =
            case List.filter (\x -> modBy x n == 0) ns of
                [ x ] ->
                    Just (n // x)

                _ ->
                    Nothing
    in
    AOC.selectLargest nums
        |> List.filterMap divPairs
        |> List.sum
```

```elm {l r}
part2 : Int
part2 =
    spreadsheet
        |> List.map divis
        |> List.sum
```

---

## Reflection

Like the first day, this is a nice functional style problem, solved with simple application of list processing functions.
