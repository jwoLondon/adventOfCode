---
follows: data/d25_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 25 (2020): Combo Breaker

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/25)

{(part1|}

**What encryption key is the handshake trying to establish?**

{|part1)}

Firstly store the card and door's public keys:

```elm {l}
parse : String -> ( Int, Int )
parse input =
    case String.lines input of
        [ cardPK, doorPK ] ->
            ( AOC.toInt cardPK, AOC.toInt doorPK )

        _ ->
            ( 1, 1 ) |> Debug.log "Bad input"
```

The core cryptographic transformation that will be iteratively applied to a subject number a certain number of times:

```elm {l}
trans : Int -> Int -> Int
trans sn v =
    modBy 20201227 (sn * v)
```

We can transform the door's public key a number of times to find the private encrypted key. How many times? The number of times it takes to transform 7 until it matches the card's public key.

```elm {l}
loopToKey : ( Int, Int ) -> ( Int, Int ) -> Int
loopToKey ( cardVal, doorVal ) ( cardPubKey, doorPubKey ) =
    if cardVal == cardPubKey then
        doorVal

    else
        loopToKey ( trans 7 cardVal, trans doorPubKey doorVal ) ( cardPubKey, doorPubKey )
```

```elm {l r}
part1 : Int
part1 =
    parse puzzleInput
        |> loopToKey ( 1, 1 )
```

---

## Reflection

A simpler than usual final day puzzle which can be solved with brute force iteration in under a second. Perhaps there is a more efficient process than iteratively applying the transformation until it matches the public key, but it does not seem necessary for the puzzle input.
