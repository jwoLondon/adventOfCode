---
follows: data/d24_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 24 (2020): Lobby Layout

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/24)

{(part1|}

Go through the renovation crew's list and determine which tiles they need to flip. After all of the instructions have been followed, **how many tiles are left with the black side up?**

{|part1)}

We can represent position on a hexgrid with three coordinates representing positions along a N-S (X), SE-NW (Y) and NE-SW (Z) axes each at 60 degrees from the others.

```elm {l}
type alias Point =
    ( Int, Int, Int )
```

We can parse input by converting each instruction into a vector in this 3-dimensional space.

```elm {l}
toVector : String -> Point
toVector dir =
    case dir of
        "e" ->
            ( 0, 1, 1 )

        "se" ->
            ( -1, 1, 0 )

        "sw" ->
            ( -1, 0, -1 )

        "w" ->
            ( 0, -1, -1 )

        "nw" ->
            ( 1, -1, 0 )

        "ne" ->
            ( 1, 0, 1 )

        _ ->
            ( 0, 0, 0 ) |> Debug.log "Bad input direction"
```

While parsing, we consume characters two at a time if they start with `n` or `s`, otherwise one at a time (`e` and `w`), converting each into a position vector:

```elm {l}
parse : List String -> List (List Point)
parse =
    let
        parseLine line =
            AOC.submatches "(n.)|(s.)|(w)|(e)" line
                |> List.filterMap identity
                |> List.map toVector
    in
    List.map parseLine
```

Following instructions simply involves summing the vectors to a final location:

```elm {l}
move : List Point -> Point
move =
    List.foldl (\( dx, dy, dz ) ( x, y, z ) -> ( x + dx, y + dy, z + dz )) ( 0, 0, 0 )
```

To count black tiles we look for an odd number of flips for each location in the input.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.map move
        |> List.foldl AOC.addToFreqTable Dict.empty
        |> Dict.values
        |> List.foldl (\flips total -> total + modBy 2 flips) 0
```

{(part2|}

**How many tiles will be black after 100 days?**

{|part2)}

We have another game of life, this time on a hex grid. Unbounded and sparse, we can use the same pattern as we did for [day 17, 2020](d17_2020.md) and [day 24, 2019](../2019/d24_2019.md).

We keep track of live (black) cells on each iteration:

```elm {l}
type alias LiveCells =
    Set Point
```

The only cells that could possibly change state will be the live cells and their immediate (hex) neighbours

```elm {l}
neighbours : Point -> Set Point
neighbours ( x, y, z ) =
    Set.fromList [ "e", "se", "sw", "w", "nw", "ne" ]
        |> Set.map (toVector >> (\( dx, dy, dz ) -> ( x + dx, y + dy, z + dz )))
```

The reproduction rule is:

- black tile with 0 or 3+ neighbours flipped to white (dies)
- white tile with 2 neighbours flipped to white (lives)

the same as Conway's original Game of Life, but on a hex grid

```elm {l}
rule : Bool -> Int -> Bool
rule isAlive n =
    --n == 3 || isAlive && n == 2
    (isAlive && (n == 1 || n == 2)) || (not isAlive && n == 2)
```

To find whether a cell lives we apply the reproduction rule:

```elm {l}
live : LiveCells -> Point -> Bool
live cells p =
    rule (Set.member p cells) (Set.intersect (neighbours p) cells |> Set.size)
```

For each subsequent generation we apply the reproduction rule to all cells and their immediate neighbourhood.

```elm {l}
generation : LiveCells -> LiveCells
generation cells =
    cells
        |> Set.foldl (neighbours >> Set.union) Set.empty
        |> Set.filter (live cells)
```

The initial state of the floor will be all the black tile locations, which, as for part 1 are those with an odd number of tile flips.

```elm {l r}
part2 : Int
part2 =
    let
        init =
            puzzleInput
                |> parse
                |> List.map move
                |> List.foldl AOC.addToFreqTable Dict.empty
                |> Dict.filter (\_ v -> modBy 2 v == 1)
                |> Dict.keys
                |> Set.fromList
    in
    AOC.iterate 100 init generation
        |> Set.size
```

---

## Reflection

I enjoyed working with another creative Game of Life variation. Kept geometry simple with the 3-axis representation of hex grids, much as for [day 11, 2017](../2017/d11_2017.md) (although rotated by 90 degrees).
