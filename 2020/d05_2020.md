---
follows: data/d05_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2020): Binary Boarding

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/5)

## Part 1

Each seat code is just a number represented in binary with 'F' and 'L' representing 0 and 'B' and 'R' representing 1. We can therefore just convert to binary to generate each seat's ID.

```elm {l}
seatId : String -> Int
seatId =
    AOC.replace "[FL]" "0" >> AOC.replace "[BR]" "1" >> AOC.binaryToDec
```

```elm {l r}
part1 : Maybe Int
part1 =
    puzzleInput
        |> List.map seatId
        |> List.maximum
```

---

## Part 2

Given the seatID is just a row-prime ordering of seats, finding the unoccupied seat just involves finding the first adjacent IDs whose IDs differ by more than 1.

```elm {l r}
part2 : List Int
part2 =
    puzzleInput
        |> List.map seatId
        |> List.sort
        |> AOC.neighbours
        |> List.filter (\( a, b ) -> b - a /= 1)
        |> List.map (\( a, b ) -> b - 1)
```

---

## Reflection

A straightforward puzzle that should be familiar to anyone who uses binary numbers or searches. It helped with part 2 to recognise that the seatID was just a 1d transform of 2d seat locations, so allows adjacency to be quickly identified.

I liked the linked video in the question on [aircraft boarding strategies](https://youtu.be/oAHbLRjF0vo).

When I initially tackled the problem I had no binary to decimal converter pre-written, so provided a more literal interpretation of the puzzle rules.

While my next iteration of the code simplified things by converting to binary, I failed to spot that because the seat column position is just the least significant 3 bits of the ID, we don't need to keep rows and columns separate. The end result was a satisfyingly compact solution.

This is a very well designed puzzle for those (like me) who don't instantly spot the full elegance of the binary representation. A literal interpretation of the instructions yields a workable solution, but in doing so, we are presented with enough hints to think about more elegant approaches. It gives a glimpse of what can make computer programming so satisfying.
