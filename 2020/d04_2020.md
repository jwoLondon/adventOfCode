---
follows: data/d04_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 4 (2020): Passport Processing

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/4)

## Part 1

We can store each passport record as a dictionary of keys and values. Keeping values as strings preserves leading zeros and keeps things simple.

```elm {l}
type alias PRecord =
    Dict String String
```

Initial parsing of the input into a list of dictionaries requires us to separate different passport records. These should all be separated by a blank line, so can be identified by two consecutive newline characters.

```elm {l}
parse : String -> List PRecord
parse =
    let
        keyVal s =
            case String.split ":" s of
                [ key, val ] ->
                    Just ( key, val )

                _ ->
                    Nothing
    in
    String.split "\n\n"
        >> List.map
            (String.split " "
                >> List.concatMap (String.lines >> List.map keyVal)
                >> List.filterMap identity
                >> Dict.fromList
            )
```

Once we have parsed into a list of records, it is a simple task to check that each has the minimum set of keys.

```elm {l}
isValid : PRecord -> Bool
isValid =
    Dict.keys
        >> Set.fromList
        >> Set.diff (Set.fromList [ "byr", "iyr", "eyr", "hgt", "hcl", "ecl", "pid" ])
        >> (==) Set.empty
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.map isValid
        |> List.filter identity
        |> List.length
```

---

## Part 2

Our dictionary already allows us to extract the value for each record type, so we just need to build a set of validation rules.

The date validation rules can treat the dates as strings since they are all 4 digits long.

```elm {l}
inDateRange : String -> ( String, String ) -> PRecord -> Bool
inDateRange key ( mn, mx ) pRec =
    case Dict.get key pRec of
        Just date ->
            date >= mn && date <= mx

        Nothing ->
            False


isByr : PRecord -> Bool
isByr =
    inDateRange "byr" ( "1920", "2002" )


isIyr : PRecord -> Bool
isIyr =
    inDateRange "iyr" ( "2010", "2020" )


isEyr : PRecord -> Bool
isEyr =
    inDateRange "eyr" ( "2020", "2030" )
```

For more complex parsing, we can use regex.

```elm {l}
isHgt : PRecord -> Bool
isHgt pRec =
    case Dict.get "hgt" pRec of
        Just hgt ->
            case AOC.submatches "^(\\d+)(in|cm)$" hgt of
                [ Just h, Just unit ] ->
                    if unit == "cm" then
                        h >= "150" && h <= "193"

                    else
                        h >= "59" && h <= "76"

                _ ->
                    False

        Nothing ->
            False
```

```elm {l}
isHcl : PRecord -> Bool
isHcl pRec =
    case Dict.get "hcl" pRec of
        Just hcl ->
            AOC.contains "^#([a-f|0-9]{6})$" hcl

        Nothing ->
            False
```

```elm {l}
isEcl : PRecord -> Bool
isEcl pRec =
    case Dict.get "ecl" pRec of
        Just ecl ->
            AOC.contains "^(amb|blu|brn|gry|grn|hzl|oth)$" ecl

        Nothing ->
            False
```

```elm {l}
isPid : PRecord -> Bool
isPid pRec =
    case Dict.get "pid" pRec of
        Just pid ->
            AOC.contains "^([0-9]{9})$" pid

        Nothing ->
            False
```

As with part one, we just filter records that pass all validation steps.

```elm {l}
isValid2 : PRecord -> Bool
isValid2 pRecord =
    List.foldl (\f -> (&&) (f pRecord))
        True
        [ isByr, isIyr, isEyr, isHgt, isHcl, isEcl, isPid ]
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.map isValid2
        |> List.filter identity
        |> List.length
```

---

## Reflection

Some practice in parsing and careful reading of the question. For part two, my initial attempt checked for heights < the given limit, not <= that limit and so underestimated the true number of valid passports. This made debugging very hard as all test cases passed. Eventual careful reading of my code allowed me to spot the error.

The fact that the `cid` was never used and that every element in a record can store a numeric value (decimal or hex) suggests this format may be used again in later puzzles.
