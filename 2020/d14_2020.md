---
follows: data/d14_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 14 (2020): Docking Data

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/14)

{(part1|}

Execute the initialization program. What is the sum of all values left in memory after it completes?

{|part1)}

To avoid unnecessary bits to integer transformations and risks of overflow, it makes sense to store all values in a binary form, with addresses as bit strings and values as lists of characters so that 'X' bits can also be stored.

```elm {l}
type alias Bits =
    List Char
```

```elm {l}
type Command
    = Mask Bits
    | Mem String Bits
```

We need to keep track of the last mask setting as well as the memory addresses that have had their value set.

```elm {l}
type alias State =
    { mask : Bits
    , mem : Dict String Bits
    }
```

Parsing the input involves extracting the mask bits and memory allocation commands, which we can do with some regex.

```elm {l}
parse : List String -> List Command
parse =
    let
        toBits =
            AOC.toInt
                >> AOC.decToBinary 36
                -- Convert to characters '0' and '1'
                >> List.map (\i -> Char.fromCode (i + 48))

        parseLine line =
            case
                AOC.submatches "mask = ([01X]+)|mem\\[(\\d+)\\] = (\\d+)" line
                    |> List.filterMap identity
            of
                [ mask ] ->
                    Mask (String.toList mask)

                [ addr, val ] ->
                    Mem (addr |> toBits |> String.fromList) (toBits val)

                _ ->
                    Mask [] |> Debug.log "bad input"
    in
    List.map parseLine
```

Once read we can issue each command in turn, modifying values by the current bit-mask.

```elm {l}
run : Command -> State -> State
run cmd state =
    case cmd of
        Mask mask ->
            { state | mask = mask }

        Mem addr val ->
            { state | mem = Dict.insert addr (applyMask state.mask val) state.mem }
```

```elm {l}
applyMask : Bits -> Bits -> Bits
applyMask =
    let
        toChar iBit =
            case iBit of
                '1' ->
                    '1'

                _ ->
                    '0'

        combine chr bit =
            case chr of
                '0' ->
                    '0'

                '1' ->
                    '1'

                _ ->
                    toChar bit
    in
    List.map2 combine
```

Finally we add all the allocated memory values.

```elm {l}
sumMemory : List Bits -> Int
sumMemory =
    let
        binaryToNum =
            List.reverse
                >> List.indexedMap
                    (\pos b ->
                        if b == '1' then
                            2 ^ pos

                        else
                            0
                    )
                >> List.sum
    in
    List.map binaryToNum
        >> List.sum
```

```elm {l r}
part1 : Int
part1 =
    let
        init =
            State ("XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX" |> String.toList) Dict.empty
    in
    puzzleInput
        |> parse
        |> List.foldl run init
        |> .mem
        |> Dict.values
        |> sumMemory
```

---

{(part2|}

Execute the initialization program using an emulator for a version 2 decoder chip. What is the sum of all values left in memory after it completes?

{|part2)}

We have new rules for the mask application:

```elm {l}
applyMask2 : Bits -> Bits -> Bits
applyMask2 mask valBits =
    let
        combine chr bit =
            case chr of
                '1' ->
                    '1'

                'X' ->
                    'X'

                _ ->
                    bit
    in
    List.map2 combine mask valBits
```

When we find an X in a bit string we need to generate two new bit strings, one with X replaced by a '0' and another with it replaced by a '1'.

```elm {l}
floatAddrs : Bits -> List Bits
floatAddrs valBits =
    case valBits |> String.fromList |> String.indices "X" |> List.head of
        Nothing ->
            [ valBits ]

        Just i ->
            floatAddrs (AOC.setListAt i '0' valBits) ++ floatAddrs (AOC.setListAt i '1' valBits)
```

We now run through each command as in Part 1, but this time with the new mask rules that generate floating addresses.

```elm {l}
run2 : Command -> State -> State
run2 cmd state =
    let
        applyFloats addr val dict =
            let
                addrs =
                    applyMask2 state.mask addr
                        |> floatAddrs
            in
            applyMask2 state.mask addr
                |> floatAddrs
                |> List.map String.fromList
                |> List.foldl (\a d -> Dict.insert a val d) dict
    in
    case cmd of
        Mask mask ->
            { state | mask = mask }

        Mem addr val ->
            { state | mem = applyFloats (addr |> String.toList) val state.mem }
```

```elm {l r}
part2 : Int
part2 =
    let
        init =
            State ("000000000000000000000000000000000000" |> String.toList) Dict.empty
    in
    puzzleInput
        |> parse
        |> List.foldl run2 init
        |> .mem
        |> Dict.values
        |> sumMemory
```

---

## Reflection

I didn't find this puzzle as hard as some of the recent ones. More a case of following detailed instructions than abstracting the problem. My initial attempt stored addresses and values as integers, but this led to overflow problems (36 bit strings) when applying the mask. Storing bit strings directly seems more efficient as well as reducing the risk of overflows.

I wonder if there are ways of improving the efficiency of part 2 to avoid exponential generation of addresses in bit strings with multiple Xs. For example, removing duplicate bit strings before expansion. As currently implemented the solution is found in around 10 seconds, which is probably good enough though.

The final summation of values at all memory addresses does risk overflow, but it would be simple to replace that step with the generation of [Bit Ints](https://package.elm-lang.org/packages/cmditch/elm-bigint/latest/BigInt) before summing.
