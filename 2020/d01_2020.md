---
follows: data/d01_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2020): Report Repair

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/1):

## Part 1

The input is sufficiently small that we could select all pairwise combinations from a list, before choosing the pair that sum to 2020. Conveniently [AOC.utilities](../examples/aocExamples.md#combinations) has a _choose k from n_ combinations function which we can apply here quite simply.

```elm {l}
product2020 : Int -> List Int -> List Int
product2020 k =
    AOC.combinations k
        >> List.filter (List.sum >> (==) 2020)
        >> List.map List.product
```

But just to keep the combination list down to a manageable size, we could remove all values that when added to the smallest input, exceed 2020 before choosing combinations:

```elm {l}
filteredInput : Int -> List Int -> List Int
filteredInput n xs =
    let
        minVal =
            List.sort xs |> List.take n |> List.sum
    in
    List.filter (\x -> x + minVal <= 2020) xs
```

```elm {l r}
part1 : List Int
part1 =
    puzzleInput
        |> filteredInput 1
        |> product2020 2
```

## Part 2

Here we just choose combinations of size k=3, this time pre-filtering the list removing values that when added to the smallest two numbers, exceed 2020:

```elm {l r}
part2 : List Int
part2 =
    puzzleInput
        |> filteredInput 2
        |> product2020 3
```

---

## Reflection

As ever, day 1 is largely a warm-up exercise with simple input parsing and straight forward application of some list processing functions. Because we have seen various combination generation tasks in previous AoCs, I was able to use my previously written [combinations function](../examples/aocExamples.md#combinations).

Nice that this puzzle provides practice in some optimisation but does not require it.
