---
follows: data/d23_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 23 (2020): Crab Cups

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/23)

{(part1|}

Using your labeling, simulate 100 moves. **What are the labels on the cups after cup 1?**

{|part1)}

The task requires us to make frequent use of random access to find the cups to move, so while a Deque might seem like a natural data structure to hold a circular list, it is not very efficient. Instead we can represent the cups in a dictionary where the key is a given cup and the value is the next cup in sequence. Effectively creating a doubly-linked list data structure. This reduces random access queries from O(n) to O(log n) and queries to next and previous entries to O(1).

We can combine the dictionary with a field to keep track of the current cup and place them in a record.

```elm {l}
type alias Cup =
    Int


type alias Circle =
    { cups : Dict Cup ( Cup, Cup )
    , current : Cup
    , size : Int
    }
```

We parse the simple input list of digits into the doubly linked list by finding each value's circular neighbours in both directions.

```elm {l}
parse : String -> Circle
parse input =
    let
        cupList =
            input
                |> String.toList
                |> List.map (String.fromChar >> AOC.toInt)

        prevs =
            AOC.circularNeighbours cupList |> List.map Tuple.first

        nexts =
            AOC.circularNeighbours cupList |> List.reverse |> AOC.rotateList |> List.reverse

        cups =
            List.map2 (\p ( c, n ) -> ( c, ( p, n ) )) prevs nexts |> Dict.fromList
    in
    { cups = cups
    , current = List.head cupList |> Maybe.withDefault 0
    , size = Dict.size cups
    }
```

It is relatively efficient to find the next or previous cup in the sequence or update its links to adjacent cups:

```elm {l}
next : Cup -> Circle -> Cup
next cup circle =
    Dict.get cup circle.cups |> Maybe.withDefault ( 0, 0 ) |> Tuple.second


prev : Cup -> Circle -> Cup
prev cup circle =
    Dict.get cup circle.cups |> Maybe.withDefault ( 0, 0 ) |> Tuple.first
```

The main process that we iterate is to remove 3 consecutive cups and place them elsewhere in the circular list.

```elm {l}
moveTriplet : Circle -> Circle
moveTriplet =
    removeTriplet >> insertTriplet
```

When we remove a triplet of cups we update the `next` link of the cup before the triplet and the `previous` link of the one following to they point to each other.

```elm {l}
removeTriplet : Circle -> ( ( Cup, Cup, Cup ), Circle )
removeTriplet circle =
    let
        c1 =
            next circle.current circle

        c2 =
            next c1 circle

        c3 =
            next c2 circle

        c4 =
            next c3 circle
    in
    ( ( c1, c2, c3 )
    , { circle
        | cups =
            circle.cups
                |> Dict.insert circle.current ( prev circle.current circle, c4 )
                |> Dict.insert c4 ( circle.current, next c4 circle )
      }
    )
```

When we insert a triplet in a new destination we update the links of the surrounding cups.

```elm {l}
insertTriplet : ( ( Cup, Cup, Cup ), Circle ) -> Circle
insertTriplet ( ( c1, c2, c3 ), circle ) =
    let
        findDest n =
            if n < 1 then
                if circle.size /= c1 && circle.size /= c2 && circle.size /= c3 then
                    circle.size

                else if circle.size - 1 /= c1 && circle.size - 1 /= c2 && circle.size - 1 /= c3 then
                    circle.size - 1

                else if circle.size - 2 /= c1 && circle.size - 2 /= c2 && circle.size - 2 /= c3 then
                    circle.size - 2

                else
                    circle.size - 3

            else if n /= c1 && n /= c2 && n /= c3 then
                n

            else
                findDest (n - 1)

        d =
            findDest (circle.current - 1)

        c4 =
            next d circle
    in
    { circle
        | current = next circle.current circle
        , cups =
            circle.cups
                |> Dict.insert d ( prev d circle, c1 )
                |> Dict.insert c1 ( d, c2 )
                |> Dict.insert c2 ( c1, c3 )
                |> Dict.insert c3 ( c2, c4 )
                |> Dict.insert c4 ( c3, next c4 circle )
    }
```

Once we have moved the triplets the required number of times we can report the list of cups following cup 1.

```elm {l}
cupLabels : Circle -> String
cupLabels circle =
    let
        nextLabel ( c, labels ) =
            ( next c circle, labels ++ String.fromInt (next c circle) )
    in
    AOC.iterate (circle.size - 1) ( 1, "" ) nextLabel
        |> Tuple.second
```

```elm {l r}
part1 : String
part1 =
    let
        init =
            parse puzzleInput
    in
    AOC.iterate 100 init moveTriplet
        |> cupLabels
```

{(part2|}

Determine which two cups will end up immediately clockwise of cup 1. **What do you get if you multiply their labels together?**

{|part2)}

For this larger variation of the game we need to be able to add a sequential list of cups to the game, ensuring each is linked to is previous and next in the list, with the final cup linking back to the first of the original cups.

```elm {l}
addCupsTo : Int -> ( Int, Int ) -> Circle -> Circle
addCupsTo n ( first, last ) circle =
    if n <= circle.size then
        circle

    else
        let
            dict =
                circle.cups
                    |> Dict.insert first ( last, next first circle )
                    |> Dict.insert last ( prev last circle, 10 )

            newCups =
                List.map3 (\pr c nx -> ( c, ( pr, nx ) ))
                    (last :: List.range 10 n)
                    (List.range 10 n)
                    (List.range 11 n ++ [ first ])
                    |> List.foldl (\( c, links ) -> Dict.insert c links) dict
        in
        { circle | cups = newCups, size = Dict.size newCups }
```

Then we can run the game as we did for part one, but this time for a larger number of iterations with the larger circle size.

```elm {l r}
part2 : Int
part2 =
    let
        input =
            puzzleInput

        circleSize =
            1000000

        numMoves =
            -- 10000000
            100

        ends =
            ( input |> String.left 1 |> AOC.toInt
            , input |> String.dropLeft (String.length input - 1) |> AOC.toInt
            )

        newCircle =
            AOC.iterate numMoves (input |> parse |> addCupsTo circleSize ends) moveTriplet
    in
    -- Currently slow (c. 3 minutes)
    -- next 1 newCircle * next (next 1 newCircle) newCircle
    157410423276
```

---

## Reflection

Not my favourite of puzzles. Unless there is some strategy for an analytic solution this one is somewhat language dependent in its ability to construct efficient data structures for random access. My initial approach of constructing a deque worked for part 1 but was too slow by far for part 2. Replacing it with a doubly linked list via a dictionary produced the necessary efficiency improvements, although still slow at around 3 minutes to calculate part 2.

It may be that an array rather than dictionary would be faster, with a theoretical O(1) query and update efficiency. But I am not sure whether Elm Arrays use some other list-based structure that would make this slower. As the approach described above produces an answer in a practical, if slow, amount of time, I haven't chosen to investigate alternative data structures.

I look for repeating cycles in subsequent iterations and there are repeats for small circle sizes, typically with a period of order 1000 for circles of around 100. However this become to slow to calculate for circle sizes of 1,000,000. It may be that a smaller sequence of cups is sufficient to indicate cycles (e.g. the first few after cup 1), but I haven't investigated this.
