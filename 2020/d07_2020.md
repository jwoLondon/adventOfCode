---
follows: data/d07_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2020): Handy Haversacks

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/7)

{(part1|}How many bag colors can eventually contain at least one shiny gold bag?{|part1)}

Parsing the input is a little fiddly as each bag statement can have a variable number of contained bags. Can be parsed with two regex statements. The resulting tree of bag containments can be represented as a dictionary of (_parent_,_children_).

```elm {l}
type alias BagTree =
    Dict String (List ( Int, String ))
```

```elm {l}
parse : List String -> BagTree
parse =
    let
        numBags s =
            case AOC.submatches "(\\d+) (.+) " s |> List.filterMap identity of
                [ n, bag ] ->
                    ( AOC.toInt n, bag )

                _ ->
                    ( 0, "" )

        parseLine line =
            case
                AOC.submatches "(.+) bag. contain (no.+|\\d+ .+)[.]" line
                    |> List.filterMap identity
            of
                [ hd, tl ] ->
                    Dict.insert hd (tl |> String.split ", " >> List.map numBags)

                _ ->
                    Dict.insert "" [] >> Debug.log "bad input"
    in
    List.foldl parseLine Dict.empty
```

We can invert the dictionary from (parent -> children) to (child -> parents), making finding what bag contains a given bag much easier.

```elm {l}
invert : BagTree -> Dict String (List String)
invert =
    let
        distrib parent children dict =
            List.foldl (\b -> AOC.updateInsert b [ parent ] ((::) parent))
                dict
                (List.map Tuple.second children)
    in
    Dict.foldl distrib Dict.empty
```

And now we can perform a breadth first search on this dictionary of containing bags:

```elm {l}
outerBags : Dict String (List String) -> Set String -> Set String -> Set String
outerBags dict todo bags =
    case Set.toList todo of
        [] ->
            bags

        bag :: tl ->
            let
                newBags =
                    (Dict.get bag dict |> Maybe.withDefault []) ++ tl |> Set.fromList
            in
            outerBags dict newBags (Set.union bags newBags)
```

```elm {l r}
part1 : Int
part1 =
    let
        containers =
            puzzleInput |> parse |> invert
    in
    outerBags containers (Set.singleton "shiny gold") Set.empty
        |> Set.size
```

{(part2|}How many individual bags are required inside your single shiny gold bag?{|part2)}

This is a more straightforward search of the original tree, accumulating bag totals to multiply by the number of containing bags.

```elm {l}
bagsWithin : BagTree -> ( Int, String ) -> Int
bagsWithin dict ( n, bag ) =
    case Dict.get bag dict of
        Just bags ->
            List.map (\( n1, s ) -> bagsWithin dict ( n1, s ) * n) bags
                |> List.sum
                |> (+) n

        Nothing ->
            n
```

After counting all contained bags, we subtract one for the shiny gold bag itself.

```elm {l r}
part2 : Int
part2 =
    bagsWithin (parse puzzleInput) ( 1, "shiny gold" ) - 1
```

---

## Reflection

I spent a large proportion of this puzzle getting my input parsing working correctly, which is never entirely satisfying.

Unusually, part 2 seemed much easier than part 1, without relying on anything from the part 1 solution. I wonder if I've missed a trick in my part 1 solution and there is a much simpler way to search the original tree of bags.
