---
follows: data/d20_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 20 (2020): Jurassic Jigsaw

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/20)

{(part1|}

Assemble the tiles into an image. What do you get if you multiply together the IDs of the four corner tiles?

{|part1)}

As well storing grid values for each tile, we also need to store its ID, so let's represent both in a tuple.

```elm {l}
type alias Grid =
    ( Int, AOC.Grid Char )
```

We can compare two grids to see if they are identical even under rotational or reflection transformation simply by comparing ids:

```elm {l}
equalId : Grid -> Grid -> Bool
equalId g1 g2 =
    Tuple.first g1 == Tuple.first g2
```

It will be useful to be able to identify one of four sides of a grid.

```elm {l}
type Side
    = Top
    | Right
    | Bottom
    | Left
```

Parsing input is straightforward:

```elm {l}
parse : String -> List Grid
parse input =
    let
        parseGrid inp =
            let
                lines =
                    String.lines inp

                id =
                    lines
                        |> List.head
                        |> Maybe.withDefault ""
                        |> String.dropLeft 5
                        |> String.dropRight 1
                        |> AOC.toInt
            in
            ( id, lines |> List.drop 1 |> List.map String.toList |> AOC.gFromLists )
    in
    List.map parseGrid (String.split "\n\n" input)
```

For part 1 we don't need to assemble all tiles as all we need are the IDs of the four corner tiles in any order. These can be found by looking for tiles that have exactly two neighbours in any configuration. We don't need to transform all tiles as it is only the edge values that determine if tiles can be matched, so we can extract a tile's four edges as four strings:

```elm {l}
edges : Grid -> List String
edges ( _, g ) =
    [ AOC.gGetRow 0 g, AOC.gGetCol 9 g, AOC.gGetRow 9 g, AOC.gGetCol 0 g ]
        |> List.map (Maybe.withDefault [] >> String.fromList)
```

Given a grid, we find all possible neighbouring tiles accounting for the fact grids could be flipped or rotated. This means edge cells may be reversed.

```elm {l}
neighbours : Grid -> List Grid -> List Grid
neighbours grid gs =
    let
        others =
            List.filter (not << equalId grid) gs

        reversed : String -> List String
        reversed s =
            [ s, s |> String.toList |> List.reverse |> String.fromList ]

        thisEdges =
            edges grid
                |> List.concatMap reversed
                |> Set.fromList
    in
    others |> List.filter (\g -> Set.intersect (Set.fromList (edges g)) thisEdges /= Set.empty)
```

Because a corner cell will have exactly two neighbours, we don't need to arrange the cells, but simply find the IDs of the 2-neighbour tiles:

```elm {l}
corners : List Grid -> List Grid
corners grids =
    List.filter (\g -> (neighbours g grids |> List.length) == 2) grids
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> corners
        |> List.map Tuple.first
        |> List.product
```

{(part2|}

Determine how rough the waters are in the sea monsters' habitat by counting the number of # that are not part of a sea monster. **How many # are not part of a sea monster?**

{|part2)}

We will be building up a mosaic of tiles which is a grid of grids.

```elm {l}
type alias Mosaic =
    AOC.Grid Grid
```

While we didn't need to arrange all tiles for part 1, we do need to for the second part. For this we will need to be able to detect which edge of a grid matches which other in order to know the correct orientation and flipping to apply to a matched grid. So now, rather than simply extracting all edges of a tile as we did for part 1, we now need to be able to extract the edge on a given side.

```elm {l}
edgeTo : Side -> Grid -> String
edgeTo side ( _, g ) =
    (case side of
        Top ->
            AOC.gGetRow 0 g

        Right ->
            AOC.gGetCol 9 g

        Bottom ->
            AOC.gGetRow 9 g

        Left ->
            AOC.gGetCol 0 g
    )
        |> Maybe.withDefault []
        |> String.fromList
```

For any grid there are 8 possible configurations of rotation and flipping (with several ways of achieving each of them). These correspond the four edge sequences plus their reverses.

1.  Untransformed
2.  Rotate 90
3.  Rotate 180 (same as flipH and flip V)
4.  Rotate 270 (same as rotate 90 then flipH and flip V)
5.  Flip H
6.  Flip V
7.  Rotate 90 and flip V
8.  Rotate 90 and flip H

By generating the set of 8 transformed grids, we can try every configuration when looking for connecting neighbouring grids.

```elm {l}
flipH : Grid -> Grid
flipH ( id, g ) =
    ( id, g |> AOC.gToLists |> List.reverse |> AOC.gFromLists )


flipV : Grid -> Grid
flipV ( id, g ) =
    ( id, g |> AOC.gToLists |> List.map List.reverse |> AOC.gFromLists )


rotate90 : Grid -> Grid
rotate90 ( id, g ) =
    ( id, g |> AOC.gTranspose ) |> flipV


variants : Grid -> List Grid
variants grid =
    List.map (\fn -> fn grid)
        [ identity
        , rotate90
        , flipH >> flipV
        , rotate90 >> flipH >> flipV
        , flipH
        , flipV
        , rotate90 >> flipV
        , rotate90 >> flipH
        ]
```

Is there a matching grid available on the given side? If there is, it may have to be rotated/flipped in order to match.

```elm {l}
neighbourTo : Side -> Grid -> List Grid -> Maybe Grid
neighbourTo side grid gs =
    let
        otherSide =
            case side of
                Left ->
                    Right

                Right ->
                    Left

                Top ->
                    Bottom

                Bottom ->
                    Top

        edge =
            edgeTo side grid

        availableTransforms =
            List.filter (not << equalId grid) gs
                |> List.concatMap variants
    in
    List.filter (\g -> edge == edgeTo otherSide g) availableTransforms
        |> List.head
```

We can start with any corner cell and place it in the top-left since the adjacency of the square tiles is invariant with rotation and mirroring. We just have to ensure that it is transformed such that its two edges that have no connecting neighbour are facing outwards.

```elm {l}
defaultGrid : Maybe Grid -> Grid
defaultGrid =
    Maybe.withDefault ( 0, AOC.gInit 0 0 ' ' )


topLeftCorner : List Grid -> Grid
topLeftCorner grids =
    grids
        |> corners
        |> List.head
        |> defaultGrid
        |> variants
        |> List.filter (\g -> neighbourTo Left g grids == Nothing && neighbourTo Top g grids == Nothing)
        |> List.head
        |> defaultGrid
```

Once a corner grid is identified, we can traverse the grid of grids looking for new grids to connect to the leading edges of our placed grids.

```elm {l}
buildMosaic : ( Int, Int ) -> List Grid -> Mosaic -> Mosaic
buildMosaic ( row, col ) available mosaic =
    case rightBottomNeighbours (AOC.gGet ( row, col ) mosaic |> defaultGrid) available of
        ( Just rGrid, Just bGrid ) ->
            buildMosaic ( row, col + 1 )
                (List.filter (\g -> (not <| equalId rGrid g) && (not <| equalId bGrid g)) available)
                (mosaic |> AOC.gSet ( row, col + 1 ) rGrid |> AOC.gSet ( row + 1, col ) bGrid)

        ( Nothing, Just bGrid ) ->
            let
                newPos =
                    if col == AOC.gColCount mosaic - 1 then
                        ( row + 1, 0 )

                    else
                        ( row, col + 1 )
            in
            buildMosaic newPos
                (List.filter (not << equalId bGrid) available)
                (mosaic |> AOC.gSet ( row + 1, col ) bGrid)

        _ ->
            mosaic
```

```elm {l}
rightBottomNeighbours : Grid -> List Grid -> ( Maybe Grid, Maybe Grid )
rightBottomNeighbours grid available =
    let
        availableTransforms =
            List.concatMap variants available

        rGrids =
            availableTransforms
                |> List.filter (\g -> edgeTo Right grid == edgeTo Left g)

        bGrids =
            availableTransforms
                |> List.filter (\g -> edgeTo Bottom grid == edgeTo Top g)
    in
    ( rGrids |> List.head, bGrids |> List.head )
```

Once we have built up the mosaic we need to strip the outer edges of each cell and compose into a single grid.

```elm {l}
largeGrid : Mosaic -> AOC.Grid Char
largeGrid mosaic =
    mosaic
        |> AOC.gMapWithLocation
            (\( r0, c0 ) ( id, grid ) ->
                AOC.gMapWithLocation (\( r1, c1 ) chr -> ( r0 * 10 + r1, c0 * 10 + c1, chr )) grid
                    |> AOC.gToList
            )
        |> AOC.gToList
        |> List.concat
        |> List.filter (\( r, c, chr ) -> modBy 10 r /= 0 && modBy 10 c /= 0 && modBy 10 r /= 9 && modBy 10 c /= 9)
        |> List.sortBy (\( r, c, _ ) -> ( r, c ))
        |> List.map AOC.tripletThird
        |> AOC.groupsOf (AOC.gColCount mosaic * 8)
        |> AOC.gFromLists
```

Now we are ready to search for the sea monsters in the aggregated grid.

```elm {l}
matchMonster : AOC.Grid Char -> Int
matchMonster grid =
    let
        gSize =
            AOC.gColCount grid

        monster =
            [ ( 0, 18 ), ( 1, 0 ), ( 1, 5 ), ( 1, 6 ), ( 1, 11 ), ( 1, 12 ), ( 1, 17 ), ( 1, 18 ), ( 1, 19 ), ( 2, 1 ), ( 2, 4 ), ( 2, 7 ), ( 2, 10 ), ( 2, 13 ), ( 2, 16 ) ]

        monsterAt ( rOff, cOff ) =
            if
                (List.filterMap (\( r, c ) -> AOC.gGet ( rOff + r, cOff + c ) grid) monster
                    |> List.filter ((==) '#')
                    |> List.length
                )
                    == 15
            then
                1

            else
                0
    in
    List.foldl (\pos -> (+) (monsterAt pos)) 0 (AOC.gridLocations ( 0, 0 ) ( gSize - 3, gSize - 20 ))
```

Bringing it all together we (i) find a top-left grid; (ii) fill the mosaic with edge matching grids; (iii) aggregate into a single grid stripping out edges; (iv) count the number of monster matches for all 8 confifurations of the grid; (v) subtract the number of monster cells from the number of hashes in the grid.

```elm {r}
part2 : Int
part2 =
    let
        allGrids =
            parse testInput

        gSize =
            sqrt (List.length allGrids |> toFloat) |> round

        topLeft =
            topLeftCorner allGrids

        available =
            List.filter (not << equalId topLeft) allGrids

        lGrid =
            AOC.gInit gSize gSize ( 0, AOC.gInit 0 0 ' ' )
                |> AOC.gSet ( 0, 0 ) topLeft
                |> buildMosaic ( 0, 0 ) available
                |> largeGrid

        numHashes =
            AOC.gToList lGrid |> List.filter ((==) '#') |> List.length

        numMonsters =
            List.map matchMonster (variants ( 0, lGrid ) |> List.map Tuple.second)
                |> List.maximum
                |> Maybe.withDefault 0
    in
    numHashes - (15 * numMonsters)
```

---

## Reflection

A great puzzle for a weekend. Lot's of steps, but sufficient variety to keep it interesting. Like many, I recognised there was no need to arrange the grid cells for part 1 only to find it became necessary for the second part. I enjoy the jousting with Eric the puzzle setter as he drops a mixture of clues and red herrings in his puzzle design.

I enjoyed the tile arrangement part of this puzzle in particular, thinking about adjacency under various transformations.

---
