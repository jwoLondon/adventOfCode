---
follows: data/d15_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 15 (2020): Rambunctious Recitation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/15)

{(part1|}

Given your starting numbers, what will be the 2020th number spoken?

{|part1)}

To play the game we will need to store the last two rounds in which a number has been spoken. A spoken number will always have been spoken once, but may not have been spoken a second or more times.

```elm {l}
type alias History =
    ( Int, Maybe Int )
```

Simple parsing of the input list stores both the numbers spoken and the turn in which they were spoken:

```elm {l}
parse : List Int -> Dict Int History
parse xs =
    List.map2 (\x turn -> ( x, ( turn, Nothing ) )) xs (List.range 1 (List.length xs))
        |> Dict.fromList
```

The game is played by finding the recent history of last number spoken and depending on whether it was spoken for the first time or not, generating a new number and updating its history accordingly.

```elm {l}
game : Int -> Int -> ( Int, Dict Int History ) -> ( Int, Dict Int History )
game maxT t ( last, dict ) =
    let
        addToHistory num turn =
            case Dict.get num dict of
                Nothing ->
                    Dict.insert num ( turn, Nothing ) dict

                Just ( tLast, _ ) ->
                    Dict.insert num ( turn, Just tLast ) dict
    in
    if t == maxT + 1 then
        ( last, dict )

    else
        case Dict.get last dict of
            Nothing ->
                ( last, dict ) |> Debug.log "Error: last number not in history"

            Just ( tLast, Nothing ) ->
                game maxT (t + 1) ( 0, addToHistory 0 t )

            Just ( tLast, Just tBeforeLast ) ->
                game maxT (t + 1) ( t - tBeforeLast - 1, addToHistory (t - tBeforeLast - 1) t )
```

We set the game off, halting after a fixed number of turns, at which point we extract the last number spoken.

```elm {l}
playGame : Int -> List Int -> Int
playGame maxT input =
    let
        lastNum =
            input |> List.reverse |> List.head |> Maybe.withDefault 0
    in
    game maxT (List.length input + 1) ( lastNum, parse input )
        |> Tuple.first
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> playGame 2020
```

{(part2|}

Given your starting numbers, what will be the 30000000th number spoken?

{|part2)}

We can apply the same process with a larger termination point.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        -- Toggle comments on lines below to run (takes about a minute to compute).
        --|> playGame 30000000
        |> always 814
```

---

## Reflection

Once I'd understood the rules of the game properly, this was quite straightforward to code. My initial attempt at part 1 stored the full history of each number's round utterances in anticipation this might be needed for part 2. However this turned out not to be the case, and because the set of histories grows quadratically, would prevent a feasible solution. It was a simple task to replace the full history with a tuple of the last two entries.

Presumably there must be a more efficient solution. As it is, the solution is found in about 1 minute, which is good enough for a Tuesday morning.

After consulting reddit, it turns out this is a [Van Eck's sequence](https://oeis.org/A181391), about which [we know very little](https://youtu.be/etMJxB-igrc) other than it is infinite, contains no cycles and has no finite maximum.
