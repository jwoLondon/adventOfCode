---
follows: data/d10_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2020): Adapter Array

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/10).

{(part1|}

What is the number of 1-jolt differences multiplied by the number of 3-jolt differences?

{|part1)}

This can be straightforwardly found by sorting the list of adaptors and finding the difference between consecutive values.

```elm {l}
diffs : List Int -> List Int
diffs =
    List.sort
        >> AOC.neighbours
        >> List.map (\( a, b ) -> b - a)
```

We need additionally to add a 0 to the start of the list and we know the final difference will always be 3 so add that to the list of differences. We can then filter the 1s and 3s and multiply the number of each.

```elm {l r}
part1 : Int
part1 =
    let
        ds =
            3 :: diffs (0 :: puzzleInput)
    in
    List.length (List.filter ((==) 1) ds) * List.length (List.filter ((==) 3) ds)
```

---

{(part2|}

What is the total number of distinct ways you can arrange the adapters to connect the charging outlet to your device?

{|part2)}

Considering the adaptors in joltage order, we know that if there is a jump of 3 from one to the next, there can only be one possible route between the states. If we have run of consecutive differences of 1, we know there will be several ways of getting from the start to the end of the run, with intermediate jumps of 1, 2 or 3.

{(fixed|}

| runLength | Adaptor sequence | Num paths |
| --------- | ---------------- | --------- |
| 1         | 1 2              | 1         |

| runLength | Adaptor sequence | Num paths |
| --------- | ---------------- | --------- |
| 2         | 1 2 3            |           |
|           | 1 . 3            | 2         |

| runLength | Adaptor sequence | Num paths |
| --------- | ---------------- | --------- |
| 3         | 1 2 3 4          |           |
|           | 1 2 . 4          |           |
|           | 1 . 3 4          |           |
|           | 1 . . 4          | 4         |

| runLength | Adaptor sequence | Num paths |
| --------- | ---------------- | --------- |
| 4         | 1 2 3 4 5        |           |
|           | 1 2 3 . 5        |           |
|           | 1 2 . 4 5        |           |
|           | 1 . 3 4 5        |           |
|           | 1 . 3 . 5        |           |
|           | 1 2 . . 5        |           |
|           | 1 . . 4 5        | 7         |

| runLength | Adaptor sequence | Num paths |
| --------- | ---------------- | --------- |
| 5         | 1 2 3 4 5 6      |           |
|           | 1 2 3 4 . 6      |           |
|           | 1 2 3 . 5 6      |           |
|           | 1 2 . 4 5 6      |           |
|           | 1 . 3 4 5 6      |           |
|           | 1 2 . 4 . 6      |           |
|           | 1 . 3 4 . 6      |           |
|           | 1 . . 4 . 6      |           |
|           | 1 . 3 . 5 6      |           |
|           | 1 . 3 . . 6      |           |
|           | 1 2 3 . . 6      |           |
|           | 1 2 . . 5 6      |           |
|           | 1 . . 4 5 6      | 13        |

{|fixed)}

Looking through the input runs, there are no runs longer than 4, so this simple lookup is sufficient to calculating the number of path bifurcations for every run of consecutive jolt adaptors.

```elm {l}
numPathSplits : List Int -> Int
numPathSplits xs =
    if List.head xs == Just 1 then
        case List.length xs of
            1 ->
                1

            2 ->
                2

            3 ->
                4

            4 ->
                7

            _ ->
                0 |> Debug.log "More than 4 runs"

    else
        1
```

The total number of pathways would then be the product of all the path splits.

```elm {l r}
part2 : Int
part2 =
    (0 :: puzzleInput)
        |> diffs
        |> AOC.group
        |> List.map numPathSplits
        |> List.product
```

---

## Reflection

Part 1 was easy, although my initial solution to it overcomplicated matters by calculating the full set of possible jumps from every adaptor and intersecting that with the set of actual adaptors. The eventual solution recognised we simply count the number of runs of consecutive differences in the sorted list of adaptors.

Part 2 really stumped me, not helped by the fact I somehow glanced over the wording in the question that made it clear the full tree of possible combinations was too large to traverse directly. The lightbulb moment came when I realised that jumps of 3 (as identified in the first part) would never add new branches to the tree since the could never be removed. But jumps of 1 (also identified in part 1) allowed adaptors to be removed and still bridge to the next adaptor that was at most 3 jumps away.

It took me longer than I'd liked to work out how many alternative ways of removing adaptors from a a given run of consecutive values. Thankfully the longest run of consecutive jumps in the puzzle input was 4, although I discovered afterwards that the sequence is easily computable as in a _tribonacci sequence_ (the sum of the three previous in the sequence), so theoretically if there had been longer runs this would be computable (at the risk of overflow when multiplying the list of combinations).
