---
follows: data/d02_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2020): Password Philosophy

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/2):

{( part1|}

Your flight departs in a few days from the coastal airport; the easiest way down to the coast from here is via toboggan.

The shopkeeper at the North Pole Toboggan Rental Shop is having a bad day. "Something's wrong with our computers; we can't log in!" You ask if you can take a look.

Their password database seems to be a little corrupted: some of the passwords wouldn't have been allowed by the Official Toboggan Corporate Policy that was in effect when they were chosen.

To try to debug the problem, they have created a list (your puzzle input) of **passwords** (according to the corrupted database) and **the corporate policy when that password was set**.

For example, suppose you have the following list:

1-3 a: abcde
1-3 b: cdefg
2-9 c: ccccccccc

Each line gives the password policy and then the password. The password policy indicates the lowest and highest number of times a given letter must appear for the password to be valid. For example, 1-3 a means that the password must contain a at least 1 time and at most 3 times.

In the above example, 2 passwords are valid. The middle password, cdefg, is not; it contains no instances of b, but needs at least 1. The first and third passwords are valid: they contain one a or nine c, both within the limits of their respective policies.

**How many passwords are valid** according to their policies?

{| part1 )}

## Parsing

Most of the work is in extracting the relevant details of the policy and password for each line of input. We can do this with some simple regex.

```elm {l}
type alias Policy =
    ( Int, Int, String )
```

```elm {l}
parse : List String -> List ( Policy, String )
parse =
    let
        parseLine line =
            case
                AOC.submatches "(\\d+)[-](\\d+) (.): (.+)" line
                    |> List.filterMap identity
            of
                [ a, b, c, p ] ->
                    ( ( AOC.toInt a, AOC.toInt b, c ), p )

                _ ->
                    ( ( 0, 0, "" ), "" ) |> Debug.log "bad input"
    in
    List.map parseLine
```

Once we have a structure to hold the policy, it is easy to implement the validity rule for any given policy and password:

```elm {l}
isValid : ( Policy, String ) -> Bool
isValid ( ( mn, mx, chr ), pwd ) =
    let
        numOccurrences c =
            String.indices c >> List.length
    in
    (numOccurrences chr pwd >= mn) && (numOccurrences chr pwd <= mx)
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.map isValid
        |> List.filter identity
        |> List.length
```

{( part2 |}

While it appears you validated the passwords correctly, they don't seem to be what the Official Toboggan Corporate Authentication System is expecting.

The shopkeeper suddenly realizes that he just accidentally explained the password policy rules from his old job at the sled rental place down the street! The Official Toboggan Corporate Policy actually works a little differently.

Each policy actually describes two **positions in the password**, where 1 means the first character, 2 means the second character, and so on. (Be careful; Toboggan Corporate Policies have no concept of "index zero"!) **Exactly one of these positions** must contain the given letter. Other occurrences of the letter are irrelevant for the purposes of policy enforcement.

Given the same example list from above:

- 1-3 a: **a**b**c**de is **valid**: position 1 contains a and position 3 does not.
- 1-3 b: **c**d**e**fg is **invalid**: neither position 1 nor position 3 contains b.
- 2-9 c: c**c**cccccc**c** is **invalid**: both position 2 and position 9 contain c.

**How many passwords are valid** according to the new interpretation of the policies?

{| part2 )}

This is just a case of providing a new validation rule remembering to count from 1 not 0.

```elm {l}
isValid2 : ( Policy, String ) -> Bool
isValid2 ( ( mn, mx, chr ), pwd ) =
    let
        m1 =
            String.dropLeft (mn - 1) pwd |> String.startsWith chr

        m2 =
            String.dropLeft (mx - 1) pwd |> String.startsWith chr
    in
    (m1 && not m2) || (m2 && not m1)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.map isValid2
        |> List.filter identity
        |> List.length
```

---

## Reflection

Another relatively simple early puzzle. Most of the work was in remembering my regex to extract the various parts of the input. I wonder if we will see more one-indexed counting in later puzzles. Off-by-one errors are common enough when working though AoC puzzles, but this might just guarantee a few more.
