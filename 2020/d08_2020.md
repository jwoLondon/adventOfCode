---
follows: data/d08_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2020): Handheld Halting

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/8)

{(part1|}

Run your copy of the boot code. Immediately before any instruction is executed a second time, what value is in the accumulator?

{|part1)}

Firstly we need to be able to represent the state of the console, including the accumulator, the memory position of the current instruction, the boot code program instructions and the status of the console. In order to detect repeated instructions, we also store a history of previously accessed memory addresses.

```elm {l}
type alias Console =
    { acc : Int
    , pos : Int
    , prog : Array Instruction
    , history : List Int
    , status : Status
    }
```

The instruction set itself is a simple:

```elm {l}
type Instruction
    = Acc Int
    | Jmp Int
    | Nop Int
```

The console can be in one of three states: it may be running a program normally, it may have entered an infinite loop or it may have completed.

```elm {l}
type Status
    = Running
    | Infinite
    | Halt
```

Parsing the input instructions is straightforward:

```elm {l}
parse : List String -> Array Instruction
parse =
    let
        toOp s =
            case String.split " " s of
                [ op, val ] ->
                    case op of
                        "acc" ->
                            Acc (AOC.toInt val)

                        "jmp" ->
                            Jmp (AOC.toInt val)

                        "nop" ->
                            Nop (AOC.toInt val)

                        _ ->
                            Nop 0 |> Debug.log "unknown instruction"

                _ ->
                    Nop 0 |> Debug.log "bad input"
    in
    List.map toOp >> Array.fromList
```

Running the program just involves issuing instructions and updating the accumulator and current memory address as appropriate. We can stop execution either if we process a memory address for a second time, or the current memory address is outside the instruction set.

```elm {l}
run : Console -> Console
run c =
    if List.member c.pos c.history then
        { c | status = Infinite }

    else if c.pos >= Array.length c.prog then
        { c | status = Halt }

    else
        case Array.get c.pos c.prog of
            Just (Acc n) ->
                run { c | acc = c.acc + n, history = c.pos :: c.history, pos = c.pos + 1 }

            Just (Jmp n) ->
                run { c | history = c.pos :: c.history, pos = c.pos + n }

            Just (Nop n) ->
                run { c | history = c.pos :: c.history, pos = c.pos + 1 }

            Nothing ->
                c |> Debug.log "Unknown instruction"
```

```elm {l r}
part1 : Status
part1 =
    { acc = 0, pos = 0, history = [], prog = parse puzzleInput, status = Running }
        |> run
        |> .status
```

{(part2|}

Fix the program so that it terminates normally by changing exactly one jmp (to nop) or nop (to jmp). What is the value of the accumulator after the program terminates?

{|part2)}

We need to be able to toggle an instruction at a given address between NOP and JMP:

```elm {l}
toggle : Int -> Console -> Console
toggle addr c =
    case Array.get addr c.prog of
        Just (Nop n) ->
            { c | prog = Array.set addr (Jmp n) c.prog }

        Just (Jmp n) ->
            { c | prog = Array.set addr (Nop n) c.prog }

        _ ->
            c
```

We can determine if we should toggle a given instruction by checking it is either Nop or Jmp. There is no point in toggling Nop 0 since that guarantees an infinite loop.

```elm {l}
canToggle : Instruction -> Bool
canToggle instr =
    case instr of
        Jmp _ ->
            True

        Nop n ->
            n /= 0

        _ ->
            False
```

Finally we bring things together by finding all the togglable instructions after running the program once, then running the program once for each of them after toggling, looking for ones that halt correctly.

```elm {l r}
part2 : List Int
part2 =
    let
        c =
            { acc = 0, pos = 0, history = [], prog = parse puzzleInput, status = Running }
    in
    run c
        |> .history
        |> List.map (\addr -> ( addr, Array.get addr c.prog |> Maybe.withDefault (Nop 0) ))
        |> List.filter (Tuple.second >> canToggle)
        |> List.map (\( addr, _ ) -> run (toggle addr c))
        |> List.filter (.status >> (==) Halt)
        |> List.map .acc
```

---

## Reflection

As has become a tradition, at least one puzzle with some form of low-level virtual machine. In this case the instruction set is very simple, so my solution may be a little over engineered. Looking at part 1, I had anticipated the possibility of circular memory address jumping, given that infinite looping was being considered. However, this turned out not to be the case.

For part 2, my first attempt simply ran the program _n_ times, where _n_ is the number of instructions in the program, after attempting to toggle each instruction. This found the solution quickly enough but was not particularly efficient. Refining so that only only running programs containing visited and togglable instructions provided a more efficient approach. It reduced the number of times the program was run from 638 to 91.

It seems a characteristic of the earlier puzzles that brute force approaches still work, inviting, but not requiring, a more efficient process. I like this level of puzzle difficulty in that it can appeal to a wider range of program sophistication levels without punishing simpler approaches too heavily.
