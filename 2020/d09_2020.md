---
follows: data/d09_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2020): Encoding Error

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/9)

{(part1|}

Find the first number in the list (after the preamble) which is not the sum of two of the 25 numbers before it.

{|part1)}

We can determine whether or not a number is present in the sum of some combination of 2 numbers in a list:

```elm {l}
isValid : Int -> List Int -> Bool
isValid x =
    AOC.pairwiseCombinations
        >> List.map (\( a, b ) -> a + b)
        >> List.member x
```

To find the first value that does not match any of the pairwise sums of the preceding _pSize_ (25) values, we can move a window of 25 elements across the list checking for the validity of the number immediately following the window.

```elm {l}
firstInvalid : Int -> List Int -> Int
firstInvalid pSize xs =
    case AOC.splitAt pSize xs of
        ( win, x :: _ ) ->
            if isValid x win then
                firstInvalid pSize (List.drop 1 xs)

            else
                x

        _ ->
            -1 |> Debug.log "No invalid numbers found"
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> firstInvalid 25
```

{(part2|}

Add together the smallest and largest number in a contiguous range that sums to the part 1 answer.

{|part2)}

We need to be able to sequentially add numbers in a list until the sum reaches or exceeds a target value. To save repeatedly summing the list as we accumulate it, we keep track of the accumulated sum.

```elm {l}
sumWhile : Int -> List Int -> ( Int, List Int ) -> ( Int, List Int )
sumWhile target xs ( acc, accXs ) =
    if acc >= target then
        ( acc, accXs )

    else
        case xs of
            [] ->
                ( acc, accXs )

            hd :: tl ->
                sumWhile target tl ( hd + acc, hd :: accXs )
```

For a given list of numbers, find a contiguous list within it that sums to a given target.

```elm {l}
sumTo : Int -> List Int -> List Int
sumTo target xs =
    if xs == [] then
        [] |> Debug.log "No contiguous list found"

    else
        let
            ( sum, contiguous ) =
                sumWhile target xs ( 0, [] )
        in
        if sum == target then
            contiguous

        else
            sumTo target (List.drop 1 xs)
```

Finally, we just sum the smallest and largest number in the contiguous list we have identified.

```elm {l}
endsSum : List Int -> Int
endsSum xs =
    List.sum (List.take 1 xs ++ List.drop (List.length xs - 1) xs)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> sumTo part1
        |> List.sort
        |> endsSum
```

---

## Reflection

A fairly straightforward application of list processing. The naive solution above has some inefficiency in that as we progress through the list of numbers in part two we are repeatedly summing lists that have many numbers in common, but for this puzzle input, it calculates the solution in milliseconds so I haven't tried to optimise.
