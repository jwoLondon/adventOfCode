---
follows: data/d11_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2020): Seating System

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/11).

{(part1|}

Simulate your seating area by applying the seating rules repeatedly until no seats change state. How many seats end up occupied?

{|part1)}

Let's first define the three possible states of a cell and a 2d location type.

```elm {l}
type Cell
    = Chair
    | Occupied
    | Empty


type alias Position =
    ( Int, Int )
```

Given the input is not very sparse and have fixed bounds, we can represent the seats as a grid. We can then parse input into the grid.

```elm {l}
parse : List String -> AOC.Grid Cell
parse =
    let
        toCell chr =
            case chr of
                'L' ->
                    Chair

                _ ->
                    Empty
    in
    List.map (String.toList >> List.map toCell) >> AOC.gFromLists
```

We can define a neighbourhood function that counts the number of neighbours of a cell

```elm {l}
type alias NCounter =
    Position -> AOC.Grid Cell -> Int
```

To perform the cell generation rules we need to be able to count a cell's immediate neighbours:

```elm {l}
occNeighbours : NCounter
occNeighbours ( r, c ) grid =
    [ ( r - 1, c - 1 )
    , ( r - 1, c )
    , ( r - 1, c + 1 )
    , ( r, c - 1 )
    , ( r, c + 1 )
    , ( r + 1, c - 1 )
    , ( r + 1, c )
    , ( r + 1, c + 1 )
    ]
        |> List.map (\pos -> AOC.gGet pos grid)
        |> List.filterMap identity
        |> List.filter ((==) Occupied)
        |> List.length
```

The core reproduction rule determines whether a chair is to be occupied depending on its current occupation and those of its immediate neighbours.

```elm {l}
rule : Cell -> Int -> Int -> Cell
rule status n maxNeighbours =
    if status == Empty then
        Empty

    else if (status == Chair && n == 0) || (status == Occupied && n < maxNeighbours) then
        Occupied

    else
        Chair
```

which allows us to decide whether a cell will be be occupied in the next generation:

```elm {l}
nextCell : NCounter -> Position -> AOC.Grid Cell -> Int -> Cell
nextCell counter pos grid maxNeighbours =
    case AOC.gGet pos grid of
        Just c ->
            rule c (counter pos grid) maxNeighbours

        Nothing ->
            Empty
```

which we can apply to all cells and their immediate neighbours on each generation:

```elm {l}
generation : NCounter -> Int -> AOC.Grid Cell -> AOC.Grid Cell
generation counter maxNeighbours grid =
    AOC.gMapWithLocation (\pos _ -> nextCell counter pos grid maxNeighbours) grid
```

As we are looking for stability we can repeatedly generate a new grid until the new one matches the previous:

```elm {l}
firstRepeat : NCounter -> Int -> AOC.Grid Cell -> AOC.Grid Cell
firstRepeat counter maxNeighbours grid =
    let
        nextGrid =
            grid |> generation counter maxNeighbours
    in
    if grid == nextGrid then
        grid

    else
        firstRepeat counter maxNeighbours nextGrid
```

When we reach stability we just count the number of occupied chairs.

```elm {l}
countOccupied : AOC.Grid Cell -> Int
countOccupied =
    AOC.gToList >> List.filter ((==) Occupied) >> List.length
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> firstRepeat occNeighbours 4
        |> countOccupied
```

---

{(part2|}

Given the new visibility method and the rule change for occupied seats becoming empty, once equilibrium is reached, how many seats end up occupied?

{|part2)}

There are only two differences for part 2, namely a different neighbourhood function that extends rays in the 8 queen's-case directions until it finds an occupied/unoccupied chair, and a neighbour tolerance of 5 rather than 4.

```elm {l}
occVisible : NCounter
occVisible pos grid =
    let
        profile k ( r, c ) ( vR, vC ) =
            case AOC.gGet ( r + k * vR, c + k * vC ) grid of
                Just Empty ->
                    profile (k + 1) ( r, c ) ( vR, vC )

                Just Occupied ->
                    True

                _ ->
                    False
    in
    [ ( -1, -1 ), ( -1, 0 ), ( -1, 1 ), ( 0, -1 ), ( 0, 1 ), ( 1, -1 ), ( 1, 0 ), ( 1, 1 ) ]
        |> List.map (profile 1 pos)
        |> List.filter identity
        |> List.length
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> firstRepeat occVisible 5
        |> countOccupied
```

---

## Reflection

AoC has managed to find yet another variant of a cellular automata puzzle, this time with a restricted set of cells and an extended neighbourhood search. My initial attempt used a set intersection approach (as [day 24, 2019](../2019/d24_2019.md)) that avoids using a grid data structure. I thought this would be useful as there was a limited set of chairs rather than a full grid. While this worked for part 1, it was slow as the chairs almost filled a large grid. For part 2, it was too slow with the extended neighbourhood search. The trouble with puzzles that look for repeating cycles like this one is that when the core algorithm is slow it is hard to predict how many iterations are needed and therefore whether the solution is feasible.

In the end I refactored to use a more conventional grid approach, which was orders of magnitude faster.

---

## Testing

For testing and debugging, we can display a grid after any number of generations.

{(fixed|} ^^^elm m=(display (parse testInput))^^^ {|fixed)}

```elm {l}
display : AOC.Grid Cell -> String
display =
    let
        fromCell cell =
            case cell of
                Chair ->
                    'L'

                Occupied ->
                    'â–ˆ'

                Empty ->
                    '.'
    in
    AOC.gMap fromCell
        >> AOC.gToLists
        >> List.map String.fromList
        >> List.intersperse "\n"
        >> String.concat
```
