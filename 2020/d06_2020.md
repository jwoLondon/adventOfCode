---
follows: data/d06_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2020): Custom Customs

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/6):

{( part1|}

As your flight approaches the regional airport where you'll switch to a much larger plane, customs declaration forms are distributed to the passengers.

The form asks a series of 26 yes-or-no questions marked a through z. All you need to do is identify the questions for **which anyone in your group** answers "yes". Since your group is just you, this doesn't take very long.

However, the person sitting next to you seems to be experiencing a language barrier and asks if you can help. For each of the people in their group, you write down the questions for which they answer "yes", one per line. For example:

```txt
abcx
abcy
abcz
```

In this group, there are **6** questions to which anyone answered "yes": a, b, c, x, y, and z. (Duplicate answers to the same question don't count extra; each question counts at most once.)

Another group asks for your help, then another, and eventually you've collected answers from every group on the plane (your puzzle input). Each group's answers are separated by a blank line, and within each group, each person's answers are on a single line. For example:

```txt
abc

a
b
c

ab
ac

a
a
a
a

b
```

This list represents answers from five groups:

- The first group contains one person who answered "yes" to 3 questions: a, b, and c.
- The second group contains three people; combined, they answered "yes" to 3 questions: a, b, and c.
- The third group contains two people; combined, they answered "yes" to 3 questions: a, b, and c.
- The fourth group contains four people; combined, they answered "yes" to only 1 question, a.
- The last group contains one person who answered "yes" to only 1 question, b.

In this example, the sum of these counts is 3 + 3 + 3 + 1 + 1 = **11**.

For each group, count the number of questions to which anyone answered "yes". **What is the sum of those counts?**

{| part1 )}

## Approach

We need to parse the input so that we can store each set of question responses within the group to which they belong.

```elm {l}
type alias Responses =
    List (List (Set Char))
```

```elm {l}
parse : String -> Responses
parse =
    String.split "\n\n"
        >> List.map (String.lines >> List.map (String.toList >> Set.fromList))
```

We can then simply accumulate the sizes of the sets of all group responses, taking care to treat each group separately.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.map (AOC.unions >> Set.size)
        |> List.sum
```

{( part2 |}

As you finish the last group's customs declaration, you notice that you misread one word in the instructions:

You don't need to identify the questions to which **anyone** answered "yes"; you need to identify the questions to which **everyone** answered "yes"!

Using the same example as above:

```txt
abc

a
b
c

ab
ac

a
a
a
a

b
```

This list represents answers from five groups:

- In the first group, everyone (all 1 person) answered "yes" to 3 questions: a, b, and c.
- In the second group, there is **no** question to which everyone answered "yes".
- In the third group, everyone answered yes to only 1 question, a. Since some people did not answer "yes" to b or c, - they don't count.
- In the fourth group, everyone answered yes to only 1 question, a.
- In the fifth group, everyone (all 1 person) answered "yes" to 1 question, b.

In this example, the sum of these counts is 3 + 0 + 1 + 1 + 1 = **6**.

For each group, count the number of questions to which everyone answered "yes". **What is the sum of those counts?**

{| part2 )}

For each group we need to know the group's collection of responses (which is what we counted in part 1) and then find its intersection with each individual set of responses. That intersection will contain only responses common to in every group member.

```elm {l r}
part2 : Int
part2 =
    let
        answers =
            parse puzzleInput

        groupAnswers =
            List.map AOC.unions answers

        numCommonAnswers group =
            List.foldl Set.intersect group >> Set.size
    in
    answers
        |> List.map2 numCommonAnswers groupAnswers
        |> List.sum
```

---

## Reflection

This was a puzzle well suited to a functional approach with bulk set operations. Care was needed to ensure individual and group responses were assembled appropriately, but ultimately, the solution a simple and concisely expressed one.

A minor complication for doing this in Elm (as opposed to Haskell, for example) is that Elm does not permit sets of sets, so we have to maintain groups as lists rather than sets. I added a generic `unions` function to the [AOC utilities module](../examples/aocExamples.md#unions), equivalent to Haskell's, for combining a list of sets, but this is just a syntactic sugar for simple union fold.
