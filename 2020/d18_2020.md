---
follows: data/d18_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 18 (2020): Operation Order

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/18).

{(part1|}

Evaluate the expression on each line of the homework; what is the sum of the resulting values?

{|part1)}

As is common when parsing complex input we can represent it with an [abstract syntax tree](https://en.wikipedia.org/wiki/Abstract_syntax_tree) (AST). In this case we need only store numeric literals or one of three binary operators:

```elm {l}
type Expr
    = Num Int
    | Add Expr Expr
    | Sub Expr Expr
    | Mul Expr Expr
```

We can turn an expression into a number by evaluating it:

```elm {l}
eval : Expr -> Int
eval expression =
    case expression of
        Num n ->
            n

        Add e1 e2 ->
            eval e1 + eval e2

        Sub e1 e2 ->
            eval e1 - eval e2

        Mul e1 e2 ->
            eval e1 * eval e2
```

For each of those three operators we define the operation itself, the text symbol to parse and its _power_, which can be used to weight operator precedence.

```elm {l}
type alias Operator =
    { op : Expr -> Expr -> Expr
    , symbol : String
    , power : Int
    }
```

We can define the power of each operator, which for part 1 is equal for all three of them:

```elm {l}
opPower : String -> Int
opPower =
    always 1
```

The Elm parser uses [parser pipelines](https://package.elm-lang.org/packages/elm/parser/latest/#parser-pipelines) that determine how consumption of input is handled, and in particular `|.` that consumes something but does not store it (equivalent to ungrouped patterns in regex) and `|=` which parses and stores the result (like a group in regex).

As we parse an expression we consume whitespace until we come to either a number or a parenthesis. At which point we are ready to continue parsing an operator if there is any more content to parse. An open parenthesis should signal the start of sub AST and the parser should succeed if matched by a closing parenthesis after the sub expression has been parsed via a lazy recursive call. The result is added to the AST.

```elm {l}
expr : (String -> Int) -> Int -> Parser Expr
expr precedence prevPower =
    let
        parenthesis =
            Parser.succeed identity
                |. Parser.symbol "("
                |= Parser.lazy (\_ -> expr precedence 0)
                |. Parser.symbol ")"
    in
    Parser.succeed identity
        |. Parser.spaces
        |= Parser.oneOf [ Parser.succeed Num |= Parser.int, parenthesis ]
        |> Parser.andThen (\leftExpr -> Parser.loop ( prevPower, leftExpr ) (operators precedence))
        |> Parser.map (\( _, exp ) -> exp)
```

When we parse for operators we look for one of the three operator symbols and possibly add it to the AST depending on its precedence. We need to compare each operator's power with the current power and if the new operator is of lower power, we stop parsing and backtrack to the left expression.

```elm {l}
operators : (String -> Int) -> ( Int, Expr ) -> Parser (Parser.Step ( Int, Expr ) ( Int, Expr ))
operators precedence ( currentPower, leftExpr ) =
    let
        checkOperator p =
            if p > currentPower then
                Parser.commit ()

            else
                Parser.problem ""

        operator { op, symbol, power } =
            Parser.backtrackable <|
                Parser.succeed (op leftExpr)
                    |. Parser.symbol symbol
                    |. checkOperator power
                    |. Parser.spaces
                    |= Parser.lazy (\_ -> expr precedence power)
    in
    Parser.succeed identity
        |. Parser.spaces
        |= Parser.oneOf
            [ Parser.succeed (\exp -> Parser.Loop ( 0, exp ))
                |= Parser.oneOf
                    [ operator (Operator Add "+" (precedence "+"))
                    , operator (Operator Sub "-" (precedence "-"))
                    , operator (Operator Mul "*" (precedence "*"))
                    ]
            , Parser.succeed ()
                |> Parser.map (\_ -> Parser.Done ( currentPower, leftExpr ))
            ]
```

We can now run the parser to build the AST, traverse it evaluating the results and finally sum them.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> List.map (Parser.run (expr opPower 0) >> Result.withDefault (Num -1) >> eval)
        |> List.sum
```

{(part2|}

What do you get if you add up the results of evaluating the homework problems using these new rules?

{|part2)}

As we have parameterised the operator precedence, we just set add and subtract to have a higher preference before parsing.

```elm {l}
opPower2 : String -> Int
opPower2 symbol =
    case symbol of
        "+" ->
            2

        "-" ->
            2

        "*" ->
            1

        _ ->
            1 |> Debug.log "Unexpected operator symbol"
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.map (Parser.run (expr opPower2 0) >> Result.withDefault (Num -1) >> eval)
        |> List.sum
```

---

## Reflection

My first attempt at this, which succeeded for part 1 was to write my own parser from scratch. Because of the simple left order precedence that was independent of operator, this was reasonably straightforward. However, part two required encoding operator precedence and my simple parser was really equipped to do this.

Elm has a very flexible [parser package](https://package.elm-lang.org/packages/elm/parser/latest/Parser), but my lack of familiarity with it meant I was reluctant to use it. However, the task is one to which it is ideally suited, so scrapped my part 1 approach and rewrote it to use the Elm parser. It creates a simple [Pratt Parser](https://en.wikipedia.org/wiki/Operator-precedence_parser#Pratt_parsing) and is based largely on this [helpful tutorial](https://medium.com/@pogiaron/writing-a-calculator-with-pratt-parsing-in-elm-a9c9dc53ca7e) from Áron Pogátsa. So while I can't claim much of a direct contribution for this solution, it has been a very useful learning experience, which no doubt Eric approves of.
