---
follows: data/d03_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2020): Toboggan Trajectory

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/3):

{( part1|}

With the toboggan login problems resolved, you set off toward the airport. While travel by toboggan might be easy, it's certainly not safe: there's very minimal steering and the area is covered in trees. You'll need to see which angles will take you near the fewest trees.

Due to the local geology, trees in this area only grow on exact integer coordinates in a grid. You make a map (your puzzle input) of the open squares (.) and trees (#) you can see. For example:

```txt
..##.......
#...#...#..
.#....#..#.
..#.#...#.#
.#...##..#.
..#.##.....
.#.#.#....#
.#........#
#.##...#...
#...##....#
.#..#...#.#
```

These aren't the only trees, though; due to something you read about once involving arboreal genetics and biome stability, the same pattern repeats to the right many times:

```txt
..##.........##.........##.........##.........##.........##.......  --->
#...#...#..#...#...#..#...#...#..#...#...#..#...#...#..#...#...#..
.#....#..#..#....#..#..#....#..#..#....#..#..#....#..#..#....#..#.
..#.#...#.#..#.#...#.#..#.#...#.#..#.#...#.#..#.#...#.#..#.#...#.#
.#...##..#..#...##..#..#...##..#..#...##..#..#...##..#..#...##..#.
..#.##.......#.##.......#.##.......#.##.......#.##.......#.##.....  --->
.#.#.#....#.#.#.#....#.#.#.#....#.#.#.#....#.#.#.#....#.#.#.#....#
.#........#.#........#.#........#.#........#.#........#.#........#
#.##...#...#.##...#...#.##...#...#.##...#...#.##...#...#.##...#...
#...##....##...##....##...##....##...##....##...##....##...##....#
.#..#...#.#.#..#...#.#.#..#...#.#.#..#...#.#.#..#...#.#.#..#...#.#  --->
```

You start on the open square (.) in the top-left corner and need to reach the bottom (below the bottom-most row on your map).

The toboggan can only follow a few specific slopes (you opted for a cheaper model that prefers rational numbers); start by **counting all the trees** you would encounter for the slope **right 3, down 1**:

From your starting position at the top-left, check the position that is right 3 and down 1. Then, check the position that is right 3 and down 1 from there, and so on until you go past the bottom of the map.

The locations you'd check in the above example are marked here with **O** where there was an open square and **X** where there was a tree:

```txt
..##.........##.........##.........##.........##.........##.......  --->
#..O#...#..#...#...#..#...#...#..#...#...#..#...#...#..#...#...#..
.#....X..#..#....#..#..#....#..#..#....#..#..#....#..#..#....#..#.
..#.#...#O#..#.#...#.#..#.#...#.#..#.#...#.#..#.#...#.#..#.#...#.#
.#...##..#..X...##..#..#...##..#..#...##..#..#...##..#..#...##..#.
..#.##.......#.X#.......#.##.......#.##.......#.##.......#.##.....  --->
.#.#.#....#.#.#.#.O..#.#.#.#....#.#.#.#....#.#.#.#....#.#.#.#....#
.#........#.#........X.#........#.#........#.#........#.#........#
#.##...#...#.##...#...#.X#...#...#.##...#...#.##...#...#.##...#...
#...##....##...##....##...#X....##...##....##...##....##...##....#
.#..#...#.#.#..#...#.#.#..#...X.#.#..#...#.#.#..#...#.#.#..#...#.#  --->
```

In this example, traversing the map using this slope would cause you to encounter **7** trees.

Starting at the top-left corner of your map and following a slope of right 3 and down 1,**how many trees would you encounter?**

{| part1 )}

## Approach

To represent the tiled woodland, we can represent each horizontal strip of trees and gaps with a rotatable deque and therefore each tile as a list of rotatable strips.

```elm {l}
type alias Tile =
    List (Deque Int)
```

By converting the input into lists of 1s and 0s, we make tree counting simpler.

```elm {l}
parse : List String -> Tile
parse =
    let
        treeToNum c =
            if c == '#' then
                1

            else
                0
    in
    List.map (String.toList >> List.map treeToNum >> Deque.fromList)
```

To traverse through the woods, proceed through each strip in a tile, rotating each by _slope_ cells more than the previous one (3 in this case).

```elm {l}
traverse : Int -> Tile -> Int
traverse slope tile =
    let
        isTree offset =
            AOC.rotateDeque offset >> Deque.first >> Maybe.withDefault 0

        offsets =
            List.map ((*) slope) (List.range 0 (List.length tile - 1))
    in
    List.map2 isTree offsets tile
        |> List.sum
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> traverse 3
```

{( part2 |}

Time to check the rest of the slopes - you need to minimize the probability of a sudden arboreal stop, after all.

Determine the number of trees you would encounter if, for each of the following slopes, you start at the top-left corner and traverse the map all the way to the bottom:

- Right 1, down 1.
- Right 3, down 1. (This is the slope you already checked.)
- Right 5, down 1.
- Right 7, down 1.
- Right 1, down 2.

In the above example, these slopes would find 2, 7, 3, 4, and 2 tree(s) respectively; multiplied together, these produce the answer **336**.

**What do you get if you multiply together the number of trees encountered on each of the listed slopes?**

{| part2 )}

For all slopes that involve moving down 1 strip, we already have functions to count the trees from part 1. For the last case moving down 2 strips at a time we can remove alternate strips from the tile before traversing it.

```elm {l}
removeAlternate : List a -> List a
removeAlternate =
    AOC.groupsOf 2 >> List.concatMap (List.take 1)
```

```elm {l r}
part2 : Int
part2 =
    let
        t =
            parse puzzleInput
    in
    traverse 1 t * traverse 3 t * traverse 5 t * traverse 7 t * traverse 1 (removeAlternate t)
```

---

## Reflection

The key to this solution was representing each horizontal transect through the woods as a rotatable list. It was easy to implement with the utility function `AOC.rotateDeque` from previous years.

My initial attempt at a part 1 answer was incorrect as I misremembered the slope as 2 across not 3 across (I think I had a knight's move in my head).
