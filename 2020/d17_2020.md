---
follows: data/d17_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 17 (2020): Conway Cubes

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/17).

{(part1|}

Starting with your given initial configuration, simulate six cycles. **How many cubes are left in the active state after the sixth cycle?**

{|part1)}

With 3 or more dimensions the likelihood of sparse volumes is high, so following a similar pattern to [Day 24, 2019](../2019/d24_2019.md) we can store live cells as a set rather than a grid of possible spaces.

```elm {l}
type alias Point =
    List Int


type alias LiveCells =
    Set Point
```

Parsing the input data involves identifying the character position of each `#` and adding its 2 dimensional position (all with higher dimensions=0).

```elm {l}
parse : Int -> List String -> Set Point
parse d input =
    let
        rows =
            List.indexedMap (\row cs -> ( -row, cs )) input

        cols ( row, cs ) =
            List.indexedMap (\col c -> ( ( col, row ), c )) (String.toList cs)
    in
    List.concatMap cols rows
        |> List.filter (\( _, c ) -> c == '#')
        |> List.map Tuple.first
        |> List.map (\( r, c ) -> [ r, c ] ++ List.repeat (d - 2) 0)
        |> Set.fromList
```

We will need to both count a cell's neighbours and use the set of neighbours as candidates for possible change in each generation. For higher dimensions we can build a list of all neighbouring cells by transforming a point of dimension d into line of dimension d+1:

```elm {l}
addDimension : List Point -> List Point
addDimension =
    List.concatMap (\p -> List.map (\p2 -> p2 :: p) [ -1, 0, 1 ])


offsetsNd : Int -> Set Point
offsetsNd n =
    AOC.iterate (n - 1) [ [ -1 ], [ 0 ], [ 1 ] ] addDimension
        |> Set.fromList
```

Finding a cell's neighbours just involves adding the offsets to a cell's position, removing the cell's position from the set of neighbours. By parameterising the set of offsets, we can provide a general n-dimensional case.

```elm {l}
neighbours : Set Point -> Point -> Set Point
neighbours offsets p =
    Set.map (List.map2 (+) p) offsets
        |> Set.remove p
```

The reproduction rule is the same as Conway's original Game of Life, despite being in more dimensions.

```elm {l}
rule : Bool -> Int -> Bool
rule isAlive n =
    n == 3 || isAlive && n == 2
```

To find whether a cell lives we apply the reproduction rule:

```elm {l}
live : Set Point -> LiveCells -> Point -> Bool
live offsets cells p =
    rule (Set.member p cells) (Set.intersect (neighbours offsets p) cells |> Set.size)
```

For each subsequent generation we simply apply the reproduction rule to all cells and their immediate neighbourhood.

```elm {l}
generation : Set Point -> LiveCells -> LiveCells
generation offsets cells =
    cells
        |> Set.foldl (neighbours offsets >> Set.union) Set.empty
        |> Set.filter (live offsets cells)
```

We initialise the system by placing the 2d slice of parsed input into the higher dimensional space, then iterate for 6 generations:

```elm {l r}
part1 : Int
part1 =
    AOC.iterate 6 (parse 3 puzzleInput) (generation (offsetsNd 3))
        |> Set.size
```

{(part2|}

Starting with your given initial configuration, simulate six cycles in a 4-dimensional space. **How many cubes are left in the active state after the sixth cycle?**

{|part2)}

As we have a general n-dimensional framework, it is a trivial case to add an additional dimension for the second part.

```elm {l r}
part2 : Int
part2 =
    AOC.iterate 6 (parse 4 puzzleInput) (generation (offsetsNd 4))
        |> Set.size
```

---

## Reflection

This was an easy puzzle for me as I had already used a cellular automata pattern for sparse cells on [Day 24, 2019](../2019/d24_2019.md). There was some minor head scratching involved in generating multi-dimensional neighbours, but nothing too taxing.

I enjoyed refactoring to generalise to any dimensions while keeping the code nice and functional. There is possibly some optimisation to be had by exploiting the fact that with a 2d input, the space will always be symmetrical about that plane.
