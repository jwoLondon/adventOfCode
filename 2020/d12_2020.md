---
follows: data/d12_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 12 (2020): Rain Risk

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/12).

{(part1|}

Figure out where the navigation instructions lead. What is the Manhattan distance between that location and the ship's starting position?

{|part1)}

A ship will have a position and a direction it faces. We can represent both as vectors rather than angle, keeping the movement specification using Cartesian rather than polar coordinates.

```elm {l}
type alias PVector =
    ( Int, Int )


type alias Ship =
    ( PVector, PVector )


type alias Trans =
    Int -> Ship -> Ship
```

Instructions to move the ship are all conformal affine transformations, additionally simplified by the fact all rotations are multiples of 90 degrees.

```elm {l}
translate : PVector -> PVector -> PVector
translate ( x1, y1 ) ( x2, y2 ) =
    ( x1 + x2, y1 + y2 )


scale : Int -> PVector -> PVector
scale sc ( x, y ) =
    ( sc * x, sc * y )


rotate : Int -> PVector -> PVector
rotate dir ( x, y ) =
    case modBy 360 dir of
        90 ->
            ( -y, x )

        180 ->
            ( -x, -y )

        270 ->
            ( y, -x )

        _ ->
            ( x, y )
```

Because we need only make one simple pass through the input data we can combine parsing with transforming the ship's location and direction as we go.

Because part 2 is similar to part 1, but with minor variations in the rotation and forward instructions, these are supplied as parameters.

```elm {l}
parse : Trans -> Trans -> Ship -> List String -> Ship
parse rotateShip forwardShip initShip =
    let
        parseInstr instr =
            case String.uncons instr |> Maybe.withDefault ( 'F', "" ) |> Tuple.mapSecond AOC.toInt of
                ( 'N', n ) ->
                    (translate >> Tuple.mapSecond) ( 0, n )

                ( 'S', n ) ->
                    (translate >> Tuple.mapSecond) ( 0, -n )

                ( 'E', n ) ->
                    (translate >> Tuple.mapSecond) ( n, 0 )

                ( 'W', n ) ->
                    (translate >> Tuple.mapSecond) ( -n, 0 )

                ( 'L', n ) ->
                    rotateShip n

                ( 'R', n ) ->
                    rotateShip -n

                ( 'F', n ) ->
                    forwardShip n

                _ ->
                    identity |> Debug.log "Unknown instruction"
    in
    List.foldl parseInstr initShip
```

After parsing the instructions we can report the ship's Manhattan distance from the origin.

```elm {l}
magnitude : PVector -> Int
magnitude ( x, y ) =
    abs x + abs y
```

```elm {l r}
part1 : Int
part1 =
    let
        rotateShip =
            rotate >> Tuple.mapFirst

        forwardShip n ship =
            Tuple.mapSecond (\v -> Tuple.first ship |> scale n |> translate v) ship
    in
    puzzleInput
        |> parse rotateShip forwardShip ( ( 1, 0 ), ( 0, 0 ) )
        |> Tuple.second
        |> magnitude
```

---

{(part2|}

Figure out where the navigation instructions _actually_ lead. What is the Manhattan distance between that location and the ship's starting position?

{|part2)}

Despite the different way the question for part 2 is specified, it is essentially a minor variation of the first part. We can consider the first part as a ship and waypoint where the direction vector is the 'ship' and the ship always stays 1 unit away from the waypoint. So to complete part 2 we just need to make some minor modifications to the rotation and forward rules to account for the ability to separate ship and waypoint.

```elm {l r}
part2 : Int
part2 =
    let
        rotateShip =
            rotate >> Tuple.mapSecond

        forwardShip n ship =
            Tuple.mapFirst (\v -> Tuple.second ship |> scale n |> translate v) ship
    in
    puzzleInput
        |> parse rotateShip forwardShip ( ( 0, 0 ), ( 10, 1 ) )
        |> Tuple.first
        |> magnitude
```

---

## Reflection

While relatively simple, I found this puzzle satisfying in uncovering the simplifications that can be made to the solution. My first solution, largely for speed of coding, used conventional trigonometry to rotate the ship/marker.

The puzzle specification doesn't guarantee that all rotations are multiples of 90 degrees, but inspection of the input suggested this would be a reasonable assumption. This greatly simplifies the rotation as rotation vectors will contain 1s, 0s and -1s only.

The key insight came in recognising that transformations are all linear and therefore can be combined, making it more obvious that the 'waypoint' of part 2 was the `ship` of part 1.

The puzzle got me thinking about whether this would be possible to solve with integer arithmetic if rotations are arbitrary integer values. It's be possible to [define trig functions in Manhattan space](http://sites.science.oregonstate.edu/~tevian/physics/taxicab/taxicab.pdf). Sine and cosine functions can be created with modulo arithmetic, so perhaps there is a more general solution.
