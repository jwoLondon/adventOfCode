---
follows: data/d21_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 21 (2020): Allergen Assessment

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/21)

{(part1|}

Determine which ingredients cannot possibly contain any of the allergens in your list. **How many times do any of those ingredients appear?**

{|part1)}

To keep code clear, distinguishing ingredients, from allergens from foods is helpful. Because all ingredient and allergen names are unique and the problem doesn't require consideration of order, a set representation of both is appropriate.

```elm {l}
type alias Ingredient =
    String


type alias Allergen =
    String


type alias Food =
    { ing : Set Ingredient, alg : Set Allergen }
```

Parse input into a tuple of ingredients and allergens for each food:

```elm {l}
parse : List String -> List Food
parse =
    let
        parseLine line =
            case
                AOC.submatches "(.+) \\(contains (.+)\\)" line
                    |> List.filterMap identity
            of
                [ ingredients, allergens ] ->
                    { ing = String.split " " ingredients |> Set.fromList
                    , alg = String.split "," allergens |> List.map String.trim |> Set.fromList
                    }

                _ ->
                    { ing = Set.empty, alg = Set.empty } |> Debug.log "bad input"
    in
    List.map parseLine
```

From this we can extract the set of all allergens to consider:

```elm {l}
allAllergens : List Food -> List Allergen
allAllergens =
    List.map .alg >> AOC.unions >> Set.toList
```

We are told that an allergen is associated with one and only one ingredient, and that if an ingredient has a named allergen, that allergen must be named in every food in which it occurs. From this we know that for a given allergen, the ingredient that contains it will appear in the intersection of all the food ingredients where the food lists the allergen:

```elm {l}
allergenCandidates : List Food -> Allergen -> Set Ingredient
allergenCandidates foods allergen =
    List.filter (.alg >> Set.member allergen) foods
        |> List.map .ing
        |> AOC.intersections
```

The set of all allocated ingredients is therefore the union of all candidate sets:

```elm {l}
allocatedIngredients : List Food -> Set Ingredient
allocatedIngredients foods =
    foods
        |> allAllergens
        |> List.map (allergenCandidates foods)
        |> AOC.unions
```

We are asked to count the number of instances of unallocated ingredients, which we can do by taking the set difference of each food's ingredient list and the set of allocated ingredients.

```elm {l r}
part1 : Int
part1 =
    let
        foods =
            parse puzzleInput
    in
    foods
        |> List.map (.ing >> AOC.flip Set.diff (allocatedIngredients foods) >> Set.size)
        |> List.sum
```

{(part2|}

What is your canonical dangerous ingredient list?

{|part2)}

This is a simple extension of the part 1 process where we progressively remove allocated ingredients from the list of allergens where an allergen has only one candidate ingredient. This is similar to the process applied in [day 16](d16_2020.md) and the process of set elimination has been abstracted and placed in `aoc.elm` as [setEliminate](../examples/aocExamples.md#seteliminate).

```elm {l}
allAllergenCandidates : List Food -> Dict Allergen (Set Ingredient)
allAllergenCandidates foods =
    List.foldl (\a -> Dict.insert a (allergenCandidates foods a)) Dict.empty (allAllergens foods)
```

```elm {l r}
part2 : String
part2 =
    puzzleInput
        |> parse
        |> allAllergenCandidates
        |> AOC.setEliminate
        |> Dict.values
        |> List.intersperse ","
        |> String.concat
```

---

## Reflection

A relatively simple but nicely designed puzzle. What I liked about it was most of the effort was in considering the problem statement and considering how to identify unclassified ingredients, while the coding to implement it was minimal. This could have been an even more interesting problem if the example case in the instructions had not been given.
