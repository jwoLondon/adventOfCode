---
follows: data/d16_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 16 (2020): Ticket Translation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/16)

{(part1|}

Consider the validity of the nearby tickets you scanned. What is your ticket scanning error rate?

{|part1)}

All ticket validation rules have a pair of ranges to consider so these can stored in a pair of tuples and the collection of rules in a dictionary referenced by their field name.

```elm {l}
type alias Rule =
    ( ( Int, Int ), ( Int, Int ) )


type alias Rules =
    Dict String Rule


type alias Ticket =
    List Int
```

Parsing of tickets is simple, and the rules can be parsed with a simple regex.

```elm {l}
parseRules : List String -> Rules
parseRules =
    let
        parseLine line =
            case
                AOC.submatches "(.+): (\\d+)-(\\d+) or (\\d+)-(\\d+)" line
                    |> List.filterMap identity
            of
                [ field, min1, max1, min2, max2 ] ->
                    ( field, ( ( AOC.toInt min1, AOC.toInt max1 ), ( AOC.toInt min2, AOC.toInt max2 ) ) )

                _ ->
                    ( "", ( ( 0, 0 ), ( 0, 0 ) ) ) |> Debug.log "bad input"
    in
    List.map parseLine >> Dict.fromList


parseTicket : String -> Ticket
parseTicket =
    String.split "," >> List.map AOC.toInt


parseTickets : List String -> List Ticket
parseTickets =
    List.map parseTicket
```

To find invalid tickets we pass each ticket's numeric values through the full set of rules looking for values that fail every single rule.

```elm {l}
invalidFields : Rules -> Ticket -> List Int
invalidFields rules =
    let
        ranges =
            Dict.values rules

        applyRules f =
            List.map
                (\( ( min1, max1 ), ( min2, max2 ) ) ->
                    not ((f >= min1 && f <= max1) || ((f >= min2) && (f <= max2)))
                )
                ranges
                |> List.all identity
    in
    List.filter applyRules
```

```elm {l r}
part1 : Int
part1 =
    let
        rules =
            parseRules puzzleInput1

        tickets =
            parseTickets puzzleInput3
    in
    List.concatMap (invalidFields rules) tickets
        |> List.sum
```

{(part2|}

Once you work out which field is which, look for the six fields on your ticket that start with the word departure. What do you get if you multiply those six values together?

{|part2)}

As we've already identified invalid ticket values we can filter valid tickets as those which contain no invalid numbers.

```elm {l}
valid : Rules -> List Ticket -> List Ticket
valid rules =
    List.filter (\ticket -> invalidFields rules ticket == [])
```

Given a number, which named rules does it satisfy?

```elm {l}
satisfiedRules : Int -> Rules -> Set String
satisfiedRules n rules =
    Dict.filter
        (\_ ( ( min1, max1 ), ( min2, max2 ) ) ->
            (n >= min1 && n <= max1) || ((n >= min2) && (n <= max2))
        )
        rules
        |> Dict.keys
        |> Set.fromList
```

We can then find the set of satisfied rules for a given field position in all valid tickets by finding the set intersection of each tickets valid rules at that position.

```elm {l}
satisfiedAtPos : Int -> Rules -> List Ticket -> Set String
satisfiedAtPos pos rules tickets =
    let
        valsAtPos =
            List.map (List.drop pos >> List.head >> Maybe.withDefault 0) tickets
    in
    List.foldl
        (\n rs ->
            Set.intersect rs (satisfiedRules n rules)
        )
        (Dict.keys rules |> Set.fromList)
        valsAtPos
```

This allows us to build a dictionary that tells us which rules satisfy all the ticket values at for each position on the ticket:

```elm {l}
rulesAtPositions : Rules -> List Ticket -> Dict Int (Set String)
rulesAtPositions rules tickets =
    let
        numFields =
            List.head tickets |> Maybe.withDefault [] |> List.length
    in
    List.map (\pos -> ( pos, satisfiedAtPos pos rules tickets )) (List.range 0 (numFields - 1))
        |> Dict.fromList
```

This isn't yet enough to identify each field's position, as some positions have more than one candidate field. However at least one has only one candidate, so we can eliminate that from all the other candidates. If we apply this iteratively until all positions have just one candidate field, we have our complete ticket description.

```elm {l}
solve : Dict Int (Set String) -> Dict String Int
solve dict =
    let
        maxCandidates =
            Dict.values dict
                |> List.map Set.size
                |> List.maximum
                |> Maybe.withDefault 0
    in
    if maxCandidates <= 1 then
        dict
            |> Dict.toList
            |> List.map (\( pos, fName ) -> ( fName |> Set.toList |> List.head |> Maybe.withDefault "", pos ))
            |> Dict.fromList

    else
        let
            confirmed =
                Dict.values dict
                    |> List.filter (\candidates -> Set.size candidates == 1)
                    |> AOC.unions
        in
        solve
            (Dict.map
                (\_ candidates ->
                    if Set.size candidates == 1 then
                        candidates

                    else
                        Set.diff candidates confirmed
                )
                dict
            )
```

The field positions of all those that start with 'departure':

```elm {l}
depPositions : Dict String Int -> List Int
depPositions =
    Dict.filter (\fName _ -> String.startsWith "departure" fName) >> Dict.values
```

Now that we know the ticket position of all the departure fields, it is a simple matter of finding their values for any valid ticket:

```elm {l}
depVals : Ticket -> List Int -> List Int
depVals ticket =
    List.map (\pos -> List.drop pos ticket |> List.head |> Maybe.withDefault 0)
```

```elm {l r}
part2 : Int
part2 =
    let
        rules =
            parseRules puzzleInput1

        myTicket =
            parseTicket puzzleInput2

        tickets =
            parseTickets puzzleInput3

        validTickets =
            valid rules tickets

        fieldPositions =
            solve (rulesAtPositions rules validTickets)
    in
    fieldPositions
        |> depPositions
        |> depVals myTicket
        |> List.product
```

---

## Reflection

I loved this puzzle. It mostly involves data shaping and validation, which is what I do in the day job. The part 2 problem is easily decomposable into a number of separate transformations making it easier to build the solution incrementally.
