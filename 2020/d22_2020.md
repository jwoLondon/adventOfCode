---
follows: data/d22_2020.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 22 (2020): Crab Combat

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2020/day/22)

{(part1|}

Play the small crab in a game of Combat using the two decks you just dealt. **What is the winning player's score?**

{|part1)}

Because we are circulating cards in the deck, we can represent them in a double ended queue.

```elm {l}
type alias Deck =
    Deque Int
```

Parsing input involves extracting the two sets of cards:

```elm {l}
parse : String -> ( List Int, List Int )
parse input =
    case String.split "\n\n" input of
        [ p1, p2 ] ->
            ( p1 |> String.lines |> List.drop 1 |> List.map AOC.toInt
            , p2 |> String.lines |> List.drop 1 |> List.map AOC.toInt
            )

        _ ->
            ( [], [] ) |> Debug.log "Bad input"
```

The rules of play are pretty straightforward:

```elm {l}
play : Deck -> Deck -> ( Int, Deck )
play deck1 deck2 =
    if Deque.length deck1 == 0 then
        -- Game ends with player 1 running out of cards
        ( 2, deck2 )

    else if Deque.length deck2 == 0 then
        -- Game ends with player 2 running out of cards
        ( 1, deck1 )

    else
        case ( Deque.popFront deck1, Deque.popFront deck2 ) of
            ( ( Just c1, d1 ), ( Just c2, d2 ) ) ->
                if c1 > c2 then
                    -- Player 1 wins this round; play again
                    play (d1 |> Deque.pushBack c1 |> Deque.pushBack c2) d2

                else
                    -- Player 2 wins this round; play again
                    play d1 (d2 |> Deque.pushBack c2 |> Deque.pushBack c1)

            _ ->
                ( 0, Deque.empty ) |> Debug.log "Unexpected empty deck"
```

Once a game has been won, we can calculate its score, and for the record, which player won.

```elm {l}
score : ( Int, Deck ) -> ( Int, Int )
score ( winner, deck ) =
    ( winner
    , deck
        |> Deque.toList
        |> List.reverse
        |> List.indexedMap (\i card -> (i + 1) * card)
        |> List.sum
    )
```

```elm {l r}
part1 : ( Int, Int )
part1 =
    let
        ( d1, d2 ) =
            parse puzzleInput
    in
    play (Deque.fromList d1) (Deque.fromList d2)
        |> score
```

{(part2|}

Defend your honor as Raft Captain by playing the small crab in a game of Recursive Combat using the same two decks as before. **What is the winning player's score?**

{|part2)}

The rules are now a little more elaborate, but the process remains the same. This time we do need to keep track of who wins each round as a sub-game's winner determines which cards are transferred in a parent game. We also need to keep track of the history of decks to avoid infinite recursion.

```elm {l}
type alias History =
    Set (List Int)
```

```elm {l}
play2 : Deck -> History -> Deck -> History -> ( Int, Deck )
play2 deck1 history1 deck2 history2 =
    if Set.member (Deque.toList deck1) history1 || Set.member (Deque.toList deck2) history2 then
        -- We've been here before so player 1 wins by default
        ( 1, deck1 )

    else if Deque.length deck1 == 0 then
        -- Round (or game if this is a top-level round) ends with player 1 running out of cards
        ( 2, deck2 )

    else if Deque.length deck2 == 0 then
        -- Round (or game if this is a top-level round) ends with player 2 running out of cards
        ( 1, deck1 )

    else
        case ( Deque.popFront deck1, Deque.popFront deck2 ) of
            ( ( Just c1, d1 ), ( Just c2, d2 ) ) ->
                if Deque.length d1 < c1 || Deque.length d2 < c2 then
                    -- Normal game
                    if c1 > c2 then
                        play2 (d1 |> Deque.pushBack c1 |> Deque.pushBack c2)
                            (Set.insert (Deque.toList deck1) history1)
                            d2
                            (Set.insert (Deque.toList deck2) history2)

                    else
                        play2 d1
                            (Set.insert (Deque.toList deck1) history1)
                            (d2 |> Deque.pushBack c2 |> Deque.pushBack c1)
                            (Set.insert (Deque.toList deck2) history2)

                else
                -- Recursive game
                if
                    (play2 (Deque.toList d1 |> List.take c1 |> Deque.fromList)
                        Set.empty
                        (Deque.toList d2 |> List.take c2 |> Deque.fromList)
                        Set.empty
                        |> Tuple.first
                    )
                        == 1
                then
                    -- Player 1 won the sub game so they grab the two cards from this game
                    play2 (d1 |> Deque.pushBack c1 |> Deque.pushBack c2)
                        (Set.insert (Deque.toList deck1) history1)
                        d2
                        (Set.insert (Deque.toList deck2) history2)

                else
                    -- Player 2 won the sub game so they grab the two cards from this game
                    play2 d1
                        (Set.insert (Deque.toList deck1) history1)
                        (d2 |> Deque.pushBack c2 |> Deque.pushBack c1)
                        (Set.insert (Deque.toList deck2) history2)

            _ ->
                ( 0, Deque.empty ) |> Debug.log "Unexpected empty deck"
```

As before, once we have a winner we calculate their score from their winning deck.

```elm {l r}
part2 : ( Int, Int )
part2 =
    let
        ( d1, d2 ) =
            parse puzzleInput
    in
    play2 (Deque.fromList d1) Set.empty (Deque.fromList d2) Set.empty
        |> score
```

---

## Reflection

Use of of double ended queues (Deques) made this quite simple one to solve. The main consideration was carefully following the sequence of rules in part 2. Because Elm cannot store sets of Deques (they are not comparable) had to convert the history of decks in part 2 into a list.

I note that "player 1" wins both games with my puzzle input despite the puzzle scenario suggesting a reversal in fortunes for the players in the second part.
