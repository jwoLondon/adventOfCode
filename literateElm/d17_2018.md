---
follows: data/d17_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 17 (2018): Reservoir Research

{( part1|}

You arrive in the year 18. If it weren't for the coat you got in 1018, you would be very cold: the North Pole base hasn't even been constructed.

Rather, it hasn't been constructed **yet**. The Elves are making a little progress, but there's not a lot of liquid water in this climate, so they're getting very dehydrated. Maybe there's more underground?

You scan a two-dimensional vertical slice of the ground nearby and discover that it is mostly **sand** with veins of clay. The scan only provides data with a granularity of **square meters**, but it should be good enough to determine how much water is trapped there. In the scan, x represents the distance to the right, and y represents the distance down. There is also a **spring of water** near the surface at x=500, y=0. The scan identifies **which square meters are clay** (your puzzle input).

For example, suppose your scan shows the following veins of clay:

    x=495, y=2..7
    y=7, x=495..501
    x=501, y=3..7
    x=498, y=2..4
    x=506, y=1..2
    x=498, y=10..13
    x=504, y=10..13
    y=13, x=498..504

Rendering clay as #, sand as ., and the water spring as +, and with x increasing to the right and y increasing downward, this becomes:

    44444455555555
    99999900000000
    45678901234567
    0 ......+.......
    1 ............#.
    2 .#..#.......#.
    3 .#..#..#......
    4 .#..#..#......
    5 .#.....#......
    6 .#.....#......
    7 .#######......
    8 ..............
    9 ..............
    10 ....#.....#...
    11 ....#.....#...
    12 ....#.....#...
    13 ....#######...

The spring of water will produce water **forever**. Water can move through sand, but is blocked by clay. Water **always moves down** when possible, and spreads to the left and right otherwise, filling space that has clay on both sides and falling out otherwise.

For example, if five squares of water are created, they will flow downward until they reach the clay and settle there. Water that has come to rest is shown here as ~, while sand through which water has passed (but which is now dry again) is shown as |:

    ......+.......
    ......|.....#.
    .#..#.|.....#.
    .#..#.|#......
    .#..#.|#......
    .#....|#......
    .#~~~~~#......
    .#######......
    ..............
    ..............
    ....#.....#...
    ....#.....#...
    ....#.....#...
    ....#######...

Two squares of water can't occupy the same location. If another five squares of water are created, they will settle on the first five, filling the clay reservoir a little more:

    ......+.......
    ......|.....#.
    .#..#.|.....#.
    .#..#.|#......
    .#..#.|#......
    .#~~~~~#......
    .#~~~~~#......
    .#######......
    ..............
    ..............
    ....#.....#...
    ....#.....#...
    ....#.....#...
    ....#######...

Water pressure does not apply in this scenario. If another four squares of water are created, they will stay on the right side of the barrier, and no water will reach the left side:

    ......+.......
    ......|.....#.
    .#..#.|.....#.
    .#..#~~#......
    .#..#~~#......
    .#~~~~~#......
    .#~~~~~#......
    .#######......
    ..............
    ..............
    ....#.....#...
    ....#.....#...
    ....#.....#...
    ....#######...

At this point, the top reservoir overflows. While water can reach the tiles above the surface of the water, it cannot settle there, and so the next five squares of water settle like this:

    ......+.......
    ......|.....#.
    .#..#||||...#.
    .#..#~~#|.....
    .#..#~~#|.....
    .#~~~~~#|.....
    .#~~~~~#|.....
    .#######|.....
    ........|.....
    ........|.....
    ....#...|.#...
    ....#...|.#...
    ....#~~~~~#...
    ....#######...

Note especially the leftmost |: the new squares of water can reach this tile, but cannot stop there. Instead, eventually, they all fall to the right and settle in the reservoir below.

After 10 more squares of water, the bottom reservoir is also full:

    ......+.......
    ......|.....#.
    .#..#||||...#.
    .#..#~~#|.....
    .#..#~~#|.....
    .#~~~~~#|.....
    .#~~~~~#|.....
    .#######|.....
    ........|.....
    ........|.....
    ....#~~~~~#...
    ....#~~~~~#...
    ....#~~~~~#...
    ....#######...

Finally, while there is nowhere left for the water to settle, it can reach a few more tiles before overflowing beyond the bottom of the scanned data:

    ......+....... (line not counted: above minimum y value)
    ......|.....#.
    .#..#||||...#.
    .#..#~~#|.....
    .#..#~~#|.....
    .#~~~~~#|.....
    .#~~~~~#|.....
    .#######|.....
    ........|.....
    ...|||||||||..
    ...|#~~~~~#|..
    ...|#~~~~~#|..
    ...|#~~~~~#|..
    ...|#######|..
    ...|.......|.. (line not counted: below maximum y value)
    ...|.......|.. (line not counted: below maximum y value)
    ...|.......|.. (line not counted: below maximum y value)

How many tiles can be reached by the water? **To prevent counting forever**, ignore tiles with a y coordinate smaller than the smallest y coordinate in your scan data or larger than the largest one. Any x coordinate is valid. In this example, the lowest y coordinate given is 1, and the highest is 13, causing the water spring (in row 0) and the water falling off the bottom of the render (in rows 14 through infinity) to be ignored.

So, in the example above, counting both water at rest (~) and other sand tiles the water can hypothetically reach (|), the total number of tiles the water can reach is 57.

**How many tiles can the water reach** within the range of y values in your scan?

{| part1 )}

## Data structures and parsing

We can model the cross-sectional slice through the ground as a grid of values representing sand, clay and water. We give the spring its own location so it can be used to translate the grid coordinates to a local origin.

```elm {l}
type alias Slice =
    Grid Cell


type alias Location =
    ( Int, Int )


type Cell
    = Clay
    | Sand
    | MovingWater
    | StandingWater
    | Spring Location
```

Initially we don't know how large the grid needs to be as it will depend on the extent of the clay strips. So we can parse the input data twice, once to find the spatial extent of the clay, and once to convert the clay strip definitions into discrete grid locations.

Clay strips can either be horizontal (starting with y value) or vertical (starting with an x value), in both cases requiring three numbers, the extent (in either x or y directions) and the offset (in either the y or x direction). Note that the grid needs to have a border wider than the horizontal extent as we need to leave room potentially for water to flow to either side.

```elm {l}
type alias Strip =
    { horizontal : Bool
    , offset : Int
    , start : Int
    , end : Int
    }


parseRange : List String -> Strip
parseRange input =
    let
        stripValues =
            submatches "([x|y])=(\\d+), [x|y]=(\\d+)\\.\\.(\\d+)"
                >> List.filterMap identity

        parseLine line =
            case stripValues line of
                [ dir, offset, start, end ] ->
                    case dir of
                        "x" ->
                            Just (Strip False (toInt offset) (toInt start) (toInt end))

                        "Y" ->
                            Just (Strip True (toInt offset) (toInt start) (toInt end))

                        _ ->
                            Nothing

                _ ->
                    Nothing

        bottom =
            List.foldl
                (\line range ->
                    case parseLine line of
                        Just strip ->
                            if strip.horizontal then
                                Strip True
                                    (max range.offset strip.offset)
                                    (min range.start strip.start)
                                    (max range.end strip.end)

                            else
                                Strip True
                                    (max range.offset strip.end)
                                    (min range.start strip.offset)
                                    (max range.end strip.offset)

                        Nothing ->
                            range
                )
                (Strip True 0 500 500)
                input
    in
    { bottom | start = bottom.start - 2, end = bottom.end + 2 }
```

Once we have the grid dimensions we can parse the input data again, this time inserting the cells containing clay into the grid.

```elm {l}
parse : List String -> Slice
parse input =
    let
        bottom =
            parseRange input

        initSlice =
            gInit (bottom.offset + 1) (bottom.end - bottom.start + 1) Sand
                |> gSet ( 0, 500 - bottom.start ) (Spring ( 0, 500 - bottom.start ))

        stripValues =
            submatches "([x|y])=(\\d+), [x|y]=(\\d+)\\.\\.(\\d+)" >> List.filterMap identity

        parseLine line =
            case stripValues line of
                [ dir, offset, start, end ] ->
                    case dir of
                        "x" ->
                            Just (Strip False (toInt offset) (toInt start) (toInt end))

                        "y" ->
                            Just (Strip True (toInt offset) (toInt start) (toInt end))

                        _ ->
                            Nothing

                _ ->
                    Nothing

        addStrip maybeStrip slice =
            case maybeStrip of
                Just strip ->
                    if strip.horizontal then
                        List.foldl
                            (\c sl ->
                                gSet ( strip.offset, c ) Clay sl
                            )
                            slice
                            (List.range (strip.start - bottom.start) (strip.end - bottom.start))

                    else
                        List.foldl
                            (\r sl ->
                                gSet ( r, strip.offset - bottom.start ) Clay sl
                            )
                            slice
                            (List.range strip.start strip.end)

                Nothing ->
                    slice
    in
    List.foldl (\line sl -> addStrip (parseLine line) sl) initSlice input
```

The outflow location of the spring (originally at (0,500) but now transformed to a local origin) can be found by extracting its coordinates from the first row in the grid.

```elm {l}
springOut : Slice -> Location
springOut =
    gGetRow 0
        >> Maybe.withDefault []
        >> List.map
            (\c ->
                case c of
                    Spring ( row, col ) ->
                        Just ( row + 1, col )

                    _ ->
                        Nothing
            )
        >> List.filterMap identity
        >> List.head
        >> Maybe.withDefault ( -1, -1 )
```

## Filling Algorithm

When downward moving water first encounters a flat surface (either of clay or standing water), the surface can be in one of 4 possible states (for arbitrary widths):

    A               |                              |
        █           |     █     =>     █ ▒ ▒ ▒ ▒ ▒ ▒ ▒ ▒ █
        █ █ █ █ █ █ █ █ █ █            █ █ █ █ █ █ █ █ █ █

    B               |                              |
        █           |           =>     █ | | | | | | | | | |
        █ █ █ █ █ █ █ █ █ █            █ █ █ █ █ █ █ █ █ █ |

    C               |                              |
                    |     █     =>   | | | | | | | | | | █
        █ █ █ █ █ █ █ █ █ █          | █ █ █ █ █ █ █ █ █ █

    D               |                              |
                    |           =>   * | | | | | | | | | | |
        █ █ █ █ █ █ █ █ █ █            █ █ █ █ █ █ █ █ █ █ |

If it is in state A, we can fill the contained basin and test for which of the four states the new surface is in (depending on the cells above the newly filled row).

If it is state B or C, we simply drain off either the left or right and continue flowing down

If it is in state D, we need to drain off both the left and right. This can be managed efficiently by draining to the right and pushing the position marked `*` onto a queue for returning to later.

We can test for these 4 states by extending left and right while the cell below is clay or standing water and the cell to the left/right is sand:

```elm {l}
get : Location -> Slice -> Cell
get loc =
    gGet loc >> Maybe.withDefault Clay


horizontalExtent : Int -> Location -> Slice -> ( Int, Bool )
horizontalExtent dx ( r, c ) slice =
    let
        next =
            get ( r, c + dx ) slice

        down =
            get ( r + 1, c ) slice
    in
    if (down == Clay || down == StandingWater) && (next == Sand || next == MovingWater) then
        horizontalExtent dx ( r, c + dx ) slice

    else if next == Clay then
        ( c, True )

    else
        ( c, False )
```

We can now proceed to add water to the slice, always moving down until it encounters a horizontal blocking surface.

We have two possible terminating conditions for modelling the downward flow:

- the water has reached the bottom of the grid
- the water encounters some flowing water (cell has already been visited).

```elm {l}
addWater : Location -> Fifo Location -> Slice -> ( Slice, Fifo Location )
addWater ( row, col ) todo slice =
    let
        current =
            get ( row, col ) slice
    in
    if row == gRowCount slice - 1 then
        -- We've hit the bottom, so we're done with this path.
        ( gSet ( row, col ) MovingWater slice, todo )

    else if current == MovingWater then
        -- We've hit a path already travelled, so bail out.
        ( slice, todo )

    else if current == Sand then
        let
            below =
                get ( row + 1, col ) slice
        in
        if below == Sand || below == MovingWater then
            addWater ( row + 1, col ) todo (set ( row, col ) MovingWater slice)

        else if below == Clay || below == StandingWater then
            let
                ( l, blockedLeft ) =
                    horizontalExtent -1 ( row, col ) slice

                ( r, blockedRight ) =
                    horizontalExtent 1 ( row, col ) slice
            in
            if blockedLeft then
                if blockedRight then
                    -- We're in a basin, so fill it and move up one row (A).
                    addWater ( row - 1, col ) todo (List.foldl (\c sl -> gSet ( row, c ) StandingWater sl) slice (List.range l r) |> gSet ( row - 1, col ) Sand)

                else
                    -- Continue draining down to the right (B).
                    addWater ( row + 1, r ) todo (List.foldl (\c sl -> gSet ( row, c ) MovingWater sl) slice (List.range l r))

            else if blockedRight then
                -- Continue draining down to the left (C).
                addWater ( row + 1, l ) todo (List.foldl (\c sl -> gSet ( row, c ) MovingWater sl) slice (List.range l r))

            else
                -- Exits left and right, so drain right and store left for later (D)
                addWater ( row + 1, r ) (Fifo.insert ( row + 1, l ) todo) (List.foldl (\c sl -> gSet ( row, c ) MovingWater sl) slice (List.range l r))

        else
            ( slice, todo )

    else
        ( slice, todo )
```

A single call to `addWater` will travel from the source location to the bottom or to a previously visited cell if it encounters one before reaching the bottom. After each call, any paths yet to traverse (flowing left of a horizontal barrier) are stored in the `todo` queue. So the final stage is to repeatedly call `addWater` until there are no unvisited sources:

```elm {l}
simulate : ( Slice, Fifo Location ) -> Slice
simulate ( slice, source ) =
    let
        ( maybeLocation, todo ) =
            Fifo.remove source
    in
    case maybeLocation of
        Nothing ->
            slice

        Just location ->
            addWater location todo slice |> simulate
```

Finally, once we have traversed all water paths we just count all cells that contain either moving or standing water. Note the question stipulates the counting should start at the first row containing any clay, so we drop the first sandy rows before we count:

```elm {l r}
part1 : Int
part1 =
    let
        slice =
            puzzleInput |> parse

        countWater =
            gToLists
                >> dropWhile (List.filter ((==) Clay) >> List.isEmpty)
                >> List.concat
                >> List.filter (\cell -> cell == MovingWater || cell == StandingWater)
                >> List.length
    in
    simulate ( slice, Fifo.empty |> Fifo.insert (springOut slice) ) |> countWater
```

{( part2 |}

After a very long time, the water spring will run dry. How much water will be retained?

In the example above, water that won't eventually drain out is shown as ~, a total of 29 tiles.

**How many water tiles are left after the water spring stops producing water and all remaining water not at rest has drained?**

{| part2 )}

This is a trivial addition involving counting only cells of standing water:

```elm {l r}
part2 : Int
part2 =
    let
        slice =
            parse puzzleInput
    in
    simulate ( slice, Fifo.empty |> Fifo.insert (springOut slice) )
        |> gToList
        |> List.filter ((==) StandingWater)
        |> List.length
```

---

## Testing

```elm {l=hidden}
displaySlice : Slice -> List String
displaySlice slice =
    let
        cellToText cell =
            case cell of
                Spring _ ->
                    "+ "

                Clay ->
                    "█ "

                Sand ->
                    "  "

                MovingWater ->
                    "| "

                StandingWater ->
                    "▒ "

        dg =
            gMap cellToText slice
    in
    List.foldl
        (\row -> flip (++) ("\n    " :: [ gGetRow row dg |> Maybe.withDefault [] |> String.concat ]))
        [ "    " ]
        (List.range 0 (gRowCount slice - 1))
```

```elm {m}
display : List String
display =
    let
        slice =
            testInput |> parse
    in
    simulate ( slice, Fifo.empty |> Fifo.insert (springOut slice) )
        |> displaySlice
```

```elm {l}
testInput : List String
testInput =
    [ "x=495, y=2..7"
    , "y=7, x=495..501"
    , "x=501, y=3..7"
    , "x=498, y=2..4"
    , "x=506, y=1..2"
    , "x=498, y=10..13"
    , "x=504, y=10..13"
    , "y=13, x=498..504"
    ]
```

## Reflection

I found this quite a challenging problem but was pleased with the final result which runs in under a second.

Initially, I modelled water flowing down one 'drip' at a time, terminating when the drop fell into a basin. While this was useful for debugging as it allowed the simulation to be controlled one water element at a time, it was far too slow when applied to the full puzzle input (which was c.245 \* 1730 cells). Combined with occasional bugs resulting in recursion without a base case being possible, this made debugging unnecessarily hard.

As with [Beverage Bandits](d15_2018.md), it was very helpful to create markdown-generating displays of the state of the grid (`displaySlice`) for debugging.

I tried several attempts at a more efficient algorithm, which were either too slow or would miss some edge cases. The main problem being how to handle horizontal surfaces that generate two paths off a barrier (D). It was only when I realised I could simply queue these up and continue until the queue was emptied, that I had a feasible and correct solution. The other major speed-up was to realise you could fill basins simply by moving the 'current' cell up one row whenever it was bounded both to the left and right. This saved having to trace any path downwards more than once.

What caused me infuriating extra work was that my puzzle answer for part 1 was indicated as incorrect when submitted. I checked, re-checked, tried alternative variations on the algorithm and still it reported as incorrect. The test case gave the correct answer, and other tests I created also agreed with a manual count.

Eventually I re-read the question and only then realised it was asking for a water count _starting at the first row containing clay_. This was easy to miss because in the test case it was also the top row and in the puzzle input, was only 6 rows from the top.

A lesson I really should have learned by now **read the question carefully!** and then **read it carefully again!**.
