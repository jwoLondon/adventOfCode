---
follows: data/d04_2016.md

id: "litvis"
---

@import "css/aoc.less"

# Day 4 (2016): Security Through Obscurity

{( part1|}

Finally, you come across an information kiosk with a list of rooms. Of course, the list is encrypted and full of decoy data, but the instructions to decode the list are barely hidden nearby. Better remove the decoy data first.

Each room consists of an encrypted name (lowercase letters separated by dashes) followed by a dash, a sector ID, and a checksum in square brackets.

A room is real (not a decoy) if the checksum is the five most common letters in the encrypted name, in order, with ties broken by alphabetization. For example:

- **aaaaa-bbb-z-y-x-123[abxyz]** is a real room because the most common letters are a (5), b (3), and then a tie between x, y, and z, which are listed alphabetically.
- **a-b-c-d-e-f-g-h-987[abcde]** is a real room because although the letters are all tied (1 of each), the first five are listed alphabetically.
- **not-a-real-room-404[oarel]** is a real room.
- **totally-real-room-200[decoy]** is not.

Of the real rooms from the list above, the sum of their sector IDs is 1514.

What is the **sum of the sector IDs of the real rooms**?

{| part1 )}

We can start by parsing the input data in order to separate the name from its sector id and checksum:

```elm {l}
type alias Room =
    { name : String
    , id : Int
    , checksum : String
    }


parse : List String -> List Room
parse =
    let
        parseLine text rooms =
            case submatches "(.+)-(\\d+)\\[(.+)\\]" text of
                [ Just name, Just id, Just checksum ] ->
                    Room name (toInt id) checksum :: rooms

                _ ->
                    rooms
    in
    List.foldl parseLine []
```

To find the commonest five letters, we can create a frequency table of the letters after removing the dashes, sort it by frequency and extract the top five keys:

```elm {l}
top5 : String -> String
top5 =
    let
        customSort ( c1, f1 ) ( c2, f2 ) =
            case compare f1 f2 of
                LT ->
                    GT

                GT ->
                    LT

                EQ ->
                    compare c1 c2
    in
    String.toList
        >> List.filter ((/=) '-')
        >> List.foldl addToFreqTable Dict.empty
        >> Dict.toList
        >> List.sortWith customSort
        >> List.take 5
        >> List.map Tuple.first
        >> String.fromList
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.filter (\r -> top5 r.name == r.checksum)
        |> List.map .id
        |> List.sum
```

{( part2 |}

With all the decoy data out of the way, it's time to decrypt this list and get moving.

The room names are encrypted by a state-of-the-art shift cipher, which is nearly unbreakable without the right software. However, the information kiosk designers at Easter Bunny HQ were not expecting to deal with a master cryptographer like yourself.

To decrypt a room name, rotate each letter forward through the alphabet a number of times equal to the room's sector ID. A becomes B, B becomes C, Z becomes A, and so on. Dashes become spaces.

For example, the real name for qzmt-zixmtkozy-ivhz-343 is very encrypted name.

**What is the sector ID **of the room where North Pole objects are stored?

{| part2 )}

A 'Caesar' shift-cypher is easy to decode with modulus 26 arithmetic :

```elm {l}
decrypt : Int -> String -> String
decrypt shifts =
    let
        aCode =
            Char.toCode 'a'

        decryptChar char =
            if char == '-' then
                ' '

            else
                modBy 26 (Char.toCode char - aCode + shifts) + aCode |> Char.fromCode
    in
    String.toList
        >> List.map decryptChar
        >> String.fromList
```

To find the sector ID of the room where north pole objects are stored we can display the decrypted names and their IDs, filtering only those with a relevant terms. Quick trial and error reveals that `north` is a suitable filtering term:

```elm {l r}
part2 : List ( Int, String )
part2 =
    puzzleInput
        |> parse
        |> List.filter (\r -> top5 r.name == r.checksum)
        |> List.filter (\r -> decrypt r.id r.name |> String.contains "north")
        |> List.map (\r -> ( r.id, decrypt r.id r.name ))
```

## Reflection

Although a relatively simple problem, it does involve several steps. Extracting the top 5 terms by frequency was a little more cumbersome that expected as there is no simple way in Elm for stable sorting a dictionary by value (frequency) rather than key (letter). This required a custom stable sort (`top5`).

Part 2 was unusual in that the specification of the answer was not precisely defined "What is the sector ID of the room where North Pole objects are stored?‚Äù. Outside of AoC, especially in visualization, this is not uncommon in tackling a problem where there is a degree of exploration required to fully understand what is required of the computation.
