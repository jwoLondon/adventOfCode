---
follows: data/d03_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 3 (2018): No Matter How You Slice It

{( part1|}

The Elves managed to locate the chimney-squeeze prototype fabric for Santa's suit (thanks to someone who helpfully wrote its box IDs on the wall of the warehouse in the middle of the night). Unfortunately, anomalies are still affecting them - nobody can even agree on how to cut the fabric.

The whole piece of fabric they're working on is a very large square - at least 1000 inches on each side.

Each Elf has made a claim about which area of fabric would be ideal for Santa's suit. All claims have an ID and consist of a single rectangle with edges parallel to the edges of the fabric. Each claim's rectangle is defined as follows:

- The number of inches between the left edge of the fabric and the left edge of the rectangle.
- The number of inches between the top edge of the fabric and the top edge of the rectangle.
- The width of the rectangle in inches.
- The height of the rectangle in inches.

A claim like #123 @ 3,2: 5x4 means that claim ID 123 specifies a rectangle 3 inches from the left edge, 2 inches from the top edge, 5 inches wide, and 4 inches tall. Visually, it claims the square inches of fabric represented by # (and ignores the square inches of fabric represented by .) in the diagram below:

    ...........
    ...........
    ...#####...
    ...#####...
    ...#####...
    ...#####...
    ...........
    ...........
    ...........

The problem is that many of the claims overlap, causing two or more claims to cover part of the same areas. For example, consider the following claims:

    #1 @ 1,3: 4x4
    #2 @ 3,1: 4x4
    #3 @ 5,5: 2x2

Visually, these claim the following areas:

    ........
    ...2222.
    ...2222.
    .11XX22.
    .11XX22.
    .111133.
    .111133.
    ........

The four square inches marked with X are claimed by both 1 and 2. (Claim 3, while adjacent to the others, does not overlap either of them.)

If the Elves all proceed with their own plans, none of them will have enough fabric. How many square inches of fabric are within two or more claims?

{| part1 )}

## Parsing and Data Structures

We can represent a rectangle of material in a record containing its ID and dimensions.

```elm {l}
type alias Rect =
    { id : Int
    , x : Int
    , y : Int
    , w : Int
    , h : Int
    }
```

This makes it quite straightforward to parse the input data and store as a list of `Rect`:

```elm {l}
parse : List String -> List Rect
parse =
    let
        rectValues =
            submatches "#(\\d+) @ (\\d+),(\\d+): (\\d+)x(\\d+)"
                >> List.filterMap identity
                >> List.map toInt

        parseLine line =
            case rectValues line of
                id :: x :: y :: w :: h :: [] ->
                    Rect id x y w h

                _ ->
                    Rect 0 0 0 0 0
    in
    List.map parseLine
```

There are several ways we could represent all the rectangles positioned on the fabric.
One would be to store all locations as a frequency table with the key being the location and the frequency being the number of overlapping rectangles.
This has the advantage of being reasonably scalable to an arbitrary spatial extent. It is particular suitable if the rectangles are dispersed and sparsely cover the fabric.

An alternative is to represent the entire rectangle of fabric as an array of integers (using `Grid` from [aoc.md](aoc.md)).
This is what we do below:

```elm {l}
type alias Fabric =
    Grid Int


gridSize : Int
gridSize =
    1000
```

## Approach

To find out how much of the fabric has an overlap of more than one rectangle, we can store the number of overlapping rectangles at each grid position by incrementing every location within each rectangle.
That is, each grid cell will store how many rectangles overlap its location.

```elm {l}
addRectangle : Rect -> Fabric -> Fabric
addRectangle rectangle fabric =
    let
        traverseRect r ( x, y ) grid =
            if y >= r.y + r.h then
                grid

            else if x >= r.x + r.w then
                traverseRect r ( r.x, y + 1 ) grid

            else
                let
                    gridValue =
                        gGet ( x, y ) grid |> Maybe.withDefault 0
                in
                traverseRect r ( x + 1, y ) (gSet ( x, y ) (gridValue + 1) grid)
    in
    traverseRect rectangle ( rectangle.x, rectangle.y ) fabric


overlapFabric : Fabric
overlapFabric =
    parse puzzleInput
        |> List.foldl addRectangle (gInit gridSize gridSize 0)
```

Once we've accumulated all rectangles we can just filter those values greater than 1 and count them.

```elm {l r}
part1 : Int
part1 =
    overlapFabric
        |> flatten
        |> List.filter (\n -> n > 1)
        |> List.length
```

{( part2 |}

Amidst the chaos, you notice that exactly one claim doesn't overlap by even a single square inch of fabric with any other claim. If you can somehow draw attention to it, maybe the Elves will be able to make Santa's suit after all!

For example, in the claims above, only claim 3 is intact after all claims are made.

What is the ID of the only claim that doesn't overlap?

{| part2 )}

## Approach

If we compare each rectangle with the fabric after all rectangles have been added, any that have no overlaps should be composed entirely of 1s.
And therefore, the sum of the values for such a rectangle should equal its area:

```elm {l}
checkRect : Fabric -> Rect -> Int -> Int
checkRect grid rect id =
    let
        traverseRect r ( x, y ) total =
            if id /= 0 then
                id

            else if y >= r.y + r.h then
                if total == r.w * r.h then
                    r.id

                else
                    0

            else if x >= r.x + r.w then
                traverseRect r ( r.x, y + 1 ) total

            else
                let
                    gVal =
                        gGet ( x, y ) grid |> Maybe.withDefault -9999999
                in
                traverseRect r ( x + 1, y ) (total + gVal)
    in
    traverseRect rect ( rect.x, rect.y ) id
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.foldl (checkRect overlapFabric) 0
```

## Reflection

The wording of the question directs us towards representing the fabric with some kind of array-based rectangle.
My mapping and visualization background just reinforces this approach to representation.
But it is probably not the most efficient in this case, especially as the rectangles in the puzzle input are compartively sparse (there are many parts of the fabric that are not covered by any rectangles).

I find the challenge, especially when trying to complete the puzzle in a short time, is to avoid automatically jumping on the most obvious data structure and approach.
Stepping back and thinking about alternatives before committing to any one approach is probably a better strategy.
Today, it was only while walking to work after having completed the puzzle as described above, that it occurred to me that it might have been better to use a dictionary based representation.

I wonder too, if using a dictionary might allow a more efficient solution to be found where a set of 'covered once' locations (for part 2) could be incrementally updated along with a running total of the area covered more than once (for part 1).
If a solution is possible that way, it would probably require the input rectangles to be sorted in some way first.
