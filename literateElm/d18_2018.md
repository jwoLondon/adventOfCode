---
follows: data/d18_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 18 (2018): Settlers of The North Pole

{( part1|}

On the outskirts of the North Pole base construction project, many Elves are collecting lumber.

The lumber collection area is 50 acres by 50 acres; each acre can be either **open ground** (`.`), **trees** (`|`), or a **lumberyard** (`#`). You take a scan of the area (your puzzle input).

Strange magic is at work here: each minute, the landscape looks entirely different. In exactly **one minute**, an open acre can fill with trees, a wooded acre can be converted to a lumberyard, or a lumberyard can be cleared to open ground (the lumber having been sent to other projects).

The change to each acre is based entirely on the **contents of that acre** as well as the number of **open, wooded, or lumberyard acres adjacent to it** at the start of each minute. Here, "adjacent" means any of the eight acres surrounding that acre. (Acres on the edges of the lumber collection area might have fewer than eight adjacent acres; the missing acres aren't counted.)

In particular:

- An **open** acre will become filled with **trees** if **three or more** adjacent acres contained trees. Otherwise, nothing happens.
- An acre filled with **trees** will become a **lumberyard** if **three or more** adjacent acres were lumberyards. Otherwise, nothing happens.
- An acre containing a **lumberyard** will remain a **lumberyard** if it was adjacent to **at least one other lumberyard** and at least **one acre containing trees**. Otherwise, it becomes **open**.

These changes happen across all acres simultaneously, each of them using the state of all acres at the beginning of the minute and changing to their new form by the end of that same minute. Changes that happen during the minute don't affect each other.

For example, suppose the lumber collection area is instead only 10 by 10 acres with this initial configuration:

Initial state:

    .#.#...|#.
    .....#|##|
    .|..|...#.
    ..|#.....#
    #.#|||#|#|
    ...#.||...
    .|....|...
    ||...#|.#|
    |.||||..|.
    ...#.|..|.

After 1 minute:

    .......##.
    ......|###
    .|..|...#.
    ..|#||...#
    ..##||.|#|
    ...#||||..
    ||...|||..
    |||||.||.|
    ||||||||||
    ....||..|.

After 2 minutes:

    .......#..
    ......|#..
    .|.|||....
    ..##|||..#
    ..###|||#|
    ...#|||||.
    |||||||||.
    ||||||||||
    ||||||||||
    .|||||||||

After 3 minutes:

    .......#..
    ....|||#..
    .|.||||...
    ..###|||.#
    ...##|||#|
    .||##|||||
    ||||||||||
    ||||||||||
    ||||||||||
    ||||||||||

After 4 minutes:

    .....|.#..
    ...||||#..
    .|.#||||..
    ..###||||#
    ...###||#|
    |||##|||||
    ||||||||||
    ||||||||||
    ||||||||||
    ||||||||||

After 5 minutes:

    ....|||#..
    ...||||#..
    .|.##||||.
    ..####|||#
    .|.###||#|
    |||###||||
    ||||||||||
    ||||||||||
    ||||||||||
    ||||||||||

After 6 minutes:

    ...||||#..
    ...||||#..
    .|.###|||.
    ..#.##|||#
    |||#.##|#|
    |||###||||
    ||||#|||||
    ||||||||||
    ||||||||||
    ||||||||||

After 7 minutes:

    ...||||#..
    ..||#|##..
    .|.####||.
    ||#..##||#
    ||##.##|#|
    |||####|||
    |||###||||
    ||||||||||
    ||||||||||
    ||||||||||

After 8 minutes:

    ..||||##..
    ..|#####..
    |||#####|.
    ||#...##|#
    ||##..###|
    ||##.###||
    |||####|||
    ||||#|||||
    ||||||||||
    ||||||||||

After 9 minutes:

    ..||###...
    .||#####..
    ||##...##.
    ||#....###
    |##....##|
    ||##..###|
    ||######||
    |||###||||
    ||||||||||
    ||||||||||

After 10 minutes:

    .||##.....
    ||###.....
    ||##......
    |##.....##
    |##.....##
    |##....##|
    ||##.####|
    ||#####|||
    ||||#|||||
    ||||||||||

After 10 minutes, there are 37 wooded acres and 31 lumberyards. Multiplying the number of wooded acres by the number of lumberyards gives the total resource value after ten minutes: 37 \* 31 = 1147.

What will the total resource value of the lumber collection area be after 10 minutes?

{| part1 )}

## Parsing and Data Structures

The form of this puzzle is similar to previous cellular automata puzzles where we model a grid of cells. The variation this time is that a cell can have one of three states rather than two.

```elm {l}
type alias Land =
    Grid Cell


type Cell
    = Open
    | Trees
    | Yard


type alias Location =
    ( Int, Int )
```

```elm {l}
parse : List String -> Grid Cell
parse input =
    let
        toCell sym =
            case sym of
                '|' ->
                    Trees

                '#' ->
                    Yard

                _ ->
                    Open

        cellValues =
            List.foldl (\l -> (++) [ l |> String.toList |> List.map toCell ]) []
                >> List.indexedMap Tuple.pair

        land =
            gInit (List.length input)
                (input |> List.head |> Maybe.withDefault "" |> String.length)
                Open
    in
    List.foldl (\( r, row ) -> gSetRow r row) land (cellValues input)
```

## Cellular Automata

To evolve the grid of land parcels, we need to be able to count the number of a cell's eight neighbours containing a particular land type:

```elm {l}
neighbours : Cell -> Location -> Land -> Int
neighbours cell ( row, col ) land =
    [ gGet ( row - 1, col - 1 ) land
    , gGet ( row - 1, col ) land
    , gGet ( row - 1, col + 1 ) land
    , gGet ( row, col - 1 ) land
    , gGet ( row, col + 1 ) land
    , gGet ( row + 1, col - 1 ) land
    , gGet ( row + 1, col ) land
    , gGet ( row + 1, col + 1 ) land
    ]
        |> List.filterMap identity
        |> List.filter ((==) cell)
        |> List.length
```

We can then encode the rules that determine the next state of any cell based on its neighbourhood:

```elm {l}
nextState : Land -> Location -> Cell -> Cell
nextState land location cell =
    case cell of
        Open ->
            if neighbours Trees location land >= 3 then
                Trees

            else
                Open

        Trees ->
            if neighbours Yard location land >= 3 then
                Yard

            else
                Trees

        Yard ->
            if neighbours Yard location land >= 1 && neighbours Trees location land >= 1 then
                Yard

            else
                Open
```

Mapping the entire grid gives us the new landuse after one minute:

```elm {l}
nextLand : Land -> Land
nextLand land =
    gMapWithLocation (nextState land) land
```

Now we just have to iterate 10 times and count the number of trees and yards:

```elm {l}
score : Land -> Int
score land =
    let
        cells =
            gToList land
    in
    (List.filter ((==) Trees) cells |> List.length)
        * (List.filter ((==) Yard) cells |> List.length)
```

```elm {l r}
part1 : Int
part1 =
    iterate 10 (parse puzzleInput) nextLand |> score
```

{( part2 |}

This important natural resource will need to last for at least thousands of years. Are the Elves collecting this lumber sustainably?

**What will the total resource value of the lumber collection area be after 1000000000 minutes?**

{| part2 )}

## Approach

As with the similar [Subterranean Sustainability puzzle](day12_2018.md), the number of iterations required by the question is sufficiently large to suggest it is possible to calculate the answer without iterating every step.

Since many cellular automata transition to a stable state (or at least show regions of stability), one approach is to examine how the scores change after each iteration, and look for repeated patterns.

This can be done through visual inspection, where it becomes clear the pattern stablises after around 500 iterations.

```elm {v}
myVis : VL.Spec
myVis =
    let
        scoreSequence n land ss =
            if n == 0 then
                List.reverse ss |> List.indexedMap (\i sc -> ( toFloat i, toFloat sc ))

            else
                let
                    next =
                        nextLand land
                in
                scoreSequence (n - 1) next (score next :: ss)

        scores =
            scoreSequence 600 (parse puzzleInput) []

        data =
            VL.dataFromColumns []
                << VL.dataColumn "iteration" (VL.nums (List.map Tuple.first scores))
                << VL.dataColumn "score" (VL.nums (List.map Tuple.second scores))

        enc =
            VL.encoding
                << VL.position VL.X [ VL.pName "iteration", VL.pMType VL.Quantitative ]
                << VL.position VL.Y [ VL.pName "score", VL.pMType VL.Quantitative ]
    in
    VL.toVegaLite [ VL.width 600, VL.height 400, data [], enc [], VL.line [] ]
```

As we did for [Subterranean Sustainability puzzle](day12_2018.md), we can just iterate to stabilty, look for the next matching output to find the period of oscillation and iterate to the target number of iterations modulus that period:

```elm {l r}
part2 : Int
part2 =
    --Reaches steady state by around 500 iterations
    let
        landAt500 =
            iterate 500 (parse puzzleInput) nextLand

        scoreAt500 =
            landAt500 |> score

        period land n =
            let
                next =
                    nextLand land
            in
            if score next == scoreAt500 then
                n - 499

            else
                period next (n + 1)
    in
    iterate (modBy (period landAt500 500) (1000000000 - 500)) landAt500 nextLand
        |> score
```

## Reflection

Nice to have an easy day after a couple of tougher puzzles in the last few days. Much of this was very similar to day 12, but the 2D, 3-state automata did show some visually pleasing patterns as they evolved.

It was good to be able to use some VegaLite to visualize the emergence of stability to help solve the problem (there is a chart showing above if viewing this in [litvis](https://github.com/gicentre/litvis) rather than directly on github). While this approach did work, there is a risk that during the transition to stability the patterns are approximately, but not precisely repeating. A more robust, but visaully less interesting approach would be to match the entire periodic sequence rather than just number from the sequence.
