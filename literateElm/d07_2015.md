---
follows: data/d07_2015.md

id: "litvis"
---

@import "css/aoc.less"

# Day 7 (2015): Some Assembly Required

{( part1|}

This year, Santa brought little Bobby Tables a set of wires and bitwise logic gates! Unfortunately, little Bobby is a little under the recommended age range, and he needs help assembling the circuit.

Each wire has an identifier (some lowercase letters) and can carry a 16-bit signal (a number from 0 to 65535). A signal is provided to each wire by a gate, another wire, or some specific value. Each wire can only get a signal from one source, but can provide its signal to multiple destinations. A gate provides no signal until all of its inputs have a signal.

The included instructions booklet describes how to connect the parts together: x AND y -> z means to connect wires x and y to an AND gate, and then connect its output to wire z.

For example:

- 123 -> x means that the signal 123 is provided to wire x.
- x AND y -> z means that the bitwise AND of wire x and wire y is provided to wire z.
- p LSHIFT 2 -> q means that the value from wire p is left-shifted by 2 and then provided to wire q.
- NOT e -> f means that the bitwise complement of the value from wire e is provided to wire f.

Other possible gates include OR (bitwise OR) and RSHIFT (right-shift). If, for some reason, you'd like to emulate the circuit instead, almost all programming languages (for example, C, JavaScript, or Python) provide operators for these gates.

For example, here is a simple circuit:

```
123 -> x
456 -> y
x AND y -> d
x OR y -> e
x LSHIFT 2 -> f
y RSHIFT 2 -> g
NOT x -> h
NOT y -> i
```

After it is run, these are the signals on the wires:

```
d: 72
e: 507
f: 492
g: 114
h: 65412
i: 65079
x: 123
y: 456
```

In little Bobby's kit's instructions booklet (provided as your puzzle input), what signal is ultimately provided to wire a?

{| part1 )}

## Approach

Let's consider the test case given in the question.
If the task had been to find the value of wire y, the solution would be easy: simply scan though the wiring instructions looking for the assignment of a value to y, and evaluate the input: 456.
If the task had been to find the value of wire i, we could scan looking for the assignment of i, and again evaluate the input: In this case applying the NOT operator to the value of y. Thus a reclusive solution might present itself:

```
eval wire:
 scan instructions to find input to wire
  apply operator on each eval inputWire
```

This would work, but note that some operators have two arguments (e.g `AND`) and so the level of recursion soon becomes very deep.
To make matters worse the same wire can be input to many others and following this algorithm, it has to be evaluated each time it is used.
Together these make the computation time for the real task unfeasible.

So we need to look for heuristics.
One heuristic is to keep a record of any previously evaluated wires so that if its value is needed again we simply look up its value rather than having to recursively navigate each time (_memoization_).

In the test case we can pass through the list of instructions just once, storing evaluated wires in some kind of table.
After the first line is evaluated we now know that wire x has a value of 123; after the second line, we also now that y has a value of 456.
This makes evaluating the third line easy because we apply bitwise AND to 123 and 456 storing the answer.
We can proceed through all lines and by the end we will have values for every wire.
Problem solved.

But wait, suppose the line `123 -> x` was the last instruction in the list, not the first.
In that case when we get to the line `x AND y -> d` we wouldnâ€™t know what the value of x is so would not be able to evaluate it.
So we will need to repeatedly build the memoization table until it includes the wire we wish to interrogate.

## Data Structures

We can store both the instructions and the memoization table of wires' known voltages as dictionaries for quick access:

```elm {l}
type alias VoltageTable =
    Dict String Int


type alias InstructionTable =
    Dict String Instruction


type Instruction
    = Assign String
    | And String String
    | Or String String
    | LShift String String
    | RShift String String
    | Not String
```

## Parsing

We can use Elm's `String.words` to split each instruction into tokens and assign them to the instruction table.

```elm {l}
parse : String -> InstructionTable -> InstructionTable
parse instructText =
    case String.words instructText of
        [ arg1, "AND", arg2, "->", wire ] ->
            Dict.insert wire (And arg1 arg2)

        [ arg1, "OR", arg2, "->", wire ] ->
            Dict.insert wire (Or arg1 arg2)

        [ arg1, "LSHIFT", numBits, "->", wire ] ->
            Dict.insert wire (LShift arg1 numBits)

        [ arg1, "RSHIFT", numBits, "->", wire ] ->
            Dict.insert wire (RShift arg1 numBits)

        [ "NOT", arg1, "->", wire ] ->
            Dict.insert wire (Not arg1)

        [ arg1, "->", wire ] ->
            Dict.insert wire (Assign arg1)

        _ ->
            identity
```

## Building the memoization table

```elm {l}
buildVoltageTable : String -> InstructionTable -> VoltageTable -> VoltageTable
buildVoltageTable wire instructions voltageTable =
    let
        newVoltageTable =
            Dict.foldl voltage voltageTable instructions
    in
    case Dict.get wire newVoltageTable of
        Just v ->
            newVoltageTable

        Nothing ->
            if Dict.size instructions == 0 then
                Dict.empty

            else
                buildVoltageTable wire instructions newVoltageTable


voltage : String -> Instruction -> VoltageTable -> VoltageTable
voltage wire instruction voltageTable =
    case instruction of
        Assign arg ->
            eval1 arg wire voltageTable identity

        Not arg ->
            eval1 arg wire voltageTable (Bitwise.and 65535 << Bitwise.complement)

        And arg1 arg2 ->
            eval2 arg1 arg2 wire voltageTable Bitwise.and

        Or arg1 arg2 ->
            eval2 arg1 arg2 wire voltageTable Bitwise.or

        LShift arg numBits ->
            eval2 arg numBits wire voltageTable (flip Bitwise.shiftLeftBy)

        RShift arg numBits ->
            eval2 arg numBits wire voltageTable (flip Bitwise.shiftRightBy)


eval1 : String -> String -> VoltageTable -> (Int -> Int) -> VoltageTable
eval1 arg wire voltageTable op =
    if Dict.member wire voltageTable then
        voltageTable

    else
        case getNum arg voltageTable of
            Just v ->
                Dict.insert wire (op v) voltageTable

            Nothing ->
                voltageTable


eval2 : String -> String -> String -> VoltageTable -> (Int -> Int -> Int) -> VoltageTable
eval2 arg1 arg2 wire voltageTable op =
    if Dict.member wire voltageTable then
        voltageTable

    else
        case ( getNum arg1 voltageTable, getNum arg2 voltageTable ) of
            ( Just num1, Just num2 ) ->
                Dict.insert wire (op num1 num2) voltageTable

            ( Just num1, Nothing ) ->
                Dict.insert arg1 num1 voltageTable

            ( Nothing, Just num2 ) ->
                Dict.insert arg2 num2 voltageTable

            ( Nothing, Nothing ) ->
                voltageTable


getNum : String -> VoltageTable -> Maybe Int
getNum arg voltageTable =
    case Dict.get arg voltageTable of
        Just v ->
            Just v

        Nothing ->
            String.toInt arg
```

Putting it all together:

```elm {l r}
part1 : Int
part1 =
    let
        voltageTable =
            buildVoltageTable "a" (List.foldl parse Dict.empty puzzleInput) Dict.empty
    in
    Dict.get "a" voltageTable |> Maybe.withDefault -1
```

{( part2 |}
Now, take the signal you got on wire a, override wire b to that signal, and reset the other wires (including wire a). What new signal is ultimately provided to wire a?
{| part2 )}

This time we build the voltage table starting with a single entry for wire b set to the value found in part 1:

```elm {l r}
part2 : Int
part2 =
    let
        voltageTable =
            buildVoltageTable "a"
                (List.foldl parse Dict.empty puzzleInput)
                (Dict.singleton "b" part1)
    in
    Dict.get "a" voltageTable |> Maybe.withDefault -1
```
