---
follows: data/d20_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 20 (2018): A Regular Map

{( part1|}

While you were learning about instruction pointers, the Elves made considerable progress. When you look up, you discover that the North Pole base construction project has completely surrounded you.

The area you are in is made up entirely of **rooms** and **doors**. The rooms are arranged in a grid, and rooms only connect to adjacent rooms when a door is present between them.

For example, drawing rooms as ., walls as #, doors as | or -, your current position as X, and where north is up, the area you're in might look like this:

    #####
    #.|.#
    #-###
    #.|X#
    #####

You get the attention of a passing construction Elf and ask for a map. "I don't have time to draw out a map of this place - it's **huge**. Instead, I can give you directions to **every room in the facility**!" He writes down some directions on a piece of parchment and runs off. In the example above, the instructions might have been ^WNE$, a [regular expression](https://en.wikipedia.org/wiki/Regular_expression) or "**regex**" (your puzzle input).

The regex matches routes (like WNE for "west, north, east") that will take you from your current room through various doors in the facility. In aggregate, the routes will take you through **every door in the facility at least once**; mapping out all of these routes will let you build a proper map and find your way around.

`^` and `$` are at the beginning and end of your regex; these just mean that the regex doesn't match anything outside the routes it describes. (Specifically, `^` matches the start of the route, and `$` matches the end of it.) These characters will not appear elsewhere in the regex.

The rest of the regex matches various sequences of the characters `N` (north), `S` (south), `E` (east), and `W` (west). In the example above, `^WNE$` matches only one route, `WNE`, which means you can move **west, then north, then east** from your current position. Sequences of letters like this always match that exact route in the same order.

Sometimes, the route can **branch**. A branch is given by a list of options separated by pipes (`|`) and wrapped in parentheses. So, ^N(E|W)N$ contains a branch: after going north, you must choose to go **either east or west** before finishing your route by going north again. By tracing out the possible routes after branching, you can determine where the doors are and, therefore, where the rooms are in the facility.

For example, consider this regex: `^ENWWW(NEEE|SSE(EE|N))$`

This regex begins with `ENWWW`, which means that from your current position, all routes must begin by moving east, north, and then west three times, in that order. After this, there is a branch. Before you consider the branch, this is what you know about the map so far, with doors you aren't sure about marked with a `?`:

    #?#?#?#?#
    ?.|.|.|.?
    #?#?#?#-#
        ?X|.?
        #?#?#

After this point, there is `(NEEE|SSE(EE|N))`. This gives you exactly two options: `NEEE` and `SSE(EE|N)`. By following `NEEE`, the map now looks like this:

    #?#?#?#?#
    ?.|.|.|.?
    #-#?#?#?#
    ?.|.|.|.?
    #?#?#?#-#
        ?X|.?
        #?#?#

Now, only `SSE(EE|N)` remains. Because it is in the same parenthesized group as `NEEE`, it starts from the same room `NEEE` started in. It states that starting from that point, there exist doors which will allow you to move south twice, then east; this ends up at another branch. After that, you can either move east twice or north once. This information fills in the rest of the doors:

    #?#?#?#?#
    ?.|.|.|.?
    #-#?#?#?#
    ?.|.|.|.?
    #-#?#?#-#
    ?.?.?X|.?
    #-#-#?#?#
    ?.|.|.|.?
    #?#?#?#?#

Once you've followed all possible routes, you know the remaining unknown parts are all walls, producing a finished map of the facility:

    #########
    #.|.|.|.#
    #-#######
    #.|.|.|.#
    #-#####-#
    #.#.#X|.#
    #-#-#####
    #.|.|.|.#
    #########

Sometimes, a list of options can have an **empty option**, like `(NEWS|WNSE|)`. This means that routes at this point could effectively skip the options in parentheses and move on immediately. For example, consider this regex and the corresponding map:

    ^ENNWSWW(NEWS|)SSSEEN(WNSE|)EE(SWEN|)NNN$

    ###########
    #.|.#.|.#.#
    #-###-#-#-#
    #.|.|.#.#.#
    #-#####-#-#
    #.#.#X|.#.#
    #-#-#####-#
    #.#.|.|.|.#
    #-###-###-#
    #.|.|.#.|.#
    ###########

This regex has one main route which, at three locations, can optionally include additional detours and be valid: `(NEWS|)`, `(WNSE|)`, and `(SWEN|)`. Regardless of which option is taken, the route continues from the position it is left at after taking those steps. So, for example, this regex matches all of the following routes (and more that aren't listed here):

    ENNWSWWSSSEENEENNN
    ENNWSWWNEWSSSSEENEENNN
    ENNWSWWNEWSSSSEENEESWENNNN
    ENNWSWWSSSEENWNSEEENNN

By following the various routes the regex matches, a full map of all of the doors and rooms in the facility can be assembled.

To get a sense for the size of this facility, you'd like to determine which room is **furthest** from you: specifically, you would like to find the room for which the **shortest path to that room would require passing through the most doors**.

In the first example `(^WNE$)`, this would be the north-east corner **3** doors away.
In the second example `(^ENWWW(NEEE|SSE(EE|N))$)`, this would be the south-east corner **10** doors away.
In the third example `(^ENNWSWW(NEWS|)SSSEEN(WNSE|)EE(SWEN|)NNN$)`, this would be the north-east corner **18 **doors away.
Here are a few more examples:

Regex: `^ESSWWN(E|NNENN(EESS(WNSE|)SSS|WWWSSSSE(SW|NNNE)))$`
Furthest room requires passing 23 doors

    #############
    #.|.|.|.|.|.#
    #-#####-###-#
    #.#.|.#.#.#.#
    #-#-###-#-#-#
    #.#.#.|.#.|.#
    #-#-#-#####-#
    #.#.#.#X|.#.#
    #-#-#-###-#-#
    #.|.#.|.#.#.#
    ###-#-###-#-#
    #.|.#.|.|.#.#
    #############

Regex: `^WSSEESWWWNW(S|NENNEEEENN(ESSSSW(NWSW|SSEN)|WSWWN(E|WWS(E|SS))))$`
Furthest room requires passing 31 doors

    ###############
    #.|.|.|.#.|.|.#
    #-###-###-#-#-#
    #.|.#.|.|.#.#.#
    #-#########-#-#
    #.#.|.|.|.|.#.#
    #-#-#########-#
    #.#.#.|X#.|.#.#
    ###-#-###-#-#-#
    #.|.#.#.|.#.|.#
    #-###-#####-###
    #.|.#.|.|.#.#.#
    #-#-#####-#-#-#
    #.#.|.|.|.#.|.#
    ###############

**What is the largest number of doors you would be required to pass through to reach a room?** That is, find the room for which the shortest path from your starting location to that room would require passing through the most doors; what is the fewest doors you can pass through to reach it?

{| part1 )}

## Data Structure

While the question describes rooms, doors and walls, all we really need to store in order to navigate the facility is the connections between rooms. A room itself can be represented by a 2d location on an unbounded plane associated with 0 to 4 neighbouring rooms. This graph structure is conveniently represented as a dictionary:

```elm {l}
type alias Location =
    ( Int, Int )


type alias Rooms =
    Dict Location (Set Location)
```

We can add a connection between two adjacent rooms (implying a door between them) by updating the dictionary representing the tree of connected rooms:

```elm {l}
connect : Location -> Location -> Rooms -> Rooms
connect room1 room2 =
    let
        addConnect r1 r2 rs =
            case Dict.get r1 rs of
                Just connected ->
                    Dict.insert r1 (Set.insert r2 connected) rs

                Nothing ->
                    Dict.insert r1 (Set.singleton r2) rs
    in
    addConnect room1 room2 >> addConnect room2 room1
```

## Parsing

The main effort involves parsing the string of instructions to generate the appropriate tree of rooms. As we do this, the parser needs to keep track of the rooms it is added, the input instructions yet to process, the current room. As we parse the input sequentially, we also need to keep track of the location at the start ('anchor') of a group (`(`), so that when we identify alternatives (`|`), we can 'backtrack' to the anchor before processing each set of alternative instructions.

We can represent all of these in a record:

```elm {l}
type alias Parser =
    { location : Location
    , anchor : Location
    , todo : List Char
    , rooms : Rooms
    }
```

Parsing needs to account for movement instructions (`N`, `S`, `E` and `W`) where we simply add a new room for each instruction; the start of a group, where we need to log a new 'anchor'; the end of a group; and an alternative (`|`), which can be regarded as an addtional group sharing the same anchor as its alternatives:

```elm {l}
parse : Parser -> Parser
parse parser =
    case parser.todo of
        [] ->
            parser

        hd :: tl ->
            if hd == 'N' || hd == 'E' || hd == 'S' || hd == 'W' then
                parse (addRoom parser)

            else if hd == '(' then
                let
                    subSequence =
                        parse { parser | todo = tl, anchor = parser.location }
                in
                parse { parser | todo = subSequence.todo, rooms = subSequence.rooms }

            else if hd == ')' then
                { parser | todo = tl, location = parser.anchor }

            else if hd == '|' then
                -- We've ended a subsequence and about to start another
                parse { parser | todo = tl, location = parser.anchor }

            else
                parse { parser | todo = tl }
```

```elm {l}
addRoom : Parser -> Parser
addRoom parser =
    let
        adjRoom dir ( row, col ) =
            case dir of
                'N' ->
                    ( row - 1, col )

                'E' ->
                    ( row, col + 1 )

                'S' ->
                    ( row + 1, col )

                'W' ->
                    ( row, col - 1 )

                _ ->
                    Debug.todo "Unknown character in input"
    in
    case parser.todo of
        [] ->
            parser

        hd :: tl ->
            { parser
                | location = adjRoom hd parser.location
                , rooms = connect parser.location (adjRoom hd parser.location) parser.rooms
                , todo = tl
            }
```

## Finding Distances from the start

Once we have a maze we can use a now standard breadth-first search to find the distance (number of doors to open) from the start to any other room.

```elm {l}
buildDist : Rooms -> Dict Location Int
buildDist rooms =
    let
        build todo distances =
            case Deque.popFront todo of
                ( Nothing, _ ) ->
                    distances

                ( Just location, remaining ) ->
                    let
                        distToLoc =
                            Dict.get location distances |> Maybe.withDefault 0

                        unvisited =
                            Dict.get location rooms
                                |> Maybe.withDefault Set.empty
                                |> Set.filter (\loc -> not (Dict.member loc distances))
                    in
                    build
                        (Set.foldl Deque.pushBack remaining unvisited)
                        (Set.foldl
                            (\neighbour ds ->
                                if not (Dict.member neighbour ds) then
                                    Dict.insert neighbour (distToLoc + 1) ds

                                else
                                    ds
                            )
                            distances
                            unvisited
                        )
    in
    build (Deque.singleton ( 0, 0 )) (Dict.singleton ( 0, 0 ) 0)
```

To find distance of the furthest room, we simply find the largest value in the dictionary of distances:

```elm {l r}
part1 : Int
part1 =
    parse (Parser ( 0, 0 ) ( 0, 0 ) (String.toList puzzleInput) Dict.empty)
        |> .rooms
        |> buildDist
        |> Dict.values
        |> List.maximum
        |> Maybe.withDefault -1
```

{( part2 |}

Okay, so the facility is **big**.

**How many rooms have a shortest path from your current location that pass through at least 1000 doors?**

{| part2 )}

This is a nice easy extension to the problem in that we just need to count the number of room distances of at least 1000:

```elm {l r}
part2 : Int
part2 =
    parse (Parser ( 0, 0 ) ( 0, 0 ) (String.toList puzzleInput) Dict.empty)
        |> .rooms
        |> buildDist
        |> Dict.values
        |> List.filter (\n -> n >= 1000)
        |> List.length
```

## Reflection

For practical reasons I wasn't able to work on this problem on the day it was released so revisted it a few days later. I loved this one, perhaps in part because the time pressure was off and I was able to spend time writing tests for maze generation and distance calculation (see the `testRoom` below). There is something satisfying about turning the instruction sequences into a maze.

There was nothing too complicated here. The only part that required a bit more thought was ensuring the location at the start of a group was stored so the parser could backtrack to this anchor when processing alternatives in a group. I had started by storing all the relevant data needed by the parser in a tuple, but switching to a record simplified and clarified the coding.

Part two was a trivial extension (as a slow programmer, it only took me 30 seconds to provide the extra code) so I was quite surprised to see this taking up to 7 minutes extra on the top 100 leaderboard. I guess they must have not built a distance graph to answer part 1.

---

## Tests

```elm {l m}
testRoom : List String
testRoom =
    let
        rooms =
            parse (Parser ( 0, 0 ) ( 0, 0 ) (String.toList test1) Dict.empty)
                |> .rooms

        distances =
            buildDist rooms
    in
    gridDisplay distances rooms
        |> gToLists
        |> List.map (\r -> "\n    " ++ String.concat r)
```

```elm {l}
test1 : String
test1 =
    "^WNE$"


test2 : String
test2 =
    "^ENWWW(NEEE|SSE(EE|N))$"


test3 : String
test3 =
    "^ENNWSWW(NEWS|)SSSEEN(WNSE|)EE(SWEN|)NNN$"


test4 : String
test4 =
    "^ESSWWN(E|NNENN(EESS(WNSE|)SSS|WWWSSSSE(SW|NNNE)))$"


test5 : String
test5 =
    "^WSSEESWWWNW(S|NENNEEEENN(ESSSSW(NWSW|SSEN)|WSWWN(E|WWS(E|SS))))$"
```

```elm {l=hidden}
gridDisplay : Dict Location Int -> Rooms -> Grid String
gridDisplay distances rooms =
    let
        ( top, left ) =
            List.foldl
                (\( row, col ) ( rowMin, colMin ) -> ( min row rowMin, min col colMin ))
                ( 9999, 9999 )
                (Dict.keys rooms)

        ( bottom, right ) =
            List.foldl
                (\( row, col ) ( rowMax, colMax ) -> ( max row rowMax, max col colMax ))
                ( -9999, -9999 )
                (Dict.keys rooms)

        grid =
            gInit ((bottom - top + 1) * 2 + 1) ((right - left + 1) * 2 + 1) "# "

        roomPos ( row, col ) =
            ( (row - top) * 2 + 1, (col - left) * 2 + 1 )

        doorPos ( row1, col1 ) ( row2, col2 ) =
            ( (row1 + row2 - 2 * top) + 1, (col1 + col2 - 2 * left) + 1 )
    in
    Dict.foldl
        (\loc connected grd ->
            Set.foldl (\d -> gSet (doorPos d loc) "  ") (gSet (roomPos loc) ((Dict.get loc distances |> Maybe.withDefault -1 |> String.fromInt) ++ " " |> String.left 2) grd) connected
        )
        grid
        rooms
```
