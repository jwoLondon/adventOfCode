---
follows: data/d04_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 4 (2018): Repose Record

{( part1|}

You've sneaked into another supply closet - this time, it's across from the prototype suit manufacturing lab. You need to sneak inside and fix the issues with the suit, but there's a guard stationed outside the lab, so this is as close as you can safely get.

As you search the closet for anything that might help, you discover that you're not the first person to want to sneak in. Covering the walls, someone has spent an hour starting every midnight for the past few months secretly observing this guard post! They've been writing down the ID of the one guard on duty that night - the Elves seem to have decided that one guard was enough for the overnight shift - as well as when they fall asleep or wake up while at their post (your puzzle input).

For example, consider the following records, which have already been organized into chronological order:

    [1518-11-01 00:00] Guard #10 begins shift
    [1518-11-01 00:05] falls asleep
    [1518-11-01 00:25] wakes up
    [1518-11-01 00:30] falls asleep
    [1518-11-01 00:55] wakes up
    [1518-11-01 23:58] Guard #99 begins shift
    [1518-11-02 00:40] falls asleep
    [1518-11-02 00:50] wakes up
    [1518-11-03 00:05] Guard #10 begins shift
    [1518-11-03 00:24] falls asleep
    [1518-11-03 00:29] wakes up
    [1518-11-04 00:02] Guard #99 begins shift
    [1518-11-04 00:36] falls asleep
    [1518-11-04 00:46] wakes up
    [1518-11-05 00:03] Guard #99 begins shift
    [1518-11-05 00:45] falls asleep
    [1518-11-05 00:55] wakes up

Timestamps are written using year-month-day hour:minute format. The guard falling asleep or waking up is always the one whose shift most recently started. Because all asleep/awake times are during the midnight hour (00:00 - 00:59), only the minute portion (00 - 59) is relevant for those events.

Visually, these records show that the guards are asleep at these times:

    Date   ID   Minute
                000000000011111111112222222222333333333344444444445555555555
                012345678901234567890123456789012345678901234567890123456789
    11-01  #10  .....####################.....#########################.....
    11-02  #99  ........................................##########..........
    11-03  #10  ........................#####...............................
    11-04  #99  ....................................##########..............
    11-05  #99  .............................................##########.....

The columns are Date, which shows the month-day portion of the relevant day; ID, which shows the guard on duty that day; and Minute, which shows the minutes during which the guard was asleep within the midnight hour. (The Minute column's header shows the minute's ten's digit in the first row and the one's digit in the second row.) Awake is shown as `.`, and asleep is shown as `#`.

Note that guards count as asleep on the minute they fall asleep, and they count as awake on the minute they wake up. For example, because Guard \#10 wakes up at 00:25 on 1518-11-01, minute 25 is marked as awake.

If you can figure out the guard most likely to be asleep at a specific time, you might be able to trick that guard into working tonight so you can have the best chance of sneaking in. You have two strategies for choosing the best guard/minute combination.

**Strategy 1**: Find the guard that has the most minutes asleep. What minute does that guard spend asleep the most?

In the example above, Guard \#10 spent the most minutes asleep, a total of 50 minutes (20+25+5), while Guard \#99 only slept for a total of 30 minutes (10+10+10). Guard \#10 was asleep most during minute 24 (on two days, whereas any other minute the guard was asleep was only seen on one day).

While this example listed the entries in chronological order, your entries are in the order you found them. You'll need to organize them before they can be analyzed.

**What is the ID of the guard you chose multiplied by the minute you chose?**
(In the above example, the answer would be 10 \* 24 = 240.)

{| part1 )}

## Data Shaping

This puzzle involves 'shaping' the input data into a grid of sleep samples.
That shaping requires several steps, the first of which is to parse the input data to generate a [tidy](https://www.jstatsoft.org/index.php/jss/article/view/v059i10/v59i10.pdf) table that includes a reference to the guard ID in each row (illustrated with the sample data in the puzzle question):

| Date-time   | (Guard ID, isAsleep) |
| ----------- | -------------------- |
| 11-01 00:00 | (10,False)           |
| 11-01 00:05 | (-1,True)            |
| 11-01 00:25 | (-1,False)           |
| 11-01 00:30 | (-1,True)            |
| 11-01 00:55 | (-1,False)           |
| 11-01 23:58 | (99,False)           |
| 11-02 00:40 | (-1,True)            |
| 11-02 00:50 | (-1,False)           |
| 11-03 00:05 | (10,False)           |
| 11-03 00:24 | (-1,True)            |
| 11-03 00:29 | (-1,False)           |
| 11-04 00:02 | (99,False)           |
| 11-04 00:36 | (-1,True)            |
| 11-04 00:46 | (-1,False)           |
| 11-05 00:03 | (99,False)           |
| 11-05 00:45 | (-1,True)            |
| 11-05 00:55 | (-1,False)           |

Each line of the input text for a guard already on duty does not contain the guard's ID (indicated by a `-1`).
For that, we need to ensure the table rows are in chronological order (achieved by placing the data into a dictionary):

```elm {l}
parse : List String -> List ( String, ( Int, Bool ) )
parse =
    let
        reposeObservation =
            submatches ".+-(\\d+-\\d+ \\d+:\\d+).+(wakes up|falls asleep|Guard #(\\d+))"
                >> List.filterMap identity

        parseLine line =
            case reposeObservation line of
                dateTime :: wOrS :: id :: [] ->
                    Dict.insert dateTime ( toInt id, False )

                dateTime :: wOrS :: [] ->
                    Dict.insert dateTime ( -1, wOrS == "falls asleep" )

                _ ->
                    identity
    in
    List.foldl parseLine Dict.empty >> Dict.toList
```

We can then interpolate the Guard IDs based on the previous non-negative ID in the ordered list to produce the tidy table:

| Date-time   | (Guard ID, isAsleep) |
| ----------- | -------------------- |
| 11-01 00:00 | (10,False)           |
| 11-01 00:05 | (10,True)            |
| 11-01 00:25 | (10,False)           |
| 11-01 00:30 | (10,True)            |
| 11-01 00:55 | (10,False)           |
| 11-01 23:58 | (99,False)           |
| 11-02 00:40 | (99,True)            |
| 11-02 00:50 | (99,False)           |
| 11-03 00:05 | (10,False)           |
| 11-03 00:24 | (10,True)            |
| 11-03 00:29 | (10,False)           |
| 11-04 00:02 | (99,False)           |
| 11-04 00:36 | (99,True)            |
| 11-04 00:46 | (99,False)           |
| 11-05 00:03 | (99,False)           |
| 11-05 00:45 | (99,True)            |
| 11-05 00:55 | (99,False)           |

```elm {l}
fillIDs : List ( String, ( Int, Bool ) ) -> List ( String, ( Int, Bool ) )
fillIDs roster =
    let
        interpolate prevID oldList newList =
            case oldList of
                [] ->
                    List.reverse newList

                ( date, ( id, wOrS ) ) :: tl ->
                    if id == -1 then
                        interpolate prevID tl (( date, ( prevID, wOrS ) ) :: newList)

                    else
                        interpolate id tl (( date, ( id, wOrS ) ) :: newList)
    in
    interpolate -1 roster []
```

The next, and most complex, data shaping stage requires us to transform the set of each day-guardID combination into a row of 60 sleep/awake states and order those rows by time. We can discard the hour:minute data from the timestamp as we are only interested in midnight to 1am and this is implicit in the 60 minute-readings in the second column:

| (month-day, guardID) | List (Bool)                                                    |
| -------------------- | -------------------------------------------------------------- |
| (11-01, 10)          | `.....####################.....#########################.....` |
| (11-02, 99)          | `........................................##########..........` |
| (11-03, 10)          | `........................#####...............................` |
| (11-04, 99)          | `....................................##########..............` |
| (11-05, 99)          | `.............................................##########.....` |

```elm {l}
type alias DayID =
    ( String, Int )


buildGrid : Dict DayID (List ( String, Bool )) -> List ( String, ( Int, Bool ) ) -> Dict DayID (List Bool)
buildGrid grid roster =
    let
        addEntry day id minute isAsleep =
            let
                gridVals =
                    Dict.get ( day, id ) grid |> Maybe.withDefault []
            in
            Dict.insert ( day, id ) (gridVals ++ [ ( minute, isAsleep ) ]) grid

        splitDateTime dateTime =
            case submatches "(\\S+) (\\d+):(\\d+)" dateTime of
                [ Just day, Just hr, Just mn ] ->
                    if hr == "00" then
                        Just ( day, mn )

                    else
                        Nothing

                _ ->
                    Nothing

        interpolateTimes tTuples =
            let
                build mn lastSW deltas sleeps =
                    if mn > 59 then
                        List.reverse sleeps

                    else
                        case deltas of
                            [] ->
                                build (mn + 1) lastSW deltas (False :: sleeps)

                            ( minute, sw ) :: tl ->
                                if toInt minute == mn then
                                    build (mn + 1) sw tl (sw :: sleeps)

                                else
                                    build (mn + 1) lastSW deltas (lastSW :: sleeps)
            in
            build 0 False tTuples []
    in
    case roster of
        [] ->
            Dict.map (\k v -> interpolateTimes v) grid

        ( dateTime, ( id, isAsleep ) ) :: tl ->
            case splitDateTime dateTime of
                Just ( day, minute ) ->
                    buildGrid (addEntry day id minute isAsleep) tl

                Nothing ->
                    buildGrid grid tl
```

## Finding the sleepiest guard

Now we have a grid of the sleeping patterns of each guard, we can create a dictionary of guard ID and the amount they sleep:

```elm {l}
guardSleeps : Dict DayID (List Bool) -> Dict Int Int
guardSleeps =
    let
        getSleeps id =
            Dict.get id >> Maybe.withDefault 0
    in
    Dict.map (\_ -> List.filter identity >> List.length)
        >> Dict.foldl
            (\( _, id ) sleeps gSleeps ->
                Dict.insert id (getSleeps id gSleeps + sleeps) gSleeps
            )
            Dict.empty
```

From which we can find the sleepiest guard:

```elm {l}
sleepyGuard : Dict Int Int -> Int
sleepyGuard =
    Dict.toList
        >> List.map (\( a, b ) -> ( b, a ))
        >> List.maximum
        >> Maybe.withDefault ( -1, -1 )
        >> Tuple.second
```

Now we know who the sleepiest guard is, we can find the minute of the hour in which they are most likely to be asleep by looking only at the rows in the grid of sleep patterns that correspond to the guard's ID, from this we can extract the (number of sleeps, minute of the hour) tuple:

```elm {l}
sleepyMinute : Int -> Dict DayID (List Bool) -> ( Int, Int )
sleepyMinute sleepyID =
    Dict.filter (\( _, id ) _ -> id == sleepyID)
        >> Dict.values
        >> transpose
        >> List.map (List.filter identity)
        >> List.indexedMap (\i bs -> ( List.length bs, i ))
        >> List.maximum
        >> Maybe.withDefault ( -1, -1 )
```

Putting it all together:

```elm {l r}
part1 : Int
part1 =
    let
        grid =
            puzzleInput |> parse |> fillIDs |> buildGrid Dict.empty

        guard =
            grid |> guardSleeps |> sleepyGuard

        minute =
            grid |> sleepyMinute guard |> Tuple.second
    in
    guard * minute
```

{( part2 |}

**Strategy 2**: Of all guards, which guard is most frequently asleep on the same minute?

In the example above, Guard #99 spent minute 45 asleep more than any other guard or minute - three times in total. (In all other cases, any guard spent any minute asleep at most twice.)

**What is the ID of the guard you chose multiplied by the minute you chose?**
(In the above example, the answer would be 99 \* 45 = 4455.)

{| part2 )}

For this second part, we have done most of the work already. The difference this time is that we need to consider the sleepiest minute of all guards, not just the guard who sleeps the most.

```elm {l r}
part2 : Int
part2 =
    let
        grid =
            puzzleInput |> parse |> fillIDs |> buildGrid Dict.empty

        allGuards =
            grid |> Dict.keys |> List.map Tuple.second |> unique

        maxSleepsGuard =
            allGuards
                |> List.map (\guard -> ( sleepyMinute guard grid, guard ))
                |> List.maximum
                |> Maybe.withDefault ( ( -1, -1 ), -1 )
    in
    (Tuple.first maxSleepsGuard |> Tuple.second) * Tuple.second maxSleepsGuard
```

## Reflection

This was an exercise in data shaping. The calculations required for the two answers were simple, but the effort required organising the data in such a way that we could count and extract the right values. My solution feels unsatisfyingly complex, so perhaps there are ways to simplify things. Functional declarative languages should be good for this kind of task, so I can't help but feel I've missed something here.

---

## Tests

```elm {l}
testInput : List String
testInput =
    """[1518-11-01 00:00] Guard #10 begins shift
[1518-11-01 00:05] falls asleep
[1518-11-01 00:25] wakes up
[1518-11-01 00:30] falls asleep
[1518-11-01 00:55] wakes up
[1518-11-01 23:58] Guard #99 begins shift
[1518-11-02 00:40] falls asleep
[1518-11-02 00:50] wakes up
[1518-11-03 00:05] Guard #10 begins shift
[1518-11-03 00:24] falls asleep
[1518-11-03 00:29] wakes up
[1518-11-04 00:02] Guard #99 begins shift
[1518-11-04 00:36] falls asleep
[1518-11-04 00:46] wakes up
[1518-11-05 00:03] Guard #99 begins shift
[1518-11-05 00:45] falls asleep
[1518-11-05 00:55] wakes up"""
        |> String.lines
```
