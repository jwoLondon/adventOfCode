---
follows: data/d05_2018.md

id: "litvis"
---

@import "css/aoc.less"

# Day 5 (2018): Alchemical Reduction

{( part1|}

You've managed to sneak in to the prototype suit manufacturing lab. The Elves are making decent progress, but are still struggling with the suit's size reduction capabilities.

While the very latest in 1518 alchemical technology might have solved their problem eventually, you can do better. You scan the chemical composition of the suit's material and discover that it is formed by extremely long polymers (one of which is available as your puzzle input).

The polymer is formed by smaller units which, when triggered, react with each other such that two adjacent units of the same type and opposite polarity are destroyed. Units' types are represented by letters; units' polarity is represented by capitalization. For instance, r and R are units with the same type but opposite polarity, whereas r and s are entirely different types and do not react.

For example:

- In aA, a and A react, leaving nothing behind.
- In abBA, bB destroys itself, leaving aA. As above, this then destroys itself, leaving nothing.
- In abAB, no two adjacent units are of the same type, and so nothing happens.
- In aabAAB, even though aa and AA are of the same type, their polarities match, and so nothing happens.

Now, consider a larger example, dabAcCaCBAcCcaDA:

    dabAcCaCBAcCcaDA  The first 'cC' is removed.
    dabAaCBAcCcaDA    This creates 'Aa', which is removed.
    dabCBAcCcaDA      Either 'cC' or 'Cc' are removed (the result is the same).
    dabCBAcaDA        No further actions can be taken.

After all possible reactions, the resulting polymer contains 10 units.

How many units remain after fully reacting the polymer you scanned?

{| part1 )}

An obvious start point is to provide a function to test for a reactive pair.
We cannot apply a simple case-insenstive comparison because a pair of identical characters do not react so we need to account for that case:

```elm {l}
react : Char -> Char -> Bool
react a b =
    (Char.toLower a == Char.toLower b) && a /= b
```

The key to an efficient processing of a long string of characters is to recognise that after removing a reactive pair, we may bring together a pair of reactive characters that were previously separated.
That means for sequential processing of a string, we have to consider the possibility that a character we have previously considered unreactive might now become reactive.
We can do this by keeping two lists, `left` and `right`, and comparing the head of each.

    [] [a, b, c, d, e, f]  -- Initial left and right lists
    [a] [b, c, d, e, f]    -- First comparison between a and b
    [b, a] [c, d, e, f]    -- Second comparison between b and c
    [c, b, a] [d, e, f]    -- Third comparison between c and d. If they were to react,
    [b, a] [e, f]          -- the next comparison would be between b and e

```elm {l}
reduce : List Char -> List Char -> List Char
reduce left right =
    case left of
        [] ->
            case right of
                hd :: tl ->
                    reduce [ hd ] tl

                [] ->
                    []

        leftHead :: leftTail ->
            case right of
                [] ->
                    left

                hd :: tl ->
                    if react leftHead hd then
                        reduce leftTail tl

                    else
                        reduce (hd :: left) tl
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> reduce []
        |> List.length
```

{( part2 |}

Time to improve the polymer.

One of the unit types is causing problems; it's preventing the polymer from collapsing as much as it should. Your goal is to figure out which unit type is causing the most problems, remove all instances of it (regardless of polarity), fully react the remaining polymer, and measure its length.

For example, again using the polymer dabAcCaCBAcCcaDA from above:

- Removing all A/a units produces dbcCCBcCcD. Fully reacting this polymer produces dbCBcD, which has length 6.
- Removing all B/b units produces daAcCaCAcCcaDA. Fully reacting this polymer produces daCAcaDA, which has length 8.
- Removing all C/c units produces dabAaBAaDA. Fully reacting this polymer produces daDA, which has length 4.
- Removing all D/d units produces abAcCaCBAcCcaA. Fully reacting this polymer produces abCBAc, which has length 6.

In this example, removing all C/c units was best, producing the answer 4.

What is the length of the shortest polymer you can produce by removing all units of exactly one type and fully reacting the result?

{| part2 )}

This is a trivial extension in that we need to first remove a letter from the input sequence, reduce it as for part 1 and store its length. If we do this for all the letters a-z, the shortest reduced string length gives us our answer.

```elm {l r}
part2 : Int
part2 =
    let
        compressed charToRemove =
            puzzleInput
                |> List.filter (\c -> c /= charToRemove && c /= Char.toUpper charToRemove)
                |> reduce []
                |> List.length
    in
    "abcdefghijklmnopqrstuvwxyz"
        |> String.toList
        |> List.map compressed
        |> List.minimum
        |> Maybe.withDefault -1
```

## Reflection

This is the kind of problem that should be easily solved in a functional language.
I did consider using regular expressions for tackling part 1 but rejected this on the grounds that part 2 might require a variation not so regex-friendly (in the end, it would not have been a problem, but until part 1 was solved I wasn't to know this).

My first approach was to do multiple passes of the input, removing any reactive pairs in each sweep. Multiple passes were needed because removing a pair might also join a formerly separated reactive pair.
This worked for the test data and subsets of the real data, but was way too slow to provide a feasable solution for the full dataset.

I thought initially the slowdown might be due to non-tail call recursion keeping track of multiple versions of the long input, so I made the mistake of trying to solve this by breaking down the input into blocks of characrters, reducing them, and then rejoining and repeating. While this produced what appeared to be a correct answer for the test data, again it was too slow for the full dataset.

A day at work on other matters gave me fresh eyes on an evening return to the problem and it then occurred to me to try a sequential approach that always compared the most recently processed character and the first unprocessed character. This allows removal of reactive pairs both backward and forward through the list.

The lesson to learn (again!) is to step back from the problem and consider alternatives before committing to one particular approach.
