---
follows: data/d17_2015.md

id: "litvis"
---

@import "css/aoc.less"

# Day 17 (2015): No Such Thing as Too Much

{( part1|}

The elves bought too much eggnog again - 150 liters this time. To fit it all into your refrigerator, you'll need to move it into smaller containers. You take an inventory of the capacities of the available containers.

For example, suppose you have containers of size 20, 15, 10, 5, and 5 liters. If you need to store 25 liters, there are four ways to do it:

- 15 and 10
- 20 and 5 (the first 5)
- 20 and 5 (the second 5)
- 15, 5, and 5

Filling all containers entirely, how many different combinations of containers can exactly fit all 150 liters of eggnog?

{| part1 )}

## Approach

This is a version of the knapsack or subset-sum problem where we need to find a combination of items that sum to a particular value.
The brute force approach is to find all combinations of items (the powerset), but we can do better than that by considering the upper- and lower-bounds of the number of items to choose.

For example we know that there is no single item that can store 150 litres, so there is no need to test combinations that include choosing one item.
Similar reasoning can be used to exclude a selection of 20 items as their sum is greater than 150 litres.

By sorting the items by capacity and accumulating the sum of the sorted items, we can simply calculate the lower and upper bounds of the combination choices we need to test:

```elm {l}
minNumItems : Int -> List Int -> Int
minNumItems target =
    List.sort
        >> List.reverse
        >> scanl (+) 0
        >> List.filter (\x -> x <= target)
        >> List.length


maxNumItems : Int -> List Int -> Int
maxNumItems target =
    List.sort
        >> scanl (+) 0
        >> List.filter (\x -> x <= target)
        >> List.length
        >> flip (-) 1
```

The combinations of items we need to test can then be constrained between upper and lower limits:

```elm {l}
allCombinations : Int -> Int -> List a -> List (List a)
allCombinations minK k items =
    if k == minK then
        combinations k items

    else
        allCombinations minK (k - 1) items ++ combinations k items
```

Now we can test all combinations between limits and test each to see if they hold the desired capacity:

```elm {l}
knapsacks : Int -> Int -> Int -> List Int -> List (List Int)
knapsacks minK maxK capacity items =
    let
        holds cap n =
            List.sum n == cap
    in
    List.filter (holds capacity) (allCombinations minK maxK items)
```

```elm {l r}
part1 : Int
part1 =
    knapsacks (minNumItems 150 puzzleInput) (maxNumItems 150 puzzleInput) 150 puzzleInput
        |> List.length
```

{( part2 |}

While playing with all the containers in the kitchen, another load of eggnog arrives! The shipping and receiving department is requesting as many containers as you can spare.

Find the minimum number of containers that can exactly fit all 150 liters of eggnog. How many different ways can you fill that number of containers and still hold exactly 150 litres?

In the example above, the minimum number of containers was two. There were three ways to use that many containers, and so the answer there would be 3.

{| part2 )}

We can apply exactly the same approach but this time set both the lower and upper bounds of our combination choice to be same lower bound.

```elm {l r}
part2 : Int
part2 =
  knapsacks (minNumItems 150 puzzleInput) (minNumItems 150 puzzleInput) 150 puzzleInput
      |> List.length
```

It is possible that constraining the combinations to the lower bound would not have yielded any answer, in which case we would have to increment the number of items by 1 until we find an answer.
But given an answer was found above, this was not necessary.
