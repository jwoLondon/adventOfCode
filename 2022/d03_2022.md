---
follows: data/d03_2022.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2022): Rucksack Reorganization

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2022/day/3).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2022-day-3).

## Part 1

Find the item type that appears in both compartments of each rucksack. **What is the sum of the priorities of those item types?**

We can start by splitting the contents of each rucksack into two equally sized sets of characters.

```elm {l}
splitEvenly : String -> List ( Set Char, Set Char )
splitEvenly =
    String.lines
        >> List.map
            (\s ->
                s
                    |> String.toList
                    |> AOC.splitAt (String.length s // 2)
                    |> Tuple.mapBoth Set.fromList Set.fromList
            )
```

Finding the common element in each rucksack's two compartments is just a case of finding the set intersection of the characters in each string.

```elm {l}
common : ( Set Char, Set Char ) -> Char
common ( left, right ) =
    Set.intersect left right |> Set.toList |> List.head |> Maybe.withDefault ' '
```

For any given character we need to find its numerical equivalent. We can use the ASCII code for each letter accounting for the fact that the priority value for uppercase and lowercase letters give a higher priority to uppercase ones, despite their ASCII codes being lower.

```elm {l}
priority : Char -> Int
priority item =
    if item < 'a' then
        Char.toCode item - 38

    else
        Char.toCode item - 96
```

Finally we just total the priority values of those intersecting items.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> splitEvenly
        |> List.map (common >> priority)
        |> List.sum
```

---

## Part 2

Find the item type that corresponds to the badges of each three-Elf group. **What is the sum of the priorities of those item types?**

This time we don't split lines into two, but instead group them into chunks of 3.

```elm {l}
toGroups : String -> List (List (Set Char))
toGroups =
    String.lines
        >> AOC.chunk 3
        >> List.map (List.map (String.toList >> Set.fromList))
```

Once grouped, we just have to find the intersecting item in each group of three before finding its numeric priority value.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> toGroups
        |> List.concatMap (AOC.intersections >> Set.toList >> List.map priority)
        |> List.sum
```

---

## Reflection

A nice simple problem involving some set intersections. Most of the effort involved conversion between strings, characters, sets and lists, but all pretty normal for Elm.
