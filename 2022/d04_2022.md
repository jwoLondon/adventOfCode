---
follows: data/d04_2022.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 4 (2022): Camp Cleanup

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2022/day/4).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2022-day-4).

## Part 1

**In how many assignment pairs does one range fully contain the other?**

First we need to parse the input, extracting the pair numerical ranges from each line.

```elm {l}
type alias RangePair =
    ( ( Int, Int ), ( Int, Int ) )
```

```elm {l}
parse : String -> List RangePair
parse =
    String.lines
        >> List.filterMap
            ((String.split ","
                >> List.filterMap ((String.split "-" >> List.map AOC.toInt) >> AOC.toTuple)
             )
                >> AOC.toTuple
            )
```

The containment condition is simply determined by comparing the maxima and minima of each pair of ranges, testing for whether the first range contains the second or the second contains the first.

```elm {l}
contains : RangePair -> Bool
contains ( ( mn1, mx1 ), ( mn2, mx2 ) ) =
    (mn1 <= mn2 && mx1 >= mx2) || (mn2 <= mn1 && mx2 >= mx1)
```

We then just count the number of true (contained) pairs.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.map contains
        |> List.filter identity
        |> List.length
```

---

## Part 2

**In how many assignment pairs do the ranges overlap?**

Overlapping can be found easily by looking for entirely separate ranges and negating the result.

```elm {l}
overlaps : RangePair -> Bool
overlaps ( ( mn1, mx1 ), ( mn2, mx2 ) ) =
    not (mx1 < mn2 || mn1 > mx2)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.map overlaps
        |> List.filter identity
        |> List.length
```

---

## Reflection

Another easy one today with very similar solutions for the Elm and [JavaScript](<(https://observablehq.com/@jwolondon/advent-of-code-2022-day-4)>) versions. I am used to testing for containments in 2d (spatial relations), so this 1d variant was straightforward. Recognising that overlap or containment relations are what are left after excluding disjoint relations helped in keeping Part 2 simple.

When I saw Part 1 I assumed Part 2 was going to require sieving multiple range pairs to find values cleaned by more than one elf, but the problem turned out to be much simpler.
