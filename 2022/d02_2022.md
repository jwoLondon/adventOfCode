---
follows: data/d02_2022.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2022): Rock Paper Scissors

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2022/day/2).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2022-day-2).

## Part 1

**What would your total score be if everything goes exactly according to your strategy guide?**

### Parsing

To keep things clear, let's store the three hand shapes in their own type:

```elm {l}
type Shape
    = Rock
    | Paper
    | Scissors
    | NoShape
```

and be able to convert an A, B, C, X, Y or Z into the appropriate shape:

```elm {l}
firstShape : Char -> Shape
firstShape chr =
    case chr of
        'A' ->
            Rock

        'B' ->
            Paper

        'C' ->
            Scissors

        _ ->
            NoShape
```

```elm {l}
secondShape : ( Shape, Char ) -> Shape
secondShape ( _, chr ) =
    case chr of
        'X' ->
            Rock

        'Y' ->
            Paper

        'Z' ->
            Scissors

        _ ->
            NoShape
```

And then read in each round into a tuple comprising the hand shape (first element) and the instruction (`X`, `Y` or `Z`) which we convert to a shape via a provided `toShape` function.

```elm {l}
parse : (( Shape, Char ) -> Shape) -> String -> List ( Shape, Shape )
parse toShape =
    let
        head =
            String.toList >> List.head >> Maybe.withDefault ' '
    in
    String.split "\n"
        >> List.map
            (\l ->
                let
                    s1 =
                        l |> head |> firstShape
                in
                ( s1, toShape ( s1, l |> String.reverse |> head ) )
            )
```

The score for each hand depends on the shape of your play and the outcome of the round.

```elm {l}
score : ( Shape, Shape ) -> Int
score ( s1, s2 ) =
    let
        shapeScore =
            case s2 of
                Rock ->
                    1

                Paper ->
                    2

                Scissors ->
                    3

                NoShape ->
                    0

        outcomeScore =
            if ( s1, s2 ) == ( Rock, Paper ) || ( s1, s2 ) == ( Paper, Scissors ) || ( s1, s2 ) == ( Scissors, Rock ) then
                6

            else if s1 == s2 then
                3

            else
                0
    in
    shapeScore + outcomeScore
```

We simply total the scores of each round.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse secondShape
        |> List.map score
        |> List.sum
```

---

## Part 2

Following the Elf's instructions for the second column, **what would your total score be if everything goes exactly according to your strategy guide?**

We just need to change the encoding rule for the X,Y,Z so it generates the shape that will lose each round.

```elm {l}
response : ( Shape, Char ) -> Shape
response ( s1, chr ) =
    case ( s1, chr ) of
        ( Rock, 'X' ) ->
            Scissors

        ( Rock, 'Z' ) ->
            Paper

        ( Paper, 'X' ) ->
            Rock

        ( Paper, 'Z' ) ->
            Scissors

        ( Scissors, 'X' ) ->
            Paper

        ( Scissors, 'Z' ) ->
            Rock

        _ ->
            s1
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse response
        |> List.map score
        |> List.sum
```

---

## Reflection

Interesting to compare with mmy [Observable solution](https://observablehq.com/@jwolondon/advent-of-code-2022-day-2). In both cases I wanted to prioritise clarity over brevity, but perhaps the way Elm encourages type safety means we have a somewhat over-engineered solution here with types and checks for empty strings. Semantically the Elm approach is more robust (particularly in separating out the second element (X,Y,Z) from the shape it implies via an encoding function, but we do lose some simplicity in the process.
