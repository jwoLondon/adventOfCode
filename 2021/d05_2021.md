---
follows: data/d05_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2021): Hydrothermal Venture

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/5).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-5).

## Part 1

Consider only horizontal and vertical lines. **At how many points do at least two lines overlap?**

### Parsing

We can use a simple regex to extract the 4 numbers that make up the endpoints of the lines and store each as a custom type.

```elm {l}
type Endpoints
    = Line ( Int, Int ) ( Int, Int )
```

```elm {l}
parse : List String -> List Endpoints
parse =
    let
        toEndpoint coordStr =
            case coordStr of
                [ x1, y1, x2, y2 ] ->
                    Just (Line ( AOC.toInt x1, AOC.toInt y1 ) ( AOC.toInt x2, AOC.toInt y2 ))

                _ ->
                    Nothing
    in
    List.map (AOC.submatches "(\\d+),(\\d+) -> (\\d+),(\\d+)" >> List.filterMap identity)
        >> List.filterMap toEndpoint
```

### Algorithm

For the first part, we only need to deal with lines parallel to the axes, so we filter just lines whose endpoints share a common x value or y value.

```elm {l}
orthogonal : List Endpoints -> List Endpoints
orthogonal =
    List.filter (\(Line ( x1, y1 ) ( x2, y2 )) -> x1 == x2 || y1 == y2)
```

For a given pair of line endpoints, we can add the coordinates of the gridded intermediate points. This assumes lines can only be orthogonal or 45 degree diagonal. That is, some combination of xIncrement = {-1,0,1} and yIncrement = {-1,0,1}.

```elm {l}
lineCoords : Endpoints -> List ( Int, Int )
lineCoords (Line ( x1, y1 ) ( x2, y2 )) =
    let
        xInc =
            AOC.sign (x2 - x1)

        yInc =
            AOC.sign (y2 - y1)

        numSteps =
            max (abs (x2 - x1)) (abs (y2 - y1))
    in
    List.range 0 numSteps |> List.map (\n -> ( x1 + n * xInc, y1 + n * yInc ))
```

Now we can accumulate the frequencies of all grid locations represented by the lines:

```elm {l}
intersections : List Endpoints -> Int
intersections =
    List.concatMap lineCoords
        >> List.foldl AOC.addToFreqTable Dict.empty
        >> Dict.filter (\_ -> (<) 1)
        >> Dict.size
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> orthogonal
        |> intersections
```

---

## Part 2

Consider all of the lines. **At how many points do at least two lines overlap?**

This is a trivial modification where we no longer filter for orthogonal lines.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> intersections
```

---

## Reflection

This felt like a much easier problem than yesterday's. As ever with the grid-based ones, there is choice between representing values in a grid structure or as a dictionary of grid coordinates. This is in part determined by how sparse the grid will be and what kind of manipulation of grid values is required. In this case, as we are simply accumulating grid values from lines, the dictionary approach seemed more appropriate.
