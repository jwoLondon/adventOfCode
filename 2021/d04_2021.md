---
follows: data/d04_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 4 (2021): Giant Squid

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/4).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-4).

## Part 1

To guarantee victory against the giant squid, figure out which board will win first. **What will your final score be if you choose that board?**

### Parsing

Each card can be stored as a list of 3-tuples _(row, col, number)_ making it easy to identify complete rows or columns from the card.

```elm {l}
type alias Card =
    List ( Int, Int, Int )
```

```elm {l}
parse : String -> List Card
parse =
    let
        indexedSet i xs todo =
            case todo of
                [] ->
                    xs

                hd :: tl ->
                    indexedSet (i + 1) (( i // 5, modBy 5 i, AOC.toInt hd ) :: xs) tl
    in
    AOC.split "\\s+" >> AOC.chunk 25 >> List.map (indexedSet 0 [])
```

### Algorithm

As we draw numbers we will remove them from the cards.

```elm {l}
mark : Int -> List Card -> List Card
mark n =
    let
        markNum =
            List.filter (\( _, _, num ) -> n /= num)
    in
    List.foldl (\c cs -> markNum c :: cs) []
```

To test whether a bingo card has a full line of marked numbers, the card should contain at least one empty row or column since they will have been removed as they were marked.

```elm {l}
fullLine : Card -> Bool
fullLine ns =
    let
        markedAt pos =
            (List.length (List.filter (\( r, _, _ ) -> r == pos) ns) == 0)
                || (List.length (List.filter (\( _, c, _ ) -> c == pos) ns) == 0)
    in
    List.any markedAt [ 0, 1, 2, 3, 4 ]
```

```elm {l}
bingoCards : List Card -> List Card -> List Card
bingoCards todo bCards =
    case todo of
        hd :: tl ->
            if fullLine hd then
                bingoCards tl (hd :: bCards)

            else
                bingoCards tl bCards

        [] ->
            bCards
```

To play the game we keep drawing new numbers until we find a bingo card with a completed row or column.

```elm {l}
play : List Int -> List Card -> Int
play ns cards =
    case ns of
        hd :: tl ->
            let
                markedCards =
                    mark hd cards
            in
            case bingoCards markedCards [] of
                [] ->
                    play tl markedCards

                bc :: _ ->
                    score hd bc

        [] ->
            0
```

When we have found a winning card and the last number that was called, we can calculate the score.

```elm {l}
score : Int -> Card -> Int
score num =
    List.map (AOC.tripletThird >> (*) num) >> List.sum
```

```elm {l r}
part1 : Int
part1 =
    play (Tuple.first puzzleInput) (parse (Tuple.second puzzleInput))
```

---

## Part 2

Figure out which board will win last. **Once it wins, what would its final score be?**

Rather tha terminating the game when the first bingo is found, we remove completed bingo cards until only one remains and then play that one.

```elm {l}
play2 : List Int -> List Card -> Int
play2 ns cards =
    if List.length cards == 1 then
        play ns cards

    else
        case ns of
            hd :: tl ->
                let
                    markedCards =
                        mark hd cards

                    bCards =
                        bingoCards markedCards []
                in
                play2 tl (List.filter (\c -> not (List.member c bCards)) markedCards)

            [] ->
                0
```

```elm {l r}
part2 : Int
part2 =
    play2 (Tuple.first puzzleInput) (parse (Tuple.second puzzleInput))
```

---

## Reflection

For an early puzzle this one was a little fiddly. There are plenty of choices for data structure that will allow easy identification of completed rows and columns. For the [JavaScript version](https://observablehq.com/@jwolondon/advent-of-code-2021-day-4) I opted for an array of 25 numbers for each card, marking off numbers by turning them into nulls, preserving the array positions. With Elm, it was easier to store each number and its position as a 3-tuple. This allowed numbers to be removed as they were matched.

The catch in this puzzle, which took me longer than I'd hoped to solve, was that in part 2, you can no longer stop marking cards as completed when the first one is found. This would easy to miss in part because the test case removed a maximum of one card per ball, whereas the real puzzle input involved some numbers removing multiple cards.
