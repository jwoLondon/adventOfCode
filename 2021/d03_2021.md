---
follows: data/d03_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2021): Binary Diagnostic

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/3).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-3).

## Part 1

Use the binary numbers in your diagnostic report to calculate the gamma rate and epsilon rate, then multiply them together. **What is the power consumption of the submarine?**

### Parsing

We can treat each input number as an array of 1s and 0s, making it easier to perform arithmetic and logical operations on each bit. Lower level bit manipulation might be faster, but the code less clear.

```elm {l}
type alias BinaryDigits =
    List Int
```

```elm {l}
parse : List String -> List BinaryDigits
parse =
    List.map (String.split "" >> List.filterMap String.toInt)
```

### Algorithm

Because we are dealing with binary digits, 'most common' can be found by summing the bits at each position and testing whether the sum is greater or less than half the number of values tested.

```elm {l}
mostCommon : List BinaryDigits -> BinaryDigits
mostCommon numbers =
    let
        numDigits =
            numbers
                |> List.head
                |> Maybe.withDefault []
                |> List.length

        totals =
            numbers
                |> List.foldl (List.map2 (+)) (List.repeat numDigits 0)

        most sum =
            -- Account for integer division and possible tie between 0s and 1s
            if sum >= (List.length numbers + 1) // 2 then
                1

            else
                0
    in
    List.map most totals
```

'Least common' will be the negation of the most common bits.

```elm {l}
negate : BinaryDigits -> BinaryDigits
negate =
    List.map ((-) 1)
```

Ultimately we will need to convert the list of bits into a single decimal number:

```elm {l}
toDecimal : BinaryDigits -> Int
toDecimal =
    List.map String.fromInt >> String.join "" >> AOC.binaryToDec
```

```elm {l r}
part1 : Int
part1 =
    let
        most =
            puzzleInput |> parse |> mostCommon

        least =
            negate most
    in
    toDecimal most * toDecimal least
```

---

## Part 2

Use the binary numbers in your diagnostic report to calculate the oxygen generator rating and CO2 scrubber rating, then multiply them together. **What is the life support rating of the submarine?**

### Algorithm Part 2

Similar to the first part but this time we repeatedly apply the most/least common test to the input numbers after filtering values whose bit at a given position match the most/least bit. We increment the bit position to consider after each filtering.

Note that unlike part one, we cannot simply negate the bits found after identifying the Oxygen bits because the history of filtering will be different for the most/least iterations though the input numbers.

```elm {l}
type LifeSupport
    = Oxygen
    | CO2Scrubber
```

```elm {l}
filterInstructions : List String -> LifeSupport -> Int
filterInstructions input lifeSupport =
    let
        filter numbers bitPos =
            if List.length numbers == 1 then
                numbers

            else
                let
                    nthBit =
                        List.drop bitPos >> List.head

                    bitMatches =
                        case lifeSupport of
                            Oxygen ->
                                mostCommon numbers

                            CO2Scrubber ->
                                negate (mostCommon numbers)
                in
                filter (List.filter (\bs -> nthBit bs == nthBit bitMatches) numbers) (bitPos + 1)
    in
    filter (parse input) 0 |> List.head |> Maybe.withDefault [] |> toDecimal
```

```elm {l r}
part2 : Int
part2 =
    filterInstructions puzzleInput Oxygen * filterInstructions puzzleInput CO2Scrubber
```

---

## Reflection

One gotcha in this puzzle is dealing with the case when the number of 1s and 0s in a list of numbers is equal. The instructions indicate such cases should be summarised with a 1. In part 1 this does not arise, but it does in part 2. Thankfully the worked example in the question includes such a case so can be tested, but if it hadn't this might have been a difficult bug to spot if not accounted for.

There is some minor inefficiency in my part 2 solution in that we calculate the most/least common bit for the entire set of bits in each number, when we only really need it for the bit position we are currently considering. But given the size of the input data, I'd argue the clarity gained by reusing the mostCommon function from part one, outweighs the computational cost.
