---
follows: data/d08_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2021): Seven Segment Search

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/8).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-8).

## Part 1

**In the output values, how many times do digits 1, 4, 7, or 8 appear?**

### Parsing

We will need to be able to treat the input and output signal blocks separately so on parsing, we store both in a custom type.

```elm {l}
type alias Block =
    Set Char


type Signal
    = Signal (List Block) (List Block)


blockToStr : Block -> String
blockToStr =
    Set.toList >> String.fromList


strToBlock : String -> Block
strToBlock =
    String.toList >> Set.fromList
```

```elm {l}
parse : List String -> List Signal
parse input =
    let
        blocks =
            input
                |> List.map (String.split " " >> List.filter ((/=) "|"))
                |> List.map (List.map (String.toList >> Set.fromList))
    in
    List.map (\bs -> Signal (List.take 10 bs) (List.drop 10 bs)) blocks
```

We can now count the length of the blocks in the output of each line filtering just those with the unique lengths.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.concatMap (\(Signal _ out) -> List.map Set.size out)
        |> List.filter (\len -> List.member len [ 2, 3, 4, 7 ])
        |> List.length
```

---

## Part 2

For each entry, determine all of the wire/segment connections and decode the four-digit output values. **What do you get if you add up all of the output values?**

A key insight is that we don't need to keep track of the individual segment-wire encodings. But instead we just need to know if the set of wires in some scrambled signal block can encode a number consistently with the set of wires in known encodings (as identified in part 1).

For a block of a given size, we can determine which possible digits it could encode.

```elm {l}
blockPossibilities : Block -> List Int
blockPossibilities block =
    case Set.size block of
        2 ->
            [ 1 ]

        3 ->
            [ 7 ]

        4 ->
            [ 4 ]

        7 ->
            [ 8 ]

        5 ->
            [ 2, 3, 5 ]

        6 ->
            [ 0, 6, 9 ]

        _ ->
            []
```

If we compare any two blocks each representing a digit, they will have some segments in common. As this is determined solely by the arrangements of segments in the display, we can encode of the number of common segments in all pairs of digits:

```elm {l}
commonWires : Int -> Int -> Int
commonWires d1 d2 =
    case d1 of
        0 ->
            AOC.getWithDefault 0 d2 [ 6, 2, 4, 4, 3, 4, 5, 3, 6, 5 ]

        1 ->
            AOC.getWithDefault 0 d2 [ 2, 2, 1, 2, 2, 1, 1, 2, 2, 2 ]

        2 ->
            AOC.getWithDefault 0 d2 [ 4, 1, 5, 4, 2, 3, 4, 2, 5, 4 ]

        3 ->
            AOC.getWithDefault 0 d2 [ 4, 2, 4, 5, 3, 4, 4, 3, 5, 5 ]

        4 ->
            AOC.getWithDefault 0 d2 [ 3, 2, 2, 3, 4, 3, 3, 2, 4, 4 ]

        5 ->
            AOC.getWithDefault 0 d2 [ 4, 1, 3, 4, 3, 5, 5, 2, 5, 5 ]

        6 ->
            AOC.getWithDefault 0 d2 [ 5, 1, 4, 4, 3, 5, 6, 2, 6, 5 ]

        7 ->
            AOC.getWithDefault 0 d2 [ 3, 2, 2, 3, 2, 2, 2, 3, 3, 3 ]

        8 ->
            AOC.getWithDefault 0 d2 [ 6, 2, 5, 5, 4, 5, 6, 3, 7, 6 ]

        9 ->
            AOC.getWithDefault 0 d2 [ 5, 2, 4, 5, 4, 5, 5, 3, 6, 6 ]

        _ ->
            0
```

We can use this expected number of common segments to test candidate digits for those blocks that have more than one encoding for a given length.

In such cases we find the number of common segments of each candidate digit with the known (singleton) digits using the common wires matrix above. Similarly we count the number of common wires in the signal patterns of each of those pairs. A block encoding will be the only one that is consistent with the mapping of all singletons.

```elm {l}
entryValues : List Block -> List ( Block, List Int )
entryValues =
    List.map (\b -> ( b, blockPossibilities b ))
```

```elm {l}
singletons : List Block -> Dict String Int
singletons =
    let
        isSingleton ( b, ds ) =
            case ds of
                [ d ] ->
                    Just ( Set.toList b |> String.fromList, d )

                _ ->
                    Nothing
    in
    entryValues
        >> List.filterMap isSingleton
        >> Dict.fromList
```

```elm {l}
processSignal : Signal -> Int
processSignal (Signal inp out) =
    let
        confirmed =
            singletons inp

        matchDigit ( bs, digits ) =
            let
                checkDigit digit =
                    if
                        confirmed
                            |> Dict.foldl
                                (\bStr d ->
                                    (++)
                                        [ (bs |> Set.intersect (strToBlock bStr) |> Set.size) == commonWires digit d ]
                                )
                                []
                            |> List.all identity
                    then
                        Just digit

                    else
                        Nothing
            in
            Dict.insert (blockToStr bs) (List.filterMap checkDigit digits |> List.head |> Maybe.withDefault 0)

        lookup =
            inp
                |> entryValues
                |> List.filter (Tuple.second >> List.length >> (<) 1)
                |> List.foldl matchDigit confirmed
    in
    out
        |> List.map (blockToStr >> (\b -> Dict.get b lookup |> Maybe.withDefault 0))
        |> List.map2 (*) [ 1000, 100, 10, 1 ]
        |> List.sum
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.map processSignal
        |> List.sum
```

---

## Reflection

Part one was very straightforward, but part two more challenging. I spent too long trying to map each wire to segment before realising we just need to look at the set size of wires for each digit mapping, eliminating inconsistent sizes between candidate digits and those already known.

While I think the approach is a good one, I feel the code could be more elegant.
