---
follows: data/d02_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2021): Dive!

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/2):

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-2).

## Part 1

**What do you get if you multiply your final horizontal position by your final depth?**

### Parsing

First we convert each line of the movement instructions into one of two possible commands, either `Forward` or `Down`, each associated with a magnitude. Original `up` instructions are converted into `down` commands with a negative magnitude.

```elm {l}
type Command
    = Forward Int
    | Down Int
```

```elm {l}
parse : List String -> List Command
parse =
    let
        toCmd s =
            case String.split " " s of
                [ cmd, val ] ->
                    if cmd == "forward" then
                        Just (Forward (AOC.toInt val))

                    else if cmd == "up" then
                        Just (Down -(AOC.toInt val))

                    else
                        Just (Down (AOC.toInt val))

                _ ->
                    Nothing
    in
    List.map toCmd >> List.filterMap identity
```

### Algorithm

Each instruction will either update the x-position of the submarine or its depth, so we can create a move function to update the submarine's position and apply it sequentially to the parsed instructions.

```elm {l}
move : Command -> ( Int, Int ) -> ( Int, Int )
move cmd ( x, d ) =
    case cmd of
        Forward v ->
            ( x + v, d )

        Down v ->
            ( x, d + v )
```

```elm {l r}
part1 : Int
part1 =
    let
        ( xPos, depth ) =
            List.foldl move ( 0, 0 ) (parse puzzleInput)
    in
    xPos * depth
```

---

## Part 2

We just need to update the move function with the new instruction responses, now keeping track of three elements of the submarine's vector, its x-position, its depth and its aim:

```elm {l}
move2 : Command -> ( Int, Int, Int ) -> ( Int, Int, Int )
move2 cmd ( x, d, aim ) =
    case cmd of
        Forward v ->
            ( x + v, d + aim * v, aim )

        Down v ->
            ( x, d, aim + v )
```

```elm {l r}
part2 : Int
part2 =
    let
        ( xPos, depth, aim ) =
            List.foldl move2 ( 0, 0, 0 ) (parse puzzleInput)
    in
    xPos * depth
```

---

## Reflection

A nice straightforward sequential update of a 2-element or 3-element vector. It has been interesting completing this one in both [JavaScript](https://observablehq.com/@jwolondon/advent-of-code-2021-day-2) and Elm. The JavaScript coding is a little quicker, but I feel more confident that Elm steers me away from bugs by forcing me to account for bad input, forgotten cases etc. In these easy puzzles, that advantage is minimal, but as complexity increases, that hand-holding becomes increasingly valuable.
