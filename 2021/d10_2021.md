---
follows: data/d10_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2021): Syntax Scoring

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/10).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-10).

## Part 1

Find the first illegal character in each corrupted line of the navigation subsystem. **What is the total syntax error score for those errors?**

### Parsing

We will store each line of input as a list of symbols.

```elm {l}
parse : List String -> List (List Char)
parse =
    List.map String.toList
```

### Algorithm

Push every opening symbol onto a stack. When we find a closing symbol we pop the last opening symbol off the stack and compare them. If they are not paired, we have a syntax error and can calculate the score.

```elm {l}
errorScore : Deque Char -> List Char -> Int
errorScore stack todo =
    case todo of
        [] ->
            0

        sym :: tl ->
            if List.member sym [ '(', '[', '{', '<' ] then
                errorScore (Deque.pushFront sym stack) tl

            else
                case Deque.popFront stack of
                    ( Just last, stack2 ) ->
                        if sym == ')' && last /= '(' then
                            3

                        else if sym == ']' && last /= '[' then
                            57

                        else if sym == '}' && last /= '{' then
                            1197

                        else if sym == '>' && last /= '<' then
                            25137

                        else
                            errorScore stack2 tl

                    _ ->
                        0
```

The answer is the sum of all error scores, with non-error lines scoring nothing.

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.foldl (\cs -> (+) (errorScore Deque.empty cs)) 0
```

---

## Part 2

Find the completion string for each incomplete line, score the completion strings, and sort the scores. **What is the middle score?**

The symbols to complete will be the pairs of those remaining on the stack after processing each line. To calculate the score we don't need to convert the opening symbols on the stack to their paired closing symbol as we know that all lines are valid after filtering out the syntax errors.

```elm {l}
autocompleteScore : List Char -> Int
autocompleteScore syms =
    let
        process sym stack =
            if List.member sym [ '(', '[', '{', '<' ] then
                Deque.pushFront sym stack

            else
                case Deque.popFront stack of
                    ( Just _, stack2 ) ->
                        stack2

                    _ ->
                        stack

        score sym =
            case sym of
                '(' ->
                    1

                '[' ->
                    2

                '{' ->
                    3

                _ ->
                    4

        accScore sym total =
            (total * 5) + score sym

        incomplete =
            List.foldl process Deque.empty syms |> Deque.toList
    in
    List.foldl accScore 0 incomplete
```

```elm {l r}
part2 : Maybe Int
part2 =
    puzzleInput
        |> parse
        |> List.filter (\s -> errorScore Deque.empty s == 0)
        |> List.map autocompleteScore
        |> AOC.median
```

---

## Reflection

An easier puzzle which makes good use of a stack for keeping track of parsed symbols.
