---
follows: data/d11_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2021): Dumbo Octopus

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/11).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-11).

## Part 1

Given the starting energy levels of the dumbo octopuses in your cavern, simulate 100 steps. **How many total flashes are there after 100 steps?**

### Parsing

As with [day 9](d09_2021.md) we can read input values into a grid.

```elm {l}
type alias Grid =
    AOC.Grid Int


parse : List String -> Grid
parse =
    List.map (String.split "" >> List.map AOC.toInt)
        >> AOC.gFromLists
```

### Algorithm

Incrementing a cell's value will propagate incrementation to neighbouring cells when it increments from 9 to 10.

We can represent the window coordinates as a list of displacements.

```elm {l}
displacements : List ( Int, Int )
displacements =
    [ ( -1, -1 ), ( -1, 0 ), ( -1, 1 ), ( 0, -1 ), ( 0, 1 ), ( 1, -1 ), ( 1, 0 ), ( 1, 1 ) ]
```

```elm {l}
increment : ( Int, Int ) -> Grid -> Grid
increment ( row, col ) grid =
    case AOC.gGet ( row, col ) grid of
        Nothing ->
            grid

        Just z ->
            if z == 9 then
                List.foldl (\( dR, dC ) g -> increment ( row + dR, col + dC ) g)
                    (AOC.gSet ( row, col ) (z + 1) grid)
                    displacements

            else
                AOC.gSet ( row, col ) (z + 1) grid
```

On each cycle we increment values, count the number that have exceeded 9 and reset those back to 0.

```elm {l}
tick : ( Grid, Int ) -> ( Grid, Int )
tick ( grid, totalScore ) =
    let
        cells =
            AOC.gMapWithLocation (\pos _ -> pos) grid |> AOC.gToList

        grid2 =
            List.foldl increment grid cells
                |> AOC.gMap
                    (\z ->
                        if z > 9 then
                            0

                        else
                            z
                    )

        score =
            AOC.gToList grid2
                |> List.filter ((==) 0)
                |> List.length
    in
    ( grid2, totalScore + score )
```

```elm {l r}
part1 : Int
part1 =
    AOC.iterate 100 ( parse puzzleInput, 0 ) tick
        |> Tuple.second
```

---

## Part 2

**What is the first step during which all octopuses flash?**

We just continue ticking though cycles until the number of flashes that occur in a single cycle is 100.

```elm {l}
stepsToAllLit : Int -> Grid -> Int
stepsToAllLit n grid =
    let
        ( grid2, score ) =
            tick ( grid, 0 )
    in
    if score == 100 then
        n + 1

    else
        stepsToAllLit (n + 1) grid2
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> stepsToAllLit 0
```

---

## Reflection

Another relatively easy puzzle that benefits from a grid array structure to keep track of state. Good to see yet another variation of cellular automata, this time with propagation outside a cell's immediate focal window.
