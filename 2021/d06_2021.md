---
follows: data/d06_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2021): Lanternfish

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/6).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-6).

## Part 1

Find a way to simulate lanternfish. **How many lanternfish would there be after 80 days?**

### Algorithm

Rather than keep track of individual fish we can recognise that all fish can only be in one of 9 chronologically ordered stages in their breeding cycle: stage 0 when it gives birth, stages 1-6 in its normal reproductive cycle and stages 7-8 when newly born.

Therefore all we need to do is keep track of how many fish are in each stage. As every day passes, we rotate the numbers counted at each stage (moving each group of fish at the same stage to the next one in their life-cycle). We then add the number at stage 0 to those at stage 6 so the recent parents and continue the reproductive cycle.

```elm {l}
incDay : List Int -> List Int
incDay freqs =
    let
        numBirths =
            List.head freqs |> Maybe.withDefault 0
    in
    freqs
        |> List.reverse
        |> AOC.rotateList
        |> List.reverse
        |> AOC.setListAt 6 (AOC.getWithDefault 0 7 freqs + numBirths)
```

The answer will be the sum of these totals after the given number of days.

```elm {l}
simulate : Int -> List Int -> Int
simulate numDays input =
    let
        init =
            Dict.fromList (List.range 0 8 |> List.map (\n -> ( n, 0 )))

        freqs =
            input
                |> List.foldl AOC.addToFreqTable init
                |> Dict.values
    in
    AOC.iterate numDays freqs incDay
        |> List.sum
```

```elm {l r}
part1 : Int
part1 =
    simulate 80 puzzleInput
```

---

## Part 2

**How many lanternfish would there be after 256 days?**

We just iterate for this larger number of days.

```elm {l r}
part2 : Int
part2 =
    simulate 256 puzzleInput
```

---

## Reflection

A classic AoC problem in that the mechanics of a process are described and a part one solution can be derived by directly simulating the mechanics, but its exponential form rules this out for a part 2 with a larger number of iterations.

My first attempt tried to avoid the problem by modelling the number of descendants a fish starting at a given stage would have after a given number of days. While this didn't require storing the list of fish explicitly, the recursive approach soon hit the infeasibility wall beyond 150 iterations or so.

I then wasted much time trying to come up with an analytical exponential function but to no avail.

Finally, after a break I looked at the worked example in the question and realised the repeating cycles meant we only needed to store the number of fish at each of the 9 life-cycle stages and then iterate once per day. A satisfying solution that can be solved in under a millisecond.
