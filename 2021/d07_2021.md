---
follows: data/d07_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2021): The Treachery of Whales

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/7).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-7).

## Part 1

Determine the horizontal position that the crabs can align to using the least fuel possible. **How much fuel must they spend to align to that position?**

### Algorithm

Determining the cost at any given position is simply the sum of the differences between each crab's position and the target position.

Because cost is linear with distance, the lowest cost will occur at least at one of the existing crab positions.

If we sort unique crab positions first, costs should decrease towards a global minimum before increasing again. We can therefore return the first cost that is not less then the previous one.

```elm {l}
leastCost : (List Int -> Int -> Int) -> List Int -> Int
leastCost cost input =
    let
        candidatePos =
            input |> Set.fromList |> Set.toList

        pairs =
            List.map2 Tuple.pair candidatePos (List.drop 1 candidatePos)
    in
    AOC.dropWhile (\( c1, c2 ) -> cost input c1 > cost input c2) pairs
        |> List.head
        |> Maybe.withDefault ( -1, 0 )
        |> Tuple.first
        |> cost input
```

```elm {l r}
part1 : Int
part1 =
    let
        cost crabs pos =
            crabs |> List.map ((-) pos >> abs) |> List.sum
    in
    puzzleInput
        |> leastCost cost
```

---

## Part 2

Determine the horizontal position that the crabs can align to using the least fuel possible so they can make you an escape route! **How much fuel must they spend to align to that position?**

The new cost function is the nth triangular number of the number of movement steps. Again, because cost increases with distance, a minimum will fall on one of the crab positions. Therefore we can simply provide this new triangular number cost function to the same minimum cost procedure.

```elm {l r}
part2 : Int
part2 =
    let
        triangular n =
            (n * n + n) // 2

        cost crabs pos =
            crabs |> List.map ((-) pos >> abs >> triangular) |> List.sum
    in
    puzzleInput
        |> leastCost cost
```

---

## Reflection

A nice simple puzzle with plenty of scope for different optimisations, or even a brute force search. I compromised with a sorted descent search, but looking at reddit, a faster one would be to jump straight to the median crab position for part one and the rounded mean position for part two. Given the cost functions are quite cheap, there's only a marginal gain to be had here.
