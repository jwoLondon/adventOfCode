---
follows: data/d09_2021.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2021): Smoke Basin

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2021/day/9).

See also, my [Observable JavaScript solution](https://observablehq.com/@jwolondon/advent-of-code-2021-day-9).

## Part 1

Find all of the low points on your heightmap. **What is the sum of the risk levels of all low points on your heightmap?**

### Parsing

Store the input DEM cells as a grid.

```elm {l}
type alias DEM =
    AOC.Grid Int


parse : List String -> DEM
parse =
    List.map (String.split "" >> List.map AOC.toInt)
        >> AOC.gFromLists
```

### Algorithm

A pit has higher rook's-case neighbours. We store the elevation and location of all pits identified.

```elm {l}
findPits : DEM -> List ( Int, Int, Int )
findPits dem =
    let
        isPit ( row, col ) z =
            if
                [ AOC.gGet ( row - 1, col ) dem
                , AOC.gGet ( row + 1, col ) dem
                , AOC.gGet ( row, col - 1 ) dem
                , AOC.gGet ( row, col + 1 ) dem
                ]
                    |> List.filterMap identity
                    |> List.all ((<) z)
            then
                Just ( row, col, z )

            else
                Nothing
    in
    dem
        |> AOC.gMapWithLocation isPit
        |> AOC.gToList
        |> List.filterMap identity
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> findPits
        |> List.map (AOC.tripletThird >> (+) 1)
        |> List.sum
```

---

## Part 2

**What do you get if you multiply together the sizes of the three largest basins?**

To find a basin we start at a pit and climb to all neighbours that are less than height 9, setting each cell to -1 as we climb.

```elm {l}
fill : ( Int, Int ) -> DEM -> DEM
fill ( row, col ) dem =
    case AOC.gGet ( row, col ) dem of
        Just z ->
            if z >= 0 && z < 9 then
                List.foldl (\pos dem2 -> fill pos dem2)
                    (AOC.gSet ( row, col ) -1 dem)
                    [ ( row - 1, col )
                    , ( row + 1, col )
                    , ( row, col - 1 )
                    , ( row, col + 1 )
                    ]

            else
                dem

        Nothing ->
            dem
```

The basin size is number of cells that have been set to -1.

```elm {l}
basinSize : DEM -> ( Int, Int, Int ) -> Int
basinSize dem ( row, col, _ ) =
    fill ( row, col ) dem
        |> AOC.gToList
        |> List.filter ((>) 0)
        |> List.length
```

From the basin sizes we select the three largest and find their product.

```elm {l r}
part2 : Int
part2 =
    let
        dem =
            parse puzzleInput
    in
    dem
        |> findPits
        |> List.map (basinSize dem)
        |> List.sort
        |> List.reverse
        |> List.take 3
        |> List.product
```

---

## Reflection

A much simpler puzzle than the previous day, and one that happens to be close to the topic of my (25 year old) PhD. Benefits from an addressable 2d grid structure to perform window focal operations.
