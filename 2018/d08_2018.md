---
follows: data/d08_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2018): Memory Maneuver

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/8):

{( part1|}

The sleigh is much easier to pull than you'd expect for something its weight. Unfortunately, neither you nor the Elves know which way the North Pole is from here.

You check your wrist device for anything that might help. It seems to have some kind of navigation system! Activating the navigation system produces more bad news: "Failed to start navigation system. Could not read software license file."

The navigation system's license file consists of a list of numbers (your puzzle input). The numbers define a data structure which, when processed, produces some kind of tree that can be used to calculate the license number.

The **tree** is made up of **nodes**; a single, outermost node forms the tree's **root**, and it contains all other nodes in the tree (or contains nodes that contain nodes, and so on).

Specifically, a node consists of:

- A **header**, which is always exactly two numbers:
- The quantity of child nodes.
- The quantity of metadata entries.
- Zero or more **child nodes** (as specified in the header).
- One or more **metadata entries** (as specified in the header).

Each child node is itself a node that has its own header, child nodes, and metadata. For example:

    2 3 0 3 10 11 12 1 1 0 1 99 2 1 1 2
    A----------------------------------
        B----------- C-----------
                         D-----

In this example, each node of the tree is also marked with an underline starting with a letter for easier identification. In it, there are four nodes:

- A, which has 2 child nodes (B, C) and 3 metadata entries (1, 1, 2).
- B, which has 0 child nodes and 3 metadata entries (10, 11, 12).
- C, which has 1 child node (D) and 1 metadata entry (2).
- D, which has 0 child nodes and 1 metadata entry (99).

The first check done on the license file is to simply add up all of the metadata entries. In this example, that sum is 1+1+2+10+11+12+2+99=138.

What is the sum of all metadata entries?

{| part1 )}

## Data Structure

Tree structures can be elegantly represented in Elm with a recursive custom type that contains both a node's metadata and a list of any children. Unlike binary trees, there is no need to explicitly account for leaf nodes as here they are simply defined as a `Node` with an empty list of children.

```elm {l}
type Tree
    = Node (List Int) (List Tree)
```

## Approach

Recursively building a tree with a fixed number of child nodes is a simple task, but here nodes can have a variable number of children, so is a little more complicated. Here we create a function to iterate through all the children of each node as we read in the serialized input (`siblings`).

Because a node's serialized metadata are separated from its initial definition, we need to keep track of the remaining serialized input as we recurse through intervening child nodes.

```elm {l}
buildTree : List Int -> ( Tree, List Int )
buildTree input =
    let
        siblings n remainingInput nodes =
            if n == 0 then
                ( nodes, remainingInput )

            else
                let
                    ( node, remaining ) =
                        buildTree remainingInput
                in
                siblings (n - 1) remaining (nodes ++ [ node ])
    in
    case input of
        nChildren :: nMeta :: tl ->
            let
                ( children, remainingInput ) =
                    siblings nChildren tl []

                ( processed, toProcess ) =
                    splitAt nMeta remainingInput
            in
            ( Node processed children, toProcess )

        _ ->
            ( Node [] [], [] )
```

Once the tree has been built, it is a simple task to traverse it, accumulating the sum of all metadata:

```elm {l}
sumMetadata : Tree -> Int
sumMetadata (Node meta children) =
    List.sum meta + List.foldl (\tree -> (+) (sumMetadata tree)) 0 children
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> List.map toInt
        |> buildTree
        |> Tuple.first
        |> sumMetadata
```

{( part2 |}

The second check is slightly more complicated: you need to find the value of the root node (A in the example above).

The **value of a node** depends on whether it has child nodes.

If a node has **no child nodes**, its value is the sum of its metadata entries. So, the value of node B is 10+11+12=33, and the value of node D is 99.

However, if a node **does have child nodes**, the metadata entries become indexes which refer to those child nodes. A metadata entry of 1 refers to the first child node, 2 to the second, 3 to the third, and so on. The value of this node is the sum of the values of the child nodes referenced by the metadata entries. If a referenced child node does not exist, that reference is skipped. A child node can be referenced multiple time and counts each time it is referenced. A metadata entry of 0 does not refer to any child node.

For example, again using the above nodes:

- Node C has one metadata entry, 2. Because node C has only one child node, 2 references a child node which does not exist, and so the value of node C is 0.
- Node A has three metadata entries: 1, 1, and 2. The 1 references node A's first child node, B, and the 2 references node A's second child node, C. Because node B has a value of 33 and node C has a value of 0, the value of node A is 33 + 33 + 0 = 66.

So, in this example, the value of the root node is 66.

**What is the value of the root node?**

{| part2 )}

This is again a simple task once we have built the tree of nodes. We traverse the tree selecting the child nodes corresponding to the indexes in the metadata. Any indices that are out of bounds are removed with a `filterMap`.

```elm {l}
nodeValue : Tree -> Int
nodeValue (Node meta children) =
    if children == [] then
        List.sum meta

    else
        meta
            |> List.filterMap (\n -> children |> List.drop (n - 1) |> List.head)
            |> List.map nodeValue
            |> List.sum
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.map toInt
        |> buildTree
        |> Tuple.first
        |> nodeValue
```

## Reflection

This is a puzzle which suits Elm (and other functional languages) well as recursive custom types and functions allow easy traversal of trees. Creating the `Tree` type was the obvious approach although it should be possible to process the serialized input accumulating metadata directly. I decided against doing this in anticipation that the second part of the puzzle would require tree traversal.

While the eventual solution was relatively simple, I did spend way too long struggling to find a way of keeping track of the read position of input data when iteratively processing a node's multiple children. After decades of imperative programming, I still struggle with intuitive recursive thinking. I should probably spend more time working with trees and graphs (perhaps the next two weeks of AoC will oblige).
