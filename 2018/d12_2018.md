---
follows: data/d12_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 12 (2018): Subterranean Sustainability

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/12).

{( part1|}

After 20 generations, what is the sum of the numbers of all pots which contain a plant?

{| part1 )}

## Data Structure

While not strictly necessary, it is convenient to store the row of pots in a record that separates the left, current and right pots as described in the question.

```elm {l}
type alias Pots =
    { left : List Char
    , current : Char
    , right : List Char
    }
```

We can then build a set of functions for moving along the line of pots, keeping track of the 'current' one. The list of left pots is reversed to make it more efficient to extract and insert pots closest to the current one via the list's head.

```elm {l}
initPots : String -> Pots
initPots input =
    case String.toList input of
        hd :: tl ->
            Pots [] hd tl

        _ ->
            Pots [] '.' []


moveRight : Pots -> Pots
moveRight pots =
    Pots (pots.current :: pots.left)
        (List.head pots.right |> Maybe.withDefault '.')
        (List.drop 1 pots.right)


moveLeft : Pots -> Pots
moveLeft pots =
    Pots (List.drop 1 pots.left)
        (List.head pots.left |> Maybe.withDefault '.')
        (pots.current :: pots.right)
```

We can extract the 'window' of five pots centred on the current pot by taking the first two items from the left and right lists and inserting the current pot in the middle. We pad the lists with two empty pots for cases where there are fewer than two items in the list.

```elm {l}
window : Pots -> List Char
window pots =
    List.reverse (pots.left ++ [ '.', '.' ] |> List.take 2)
        ++ (pots.current :: List.take 2 (pots.right ++ [ '.', '.' ]))
```

## Approach

This problem is describing a one-dimensional cellular automaton. It involves iteratively applying a rule set to a cell based on its state and the state of its neighbours. We can apply that process here to calculate the score after a number of applications of the rule set.

### Parsing the rules

We don't need to import all the rules to solve this problem as we can default to 'no grow' if a pattern does not match. So parsing the input data searches only for strings that end with a `#`.

All the 'grow' rules are added to a dictionary as they are parsed.

```elm {l}
type alias Rules =
    Dict (List Char) Char


growRules : Rules
growRules =
    let
        growRule =
            AOC.submatches "(.+) => #"
                >> List.filterMap identity
                >> List.concatMap String.toList
    in
    puzzleInput
        |> List.foldl (\line -> Dict.insert (growRule line) '#') Dict.empty
        >> Dict.remove []
```

Once we have the rule set we can search for rule patterns around the current pot to generate its next state:

### Applying the rules

```elm {l}
grow : Pots -> Rules -> Char
grow pots =
    Dict.get (window pots) >> Maybe.withDefault '.'
```

We can apply the rules to the current pot, moving both left and right across the line of pots.

```elm {l}
nextGeneration : Pots -> Pots
nextGeneration pots =
    let
        growRight grownPots ps =
            if not (List.member '#' ((List.head ps.left |> Maybe.withDefault '.') :: ps.current :: ps.right)) then
                List.reverse grownPots

            else
                let
                    newPots =
                        moveRight ps
                in
                growRight (grow newPots growRules :: grownPots) newPots

        growLeft grownPots ps =
            if not (List.member '#' ((List.head ps.right |> Maybe.withDefault '.') :: ps.current :: ps.left)) then
                List.reverse grownPots

            else
                let
                    newPots =
                        moveLeft ps
                in
                growLeft (grow newPots growRules :: grownPots) newPots
    in
    Pots (growLeft [] pots) (grow pots growRules) (growRight [] pots)
```

## Calculating the pot score

Finding the score for any given line of pots just requires us to search for each `#` in an indexed version of the lists and subtracting the left score from the right score.

```elm {l}
score : Pots -> Int
score pots =
    let
        potScores =
            List.indexedMap
                (\i c ->
                    if c == '#' then
                        i + 1

                    else
                        0
                )
                >> List.sum
    in
    potScores pots.right - potScores pots.left
```

Iterating 20 times gives us the answer.

```elm {l r}
part1 : Int
part1 =
    AOC.iterate 20 (initPots initialStateInput) nextGeneration
        |> score
```

{( part2 |}

After fifty billion (50000000000) generations, what is the sum of the numbers of all pots which contain a plant?

{| part2 )}

The number of iterations required to answer the second part suggests that iterating isn't going to give us a practical solution.

Recognising that we are modelling a cellular automaton suggests that the pattern may reach some kind of stability after a fixed number of iterations. Once that stability has been reached, we can create the possible change in score as a function of the number of stable generations.

One of the difficulties with this approach is characterising stability. Some automata simply stop, in which case their score never changes. Others such as [oscillators, gliders and guns](<https://en.wikipedia.org/wiki/Gun_(cellular_automaton)>) repeat over a finite period so could in have a predictable effect on the total score.

One simple approach that works with the input data and ruleset is to check for repeated patterns with a period of 1 (i.e. the arrangement of grown pots does not change between consecutive generations, even it if 'moves' along the line).

We can test for this by trimming the row of pots between the leftmost and rightmost grown pots and comparing the arrangement with its next iteration. The difference in score between such a matched pair gives us the coefficient necessary to predict the score and the number of iterations (and score at that point) at which the match is first detected gives us a constant to add:

```elm {l}
trimmed : Pots -> String
trimmed pots =
    List.reverse pots.left
        ++ (pots.current :: pots.right)
        |> String.fromList
        |> String.replace "." " "
        |> String.trim


growToSteadyState : Int -> Pots -> ( Int, Int, Int )
growToSteadyState iteration pots =
    let
        nextPots =
            nextGeneration pots
    in
    if trimmed pots == trimmed nextPots then
        ( iteration, score pots, score nextPots - score pots )

    else
        growToSteadyState (iteration + 1) nextPots
```

```elm {l r}
part2 : Int
part2 =
    let
        ( iterationsToSteady, scoreToSteady, incScore ) =
            growToSteadyState 0 (initPots initialStateInput)
    in
    scoreToSteady + (50000000000 - iterationsToSteady) * incScore
```

---

## Reflection

Another entertaining puzzle that forced us to abandon any brute-force approach to solving it in part 2. I probably over-engineered the data structure for solving part one, as I was expecting to have to use a reasonably efficient one for part two. The scale of part 2 soon forced me to abandon that approach.

It took me a long time to get to the stability modelling approach that I eventually used to solve the problem. What prompted me to get there in the end was thinking about gliders in Conway's Game of Life. Initially I dismissed the idea as I thought they would be too hard to detect. I only speculatively tried to search for 1-period gliders and was pleasantly surprised to detect them. This could have been much harder had not all plants turned into gliders, or some headed on different directions (so the trimming technique would not have worked).

If all had turned into gliders but with longer periods, comparing adjacent generations would not have worked. But one could store a set of states and compare each new one to see if it had occurred before. This would at least make the approach suitable for a wider range of possible configurations/rulesets, although in no way guaranteeing a solution.

I find puzzles like part b the most rewarding of the AoC puzzle types. It's not simply a grind coding up some data structure, but rather requires a but of creative problem solving to 'get' the trick leading to a solution.
