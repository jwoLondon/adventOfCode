---
follows: data/d03_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2018): No Matter How You Slice It

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/3).

{( part1|}

How many square inches of fabric are within two or more claims?

{| part1 )}

## Parsing and Data Structures

We can represent a rectangle of material in a record containing its ID and dimensions.

```elm {l}
type alias Rect =
    { id : Int
    , x : Int
    , y : Int
    , w : Int
    , h : Int
    }
```

This makes it quite straightforward to parse the input data and store as a list of `Rect`:

```elm {l}
parse : List String -> List Rect
parse =
    let
        rectValues =
            AOC.submatches "#(\\d+) @ (\\d+),(\\d+): (\\d+)x(\\d+)"
                >> List.filterMap identity
                >> List.map AOC.toInt

        parseLine line =
            case rectValues line of
                id :: x :: y :: w :: h :: [] ->
                    Rect id x y w h

                _ ->
                    Rect 0 0 0 0 0
    in
    List.map parseLine
```

There are several ways we could represent all the rectangles positioned on the fabric. One would be to store all locations as a frequency table, with the key being the location and the frequency being the number of overlapping rectangles. This has the advantage of being reasonably scalable to an arbitrary spatial extent. It is particularly suitable if the rectangles are dispersed and sparsely cover the fabric.

An alternative is to represent the entire rectangle of fabric as an array of integers (using `Grid` from [Aoc.elm](../src/Aoc.elm)). This is what we do below:

```elm {l}
type alias Fabric =
    AOC.Grid Int


gridSize : Int
gridSize =
    1000
```

## Approach

To find out how much of the fabric has an overlap of more than one rectangle, we can store the number of overlapping rectangles at each grid position by incrementing every location within each rectangle. That is, each grid cell will store how many rectangles overlap its location.

```elm {l}
addRectangle : Rect -> Fabric -> Fabric
addRectangle rectangle fabric =
    let
        traverseRect r ( x, y ) grid =
            if y >= r.y + r.h then
                grid

            else if x >= r.x + r.w then
                traverseRect r ( r.x, y + 1 ) grid

            else
                let
                    gridValue =
                        AOC.gGet ( x, y ) grid |> Maybe.withDefault 0
                in
                traverseRect r ( x + 1, y ) (AOC.gSet ( x, y ) (gridValue + 1) grid)
    in
    traverseRect rectangle ( rectangle.x, rectangle.y ) fabric


overlapFabric : Fabric
overlapFabric =
    parse puzzleInput
        |> List.foldl addRectangle (AOC.gInit gridSize gridSize 0)
```

Once we've accumulated all rectangles we can just filter those values greater than 1 and count them.

```elm {l r}
part1 : Int
part1 =
    overlapFabric
        |> AOC.gToList
        |> List.filter (\n -> n > 1)
        |> List.length
```

{( part2 |}

What is the ID of the only claim that doesn't overlap?

{| part2 )}

If we compare each rectangle with the fabric after all rectangles have been added, any that have no overlaps should be composed entirely of 1s.
And therefore, the sum of the values for such a rectangle should equal its area:

```elm {l}
checkRect : Fabric -> Rect -> Int -> Int
checkRect grid rect id =
    let
        traverseRect r ( x, y ) total =
            if id /= 0 then
                id

            else if y >= r.y + r.h then
                if total == r.w * r.h then
                    r.id

                else
                    0

            else if x >= r.x + r.w then
                traverseRect r ( r.x, y + 1 ) total

            else
                let
                    gVal =
                        AOC.gGet ( x, y ) grid |> Maybe.withDefault -9999999
                in
                traverseRect r ( x + 1, y ) (total + gVal)
    in
    traverseRect rect ( rect.x, rect.y ) id
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> List.foldl (checkRect overlapFabric) 0
```

---

## Reflection

The wording of the question directs us towards representing the fabric with some kind of array-based rectangle. My mapping and visualization background just reinforces this approach to representation. But it is probably not the most efficient in this case, especially as the rectangles in the puzzle input are comparatively sparse (there are many parts of the fabric that are not covered by any rectangles).

I find the challenge, especially when trying to complete the puzzle in a short time, is to avoid automatically jumping on the most obvious data structure and approach. Stepping back and thinking about alternatives before committing to any one approach is probably a better strategy. Today, it was only while walking to work after having completed the puzzle as described above, that it occurred to me that it might have been better to use a dictionary based representation.

I wonder too, if using a dictionary might allow a more efficient solution to be found where a set of 'covered once' locations (for part 2) could be incrementally updated along with a running total of the area covered more than once (for part 1). If a solution is possible that way, it would probably require the input rectangles to be sorted in some way first.
