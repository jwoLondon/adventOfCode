---
follows: data/d24_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 24 (2018): Immune System Simulator 20XX

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/24):

{( part1|}

After a weird buzzing noise, you appear back at the man's cottage. He seems relieved to see his friend, but quickly notices that the little reindeer caught some kind of cold while out exploring.

The portly man explains that this reindeer's immune system isn't similar to regular reindeer immune systems:

The **immune system** and the **infection** each have an army made up of several **groups**; each **group** consists of one or more identical units. The armies repeatedly **fight** until only one army has units remaining.

**Units** within a group all have the same **hit points** (amount of damage a unit can take before it is destroyed), **attack damage** (the amount of damage each unit deals), an **attack type**, an initiative (higher **initiative** units attack first and win ties), and sometimes **weaknesses** or **immunities**. Here is an example group:

    18 units each with 729 hit points (weak to fire; immune to cold, slashing) with an attack that does 8 radiation damage at initiative 10

Each group also has an **effective power**: the number of units in that group multiplied by their attack damage. The above group has an effective power of 18 \* 8 = 144. Groups never have zero or negative units; instead, the group is removed from combat.

Each **fight** consists of two phases: **target selection** and **attacking**.

During the **target selection** phase, each group attempts to choose one target. In decreasing order of effective power, groups choose their targets; in a tie, the group with the higher initiative chooses first. The attacking group chooses to target the group in the enemy army to which it would deal the most damage (after accounting for weaknesses and immunities, but not accounting for whether the defending group has enough units to actually receive all of that damage).

If an attacking group is considering two defending groups to which it would deal equal damage, it chooses to target the defending group with the largest effective power; if there is still a tie, it chooses the defending group with the highest initiative. If it cannot deal any defending groups damage, it does not choose a target. Defending groups can only be chosen as a target by one attacking group.

At the end of the target selection phase, each group has selected zero or one groups to attack, and each group is being attacked by zero or one groups.

During the **attacking** phase, each group deals damage to the target it selected, if any. Groups attack in decreasing order of initiative, regardless of whether they are part of the infection or the immune system. (If a group contains no units, it cannot attack.)

The damage an attacking group deals to a defending group depends on the attacking group's attack type and the defending group's immunities and weaknesses. By default, an attacking group would deal damage equal to its **effective power** to the defending group. However, if the defending group is **immune** to the attacking group's attack type, the defending group instead takes **no damage**; if the defending group is **weak** to the attacking group's attack type, the defending group instead takes **double damage**.

The defending group only loses **whole units** from damage; damage is always dealt in such a way that it kills the most units possible, and any remaining damage to a unit that does not immediately kill it is ignored. For example, if a defending group contains 10 units with 10 hit points each and receives 75 damage, it loses exactly 7 units and is left with 3 units at full health.

After the fight is over, if both armies still contain units, a new fight begins; combat only ends once one army has lost all of its units.

For example, consider the following armies:

    Immune System:
    17 units each with 5390 hit points (weak to radiation, bludgeoning) with
    an attack that does 4507 fire damage at initiative 2
    989 units each with 1274 hit points (immune to fire; weak to bludgeoning,
    slashing) with an attack that does 25 slashing damage at initiative 3

    Infection:
    801 units each with 4706 hit points (weak to radiation) with an attack
    that does 116 bludgeoning damage at initiative 1
    4485 units each with 2961 hit points (immune to radiation; weak to fire,
    cold) with an attack that does 12 slashing damage at initiative 4

If these armies were to enter combat, the following fights, including details during the target selection and attacking phases, would take place:

    Immune System:
    Group 1 contains 17 units
    Group 2 contains 989 units
    Infection:
    Group 1 contains 801 units
    Group 2 contains 4485 units

    Infection group 1 would deal defending group 1 185832 damage
    Infection group 1 would deal defending group 2 185832 damage
    Infection group 2 would deal defending group 2 107640 damage
    Immune System group 1 would deal defending group 1 76619 damage
    Immune System group 1 would deal defending group 2 153238 damage
    Immune System group 2 would deal defending group 1 24725 damage

    Infection group 2 attacks defending group 2, killing 84 units
    Immune System group 2 attacks defending group 1, killing 4 units
    Immune System group 1 attacks defending group 2, killing 51 units
    Infection group 1 attacks defending group 1, killing 17 units

    Immune System:
    Group 2 contains 905 units
    Infection:
    Group 1 contains 797 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 184904 damage
    Immune System group 2 would deal defending group 1 22625 damage
    Immune System group 2 would deal defending group 2 22625 damage

    Immune System group 2 attacks defending group 1, killing 4 units
    Infection group 1 attacks defending group 2, killing 144 units

    Immune System:
    Group 2 contains 761 units
    Infection:
    Group 1 contains 793 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 183976 damage
    Immune System group 2 would deal defending group 1 19025 damage
    Immune System group 2 would deal defending group 2 19025 damage

    Immune System group 2 attacks defending group 1, killing 4 units
    Infection group 1 attacks defending group 2, killing 143 units

    Immune System:
    Group 2 contains 618 units
    Infection:
    Group 1 contains 789 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 183048 damage
    Immune System group 2 would deal defending group 1 15450 damage
    Immune System group 2 would deal defending group 2 15450 damage

    Immune System group 2 attacks defending group 1, killing 3 units
    Infection group 1 attacks defending group 2, killing 143 units

    Immune System:
    Group 2 contains 475 units
    Infection:
    Group 1 contains 786 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 182352 damage
    Immune System group 2 would deal defending group 1 11875 damage
    Immune System group 2 would deal defending group 2 11875 damage

    Immune System group 2 attacks defending group 1, killing 2 units
    Infection group 1 attacks defending group 2, killing 142 units

    Immune System:
    Group 2 contains 333 units
    Infection:
    Group 1 contains 784 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 181888 damage
    Immune System group 2 would deal defending group 1 8325 damage
    Immune System group 2 would deal defending group 2 8325 damage

    Immune System group 2 attacks defending group 1, killing 1 unit
    Infection group 1 attacks defending group 2, killing 142 units

    Immune System:
    Group 2 contains 191 units
    Infection:
    Group 1 contains 783 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 181656 damage
    Immune System group 2 would deal defending group 1 4775 damage
    Immune System group 2 would deal defending group 2 4775 damage

    Immune System group 2 attacks defending group 1, killing 1 unit
    Infection group 1 attacks defending group 2, killing 142 units

    Immune System:
    Group 2 contains 49 units
    Infection:
    Group 1 contains 782 units
    Group 2 contains 4434 units

    Infection group 1 would deal defending group 2 181424 damage
    Immune System group 2 would deal defending group 1 1225 damage
    Immune System group 2 would deal defending group 2 1225 damage

    Immune System group 2 attacks defending group 1, killing 0 units
    Infection group 1 attacks defending group 2, killing 49 units

    Immune System:
    No groups remain.
    Infection:
    Group 1 contains 782 units
    Group 2 contains 4434 units

In the example above, the winning army ends up with 782 + 4434 = 5216 units.

You scan the reindeer's condition (your puzzle input); the white-bearded man looks nervous. As it stands now, **how many units would the winning army have?**

{| part1 )}

## Parsing and data structures

The relatively complex set of attributes of the 'unita' that make up a 'groupa' that make up 'armies' can be represented with appropriate records and custom types.

### Units

```elm {l}
type alias Unit =
    { hp : Int
    , imm : List Attack
    , weak : List Attack
    , dam : Int
    , atk : Attack
    , initiative : Int
    }


type Attack
    = Radiation
    | Bludgeon
    | Cold
    | Fire
    | Slash
```

### Groups

```elm {l}
type Side
    = ImmuneSystem
    | Infection


type alias Group =
    { nUnits : Int
    , unit : Unit
    , side : Side
    }
```

### Parsing

The number of input items is relatively small (20) but each is relatively complex (for example, the order of weak and immune defences varies between lines), so for now, rather than creating a regex for parsing, we will define the immune system and infection groups directly.

```elm {l}
parseImmune : List Group
parseImmune =
    [ Group 6638 (Unit 2292 [] [ Radiation ] 3 Cold 18) ImmuneSystem
    , Group 3906 (Unit 12319 [ Bludgeon, Cold, Fire ] [] 24 Cold 14) ImmuneSystem
    , Group 2114 (Unit 7254 [ Fire, Radiation ] [] 25 Radiation 12) ImmuneSystem
    , Group 3157 (Unit 1184 [] [ Bludgeon ] 3 Cold 9) ImmuneSystem
    , Group 502 (Unit 10367 [] [ Bludgeon ] 181 Fire 13) ImmuneSystem
    , Group 1017 (Unit 10287 [] [] 88 Cold 1) ImmuneSystem
    , Group 20 (Unit 1333 [ Radiation, Slash ] [ Bludgeon ] 508 Fire 3) ImmuneSystem
    , Group 6263 (Unit 6146 [] [] 9 Cold 2) ImmuneSystem
    , Group 5294 (Unit 7781 [ Fire ] [ Slash ] 12 Slash 5) ImmuneSystem
    , Group 807 (Unit 4206 [] [ Slash, Bludgeon ] 44 Fire 7) ImmuneSystem
    ]


parseInfection : List Group
parseInfection =
    [ Group 1756 (Unit 36633 [ Bludgeon ] [] 38 Bludgeon 17) Infection
    , Group 1087 (Unit 39027 [] [] 64 Slash 10) Infection
    , Group 2744 (Unit 49920 [] [ Slash ] 30 Fire 4) Infection
    , Group 3849 (Unit 21334 [] [ Bludgeon ] 10 Cold 6) Infection
    , Group 631 (Unit 37995 [] [ Radiation ] 85 Fire 11) Infection
    , Group 770 (Unit 45051 [] [ Radiation, Slash ] 114 Radiation 20) Infection
    , Group 1742 (Unit 41529 [ Cold, Fire ] [] 46 Bludgeon 19) Infection
    , Group 139 (Unit 31968 [] [ Slash ] 458 Cold 16) Infection
    , Group 5191 (Unit 28059 [] [] 8 Radiation 8) Infection
    , Group 5797 (Unit 33966 [] [] 11 Cold 15) Infection
    ]
```

We can create simple methods for extracting the attributes we will need from these records:

```elm {l}
power : Group -> Int
power grp =
    grp.nUnits * grp.unit.dam


powerThenInitiative : Group -> ( Int, Int )
powerThenInitiative grp =
    ( -1 * grp.nUnits * grp.unit.dam, -1 * grp.unit.initiative )


damageThenPowerThenInitiative : Group -> Group -> ( Int, Int, Int )
damageThenPowerThenInitiative aGrp dGrp =
    ( -1 * damage aGrp dGrp
    , -1 * dGrp.nUnits * dGrp.unit.dam
    , -1 * dGrp.unit.initiative
    )


damage : Group -> Group -> Int
damage aGrp dGrp =
    if List.member aGrp.unit.atk dGrp.unit.weak then
        2

    else if List.member aGrp.unit.atk dGrp.unit.imm then
        0

    else
        1
```

## Rules of Combat

Let's separate the two phases of combat.

### Target Selection

Firstly, we have the selection, which is based on ordering attacking and defending groups as specified in the question.

```elm {l}
selection : List Group -> List Group -> List ( Group, Maybe Group )
selection atkGroups defGroups =
    let
        aSorted =
            atkGroups
                |> List.sortBy powerThenInitiative

        dSorted atkGrp =
            List.sortBy (damageThenPowerThenInitiative atkGrp)

        chooseTarget aGrps dGrps combatants =
            case aGrps of
                [] ->
                    combatants

                atkHd :: atkTl ->
                    case dSorted atkHd dGrps of
                        [] ->
                            -- No defenders available for this attacker
                            chooseTarget atkTl dGrps (( atkHd, Nothing ) :: combatants)

                        defHd :: defTl ->
                            -- Most vulnerable defender against this attack is immune, so no comabt for attacker
                            if damage atkHd defHd == 0 then
                                chooseTarget atkTl dGrps (( atkHd, Nothing ) :: combatants)

                            else
                                -- Matched attacker with most vulnerable defender.
                                chooseTarget atkTl defTl (( atkHd, Just defHd ) :: combatants)
    in
    chooseTarget aSorted defGroups []
```

##Â Combat

The trick in defining the combat phase is to recognise that during combat, a group that is yet to attack may have itself been attack and so reducing its power. To manage this dynamic change during battle, we can create a dictionary of group IDs to groups. The battle sequence is defined by a fixed sequence of group ID pairs (attackerID,defenderID) and we can lookup and amend the groups in dictionary as battle proceeds.

We can use a group's `initiative` as its ID as these are unique to each group and unchanging.

```elm {l}
combat : List ( Group, Maybe Group ) -> ( List Group, List Group )
combat combatants =
    let
        groups =
            combatants
                |> List.map (\( aGrp, _ ) -> ( aGrp.unit.initiative, aGrp ))
                |> Dict.fromList

        validCombatant ( aGrp, maybeDGrp ) =
            case maybeDGrp of
                Just dGrp ->
                    Just ( aGrp, dGrp )

                Nothing ->
                    Nothing

        sortedIDs =
            combatants
                |> List.filterMap validCombatant
                |> List.sortBy (\( aGrp, dGrp ) -> aGrp.unit.initiative)
                |> List.reverse
                |> List.map (\( atk, def ) -> ( atk.unit.initiative, def.unit.initiative ))

        fight ( aID, dID ) grps =
            case ( Dict.get aID grps, Dict.get dID grps ) of
                ( Just aGrp, Just dGrp ) ->
                    let
                        dam =
                            power aGrp * damage aGrp dGrp

                        unitsKilled =
                            dam // dGrp.unit.hp
                    in
                    if unitsKilled >= dGrp.nUnits then
                        -- Attacker has killed all defender's units
                        Dict.remove dGrp.unit.initiative grps

                    else
                        Dict.insert dGrp.unit.initiative { dGrp | nUnits = dGrp.nUnits - unitsKilled } grps

                _ ->
                    grps
    in
    List.foldl fight groups sortedIDs
        |> Dict.values
        |> List.partition (\grp -> grp.side == ImmuneSystem)
```

A battle involves repeated selection and combat until the outcome of a combat results in groups from one side only. We need to guard against the possiblity that the damage of the remaining depleted units of both sides is not sufficient to remove any enemy units, so we need to check for a stalemate as well as any one side winning.

```elm {l}
battle : List Group -> List Group -> List Group
battle imGroups inGroups =
    let
        unitsBefore =
            List.map .nUnits (imGroups ++ inGroups) |> List.sum

        unitsAfter =
            List.map .nUnits (im2 ++ in2) |> List.sum

        ( im2, in2 ) =
            combat (selection imGroups inGroups ++ selection inGroups imGroups)
    in
    if unitsBefore == unitsAfter || im2 == [] || in2 == [] then
        im2 ++ in2

    else
        battle im2 in2
```

Finally, we commence battle and count the umber of units.

```elm {l r}
part1 : Int
part1 =
    battle parseInfection parseImmune
        |> List.map .nUnits
        |> List.sum
```

{( part2 |}

Things aren't looking good for the reindeer. The man asks whether more milk and cookies would help you think.

If only you could give the reindeer's immune system a boost, you might be able to change the outcome of the combat.

A **boost** is an integer increase in immune system units' attack damage. For example, if you were to boost the above example's immune system's units by 1570, the armies would instead look like this:

    Immune System:
    17 units each with 5390 hit points (weak to radiation, bludgeoning) with
     an attack that does 6077 fire damage at initiative 2
    989 units each with 1274 hit points (immune to fire; weak to bludgeoning,
     slashing) with an attack that does 1595 slashing damage at initiative 3

    Infection:
    801 units each with 4706 hit points (weak to radiation) with an attack
     that does 116 bludgeoning damage at initiative 1
    4485 units each with 2961 hit points (immune to radiation; weak to fire,
     cold) with an attack that does 12 slashing damage at initiative 4

With this boost, the combat proceeds differently:

    Immune System:
    Group 2 contains 989 units
    Group 1 contains 17 units
    Infection:
    Group 1 contains 801 units
    Group 2 contains 4485 units

    Infection group 1 would deal defending group 2 185832 damage
    Infection group 1 would deal defending group 1 185832 damage
    Infection group 2 would deal defending group 1 53820 damage
    Immune System group 2 would deal defending group 1 1577455 damage
    Immune System group 2 would deal defending group 2 1577455 damage
    Immune System group 1 would deal defending group 2 206618 damage

    Infection group 2 attacks defending group 1, killing 9 units
    Immune System group 2 attacks defending group 1, killing 335 units
    Immune System group 1 attacks defending group 2, killing 32 units
    Infection group 1 attacks defending group 2, killing 84 units

    Immune System:
    Group 2 contains 905 units
    Group 1 contains 8 units
    Infection:
    Group 1 contains 466 units
    Group 2 contains 4453 units

    Infection group 1 would deal defending group 2 108112 damage
    Infection group 1 would deal defending group 1 108112 damage
    Infection group 2 would deal defending group 1 53436 damage
    Immune System group 2 would deal defending group 1 1443475 damage
    Immune System group 2 would deal defending group 2 1443475 damage
    Immune System group 1 would deal defending group 2 97232 damage

    Infection group 2 attacks defending group 1, killing 8 units
    Immune System group 2 attacks defending group 1, killing 306 units
    Infection group 1 attacks defending group 2, killing 29 units

    Immune System:
    Group 2 contains 876 units
    Infection:
    Group 2 contains 4453 units
    Group 1 contains 160 units

    Infection group 2 would deal defending group 2 106872 damage
    Immune System group 2 would deal defending group 2 1397220 damage
    Immune System group 2 would deal defending group 1 1397220 damage

    Infection group 2 attacks defending group 2, killing 83 units
    Immune System group 2 attacks defending group 2, killing 427 units

    After a few fights...

    Immune System:
    Group 2 contains 64 units
    Infection:
    Group 2 contains 214 units
    Group 1 contains 19 units

    Infection group 2 would deal defending group 2 5136 damage
    Immune System group 2 would deal defending group 2 102080 damage
    Immune System group 2 would deal defending group 1 102080 damage

    Infection group 2 attacks defending group 2, killing 4 units
    Immune System group 2 attacks defending group 2, killing 32 units

    Immune System:
    Group 2 contains 60 units
    Infection:
    Group 1 contains 19 units
    Group 2 contains 182 units

    Infection group 1 would deal defending group 2 4408 damage
    Immune System group 2 would deal defending group 1 95700 damage
    Immune System group 2 would deal defending group 2 95700 damage

    Immune System group 2 attacks defending group 1, killing 19 units

    Immune System:
    Group 2 contains 60 units
    Infection:
    Group 2 contains 182 units

    Infection group 2 would deal defending group 2 4368 damage
    Immune System group 2 would deal defending group 2 95700 damage

    Infection group 2 attacks defending group 2, killing 3 units
    Immune System group 2 attacks defending group 2, killing 30 units

    After a few more fights...

    Immune System:
    Group 2 contains 51 units
    Infection:
    Group 2 contains 40 units

    Infection group 2 would deal defending group 2 960 damage
    Immune System group 2 would deal defending group 2 81345 damage

    Infection group 2 attacks defending group 2, killing 0 units
    Immune System group 2 attacks defending group 2, killing 27 units

    Immune System:
    Group 2 contains 51 units
    Infection:
    Group 2 contains 13 units

    Infection group 2 would deal defending group 2 312 damage
    Immune System group 2 would deal defending group 2 81345 damage

    Infection group 2 attacks defending group 2, killing 0 units
    Immune System group 2 attacks defending group 2, killing 13 units
    Immune System:
    Group 2 contains 51 units
    Infection:
    No groups remain.

This boost would allow the immune system's armies to win! It would be left with **51** units.

You don't even know **how** you could boost the reindeer's immune system or what effect it might have, so you need to be cautious and find the **smallest boost** that would allow the immune system to win.

**How many units does the immune system have left** after getting the smallest boost it needs to win?

{| part2 )}

## Boosting the Immune System

Incrementing the attack damage of the immune system's groups is simple:

```elm {l}
incBoost : List Group -> List Group
incBoost grp =
    let
        boostedUnit unit =
            { unit | dam = unit.dam + 1 }
    in
    List.map (\g -> { g | unit = boostedUnit g.unit }) grp
```

Now we just increment the immune system damage until it results in a victorious battle.

```elm {l}
minBoost : List Group -> List Group -> Int
minBoost infGroup immGroup =
    let
        immuneOnly =
            List.map .side >> List.all ((==) ImmuneSystem)

        battleOutcome =
            battle infGroup immGroup
    in
    if immuneOnly battleOutcome then
        battleOutcome |> List.map .nUnits |> List.sum

    else
        minBoost infGroup (incBoost immGroup)
```

```elm {l r}
part2 : Int
part2 =
    minBoost parseInfection parseImmune
```

## Reflection

Another puzzle in the 'code the complex rules carefully' category, but enjoyable nonetheless. This required quite a bit of custom sorting, which thankfully is quite straightforward in Elm using [List.sortBy](https://package.elm-lang.org/packages/elm/core/latest/List#sortBy). Placing the comparable sort criteria into a tuple allowed a single call to `sortBy` for a sequence of chained sort operations.

I was troubled with a hard to spot bug (which passed the test, but not real input) where I had mistakenly removed any non-combatants after battle. This erroneously removed healthy units that were simply unable to find an enemy to attack. This passed the test because that only occurs on the first round (where the removal wasn't applied prior to any battle).

For part 2, I thought I was going to need to do some kind of binary search to find the lowest boost value, but thought I'd try an incremental approach first. As this yielded a result in a couple of seconds, I didn't feel the need to make this more efficient.

---

## Testing

```elm {l}
testImmune : List Group
testImmune =
    [ Group 17 (Unit 5390 [] [ Radiation, Bludgeon ] 4507 Fire 2) ImmuneSystem
    , Group 989 (Unit 1274 [ Fire ] [ Bludgeon, Slash ] 25 Slash 3) ImmuneSystem
    ]


testInfection : List Group
testInfection =
    [ Group 801 (Unit 4706 [] [ Radiation ] 116 Bludgeon 1) Infection
    , Group 4485 (Unit 2961 [ Radiation ] [ Fire, Cold ] 12 Slash 4) Infection
    ]


groupText : Group -> String
groupText grp =
    sideText grp.side ++ " id: " ++ String.fromInt grp.unit.initiative ++ ", " ++ String.fromInt grp.nUnits ++ " units. Pow:" ++ String.fromInt (power grp)


groupTextShort : Group -> String
groupTextShort grp =
    sideText grp.side ++ " " ++ String.fromInt grp.unit.initiative


sideText : Side -> String
sideText side =
    case side of
        Infection ->
            "Infection"

        ImmuneSystem ->
            "Immune System"


combatText : ( Group, Maybe Group ) -> String
combatText ( grp, maybeDef ) =
    case maybeDef of
        Just dGrp ->
            groupTextShort grp ++ " would deal defending " ++ groupTextShort dGrp ++ " " ++ String.fromInt (power grp * damage grp dGrp) ++ " damage"

        Nothing ->
            groupTextShort grp ++ " has noone to fight"
```
