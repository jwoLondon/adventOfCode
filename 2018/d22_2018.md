---
follows: data/d22_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 22 (2018): Mode Maze

{( part1|}

This is it, your final stop: the year -483. It's snowing and dark outside; the only light you can see is coming from a small cottage in the distance. You make your way there and knock on the door.

A portly man with a large, white beard answers the door and invites you inside. For someone living near the North Pole in -483, he must not get many visitors, but he doesn't act surprised to see you. Instead, he offers you some milk and cookies.

After talking for a while, he asks a favor of you. His friend hasn't come back in a few hours, and he's not sure where he is. Scanning the region briefly, you discover one life signal in a cave system nearby; his friend must have taken shelter there. The man asks if you can go there to retrieve his friend.

The cave is divided into square **regions** which are either dominantly **rocky**, **narrow**, or **wet** (called its **type**). Each region occupies exactly one **coordinate** in X,Y format where X and Y are integers and zero or greater. (Adjacent regions can be the same type.)

The scan (your puzzle input) is not very detailed: it only reveals the depth of the cave system and the **coordinates of the target**. However, it does not reveal the type of each region. The mouth of the cave is at 0,0.

The man explains that due to the unusual geology in the area, there is a method to determine any region's type based on its **erosion level**. The erosion level of a region can be determined from its **geologic index**. The geologic index can be determined using the first rule that applies from the list below:

- The region at 0,0 (the mouth of the cave) has a geologic index of 0.
- The region at the coordinates of the target has a geologic index of 0.
- If the region's Y coordinate is 0, the geologic index is its X coordinate times 16807.
- If the region's X coordinate is 0, the geologic index is its Y coordinate times 48271.
- Otherwise, the region's geologic index is the result of multiplying the erosion levels of the regions at X-1,Y and X,Y-1.

A region's **erosion level** is its **geologic index** plus the cave system's **depth**, all modulo 20183. Then:

- If the **erosion level modulo 3** is 0, the region's type is **rocky**.
- If the **erosion level modulo 3** is 1, the region's type is **wet**.
- If the **erosion level modulo 3** is 2, the region's type is **narrow**.
-

For example, suppose the cave system's depth is 510 and the target's coordinates are 10,10. Using % to represent the modulo operator, the cavern would look as follows:

- At 0,0, the geologic index is 0. The erosion level is (0 + 510) % 20183 = 510. The type is 510 % 3 = 0, **rocky**.
- At 1,0, because the Y coordinate is 0, the geologic index is 1 \* 16807 = 16807. The erosion level is (16807 + 510) % 20183 = 17317. The type is 17317 % 3 = 1, **wet**.
- At 0,1, because the X coordinate is 0, the geologic index is 1 \* 48271 = 48271. The erosion level is (48271 + 510) % 20183 = 8415. The type is 8415 % 3 = 0, **rocky**.
- At 1,1, neither coordinate is 0 and it is not the coordinate of the target, so the geologic index is the erosion level of 0,1 (8415) times the erosion level of 1,0 (17317), 8415 \* 17317 = 145722555. The erosion level is (145722555 + 510) % 20183 = 1805. The type is 1805 % 3 = 2, **narrow**.
- At 10,10, because they are the target's coordinates, the geologic index is 0. The erosion level is (0 + 510) % 20183 = 510. The type is 510 % 3 = 0, **rocky**.

Drawing this same cave system with rocky as ., wet as =, narrow as |, the mouth as M, the target as T, with 0,0 in the top-left corner, X increasing to the right, and Y increasing downward, the top-left corner of the map looks like this:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Before you go in, you should determine the **risk level** of the area. For the rectangle that has a top-left corner of region 0,0 and a bottom-right corner of the region containing the target, add up the risk level of each individual region: 0 for rocky regions, 1 for wet regions, and 2 for narrow regions.

In the cave system above, because the mouth is at 0,0 and the target is at 10,10, adding up the risk level of all regions with an X coordinate from 0 to 10 and a Y coordinate from 0 to 10, this total is 114.

**What is the total risk level for the smallest rectangle that includes 0,0 and the target's coordinates?**

{| part1 )}

## Data Structure

For the first part, it makes sense to create a grid of erosion region types and to use a custom type to identify Rocky, Narrow and Wet regions.

```elm {l}
type RegionType
    = Rocky
    | Narrow
    | Wet


type alias Location =
    ( Int, Int )


type alias Cave =
    Grid RegionType
```

Given a geoIndex, the erosion level and region types are simply calculated:

```elm {l}
erosionLevel : Int -> Int -> Int
erosionLevel depth geoIndex =
    modBy 20183 (geoIndex + depth)


regionType : Int -> RegionType
regionType eLevel =
    if modBy 3 eLevel == 0 then
        Rocky

    else if modBy 3 eLevel == 1 then
        Wet

    else
        Narrow
```

The geoIndex at a given location is dependent on geoIndices at other locations. While this could be calculated recursively, with the base cases provided along the top row, this becomes prohibitively expensive for larger grids. Instead, we can calculate results sequentially from the top left of the grid row by row, using the stored values of earlier rows to memoize dependent results. This allows the grid to be calculated with a single O(n) pass of the grid.

Note that `geoIndices` and `mapCave` will create a grid from (0,0) up to the target location. Later in part 2, we will need to extend this grid, so we have an (unused in part 1) _buffer_ around the grid for extending it beyond the target location.

```elm {l}
geoIndices : Int -> Int -> Location -> Grid Int
geoIndices buffer depth ( tRow, tCol ) =
    let
        initGrid =
            gInit (tRow + buffer + 1) (tCol + buffer + 1) -1

        addGeoIndex : Location -> Grid Int -> Grid Int
        addGeoIndex ( row, col ) grd =
            if (gGet ( row, col ) grd |> Maybe.withDefault -1) >= 0 then
                grd

            else if ( row, col ) == ( 0, 0 ) then
                gSet ( row, col ) 0 grd

            else if ( row, col ) == ( tRow, tCol ) then
                gSet ( row, col ) 0 grd

            else if row == 0 then
                gSet ( row, col ) (modBy 20183 (col * 16807)) grd

            else if col == 0 then
                gSet ( row, col ) (modBy 20183 (row * 48271)) grd

            else
                let
                    ( e1, e2 ) =
                        ( erosionLevel depth (gGet ( row, col - 1 ) grd |> Maybe.withDefault -1)
                        , erosionLevel depth (gGet ( row - 1, col ) grd |> Maybe.withDefault -1)
                        )
                in
                if e1 >= 0 && e2 >= 0 then
                    gSet ( row, col ) (modBy 20183 (e1 * e2)) grd

                else
                    grd
    in
    List.foldl addGeoIndex initGrid (gridLocations ( 0, 0 ) ( tRow + buffer, tCol + buffer ))


mapCave : Int -> Int -> Location -> Cave
mapCave buffer depth =
    geoIndices buffer depth
        >> gMap (erosionLevel depth)
        >> gMap regionType
```

The risk level is easily calculated by converting the custom types back to numerical values. While there is minor efficiency gain to be had by recording region types directly as numbers, the resulting loss of program clarity is probably not worth the gain.

```elm {l}
riskLevel : Int -> Grid RegionType -> Int
riskLevel buffer regions =
    let
        typeRisk rType =
            case rType of
                Rocky ->
                    0

                Wet ->
                    1

                Narrow ->
                    2
    in
    regions
        |> gMap typeRisk
        |> gToLists
        |> List.reverse
        |> List.drop buffer
        |> List.map (List.reverse >> List.drop buffer)
        |> List.concat
        |> List.sum
```

```elm {l r}
part1 : Int
part1 =
    mapCave 0 4002 ( 746, 5 ) |> riskLevel 0
```

{( part2 |}

Okay, it's time to go rescue the man's friend.

As you leave, he hands you some tools: a **torch** and some **climbing gear**. You can't equip both tools at once, but you can choose to use **neither**.

Tools can only be used in certain regions:

- In **rocky** regions, you can use the **climbing gear** or the **torch**. You cannot use neither (you'll likely slip and fall).
- In **wet** regions, you can use the **climbing gear** or **neither** tool. You cannot use the torch (if it gets wet, you won't have a light source).
- In **narrow** regions, you can use the **torch** or **neither** tool. You cannot use the climbing gear (it's too bulky to fit).

You start at 0,0 (the mouth of the cave) with **the torch equipped** and must reach the target coordinates as quickly as possible. The regions with negative X or Y are solid rock and cannot be traversed. The fastest route might involve entering regions beyond the X or Y coordinate of the target.

You can **move to an adjacent** region (up, down, left, or right; never diagonally) if your currently equipped tool allows you to enter that region. Moving to an adjacent region takes **one minute**. (For example, if you have the **torch** equipped, you can move between **rocky** and **narrow** regions, but cannot enter wet regions.)

You can **change your currently equipped tool or put both away** if your new equipment would be valid for your current region. Switching to using the **climbing gear**, **torch**, or **neither** always takes **seven minutes**, regardless of which tools you start with. (For example, if you are in a **rocky** region, you can switch from the **torch** to the **climbing gear**, but you cannot switch to **neither**.)

Finally, once you reach the target, you need the **torch** equipped before you can find him in the dark. The target is always in a **rocky** region, so if you arrive there with **climbing gear** equipped, you will need to spend seven minutes switching to your **torch**.

For example, using the same cave system as above, starting in the top left corner (0,0) and moving to the bottom right corner (the target, 10,10) as quickly as possible, one possible route is as follows, with your current position marked X:

Initially:

    X=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Down:

    M=.|=.|.|=.|=|=.
    X|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Right:

    M=.|=.|.|=.|=|=.
    .X=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Switch from using the torch to neither tool:

    M=.|=.|.|=.|=|=.
    .X=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Right 3:

    M=.|=.|.|=.|=|=.
    .|=|X|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Switch from using neither tool to the climbing gear:

    M=.|=.|.|=.|=|=.
    .|=|X|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Down 7:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..X==..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Right:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..=X=..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Down 3:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||.X.|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Right:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||..X|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Down:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.X..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Right 4:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===T===||
    =|||...|==..|=.|
    =.=|=.=..=X||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Up 2:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===X===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

Switch from using the climbing gear to the torch:

    M=.|=.|.|=.|=|=.
    .|=|=|||..|.=...
    .==|....||=..|==
    =.|....|.==.|==.
    =|..==...=.|==..
    =||.=.=||=|=..|=
    |.=.===|||..=..|
    |..==||=.|==|===
    .=..===..=|.|||.
    .======|||=|=.|=
    .===|=|===X===||
    =|||...|==..|=.|
    =.=|=.=..=.||==|
    ||=|=...|==.=|==
    |=.=||===.|||===
    ||.|==.|.|.||=||

This is tied with other routes as the **fastest way to reach the target: 45** minutes. In it, 21 minutes are spent switching tools (three times, seven minutes each) and the remaining 24 minutes are spent moving.

**What is the fewest number of minutes you can take to reach the target?**

{| part2 )}

## Equipment choices

We are always deploying equipment in one of three states.

```elm {l}
type Equipment
    = Climbing
    | Torch
    | Neither
```

## Approach

We can build a graph of times from each room to its neighbours. If a neighbouring room is of the same type, there will be two possible times, corresponding to the two equipment choices for that region type (1 if no equipment change, 8 if a change is made). If a neighbour is of a different type, there will be a single choice, corresponding to the equipment compatible with both regions.

We will store the graph as a dictionary with the keys corresponding to a 3-tuple (row,column,equipment) and the value being the shortest time to that location with that equipment type. Because Elm requires dictionary keys to be [comparable](https://package.elm-lang.org/packages/elm/core/latest/Dict) we need to convert the equipment custom type into a numeric value before assembling a key.

```elm {l}
type alias Times =
    Dict ( Int, Int, Int ) Int


type alias EquippedLocation =
    ( Int, Int, Equipment )


equipCode : ( Int, Int, Equipment ) -> ( Int, Int, Int )
equipCode locEquip =
    case locEquip of
        ( row, col, Climbing ) ->
            ( row, col, 1 )

        ( row, col, Torch ) ->
            ( row, col, 2 )

        ( row, col, Neither ) ->
            ( row, col, 3 )


equipBetween : Maybe RegionType -> Maybe RegionType -> List Equipment
equipBetween rt1 rt2 =
    case ( rt1, rt2 ) of
        ( Just Rocky, Just Rocky ) ->
            [ Climbing, Torch ]

        ( Just Wet, Just Wet ) ->
            [ Climbing, Neither ]

        ( Just Narrow, Just Narrow ) ->
            [ Torch, Neither ]

        ( Just Rocky, Just Wet ) ->
            [ Climbing ]

        ( Just Wet, Just Rocky ) ->
            [ Climbing ]

        ( Just Rocky, Just Narrow ) ->
            [ Torch ]

        ( Just Narrow, Just Rocky ) ->
            [ Torch ]

        ( Just Wet, Just Narrow ) ->
            [ Neither ]

        ( Just Narrow, Just Wet ) ->
            [ Neither ]

        _ ->
            []
```

We can build the time graph using a breadth-first search, starting at the cave mouth until all grid values have been processed. Even though we are applying a breadth-first search, it is possible that a more distant region might provide a quicker route to a neighbour if it involves no change in equipment. As a result, every time we find a new shortest time to a given region, we need to add it back to the queue of items to process in as it will probably have an effect of reducing the time to its neighbours.

```elm {l}
search : Cave -> Fifo EquippedLocation -> Times -> Times
search cave todo times =
    let
        updateShortest locEq newTime ts =
            case Dict.get (equipCode locEq) ts of
                Just oldTime ->
                    if newTime < oldTime then
                        -- We've found a new shorter time
                        ( Just locEq, Dict.insert (equipCode locEq) newTime ts )

                    else
                        -- Slower than previous visit with this equipement
                        ( Nothing, ts )

                Nothing ->
                    -- We've not been here before with this equipment
                    ( Just locEq, Dict.insert (equipCode locEq) newTime ts )

        time : Equipment -> Equipment -> Int
        time eq1 eq2 =
            if eq1 == eq2 then
                0

            else
                7

        quickestRegions ts ( row0, col0, equip0 ) locs equippedLocs =
            case locs of
                [] ->
                    ( equippedLocs, ts )

                ( row1, col1 ) :: tl ->
                    let
                        newEquips =
                            equipBetween (gGet ( row0, col0 ) cave) (gGet ( row1, col1 ) cave)

                        timeHere =
                            Dict.get (equipCode ( row0, col0, equip0 )) ts |> Maybe.withDefault 0

                        ( maybeVisits, newTimes2 ) =
                            List.foldl
                                (\equip1 ( visited, newTimes ) ->
                                    let
                                        ( maybeVisit, newTs ) =
                                            updateShortest ( row1, col1, equip1 )
                                                -- currentTime + time to change equip + time to move
                                                (timeHere + time equip0 equip1 + abs (row1 - row0) + abs (col1 - col0))
                                                newTimes
                                    in
                                    ( maybeVisit :: visited, newTs )
                                )
                                ( [], ts )
                                newEquips
                    in
                    quickestRegions newTimes2 ( row0, col0, equip0 ) tl (List.filterMap identity maybeVisits ++ equippedLocs)
    in
    case Fifo.remove todo of
        ( Nothing, _ ) ->
            times

        ( Just ( row, col, equip ), remaining ) ->
            let
                ( toVisit, newTimes ) =
                    quickestRegions times
                        ( row, col, equip )
                        [ ( row, col ), ( row - 1, col ), ( row, col + 1 ), ( row + 1, col ), ( row, col - 1 ) ]
                        []
            in
            --search cave (Deque.append remaining (Deque.fromList toVisit)) newTimes
            search cave (Fifo.fromList (toVisit ++ Fifo.toList remaining)) newTimes


timeTo : EquippedLocation -> Times -> Int
timeTo eqLoc =
    Dict.get (equipCode eqLoc) >> Maybe.withDefault 9999
```

There is the possibility that the quickest route to the target location involves moving further away from the mouth than the target itself, so we need to introduce a 'buffer' that extends the cave system beyond the target by some number of rows and columns. It is not clear how wide that buffer should be at the outset, but giving it a 100 yields the correct result.

```elm {l r}
part2 : Int
part2 =
    let
        buffer =
            --100
            5

        tgt =
            --( 746, 5, Torch )
            ( 10, 10, Torch )

        cave =
            --mapCave buffer 4002 ( tripletFirst tgt, tripletSecond tgt )
            mapCave buffer 510 ( tripletFirst tgt, tripletSecond tgt )
    in
    -- Slow: Remove comments to calculate result
    search cave (Fifo.empty |> Fifo.insert ( 0, 0, Torch )) Dict.empty
        |> Dict.get (equipCode tgt)
        |> Maybe.withDefault -1



--1032
```

## Reflection

I enjoyed this puzzle, but ultimately not happy with my solution to part 2.

My first attempt at part two was unnecessarily complicated in trying to store multiple graphs for different equipment types. This soon led to unwieldy code which I abandoned. I was pleased to be able recognise that the equipment choices could be represented as a simple graph of (row,col,equipment) nodes. However the building of this graph is slow. This is because new shortest times can cascade back through the graph as neighbouring values are updated.

Perhaps storing the todo values in a priority queue ordered by time rather than recency might be more efficient as fewer updates to previously calculated regions are required. Elm doesn't have a priority queue structure out of the box, so I haven't implemented this yet.

I wonder if there might be a way of automatically determining the buffer size required to guarantee the shortest route to the target. This will require more thought on the geoIndex patterns and the pathways that involve no change in equipment.

---

## Tests

```elm {m}
caveText : List String
caveText =
    let
        buffer =
            5

        tgt =
            ( 10, 10 )

        cave =
            mapCave buffer 510 tgt

        regionText rType =
            case rType of
                Rocky ->
                    ". "

                Wet ->
                    "= "

                Narrow ->
                    "| "
    in
    cave
        |> gMap regionText
        |> gSet tgt "T "
        |> gToLists
        |> List.map (\r -> "\n    " ++ String.concat r)
        |> flip (++) [ "\n\n    Risk level: " ++ String.fromInt (riskLevel buffer cave) ]
```
