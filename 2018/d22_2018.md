---
follows: data/d22_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 22 (2018): Mode Maze

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/22).

{( part1|}

What is the total risk level for the smallest rectangle that includes 0,0 and the target's coordinates?

{| part1 )}

## Data Structure

For the first part, it makes sense to create a grid of erosion region types and to use a custom type to identify Rocky, Narrow and Wet regions.

```elm {l}
type RegionType
    = Rocky
    | Narrow
    | Wet


type alias Location =
    ( Int, Int )


type alias Cave =
    AOC.Grid RegionType
```

Given a geoIndex, the erosion level and region types are simply calculated:

```elm {l}
erosionLevel : Int -> Int -> Int
erosionLevel depth geoIndex =
    modBy 20183 (geoIndex + depth)


regionType : Int -> RegionType
regionType eLevel =
    if modBy 3 eLevel == 0 then
        Rocky

    else if modBy 3 eLevel == 1 then
        Wet

    else
        Narrow
```

The geoIndex at a given location is dependent on geoIndices at other locations. While this could be calculated recursively, with the base cases provided along the top row, this becomes prohibitively expensive for larger grids. Instead, we can calculate results sequentially from the top left of the grid row by row, using the stored values of earlier rows to memoize dependent results. This allows the grid to be calculated with a single O(n) pass of the grid.

Note that `geoIndices` and `mapCave` will create a grid from (0,0) up to the target location. Later in part 2, we will need to extend this grid, so we have an (unused in part 1) _buffer_ around the grid for extending it beyond the target location.

```elm {l}
geoIndices : Int -> Int -> Location -> AOC.Grid Int
geoIndices buffer depth ( tRow, tCol ) =
    let
        initGrid =
            AOC.gInit (tRow + buffer + 1) (tCol + buffer + 1) -1

        addGeoIndex : Location -> AOC.Grid Int -> AOC.Grid Int
        addGeoIndex ( row, col ) grd =
            if (AOC.gGet ( row, col ) grd |> Maybe.withDefault -1) >= 0 then
                grd

            else if ( row, col ) == ( 0, 0 ) then
                AOC.gSet ( row, col ) 0 grd

            else if ( row, col ) == ( tRow, tCol ) then
                AOC.gSet ( row, col ) 0 grd

            else if row == 0 then
                AOC.gSet ( row, col ) (modBy 20183 (col * 16807)) grd

            else if col == 0 then
                AOC.gSet ( row, col ) (modBy 20183 (row * 48271)) grd

            else
                let
                    ( e1, e2 ) =
                        ( erosionLevel depth (AOC.gGet ( row, col - 1 ) grd |> Maybe.withDefault -1)
                        , erosionLevel depth (AOC.gGet ( row - 1, col ) grd |> Maybe.withDefault -1)
                        )
                in
                if e1 >= 0 && e2 >= 0 then
                    AOC.gSet ( row, col ) (modBy 20183 (e1 * e2)) grd

                else
                    grd
    in
    List.foldl addGeoIndex initGrid (AOC.gridLocations ( 0, 0 ) ( tRow + buffer, tCol + buffer ))


mapCave : Int -> Int -> Location -> Cave
mapCave buffer depth =
    geoIndices buffer depth
        >> AOC.gMap (erosionLevel depth)
        >> AOC.gMap regionType
```

The risk level is easily calculated by converting the custom types back to numerical values. While there is minor efficiency gain to be had by recording region types directly as numbers, the resulting loss of program clarity is probably not worth the gain.

```elm {l}
riskLevel : Int -> AOC.Grid RegionType -> Int
riskLevel buffer regions =
    let
        typeRisk rType =
            case rType of
                Rocky ->
                    0

                Wet ->
                    1

                Narrow ->
                    2
    in
    regions
        |> AOC.gMap typeRisk
        |> AOC.gToLists
        |> List.reverse
        |> List.drop buffer
        |> List.map (List.reverse >> List.drop buffer)
        |> List.concat
        |> List.sum
```

```elm {l r}
part1 : Int
part1 =
    mapCave 0 4002 ( 746, 5 ) |> riskLevel 0
```

{( part2 |}

What is the fewest number of minutes you can take to reach the target?

{| part2 )}

## Equipment choices

We are always deploying equipment in one of three states.

```elm {l}
type Equipment
    = Climbing
    | Torch
    | Neither
```

## Approach

We can build a graph of times from each room to its neighbours. If a neighbouring room is of the same type, there will be two possible times, corresponding to the two equipment choices for that region type (1 if no equipment change, 8 if a change is made). If a neighbour is of a different type, there will be a single choice, corresponding to the equipment compatible with both regions.

We will store the graph as a dictionary with the keys corresponding to a 3-tuple (row,column,equipment) and the value being the shortest time to that location with that equipment type. Because Elm requires dictionary keys to be [comparable](https://package.elm-lang.org/packages/elm/core/latest/Dict) we need to convert the equipment custom type into a numeric value before assembling a key.

```elm {l}
type alias Times =
    Dict ( Int, Int, Int ) Int


type alias EquippedLocation =
    ( Int, Int, Equipment )


equipCode : ( Int, Int, Equipment ) -> ( Int, Int, Int )
equipCode locEquip =
    case locEquip of
        ( row, col, Climbing ) ->
            ( row, col, 1 )

        ( row, col, Torch ) ->
            ( row, col, 2 )

        ( row, col, Neither ) ->
            ( row, col, 3 )


equipBetween : Maybe RegionType -> Maybe RegionType -> List Equipment
equipBetween rt1 rt2 =
    case ( rt1, rt2 ) of
        ( Just Rocky, Just Rocky ) ->
            [ Climbing, Torch ]

        ( Just Wet, Just Wet ) ->
            [ Climbing, Neither ]

        ( Just Narrow, Just Narrow ) ->
            [ Torch, Neither ]

        ( Just Rocky, Just Wet ) ->
            [ Climbing ]

        ( Just Wet, Just Rocky ) ->
            [ Climbing ]

        ( Just Rocky, Just Narrow ) ->
            [ Torch ]

        ( Just Narrow, Just Rocky ) ->
            [ Torch ]

        ( Just Wet, Just Narrow ) ->
            [ Neither ]

        ( Just Narrow, Just Wet ) ->
            [ Neither ]

        _ ->
            []
```

We can build the time graph using a breadth-first search, starting at the cave mouth until all grid values have been processed. Even though we are applying a breadth-first search, it is possible that a more distant region might provide a quicker route to a neighbour if it involves no change in equipment. As a result, every time we find a new shortest time to a given region, we need to add it back to the queue of items to process in as it will probably have an effect of reducing the time to its neighbours.

```elm {l}
search : Cave -> Fifo EquippedLocation -> Times -> Times
search cave todo times =
    let
        updateShortest locEq newTime ts =
            case Dict.get (equipCode locEq) ts of
                Just oldTime ->
                    if newTime < oldTime then
                        -- We've found a new shorter time
                        ( Just locEq, Dict.insert (equipCode locEq) newTime ts )

                    else
                        -- Slower than previous visit with this equipement
                        ( Nothing, ts )

                Nothing ->
                    -- We've not been here before with this equipment
                    ( Just locEq, Dict.insert (equipCode locEq) newTime ts )

        time : Equipment -> Equipment -> Int
        time eq1 eq2 =
            if eq1 == eq2 then
                0

            else
                7

        quickestRegions ts ( row0, col0, equip0 ) locs equippedLocs =
            case locs of
                [] ->
                    ( equippedLocs, ts )

                ( row1, col1 ) :: tl ->
                    let
                        newEquips =
                            equipBetween (AOC.gGet ( row0, col0 ) cave) (AOC.gGet ( row1, col1 ) cave)

                        timeHere =
                            Dict.get (equipCode ( row0, col0, equip0 )) ts |> Maybe.withDefault 0

                        ( maybeVisits, newTimes2 ) =
                            List.foldl
                                (\equip1 ( visited, newTimes ) ->
                                    let
                                        ( maybeVisit, newTs ) =
                                            updateShortest ( row1, col1, equip1 )
                                                -- currentTime + time to change equip + time to move
                                                (timeHere + time equip0 equip1 + abs (row1 - row0) + abs (col1 - col0))
                                                newTimes
                                    in
                                    ( maybeVisit :: visited, newTs )
                                )
                                ( [], ts )
                                newEquips
                    in
                    quickestRegions newTimes2 ( row0, col0, equip0 ) tl (List.filterMap identity maybeVisits ++ equippedLocs)
    in
    case Fifo.remove todo of
        ( Nothing, _ ) ->
            times

        ( Just ( row, col, equip ), remaining ) ->
            let
                ( toVisit, newTimes ) =
                    quickestRegions times
                        ( row, col, equip )
                        [ ( row, col ), ( row - 1, col ), ( row, col + 1 ), ( row + 1, col ), ( row, col - 1 ) ]
                        []
            in
            --search cave (Deque.append remaining (Deque.fromList toVisit)) newTimes
            search cave (Fifo.fromList (toVisit ++ Fifo.toList remaining)) newTimes


timeTo : EquippedLocation -> Times -> Int
timeTo eqLoc =
    Dict.get (equipCode eqLoc) >> Maybe.withDefault 9999
```

There is the possibility that the quickest route to the target location involves moving further away from the mouth than the target itself, so we need to introduce a 'buffer' that extends the cave system beyond the target by some number of rows and columns. It is not clear how wide that buffer should be at the outset, but giving it a 100 yields the correct result.

```elm {l r}
part2 : Int
part2 =
    let
        buffer =
            --100
            5

        tgt =
            --( 746, 5, Torch )
            ( 10, 10, Torch )

        cave =
            --mapCave buffer 4002 ( AOC.tripletFirst tgt, AOC.tripletSecond tgt )
            mapCave buffer 510 ( AOC.tripletFirst tgt, AOC.tripletSecond tgt )
    in
    -- Slow: Remove comments to calculate result
    search cave (Fifo.empty |> Fifo.insert ( 0, 0, Torch )) Dict.empty
        |> Dict.get (equipCode tgt)
        |> Maybe.withDefault -1



--1032
```

---

## Reflection

I enjoyed this puzzle, but ultimately not happy with my solution to part 2.

My first attempt at part two was unnecessarily complicated in trying to store multiple graphs for different equipment types. This soon led to unwieldy code which I abandoned. I was pleased to be able recognise that the equipment choices could be represented as a simple graph of (row,col,equipment) nodes. However the building of this graph is slow. This is because new shortest times can cascade back through the graph as neighbouring values are updated.

Perhaps storing the todo values in a priority queue ordered by time rather than recency might be more efficient as fewer updates to previously calculated regions are required. Elm doesn't have a priority queue structure out of the box, so I haven't implemented this yet.

I wonder if there might be a way of automatically determining the buffer size required to guarantee the shortest route to the target. This will require more thought on the geoIndex patterns and the pathways that involve no change in equipment.

---

## Tests

```elm {m}
caveText : List String
caveText =
    let
        buffer =
            5

        tgt =
            ( 10, 10 )

        cave =
            mapCave buffer 510 tgt

        regionText rType =
            case rType of
                Rocky ->
                    ". "

                Wet ->
                    "= "

                Narrow ->
                    "| "
    in
    cave
        |> AOC.gMap regionText
        |> AOC.gSet tgt "T "
        |> AOC.gToLists
        |> List.map (\r -> "\n    " ++ String.concat r)
        |> AOC.flip (++) [ "\n\n    Risk level: " ++ String.fromInt (riskLevel buffer cave) ]
```
