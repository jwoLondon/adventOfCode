---
follows: data/d05_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2018): Alchemical Reduction

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/5).

{( part1|}

How many units remain after fully reacting the polymer you scanned?

{| part1 )}

An obvious start point is to provide a function to test for a reactive pair. We cannot apply a simple case-insensitive comparison because a pair of identical characters do not react so we need to account for that case:

```elm {l}
react : Char -> Char -> Bool
react a b =
    (Char.toLower a == Char.toLower b) && a /= b
```

The key to an efficient processing of a long string of characters is to recognise that after removing a reactive pair, we may bring together a pair of reactive characters that were previously separated. That means for sequential processing of a string, we have to consider the possibility that a character we have previously considered unreactive might now become reactive. We can do this by keeping two lists, `left` and `right`, and comparing the head of each.

```txt
[] [a, b, c, d, e, f]  -- Initial left and right lists
[a] [b, c, d, e, f]    -- First comparison between a and b
[b, a] [c, d, e, f]    -- Second comparison between b and c
[c, b, a] [d, e, f]    -- Third comparison between c and d. If they were to react,
[b, a] [e, f]          -- the next comparison would be between b and e
```

So this is just a simple reduce function to be provided to `List.foldl`:

```elm {l}
reduce : Char -> List Char -> List Char
reduce rightChar left =
    case List.head left of
        Nothing ->
            [ rightChar ]

        Just leftChar ->
            if react leftChar rightChar then
                List.drop 1 left

            else
                rightChar :: left
```

```elm {l r}
part1 : Int
part1 =
    List.foldl reduce [] puzzleInput |> List.length
```

{( part2 |}

What is the length of the shortest polymer you can produce by removing all units of exactly one type and fully reacting the result?

{| part2 )}

This is a trivial extension in that we need to first remove a letter from the input sequence, reduce it as for part 1 and store its length. If we do this for all the letters a-z, the shortest reduced string length gives us our answer.

One optimisation we can apply is to reduce the input string once before the letter removals.

```elm {l r}
part2 : Int
part2 =
    let
        reducedInput =
            List.foldl reduce [] puzzleInput

        compressed charToRemove =
            reducedInput
                |> List.filter (\c -> Char.toLower c /= charToRemove)
                |> List.foldl reduce []
                |> List.length
    in
    "abcdefghijklmnopqrstuvwxyz"
        |> String.toList
        |> List.map compressed
        |> List.minimum
        |> Maybe.withDefault -1
```

---

## Reflection

This is the kind of problem that should be easily solved in a functional language. I did consider using regular expressions for tackling part 1 but rejected this on the grounds that part 2 might require a variation not so regex-friendly (in the end, it would not have been a problem, but until part 1 was solved I wasn't to know this).

My first approach was to do multiple passes of the input, removing any reactive pairs in each sweep. Multiple passes were needed because removing a pair might also join a formerly separated reactive pair. This worked for the test data and subsets of the real data, but was way too slow to provide a feasible solution for the full dataset.

I thought initially the slowdown might be due to non-tail call recursion keeping track of multiple versions of the long input, so I made the mistake of trying to solve this by breaking down the input into blocks of characters, reducing them, and then rejoining and repeating. While this produced what appeared to be a correct answer for the test data, again it was too slow for the full dataset. I should have paid attention to the stench of too many bad code smells.

A day at work on other matters gave me fresh eyes on an evening return to the problem and it then occurred to me to try a sequential approach that always compared the most recently processed character and the first unprocessed character.

My initial solution using this approach looked like this:

```elm
reduce : List Char -> List Char -> List Char
reduce left right =
    case left of
        [] ->
            case right of
                hd :: tl ->
                    reduce [ hd ] tl

                [] ->
                    []

        leftHead :: leftTail ->
            case right of
                [] ->
                    left

                hd :: tl ->
                    if react leftHead hd then
                        reduce leftTail tl

                    else
                        reduce (hd :: left) tl
```

Because my earlier failed approach involved extracting two values at a time from the head of the list of characters I had ruled out using a fold as that only extracts one item at at time. However, the approach shown here also extracts one item at a time comparing it with the head of the accumulator (`left`). This can therefore be simplified using a standard `List.foldl` call (as described in the main solution).

The lesson to learn (again!) is to step back from the problem and consider alternatives before committing to one particular approach.
