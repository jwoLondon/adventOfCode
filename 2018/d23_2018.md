---
follows: data/d23_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 23 (2018): Experimental Emergency Teleportation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/23):

{( part1|}

Using your torch to search the darkness of the rocky cavern, you finally locate the man's friend: a small **reindeer**.

You're not sure how it got so far in this cave. It looks sick - too sick to walk - and too heavy for you to carry all the way back. Sleighs won't be invented for another 1500 years, of course.

The only option is **experimental emergency teleportation**.

You hit the "experimental emergency teleportation" button on the device and push **I accept the risk** on no fewer than 18 different warning messages. Immediately, the device deploys hundreds of tiny **nanobots** which fly around the cavern, apparently assembling themselves into a very specific **formation**. The device lists the X,Y,Z position (pos) for each nanobot as well as its **signal radius** (r) on its tiny screen (your puzzle input).

Each nanobot can transmit signals to any integer coordinate which is a distance away from it **less than or equal** to its signal radius (as measured by[ Manhattan distance](https://en.wikipedia.org/wiki/Taxicab_geometry)). Coordinates a distance away of less than or equal to a nanobot's signal radius are said to be **in range** of that nanobot.

Before you start the teleportation process, you should determine which nanobot is the **strongest** (that is, which has the largest signal radius) and then, for that nanobot, **the total number of nanobots that are in range** of it, **including itself**.

For example, given the following nanobots:

    pos=<0,0,0>, r=4
    pos=<1,0,0>, r=1
    pos=<4,0,0>, r=3
    pos=<0,2,0>, r=1
    pos=<0,5,0>, r=3
    pos=<0,0,3>, r=1
    pos=<1,1,1>, r=1
    pos=<1,1,2>, r=1
    pos=<1,3,1>, r=1

The strongest nanobot is the first one (position 0,0,0) because its signal radius, 4 is the largest. Using that nanobot's location and signal radius, the following nanobots are in or out of range:

- The nanobot at 0,0,0 is distance 0 away, and so it is **in range**.
- The nanobot at 1,0,0 is distance 1 away, and so it is **in range**.
- The nanobot at 4,0,0 is distance 4 away, and so it is **in range**.
- The nanobot at 0,2,0 is distance 2 away, and so it is **in range**.
- The nanobot at 0,5,0 is distance 5 away, and so it is **not** in range.
- The nanobot at 0,0,3 is distance 3 away, and so it is **in range**.
- The nanobot at 1,1,1 is distance 3 away, and so it is **in range**.
- The nanobot at 1,1,2 is distance 4 away, and so it is **in range**.
- The nanobot at 1,3,1 is distance 5 away, and so it is **not** in range.

In this example, in total, 7 nanobots are in range of the nanobot with the largest signal radius.

Find the nanobot with the largest signal radius. **How many nanobots are in range of its signals**?

{| part1 )}

## Data Structures and Parsing

Parsing the input just involves some simple regex and a record for storing a bot's location and radius.

```elm {l}
type alias Bot =
    { x : Int
    , y : Int
    , z : Int
    , r : Int
    }


type alias Location =
    ( Int, Int, Int )


parse : List String -> List Bot
parse =
    let
        posValues =
            AOC.submatches "pos=<(-*\\d+),(-*\\d+),(-*\\d+)>, r=(\\d+)"
                >> List.filterMap identity
                >> List.map AOC.toInt

        parseLine line =
            case posValues line of
                [ x, y, z, r ] ->
                    Just (Bot x y z r)

                _ ->
                    Nothing
    in
    List.map parseLine >> List.filterMap identity
```

## Approach

Finding the strongest bot is a simple fold over all the bots storing the one with the largest radius.

```elm {l}
strongest : List Bot -> Bot
strongest bots =
    List.foldl
        (\bot sBot ->
            if bot.r > sBot.r then
                bot

            else
                sBot
        )
        (Bot 0 0 0 0)
        bots
```

To find all bots within a given radius, we need to be able to calculate the 3d Manhattan distance and then find the bots that are within the radius of the strongest bot:

```elm {l}
dist : Location -> Location -> Int
dist ( x1, y1, z1 ) ( x2, y2, z2 ) =
    abs (x1 - x2) + abs (y1 - y2) + abs (z1 - z2)


botNeighbours : Bot -> List Bot -> List Bot
botNeighbours bot bots =
    List.foldl
        (\b bs ->
            if dist ( bot.x, bot.y, bot.z ) ( b.x, b.y, b.z ) <= bot.r then
                b :: bs

            else
                bs
        )
        []
        bots
```

```elm {l r}
part1 : Int
part1 =
    let
        bots =
            parse puzzleInput
    in
    bots
        |> botNeighbours (strongest bots)
        |> List.length
```

{( part2 |}

Now, you just need to figure out where to position yourself so that you're actually teleported when the nanobots activate.

To increase the probability of success, you need to find the coordinate which puts you **in range of the largest number of nanobots**. If there are multiple, choose one **closest to your position** (0,0,0, measured by Manhattan distance).

For example, given the following nanobot formation:

    pos=<10,12,12>, r=2
    pos=<12,14,12>, r=2
    pos=<16,12,12>, r=4
    pos=<14,14,14>, r=6
    pos=<50,50,50>, r=200
    pos=<10,10,10>, r=5

Many coordinates are in range of some of the nanobots in this formation. However, only the coordinate 12,12,12 is in range of the most nanobots: it is in range of the first five, but is not in range of the nanobot at 10,10,10. (All other coordinates are in range of fewer than five nanobots.) This coordinate's distance from 0,0,0 is **36**.

Find the coordinates that are in range of the largest number of nanobots. **What is the shortest Manhattan distance between any of those points and 0,0,0?**

{| part2 )}

## Approach

We could in theory count the number of bot range intersections with every integer location in the 3d space containing all bots. But this would be prohibitively expensive. Starting at some likely location, such as the weighted centroid, weighted by inverse radius cubed (the point of maximum intersection is likely to be nearer small radius bots than larger radius ones) might allow some local hill climbing approach to work. However, there is a strong likelihood of reaching a local maximum only and with a large space to navigate, this is still a costly approach.

Instead, we can store the number of intersecting bots in a large volume containing them all, then subdivide into 8 smaller volumes, storing the number of intersections in each. If we sort those volumes by number of intersections then distance from origin and then size, we continue to subdivide the top entry adding the subdivisions to the sorted list and selecting the first item until the volume is 1 cubic unit. That should contain the position with the highest number of intersections.

Firstly, let's create some helper functions for providing the distance of any location to the origin and to count the number of intersecting bot ranges:

```elm {l}
toOrigin : Location -> Int
toOrigin ( x, y, z ) =
    abs x + abs y + abs z


numOverlaps : List Bot -> ( Location, Location ) -> Int
numOverlaps bots ( ( minX, minY, minZ ), ( maxX, maxY, maxZ ) ) =
    let
        distToRegion bot =
            max 0 (minX - bot.x)
                + max 0 (minY - bot.y)
                + max 0 (minZ - bot.z)
                + max 0 (bot.x - maxX)
                + max 0 (bot.y - maxY)
                + max 0 (bot.z - maxZ)
    in
    bots
        |> List.filter (\b -> distToRegion b <= b.r)
        |> List.length
```

We can store the sorted volumes in a dictionary with a key being the 3-tuple (numOverlaps,distFromOrigin,size). We cannot use the standard Elm [priority queue](https://package.elm-lang.org/packages/HAN-ASD-DT/priority-queue/latest/) to store this as the priority is a 32 bit integer and the range of priorities could initially exceed the 32 bit capacity (e.g. distance from origin and size are initially 10 digit integers).

Here we create some helper methods for inserting dictionary entries and removing the top priority entry. Note the multiplication by -1 of the number of overlaps to ensure the first entry in the sorted list of volumes is the one with the lowest numeric value, representing the highest number of overlaps.

```elm {l}
type alias Volumes =
    Dict ( Int, Int, Int ) ( Location, Location )


push : List Bot -> ( Location, Location ) -> Volumes -> Volumes
push bots ( minPos, maxPos ) =
    Dict.insert
        ( -1 * numOverlaps bots ( minPos, maxPos )
        , toOrigin minPos
        , dist maxPos minPos
        )
        ( minPos, maxPos )


pop : Volumes -> ( Maybe ( Location, Location ), Volumes )
pop volumes =
    case Dict.toList volumes |> List.head of
        Just ( key, val ) ->
            ( Just val, Dict.remove key volumes )

        Nothing ->
            ( Nothing, volumes )
```

The recursive subdivision pops the volume off the sorted list of volumes, so always deals with the one with the most intersections. It then adds 8 smaller octants to the sorted list and repeats until the volume at the top of the list is just one cubic unit.

```elm {l}
subdivide : List Bot -> Volumes -> Int
subdivide bots volumes =
    let
        ( maybeVol, remaining ) =
            pop volumes
    in
    case maybeVol of
        Just ( ( minX, minY, minZ ), ( maxX, maxY, maxZ ) ) ->
            if dist ( minX, minY, minZ ) ( maxX, maxY, maxZ ) == 0 then
                -- We've narrowed down volume to a single location
                toOrigin ( minX, minY, minZ )

            else
                let
                    wX =
                        max 1 ((maxX - minX) // 2)

                    wY =
                        max 1 ((maxY - minY) // 2)

                    wZ =
                        max 1 ((maxZ - minZ) // 2)

                    octants =
                        [ ( ( minX, minY, minZ ), ( minX + wX - 1, minY + wY - 1, minZ + wZ - 1 ) )
                        , ( ( minX + wX, minY, minZ ), ( maxX, minY + wY - 1, minZ + wZ - 1 ) )
                        , ( ( minX, minY + wY, minZ ), ( minX + wX - 1, maxY, minZ + wZ - 1 ) )
                        , ( ( minX + wX, minY + wY, minZ ), ( maxX, maxY, minZ + wZ - 1 ) )
                        , ( ( minX, minY, minZ + wZ ), ( minX + wX - 1, minY + wY - 1, maxZ ) )
                        , ( ( minX + wX, minY, minZ + wZ ), ( maxX, minY + wY - 1, maxZ ) )
                        , ( ( minX, minY + wY, minZ + wZ ), ( minX + wX - 1, maxY, maxZ ) )
                        , ( ( minX + wX, minY + wY, minZ + wZ ), ( maxX, maxY, maxZ ) )
                        ]
                in
                subdivide bots (List.foldl (push bots) remaining octants)

        Nothing ->
            -1
```

Once we have a single location, we just find its distance from the origin to get our answer.

```elm {l r}
part2 : Int
part2 =
    let
        bots =
            puzzleInput |> parse

        minPos =
            bots
                |> List.foldl (\b ( mx, my, mz ) -> ( min (b.x - b.r) mx, min (b.y - b.r) my, min (b.z - b.r) mz )) ( 9999999, 9999999, 9999999 )

        maxPos =
            bots
                |> List.foldl (\b ( mx, my, mz ) -> ( max (b.x + b.r) mx, max (b.y + b.r) my, max (b.z + b.r) mz )) ( -9999999, -9999999, -9999999 )
    in
    subdivide bots (Dict.empty |> push bots ( minPos, maxPos ))
```

## Reflection

This was by far the puzzle that challenged me the most. Completing part 1 was easy, and gave me one of my highest entries in the global list of completion times. But part 2 left me stuck for several days. One of the difficulties is that it is possible to generate credible, but incorrect answers that are difficult to test because the solution space is so large.

My initial naive attempt was to try a local hill climbing method that started in a location likely to be close to the point of maximum overlap. I thought this could be found as the centroid of all bots, weighted by the inverse square of their radius. This proved not only a poor estimate of the true location of maximum overlap, but with so many local hills, an approach unlikely to yield a result and an expensive way to navigate such a huge 3d space.

My second attempt involved looking for patterns in the local maxima of overlaps to see if there was some identifiable structure to produce an analytic solution, but again the size of the space made this infeasible.

My third attempt tried to recursively subdivide the space selecting the volume of maximum overlap each time until we had narrowed down the space to a single cubic unit. This worked with the test input and produced a credible but incorrect result. I eventually realised that this would not guarantee a correct result in the case of ties. This could be shown by varying the bounds of the initial volume by some small amount which would sometimes change the location of the final selected location. Frustratingly, the distance of the incorrect result using this method was only one unit way from the correct distance (but far away in absolute position).

The final approach as detailed in the solution above modified my previous approach by keeping track of all subdivided volumes and re-sorting at each recursive call (effectively achieved automatically by storing the number of overlaps, distance from origin and size as a sorted key set in a dictionary). This guaranteed that subdivisions were only excluded from the search if there were smaller subdivisions with higher numbers of intersections found.

The density distribution of the puzzle input, which builds towards the point of maximum overlap meant this approach was fast (under a second). But it is worth noting that had there been a more homogeneous distribution of small cluster densities this could have been a slower search.

---

## Tests

```elm {l}
testInput1 : List String
testInput1 =
    [ "pos=<0,0,0>, r=4"
    , "pos=<1,0,0>, r=1"
    , "pos=<4,0,0>, r=3"
    , "pos=<0,2,0>, r=1"
    , "pos=<0,5,0>, r=3"
    , "pos=<0,0,3>, r=1"
    , "pos=<1,1,1>, r=1"
    , "pos=<1,1,2>, r=1"
    , "pos=<1,3,1>, r=1"
    ]


testInput2 : List String
testInput2 =
    [ "pos=<10,12,12>, r=2"
    , "pos=<12,14,12>, r=2"
    , "pos=<16,12,12>, r=4"
    , "pos=<14,14,14>, r=6"
    , "pos=<50,50,50>, r=200"
    , "pos=<10,10,10>, r=5"
    ]


testInput3 : List String
testInput3 =
    [ "pos=<1,1,0>, r=1"
    , "pos=<15,1,0>, r=1"
    , "pos=<15,15,0>, r=1"
    , "pos=<1,15,0>, r=1"
    , "pos=<1,15,0>, r=1"
    , "pos=<2,2,0>, r=5"
    ]
```
