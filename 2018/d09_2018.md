---
follows: data/d09_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2018): Marble Mania

{( part1|}

You talk to the Elves while you wait for your navigation system to initialize. To pass the time, they introduce you to their favorite marble game.

The Elves play this game by taking turns arranging the marbles in a **circle** according to very particular rules. The marbles are numbered starting with 0 and increasing by 1 until every marble has a number.

First, the marble numbered 0 is placed in the circle. At this point, while it contains only a single marble, it is still a circle: the marble is both clockwise from itself and counter-clockwise from itself. This marble is designated the **current marble**.

Then, each Elf takes a turn placing the **lowest-numbered remaining marble** into the circle between the marbles that are 1 and 2 marbles **clockwise** of the current marble. (When the circle is large enough, this means that there is one marble between the marble that was just placed and the current marble.) The marble that was just placed then becomes the **current marble**.

However, if the marble that is about to be placed has a number which is a multiple of 23, **something entirely different happens**. First, the current player keeps the marble they would have placed, adding it to their **score**. In addition, the marble 7 marbles **counter-clockwise** from the current marble is **removed** from the circle and **also** added to the current player's score. The marble located immediately **clockwise** of the marble that was removed becomes the new **current marble**.

For example, suppose there are 9 players. After the marble with value 0 is placed in the middle, each player (shown in square brackets) takes a turn. The result of each of those turns would produce circles of marbles like this, where clockwise is to the right and the resulting current marble is in parentheses:

    [-](0)
    [1] 0 (1)
    [2] 0 (2) 1
    [3] 0 2 1 (3)
    [4] 0 (4) 2 1 3
    [5] 0 4 2 (5) 1 3
    [6] 0 4 2 5 1 (6) 3
    [7] 0 4 2 5 1 6 3 (7)
    [8] 0 (8) 4 2 5 1 6 3 7
    [9] 0 8 4 (9) 2 5 1 6 3 7
    [1] 0 8 4 9 2(10) 5 1 6 3 7
    [2] 0 8 4 9 2 10 5(11) 1 6 3 7
    [3] 0 8 4 9 2 10 5 11 1(12) 6 3 7
    [4] 0 8 4 9 2 10 5 11 1 12 6(13) 3 7
    [5] 0 8 4 9 2 10 5 11 1 12 6 13 3(14) 7
    [6] 0 8 4 9 2 10 5 11 1 12 6 13 3 14 7(15)
    [7] 0(16) 8 4 9 2 10 5 11 1 12 6 13 3 14 7 15
    [8] 0 16 8(17) 4 9 2 10 5 11 1 12 6 13 3 14 7 15
    [9] 0 16 8 17 4(18) 9 2 10 5 11 1 12 6 13 3 14 7 15
    [1] 0 16 8 17 4 18 9(19) 2 10 5 11 1 12 6 13 3 14 7 15
    [2] 0 16 8 17 4 18 9 19 2(20)10 5 11 1 12 6 13 3 14 7 15
    [3] 0 16 8 17 4 18 9 19 2 20 10(21) 5 11 1 12 6 13 3 14 7 15
    [4] 0 16 8 17 4 18 9 19 2 20 10 21 5(22)11 1 12 6 13 3 14 7 15
    [5] 0 16 8 17 4 18(19) 2 20 10 21 5 22 11 1 12 6 13 3 14 7 15
    [6] 0 16 8 17 4 18 19 2(24)20 10 21 5 22 11 1 12 6 13 3 14 7 15
    [7] 0 16 8 17 4 18 19 2 24 20(25)10 21 5 22 11 1 12 6 13 3 14 7 15

The goal is to be the **player with the highest score** after the last marble is used up. Assuming the example above ends after the marble numbered 25, the winning score is 23 + 9 = **32** (because player 5 kept marble 23 and removed marble 9, while no other player got any points in this very short example game).

Here are a few more examples:

- 10 players; last marble is worth 1618 points: high score is **8317**
- 13 players; last marble is worth 7999 points: high score is **146373**
- 17 players; last marble is worth 1104 points: high score is **2764**
- 21 players; last marble is worth 6111 points: high score is **54718**
- 30 players; last marble is worth 5807 points: high score is **37305**

**What is the winning Elf's score?**

{| part1 )}

## Approach

Circular lists like those in the marble game can be represented as an array and a pointer to the current position. Using modulo arithmetic, we can then increment or decrement this pointer and insert or remove values from the array at the pointer position.

The layout of the example in the question encourages this approach with each line of marbles represented from the start marble of 0 and the marble at the pointer position highlighted in brackets. However in Elm (and many other languages) array slicing and appending is slow.

Implementing this approach took around 13 seconds (and is infeasible for part 2).

A simpler approach is to keep the list of marbles with the head of the list always at the 'pointer' position. Storing the marbles in a deque (double-ended queue) allows fast access to both ends of the list allowing it to be rotated clockwise (positive n) or anticlockwise (negative n). This can be achieved with `rotate` from [aoc.md](../aoc.md).

We can keep track of the game in a record containing the latest deque of marbles, the player scores and the latest player to have placed a marble.

```elm {l}
type alias Game =
    { marbles : Deque Int
    , numPlayers : Int
    , players : Dict Int Int
    , player : Int
    }
```

The rules for playing the game are declared in `addMarble` where we account for the two possibilities (marbles divisible by 23 and those that are not) and update date the player scores when necessary.

```elm {l}
addMarble : Int -> Game -> Game
addMarble marble game =
    if modBy 23 marble == 0 then
        let
            ( removedMarble, newDeque ) =
                game.marbles |> rotate -7 |> Deque.popFront

            score =
                marble + (removedMarble |> Maybe.withDefault 0)

            scores =
                Dict.update game.player
                    (\maybeScore ->
                        case maybeScore of
                            Just oldScore ->
                                Just (oldScore + score)

                            Nothing ->
                                Just score
                    )
                    game.players
        in
        Game newDeque
            game.numPlayers
            scores
            (modBy game.numPlayers (game.player + 1))

    else
        Game (game.marbles |> rotate 2 |> Deque.pushFront marble)
            game.numPlayers
            game.players
            (modBy game.numPlayers (game.player + 1))
```

Playing the game to completion just involves folding a list of marbles over `addMarble` to calculate the reult.

```elm {l}
play : Int -> Int -> Int
play numPlayers numMarbles =
    Array.foldl (\marble game -> addMarble marble game)
        (Game (Deque.singleton 0) numPlayers Dict.empty 0)
        (List.range 1 numMarbles |> Array.fromList)
        |> .players
        |> Dict.values
        |> List.maximum
        |> Maybe.withDefault -1
```

Parsing the input statement is simple, so is included inline. The result is calculated in a few milliseconds.

```elm {l r}
part1 : Int
part1 =
    case puzzleInput |> submatches "(\\d+).+ (\\d+)" |> List.filterMap identity of
        [ numPlayers, numMarbles ] ->
            play (toInt numPlayers) (toInt numMarbles)

        _ ->
            0
```

{( part2 |}

Amused by the speed of your answer, the Elves are curious:

**What would the new winning Elf's score be if the number of the last marble were 100 times larger?**

{| part2 )}

Solving this requires the speed of the deque rotations and calculates the answer in a few seconds.

```elm {l r}
part2 : Int
part2 =
    case puzzleInput |> submatches "(\\d+).+ (\\d+)" |> List.filterMap identity of
        [ numPlayers, numMarbles ] ->
            play (toInt numPlayers) (100 * toInt numMarbles)

        _ ->
            0
```

## Reflection

I really liked this puzzle. It is subtly designed to encourage solving part 1 using array slicing, leading to the inevitable gotcha in part two. I fell for this, despite answering AoC puzzles in previous years using a rotating queue. It was satisfying to see the answer being found so much more quickly once the deque was used.

This is the first puzzle this year where a naive algorithm is unlikely to be able to arrive at a solution in a reasonable time. Moving from array slicing to the deque demonstrted nicely how the underlying datastructure and implementation can be critical in more computational intensive tasks.
