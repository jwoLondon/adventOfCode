---
follows: data/d09_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2018): Marble Mania

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/9).

{( part1|}

What is the winning Elf's score?

{| part1 )}

## Approach

Circular lists like those in the marble game can be represented as an array and a pointer to the current position. Using modulo arithmetic, we can then increment or decrement this pointer and insert or remove values from the array at the pointer position.

The layout of the example in the question encourages this approach with each line of marbles represented from the start marble of 0 and the marble at the pointer position highlighted in brackets. However in Elm (and many other languages) array slicing and appending is slow.

Implementing this approach took around 13 seconds (and is infeasible for part 2).

A simpler approach is to keep the list of marbles with the head of the list always at the 'pointer' position. Storing the marbles in a deque (double-ended queue) allows fast access to both ends of the list allowing it to be rotated clockwise (positive n) or anticlockwise (negative n). This can be achieved with `rotate` from [aoc.md](../aoc.md).

We can keep track of the game in a record containing the latest deque of marbles, the player scores and the latest player to have placed a marble.

```elm {l}
type alias Game =
    { marbles : Deque Int
    , numPlayers : Int
    , players : Dict Int Int
    , player : Int
    }
```

The rules for playing the game are declared in `addMarble` where we account for the two possibilities (marbles divisible by 23 and those that are not) and update date the player scores when necessary.

```elm {l}
addMarble : Int -> Game -> Game
addMarble marble game =
    if modBy 23 marble == 0 then
        let
            ( removedMarble, newDeque ) =
                game.marbles |> AOC.rotateDeque -7 |> Deque.popFront

            score =
                marble + (removedMarble |> Maybe.withDefault 0)

            scores =
                Dict.update game.player
                    (\maybeScore ->
                        case maybeScore of
                            Just oldScore ->
                                Just (oldScore + score)

                            Nothing ->
                                Just score
                    )
                    game.players
        in
        Game newDeque
            game.numPlayers
            scores
            (modBy game.numPlayers (game.player + 1))

    else
        Game (game.marbles |> AOC.rotateDeque 2 |> Deque.pushFront marble)
            game.numPlayers
            game.players
            (modBy game.numPlayers (game.player + 1))
```

Playing the game to completion just involves folding a list of marbles over `addMarble` to calculate the reult.

```elm {l}
play : Int -> Int -> Int
play numPlayers numMarbles =
    Array.foldl (\marble game -> addMarble marble game)
        (Game (Deque.singleton 0) numPlayers Dict.empty 0)
        (List.range 1 numMarbles |> Array.fromList)
        |> .players
        |> Dict.values
        |> List.maximum
        |> Maybe.withDefault -1
```

Parsing the input statement is simple, so is included inline. The result is calculated in a few milliseconds.

```elm {l r}
part1 : Int
part1 =
    case puzzleInput |> AOC.submatches "(\\d+).+ (\\d+)" |> List.filterMap identity of
        [ numPlayers, numMarbles ] ->
            play (AOC.toInt numPlayers) (AOC.toInt numMarbles)

        _ ->
            0
```

{( part2 |}

What would the new winning Elf's score be if the number of the last marble were 100 times larger?

{| part2 )}

Solving this requires the speed of the deque rotations and calculates the answer in a few seconds.

```elm {l r}
part2 : Int
part2 =
    case puzzleInput |> AOC.submatches "(\\d+).+ (\\d+)" |> List.filterMap identity of
        [ numPlayers, numMarbles ] ->
            play (AOC.toInt numPlayers) (100 * AOC.toInt numMarbles)

        _ ->
            0
```

---

## Reflection

I really liked this puzzle. It is subtly designed to encourage solving part 1 using array slicing, leading to the inevitable gotcha in part two. I fell for this, despite answering AoC puzzles in previous years using a rotating queue. It was satisfying to see the answer being found so much more quickly once the deque was used.

This is the first puzzle this year where a naive algorithm is unlikely to be able to arrive at a solution in a reasonable time. Moving from array slicing to the deque demonstrated nicely how the underlying data structure and implementation can be critical in more computational intensive tasks.
