---
# follows: data/d19_2018.md
follows: d16_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 19 (2018): Go With The Flow

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/19):

{( part1|}

With the Elves well on their way constructing the North Pole base, you turn your attention back to understanding the inner workings of programming the device.

You can't help but notice that the device's opcodes don't contain any **flow control** like jump instructions. The device's manual goes on to explain:

"In programs where flow control is required, the instruction pointer can be **bound to a register** so that it can be manipulated directly. This way, setr/seti can function as absolute jumps, addr/addi can function as relative jumps, and other opcodes can cause truly fascinating effects."

This mechanism is achieved through a declaration like #ip 1, which would modify register 1 so that accesses to it let the program indirectly access the instruction pointer itself. To compensate for this kind of binding, there are now six registers (numbered 0 through 5); the five not bound to the instruction pointer behave as normal. Otherwise, the same rules apply as the [last time you worked with this device](d16_2018.md).

When the **instruction pointer** is bound to a register, its value is written to that register just before each instruction is executed, and the value of that register is written back to the instruction pointer immediately after each instruction finishes execution. Afterward, move to the next instruction by adding one to the instruction pointer, even if the value in the instruction pointer was just updated by an instruction. (Because of this, instructions must effectively set the instruction pointer to the instruction **before** the one they want executed next.)

The instruction pointer is 0 during the first instruction, 1 during the second, and so on. If the instruction pointer ever causes the device to attempt to load an instruction outside the instructions defined in the program, the program instead immediately halts. The instruction pointer starts at 0.

It turns out that this new information is already proving useful: the CPU in the device is not very powerful, and a background process is occupying most of its time. You dump the background process' declarations and instructions to a file (your puzzle input), making sure to use the names of the opcodes rather than the numbers.

For example, suppose you have the following program:

    #ip 0
    seti 5 0 1
    seti 6 0 2
    addi 0 1 0
    addr 1 2 3
    setr 1 0 0
    seti 8 0 4
    seti 9 0 5

When executed, the following instructions are executed. Each line contains the value of the instruction pointer at the time the instruction started, the values of the six registers before executing the instructions (in square brackets), the instruction itself, and the values of the six registers after executing the instruction (also in square brackets).

    ip=0 [0, 0, 0, 0, 0, 0] seti 5 0 1 [0, 5, 0, 0, 0, 0]
    ip=1 [1, 5, 0, 0, 0, 0] seti 6 0 2 [1, 5, 6, 0, 0, 0]
    ip=2 [2, 5, 6, 0, 0, 0] addi 0 1 0 [3, 5, 6, 0, 0, 0]
    ip=4 [4, 5, 6, 0, 0, 0] setr 1 0 0 [5, 5, 6, 0, 0, 0]
    ip=6 [6, 5, 6, 0, 0, 0] seti 9 0 5 [6, 5, 6, 0, 0, 9]

In detail, when running this program, the following events occur:

- The first line (#ip 0) indicates that the instruction pointer should be bound to register 0 in this program. This is not an instruction, and so the value of the instruction pointer does not change during the processing of this line.
- The instruction pointer contains 0, and so the first instruction is executed (seti 5 0 1). It updates register 0 to the current instruction pointer value (0), sets register 1 to 5, sets the instruction pointer to the value of register 0 (which has no effect, as the instruction did not modify register 0), and then adds one to the instruction pointer.
- The instruction pointer contains 1, and so the second instruction, seti 6 0 2, is executed. This is very similar to the instruction before it: 6 is stored in register 2, and the instruction pointer is left with the value 2.
- The instruction pointer is 2, which points at the instruction addi 0 1 0. This is like a relative jump: the value of the instruction pointer, 2, is loaded into register 0. Then, addi finds the result of adding the value in register 0 and the value 1, storing the result, 3, back in register 0. Register 0 is then copied back to the instruction pointer, which will cause it to end up 1 larger than it would have otherwise and skip the next instruction (addr 1 2 3) entirely. Finally, 1 is added to the instruction pointer.
- The instruction pointer is 4, so the instruction setr 1 0 0 is run. This is like an absolute jump: it copies the value contained in register 1, 5, into register 0, which causes it to end up in the instruction pointer. The instruction pointer is then incremented, leaving it at 6.
- The instruction pointer is 6, so the instruction seti 9 0 5 stores 9 into register 5. The instruction pointer is incremented, causing it to point outside the program, and so the program ends.

**What value is left in register 0 when the background process halts?**

{| part1 )}

## Parsing and data structures

Given the similarity with [Chronal Classification](d16_2018.md) we can share much of the infrastructure necessary to store the instruction set (this document inherits the Day 16 document, so we have all that functionality for free).

What differs here is that we are provided with named operators in the input data and we have a special register for storing the pointer to the next instruction to execute. We can represent both, along with the registers and program of operators, as a record:

```elm {l}
type alias Program =
    { pointerReg : Int
    , registers : Registers
    , prog : Dict Int (Registers -> Registers)
    , ip : Int
    }
```

We can parse the input data to create a dictionary of operations referenced by their position in the program:

```elm {l}
parseInstructions : List String -> Dict Int (Registers -> Registers)
parseInstructions =
    let
        toOp instr =
            case instr of
                [ "addr", p1, p2, p3 ] ->
                    addr (toInt p1) (toInt p2) (toInt p3)

                [ "addi", p1, p2, p3 ] ->
                    addi (toInt p1) (toInt p2) (toInt p3)

                [ "mulr", p1, p2, p3 ] ->
                    mulr (toInt p1) (toInt p2) (toInt p3)

                [ "muli", p1, p2, p3 ] ->
                    muli (toInt p1) (toInt p2) (toInt p3)

                [ "banr", p1, p2, p3 ] ->
                    banr (toInt p1) (toInt p2) (toInt p3)

                [ "bani", p1, p2, p3 ] ->
                    bani (toInt p1) (toInt p2) (toInt p3)

                [ "borr", p1, p2, p3 ] ->
                    borr (toInt p1) (toInt p2) (toInt p3)

                [ "bori", p1, p2, p3 ] ->
                    bori (toInt p1) (toInt p2) (toInt p3)

                [ "setr", p1, p2, p3 ] ->
                    setr (toInt p1) (toInt p2) (toInt p3)

                [ "seti", p1, p2, p3 ] ->
                    seti (toInt p1) (toInt p2) (toInt p3)

                [ "gtir", p1, p2, p3 ] ->
                    gtir (toInt p1) (toInt p2) (toInt p3)

                [ "gtri", p1, p2, p3 ] ->
                    gtri (toInt p1) (toInt p2) (toInt p3)

                [ "gtrr", p1, p2, p3 ] ->
                    gtrr (toInt p1) (toInt p2) (toInt p3)

                [ "eqir", p1, p2, p3 ] ->
                    eqir (toInt p1) (toInt p2) (toInt p3)

                [ "eqri", p1, p2, p3 ] ->
                    eqri (toInt p1) (toInt p2) (toInt p3)

                [ "eqrr", p1, p2, p3 ] ->
                    eqrr (toInt p1) (toInt p2) (toInt p3)

                _ ->
                    Debug.todo "unknown instruction"
    in
    List.map (String.split " " >> toOp)
        >> List.indexedMap Tuple.pair
        >> Dict.fromList
```

## Running the Program

Running the program involves iterating over the three-step process

- write instruction pointer (ip) to the special register
- apply operation pointed to by the ip
- update the ip with the possibly new contents of the special register and increment it

Flow continues until the ip points outside the program.

```elm {l}
run : Program -> Program
run prog0 =
    let
        prog1 =
            { prog0 | registers = set prog0.pointerReg prog0.ip prog0.registers }
    in
    case Dict.get prog1.ip prog1.prog of
        Nothing ->
            -- Outside program so halt
            prog1

        Just op ->
            let
                prog2 =
                    { prog1 | registers = op prog1.registers }

                newIP =
                    get prog2.pointerReg prog2.registers
            in
            run
                { prog2
                    | registers = set prog2.pointerReg newIP prog2.registers
                    , ip = newIP + 1
                }
```

```elm {l r}
part1Day19 : Int
part1Day19 =
    let
        program =
            Program 5 (init [ 0, 0, 0, 0, 0, 0 ]) (parseInstructions puzzleInputDay19) 0
    in
    run program
        |> .registers
        |> get 0
```

{( part2 |}

A new background process immediately spins up in its place. It appears identical, but on closer inspection, you notice that **this time, register 0 started with the value 1.**

**What value is left in register 0 when this new background process halts?**

{| part2 )}

Running the program with register 0 set to 1 reveals the program would take too long to compute directly, so the task becomes one of building a higher-level and optimised program from the instruction set. Doing so requires inspection of the program to identify its structure.

## Stage One: Renaming Registers and instructions

To make interpreting the program easier, we can name registers 0-6 as letters, so as not to confuse registers from their values or addresses.

- Registers: 0 to 4 labelled a-e; register 5 labelled 'pointer'.
- `seti`/`setr` ignore parameter B so this parameter is not displayed.
- operations that affect register 5 are jumps of some kind, relative indicated by `jmp`, absolute by `JMP`.
- `gt..` and `eq..` operations can be summarised as by a ternary operator.
- Instruction at address 16 will jump to address 256, so halting the program.

| Address | Instruction | Param A | Param B | Param C | action             |
| ------: | ----------- | ------: | ------: | ------: | ------------------ |
|       0 | `addi`      |       5 |      16 |       5 | jmp +16            |
|       1 | `seti`      |       1 |         |       3 | d = 1              |
|       2 | `seti`      |       1 |         |       2 | c = 1              |
|       3 | `mulr`      |       3 |       2 |       4 | e = c \* d         |
|       4 | `eqrr`      |       4 |       1 |       4 | e = (b==e)? 1 : 0  |
|       5 | `addr`      |       4 |       5 |       5 | jmp +e             |
|       6 | `addi`      |       5 |       1 |       5 | jmp +1             |
|       7 | `addr`      |       3 |       0 |       0 | a = a + d          |
|       8 | `addi`      |       2 |       1 |       2 | c = c + 1          |
|       9 | `gtrr`      |       2 |       1 |       4 | e = (c > b)? 1 : 0 |
|      10 | `addr`      |       5 |       4 |       5 | jmp +e             |
|      11 | `seti`      |       2 |         |       5 | JMP 2              |
|      12 | `addi`      |       3 |       1 |       3 | d = d + 1          |
|      13 | `gtrr`      |       3 |       1 |       4 | e = (d > b)? 1 : 0 |
|      14 | `addr`      |       4 |       5 |       5 | jmp +e             |
|      15 | `seti`      |       1 |         |       5 | JMP 1              |
|      16 | `mulr`      |       5 |       5 |       5 | HALT               |
|      17 | `addi`      |       1 |       2 |       1 | b = b + 2          |
|      18 | `mulr`      |       1 |       1 |       1 | b = b \* b         |
|      19 | `mulr`      |       5 |       1 |       1 | b = b \* pointer   |
|      20 | `muli`      |       1 |      11 |       1 | b = b \* 11        |
|      21 | `addi`      |       4 |       1 |       4 | e = e + 1          |
|      22 | `mulr`      |       4 |       5 |       4 | e = e \* pointer   |
|      23 | `addi`      |       4 |       9 |       4 | e = e + 9          |
|      24 | `addr`      |       1 |       4 |       1 | b = b + e          |
|      25 | `addr`      |       5 |       0 |       5 | jmp +a             |
|      26 | `seti`      |       0 |         |       5 | JMP 0              |
|      27 | `setr`      |       5 |         |       4 | e = pointer        |
|      28 | `mulr`      |       4 |       5 |       4 | e = e \* pointer   |
|      29 | `addr`      |       5 |       4 |       4 | e = e + pointer    |
|      30 | `mulr`      |       5 |       4 |       4 | e = e \* pointer   |
|      31 | `muli`      |       4 |      14 |       4 | e = e \* 14        |
|      32 | `mulr`      |       4 |       5 |       4 | e = e \* pointer   |
|      33 | `addr`      |       1 |       4 |       1 | b = b + e          |
|      34 | `seti`      |       0 |         |       0 | a = 0              |
|      35 | `seti`      |       0 |         |       5 | JMP 0              |

## State Two: Identifying code blocks

We can separate the program flow instructions from the others, revealing blocks of code that perform specific actions (displayed below in order they are first encountered):

| Block | Addresses | Action                            |
| ----- | --------- | --------------------------------- |
| 1     | 17 - 24   | set b to 4\*19\*11 + 31           |
| 2     | 27 - 34   | inc b by (27\*28 +29)\*30\*14\*32 |
| 3     | 1 - 4     | set e to c\*d                     |
| 4     | 7 - 9     | inc c by 1; inc a by d            |
| 5     | 12 - 13   | inc d                             |

## Stage Three: Replace with higher level control flow instructions

The control flow instructions between blocks show a similar pattern - setting register e to the result of comparing two registers and jumping to one of two locations depending on the result. The location jumped to is one of the code blocks above.
This yields a higher level program:

```java
// Blocks 1 and 2:
if (a == 0) {
    b = 867;
}
else {
    b =  10551267;
}
a = 0;

// Blocks 3, 4 and 5
d = 1;
do {
  c = 1;
  do {
    if (b == c*d) {
      a = a + d;
    }
    c = c + 1;
  }
  while (c <= b);

  d = d + 1;
}
while (d <= b);
```

It is now easier to spot that main nested loops are identifying the factors of b by testing all permutations of c\*d, incrementing c in the inner loop and d in the outer loop.

This would be a long process for such a large value of b if we were to use such a brute-force method. Instead, we can calculate the value directly in Elm in about 4 seconds:

```elm {l r}
part2Day19 : Int
part2Day19 =
    factors 10551267 |> List.sum
```

## Reflection

I found part one quite straightforward (albeit taking a while to spot an subtle mistyping in my parsing function that led to `eqri` being interpreted as `eqir`).

Part two I found much harder. I don't work with assembly language regularly (in fact AoC and the [Synacor Challenge](https://challenge.synacor.com) are the only times I have done so in the last 35 years). The process of converting low-level to higher-level instructions described above took me a couple of hours or so. But once in more Java-like language the sum of factors calculation popped out more obviously.

I doubt this process could be automatically replicated as even with the higher-level instructions, this brute-force factors algorithm is too inefficient to operate on such a large value of b. It would be possible to at least extract b from the program automatically and then apply a more sensible factors algorithm, but the main satisfaction to be gained from this puzzle was to identify what the program did rather than to implement it.

---

## Data

```elm {l=hidden}
puzzleInputDay19 : List String
puzzleInputDay19 =
    [ "addi 5 16 5"
    , "seti 1 5 3"
    , "seti 1 4 2"
    , "mulr 3 2 4"
    , "eqrr 4 1 4"
    , "addr 4 5 5"
    , "addi 5 1 5"
    , "addr 3 0 0"
    , "addi 2 1 2"
    , "gtrr 2 1 4"
    , "addr 5 4 5"
    , "seti 2 2 5"
    , "addi 3 1 3"
    , "gtrr 3 1 4"
    , "addr 4 5 5"
    , "seti 1 0 5"
    , "mulr 5 5 5"
    , "addi 1 2 1"
    , "mulr 1 1 1"
    , "mulr 5 1 1"
    , "muli 1 11 1"
    , "addi 4 1 4"
    , "mulr 4 5 4"
    , "addi 4 9 4"
    , "addr 1 4 1"
    , "addr 5 0 5"
    , "seti 0 5 5"
    , "setr 5 6 4"
    , "mulr 4 5 4"
    , "addr 5 4 4"
    , "mulr 5 4 4"
    , "muli 4 14 4"
    , "mulr 4 5 4"
    , "addr 1 4 1"
    , "seti 0 3 0"
    , "seti 0 1 5"
    ]
```

```elm {l}
testInput =
    [ "seti 5 0 1"
    , "seti 6 0 2"
    , "addi 0 1 0"
    , "addr 1 2 3"
    , "setr 1 0 0"
    , "seti 8 0 4"
    , "seti 9 0 5"
    ]
```
