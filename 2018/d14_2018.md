---
follows: data/d14_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 14 (2018): Chocolate Charts

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/14):

{( part1|}

You finally have a chance to look at all of the produce moving around. Chocolate, cinnamon, mint, chili peppers, nutmeg, vanilla... the Elves must be growing these plants to make **hot chocolate**! As you realize this, you hear a conversation in the distance. When you go to investigate, you discover two Elves in what appears to be a makeshift underground kitchen/laboratory.

The Elves are trying to come up with the ultimate hot chocolate recipe; they're even maintaining a scoreboard which tracks the quality **score** (0-9) of each recipe.

Only two recipes are on the board: the first recipe got a score of 3, the second, 7. Each of the two Elves has a **current recipe**: the first Elf starts with the first recipe, and the second Elf starts with the second recipe.

To create new recipes, the two Elves combine their current recipes. This creates new recipes from the **digits of the sum** of the current recipes' scores. With the current recipes' scores of 3 and 7, their sum is 10, and so two new recipes would be created: the first with score 1 and the second with score 0. If the current recipes' scores were 2 and 3, the sum, 5, would only create one recipe (with a score of 5) with its single digit.

The new recipes are added to the end of the scoreboard in the order they are created. So, after the first round, the scoreboard is 3, 7, 1, 0.

After all new recipes are added to the scoreboard, each Elf picks a new current recipe. To do this, the Elf steps forward through the scoreboard a number of recipes equal to **1 plus the score of their current recipe**. So, after the first round, the first Elf moves forward 1 + 3 = 4 times, while the second Elf moves forward 1 + 7 = 8 times. If they run out of recipes, they loop back around to the beginning. After the first round, both Elves happen to loop around until they land on the same recipe that they had in the beginning; in general, they will move to different recipes.

Drawing the first Elf as parentheses and the second Elf as square brackets, they continue this process:

    (3)[7]
    (3)[7] 1 0
    3 7 1 [0](1) 0
    3 7 1 0 [1] 0 (1)
    (3) 7 1 0 1 0 [1] 2
    3 7 1 0 (1) 0 1 2 [4]
    3 7 1 [0] 1 0 (1) 2 4 5
    3 7 1 0 [1] 0 1 2 (4) 5 1
    3 (7) 1 0 1 0 [1] 2 4 5 1 5
    3 7 1 0 1 0 1 2 [4](5) 1 5 8
    3 (7) 1 0 1 0 1 2 4 5 1 5 8 [9]
    3 7 1 0 1 0 1 [2] 4 (5) 1 5 8 9 1 6
    3 7 1 0 1 0 1 2 4 5 [1] 5 8 9 1 (6) 7
    3 7 1 0 (1) 0 1 2 4 5 1 5 [8] 9 1 6 7 7
    3 7 [1] 0 1 0 (1) 2 4 5 1 5 8 9 1 6 7 7 9
    3 7 1 0 [1] 0 1 2 (4) 5 1 5 8 9 1 6 7 7 9 2

The Elves think their skill will improve after making a few recipes (your puzzle input). However, that could take ages; you can speed this up considerably by identifying **the scores of the ten recipes** after that. For example:

- If the Elves think their skill will improve after making 9 recipes, the scores of the ten recipes after the first nine on the scoreboard would be 5158916779 (highlighted in the last line of the diagram).
- After 5 recipes, the scores of the next ten would be 0124515891.
- After 18 recipes, the scores of the next ten would be 9251071085.
- After 2018 recipes, the scores of the next ten would be 5941429882.

**What are the scores of the ten recipes immediately after the number of recipes in your puzzle input?**

{| part1 )}

## Data Structure

We need to be able to store the sequence of recipe scores and query scores from it at arbitrary locations. While the circular nature of the querying might suggest a deque, we have two diverging query points (elf 1 and elf 2), so querying both involves many rotations as the sequence lengthens, so we gain little in efficiency. Instead, an array with fast random access is sufficient. We also need to keep track of the two elf positions with a couple of pointers.

As the sequence lengthens, performing array slicing in order to retreive a block from the end is costly, so we additionally store a fixed length deque that will store the most recent items added to the sequence. We don't need this to be double-ended as we only push to one end of the structure, but Elm does provide a convenient fixed length collection in the form of a [Bounded Deque](https://package.elm-lang.org/packages/folkertdev/elm-deque/latest/BoundedDeque).

We can wrap these up in a record and provide a convenience function for initialising it:

```elm {l}
type alias Scoreboard =
    { scores : Array Int
    , recentBlock : BoundedDeque Int
    , p1 : Int
    , p2 : Int
    }


initScoreboard : Int -> Int -> Int -> Scoreboard
initScoreboard blockSize s1 s2 =
    Scoreboard (Array.fromList [ s1, s2 ]) (BoundedDeque.empty blockSize) 0 1
```

## Growing the recipe scores

At the heart of the solution is the encoding of the recipe growing rules. These are straightforward to represent, noting that we may increase the length of the scores by one or two items depending on whether the sum of the two values at the elf pointers are greater than nine.

```elm {l}
nextScoreboard : Scoreboard -> Scoreboard
nextScoreboard sb =
    let
        score p =
            Array.get p sb.scores |> Maybe.withDefault 0

        sum =
            score sb.p1 + score sb.p2

        ( newScores, recentBlock ) =
            if sum > 9 then
                ( sb.scores |> Array.push 1 |> Array.push (modBy 10 sum)
                , sb.recentBlock |> BoundedDeque.pushBack 1 |> BoundedDeque.pushBack (modBy 10 sum)
                )

            else
                ( sb.scores |> Array.push sum
                , sb.recentBlock |> BoundedDeque.pushBack sum
                )

        p1 =
            modBy (Array.length newScores) (sb.p1 + score sb.p1 + 1)

        p2 =
            modBy (Array.length newScores) (sb.p2 + score sb.p2 + 1)
    in
    Scoreboard newScores recentBlock p1 p2
```

We can now iterate over the recipe generation process until we have created enough recipe scores.

```elm {l}
addRecipe : Scoreboard -> Int -> Scoreboard
addRecipe sb n =
    if Array.length sb.scores < (n + 10) then
        addRecipe (nextScoreboard sb) n

    else
        sb
```

```elm {l r}
part1 : String
part1 =
    puzzleInput
        |> AOC.toInt
        |> addRecipe (initScoreboard 10 3 7)
        |> .recentBlock
        |> BoundedDeque.toList
        |> List.map String.fromInt
        |> String.concat
```

{( part2 |}

As it turns out, you got the Elves' plan backwards. They actually want to know how many recipes appear on the scoreboard to the left of the first recipes whose scores are the digits from your puzzle input.

- 51589 first appears after 9 recipes.
- 01245 first appears after 5 recipes.
- 92510 first appears after 18 recipes.
- 59414 first appears after 2018 recipes.

**How many recipes appear on the scoreboard to the left of the score sequence in your puzzle input?**

{| part2 )}

For the second part we also need to iteratively generate new scores, this time terminating when the last items added match the puzzle input.

Because we have kept track of the last items to be added after each iteration, we can compare the block with the puzzle to test for a match. The catch is that because we sometimes add two scores in a single iteration we need to compare with both the last 6 and last 7 items (for a 6 digit input sequence).

```elm {l}
tenBefore : List Int -> Scoreboard -> Int
tenBefore pattern scoreboard =
    let
        matchInLast sequence lastBlock =
            (sequence == List.take 6 lastBlock) || (sequence == List.drop 1 lastBlock)

        add sb =
            if matchInLast pattern (BoundedDeque.toList sb.recentBlock) then
                sb

            else
                add (nextScoreboard sb)

        finalScores =
            add scoreboard

        offset =
            if pattern == List.take 6 (BoundedDeque.toList finalScores.recentBlock) then
                0

            else
                1
    in
    Array.length finalScores.scores - BoundedDeque.length finalScores.recentBlock + offset
```

```elm {l r}
part2 : Int
part2 =
    let
        inputList =
            puzzleInput |> String.split "" |> List.map AOC.toInt
    in
    tenBefore inputList (initScoreboard (List.length inputList + 1) 3 7)
```

## Reflection

I found this puzzle quite frustrating in Elm. Conceptually it is not hard to solve, but it does rely on having a data structure that allows efficient query and slicing of very long lists. My initial attempts using an array and array slicing for taking the last 6/7 items for comparison was way too slow in Elm to be practical. This made debugging and spotting 'off-by-one errors' tedious to spot, as generation of a 20 million item array was taking an hour plus to compute. Especially problematic because the examples in the question were contrived not to reveal the 'gotcha' generated by matching against a most recent addition of two recipe scores.

Getting frustrated I coded up the same algorithm in Java, which due to its more efficient array processing, found the answer to part two in less than three seconds. This led me to introduce the bounded deque in my Elm solution so I could avoid slicing the long array. Using this approach in Elm it calculated the part 2 solution, which generates an array of over 20 million items, in around 20 seconds.

Despite a somewhat frustrating experience I was reasonably pleased with the final Elm solution. Not sure if how I might improve the solution reduce computation time further.
