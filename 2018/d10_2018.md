---
follows: data/d10_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2018): The Stars Align

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/10).

{( part1|}

What message will eventually appear in the sky?

{| part1 )}

## Parsing and Data Structures

We will need to store the position and movement vector of each star, so we can parse into a record as with previous puzzles.

```elm {l}
type alias Light =
    { x : Int
    , y : Int
    , vx : Int
    , vy : Int
    }


parse : List String -> List Light
parse =
    let
        rectValues =
            AOC.submatches "< *(-*\\d+), *(-*\\d+).+< *(-*\\d+), *(-*\\d)"
                >> List.filterMap identity
                >> List.map AOC.toInt

        parseLine line =
            case rectValues line of
                [ x, y, vx, vy ] ->
                    Just (Light x y vx vy)

                _ ->
                    Nothing
    in
    List.filterMap parseLine
```

## Approach

Given the example input, it is a fairly safe assumption that the lights will converge on the message pattern before diverging again, so finding the message involves iteratively moving the stars while it results in a smaller bounding rectangle.

Calculating the bounding rectangle and its area:

```elm {l}
bounds : List Light -> ( ( Int, Int ), ( Int, Int ) )
bounds lights =
    ( ( lights |> List.map .x |> List.minimum |> Maybe.withDefault 0
      , lights |> List.map .y |> List.minimum |> Maybe.withDefault 0
      )
    , ( lights |> List.map .x |> List.maximum |> Maybe.withDefault 0
      , lights |> List.map .y |> List.maximum |> Maybe.withDefault 0
      )
    )


area : List Light -> Int
area lights =
    let
        ( ( minX, minY ), ( maxX, maxY ) ) =
            bounds lights
    in
    (maxX - minX) * (maxY - minY)
```

We can continue to move the lights until doing so no longer reduces the bounding area, keeping track of the number of time steps required to reach the minimum.

```elm {l}
moveToMinArea : ( List Light, Int ) -> ( List Light, Int )
moveToMinArea ( lights, t ) =
    let
        move =
            List.map (\l -> Light (l.x + l.vx) (l.y + l.vy) l.vx l.vy)
    in
    if area (move lights) < area lights then
        moveToMinArea ( move lights, t + 1 )

    else
        ( lights, t )
```

```elm {l}
message : ( List Light, Int )
message =
    moveToMinArea ( parse puzzleInput, 0 )
```

## Generating Output

Because we will need to display the lights at their closest configuration, we will need to genrate a grid of symbols representing their locations. Within the literate Elm environment, we can generate markdown pre-formatted output of this grid to display it.

```elm {l}
markdown : List Light -> List String
markdown lights =
    let
        ( ( minX, minY ), ( maxX, maxY ) ) =
            bounds lights

        toMbrOrigin ox oy =
            List.map (\l -> Light (l.x - ox) (l.y - oy) l.vx l.vy)

        grid =
            List.foldl (\l g -> AOC.gSet ( l.y, l.x ) "â–ˆ" g) (AOC.gInit (1 + maxY - minY) (1 + maxX - minX) " ") (toMbrOrigin minX minY lights)
    in
    displayGrid (1 + maxY - minY) grid |> List.intersperse "\n    "


displayGrid : Int -> AOC.Grid String -> List String
displayGrid numRows grd =
    List.foldl
        (\row lines -> lines ++ [ AOC.gGetRow row grd |> Maybe.withDefault [] |> String.concat ])
        [ "    " ]
        (List.range 0 (numRows - 1))
```

```elm {l}
part1 : List String
part1 =
    message |> Tuple.first |> markdown
```

{(fixed|}^^^elm{m=part1}^^^{|fixed)}

{( part2 |}

Exactly how many seconds would they have needed to wait for that message to appear?

{| part2 )}

Here we simply extract the number of steps required to reach the minimum area:

```elm {l r}
part2 : Int
part2 =
    message |> Tuple.second
```

---

## Reflection

I really enjoyed this puzzle. Being able to display the light grid at any time t was straightforward to code, and it was good to be able to take advantage of Litvis's markdown generating option to show the grid. The 'ah-ha!' moment came when I realised that minimising the bounding area was the likely halt condition for moving the lights. There is something magic about seeing the message appear out of apparently random looking distribution of lights.
