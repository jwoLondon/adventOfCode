---
follows: data/d10_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2018): The Stars Align

{( part1|}

It's no use; your navigation system simply isn't capable of providing walking directions in the arctic circle, and certainly not in 1018.

The Elves suggest an alternative. In times like these, North Pole rescue operations will arrange points of light in the sky to guide missing Elves back to base. Unfortunately, the message is easy to miss: the points move slowly enough that it takes hours to align them, but have so much momentum that they only stay aligned for a second. If you blink at the wrong time, it might be hours before another message appears.

You can see these points of light floating in the distance, and record their position in the sky and their velocity, the relative change in position per second (your puzzle input). The coordinates are all given from your perspective; given enough time, those positions and velocities will move the points into a cohesive message!

Rather than wait, you decide to fast-forward the process and calculate what the points will eventually spell.

For example, suppose you note the following points:

```
position=< 9,  1> velocity=< 0,  2>
position=< 7,  0> velocity=<-1,  0>
position=< 3, -2> velocity=<-1,  1>
position=< 6, 10> velocity=<-2, -1>
position=< 2, -4> velocity=< 2,  2>
position=<-6, 10> velocity=< 2, -2>
position=< 1,  8> velocity=< 1, -1>
position=< 1,  7> velocity=< 1,  0>
position=<-3, 11> velocity=< 1, -2>
position=< 7,  6> velocity=<-1, -1>
position=<-2,  3> velocity=< 1,  0>
position=<-4,  3> velocity=< 2,  0>
position=<10, -3> velocity=<-1,  1>
position=< 5, 11> velocity=< 1, -2>
position=< 4,  7> velocity=< 0, -1>
position=< 8, -2> velocity=< 0,  1>
position=<15,  0> velocity=<-2,  0>
position=< 1,  6> velocity=< 1,  0>
position=< 8,  9> velocity=< 0, -1>
position=< 3,  3> velocity=<-1,  1>
position=< 0,  5> velocity=< 0, -1>
position=<-2,  2> velocity=< 2,  0>
position=< 5, -2> velocity=< 1,  2>
position=< 1,  4> velocity=< 2,  1>
position=<-2,  7> velocity=< 2, -2>
position=< 3,  6> velocity=<-1, -1>
position=< 5,  0> velocity=< 1,  0>
position=<-6,  0> velocity=< 2,  0>
position=< 5,  9> velocity=< 1, -2>
position=<14,  7> velocity=<-2,  0>
position=<-3,  6> velocity=< 2, -1>
```

Each line represents one point. Positions are given as `<X, Y>` pairs: X represents how far left (negative) or right (positive) the point appears, while Y represents how far up (negative) or down (positive) the point appears.

At 0 seconds, each point has the position given. Each second, each point's velocity is added to its position. So, a point with velocity `<1, -2>` is moving to the right, but is moving upward twice as quickly. If this point's initial position were `<3, 9>`, after 3 seconds, its position would become `<6, 3>`.

Over time, the points listed above would move like this:

Initially:

```
........#.............
................#.....
.........#.#..#.......
......................
#..........#.#.......#
...............#......
....#.................
..#.#....#............
.......#..............
......#...............
...#...#.#...#........
....#..#..#.........#.
.......#..............
...........#..#.......
#...........#.........
...#.......#..........
```

After 1 second:

```
......................
......................
..........#....#......
........#.....#.......
..#.........#......#..
......................
......#...............
....##.........#......
......#.#.............
.....##.##..#.........
........#.#...........
........#...#.....#...
..#...........#.......
....#.....#.#.........
......................
......................
```

After 2 seconds:

```
......................
......................
......................
..............#.......
....#..#...####..#....
......................
........#....#........
......#.#.............
.......#...#..........
.......#..#..#.#......
....#....#.#..........
.....#...#...##.#.....
........#.............
......................
......................
......................
```

After 3 seconds:

```
......................
......................
......................
......................
......#...#..###......
......#...#...#.......
......#...#...#.......
......#####...#.......
......#...#...#.......
......#...#...#.......
......#...#...#.......
......#...#..###......
......................
......................
......................
......................
```

After 4 seconds:

```
......................
......................
......................
............#.........
........##...#.#......
......#.....#..#......
.....#..##.##.#.......
.......##.#....#......
...........#....#.....
..............#.......
....#......#...#......
.....#.....##.........
...............#......
...............#......
......................
......................
```

After 3 seconds, the message appeared briefly: `HI`. Of course, your message will be much longer and will take many more seconds to appear.

**What message will eventually appear in the sky?**

{| part1 )}

## Parsing and Data Structures

We will need to store the position and movement vector of each star, so we can parse into a record as with previous puzzles.

```elm {l}
type alias Light =
    { x : Int
    , y : Int
    , vx : Int
    , vy : Int
    }


parse : List String -> List Light
parse =
    let
        rectValues =
            submatches "< *(-*\\d+), *(-*\\d+).+< *(-*\\d+), *(-*\\d)"
                >> List.filterMap identity
                >> List.map toInt

        parseLine line =
            case rectValues line of
                [ x, y, vx, vy ] ->
                    Just (Light x y vx vy)

                _ ->
                    Nothing
    in
    List.filterMap parseLine
```

## Approach

Given the example input, it is a fairly safe assumption that the lights will converge on the message pattern before diverging again, so finding the message involves iteratively moving the stars while it results in a smaller bounding rectangle.

Calculatig the bounding rectangle and its area:

```elm {l}
bounds : List Light -> ( ( Int, Int ), ( Int, Int ) )
bounds lights =
    ( ( lights |> List.map .x |> List.minimum |> Maybe.withDefault 0
      , lights |> List.map .y |> List.minimum |> Maybe.withDefault 0
      )
    , ( lights |> List.map .x |> List.maximum |> Maybe.withDefault 0
      , lights |> List.map .y |> List.maximum |> Maybe.withDefault 0
      )
    )


area : List Light -> Int
area lights =
    let
        ( ( minX, minY ), ( maxX, maxY ) ) =
            bounds lights
    in
    (maxX - minX) * (maxY - minY)
```

We can continue to move the lights until doing so no longer reduces the bounding area, keeping track of the number of time steps required to reach the minimum.

```elm {l}
moveToMinArea : ( List Light, Int ) -> ( List Light, Int )
moveToMinArea ( lights, t ) =
    let
        move =
            List.map (\l -> Light (l.x + l.vx) (l.y + l.vy) l.vx l.vy)
    in
    if area (move lights) < area lights then
        moveToMinArea ( move lights, t + 1 )

    else
        ( lights, t )
```

```elm {l}
message : ( List Light, Int )
message =
    moveToMinArea ( parse puzzleInput, 0 )
```

## Generating output

Because we will need to display the lights at their closest configuration, we will need to genrate a grid of symbols representing their locations. Within the literate Elm environment, we can generate markdown pre-formatted output of this grid to display it.

```elm {l}
markdown : List Light -> List String
markdown lights =
    let
        ( ( minX, minY ), ( maxX, maxY ) ) =
            bounds lights

        toMbrOrigin ox oy =
            List.map (\l -> Light (l.x - ox) (l.y - oy) l.vx l.vy)

        grid =
            List.foldl (\l g -> gSet ( l.y, l.x ) "â–ˆ" g) (gInit (1 + maxY - minY) (1 + maxX - minX) " ") (toMbrOrigin minX minY lights)
    in
    displayGrid (1 + maxY - minY) grid |> List.intersperse "\n    "


displayGrid : Int -> Grid String -> List String
displayGrid numRows grd =
    List.foldl
        (\row lines -> lines ++ [ gGetRow row grd |> Maybe.withDefault [] |> String.concat ])
        [ "    " ]
        (List.range 0 (numRows - 1))
```

```elm {l m}
part1 : List String
part1 =
    message |> Tuple.first |> markdown
```

{( part2 |}

Good thing you didn't have to wait, because that would have taken a long time - much longer than the 3 seconds in the example above.

Impressed by your sub-hour communication capabilities, the Elves are curious: **exactly how many seconds would they have needed to wait for that message to appear?**

{| part2 )}

Here we simply extract the number of steps required to reach the minimum area:

```elm {l r}
part2 : Int
part2 =
    message |> Tuple.second
```

## Reflection

I really enjoyed this puzzle. Being able to display the light grid at any time t was straightforward to code, and it was good to be able to take advantage of Litvis's markdown generating option to show the grid. The 'ah-ha!' moment came when I realised that minimising the bounding area was the likely halt condition for moving the lights. There is something magic about seeing the message appear out of apparently random looking distribution of lights.
