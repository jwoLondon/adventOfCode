---
follows: data/d18_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 18 (2018): Settlers of The North Pole

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/18).

{( part1|}

What will the total resource value of the lumber collection area be after 10 minutes?

{| part1 )}

## Parsing and Data Structures

The form of this puzzle is similar to previous cellular automata puzzles where we model a grid of cells. The variation this time is that a cell can have one of three states rather than two.

```elm {l}
type alias Land =
    AOC.Grid Cell


type Cell
    = Open
    | Trees
    | Yard


type alias Location =
    ( Int, Int )
```

```elm {l}
parse : List String -> AOC.Grid Cell
parse input =
    let
        toCell sym =
            case sym of
                '|' ->
                    Trees

                '#' ->
                    Yard

                _ ->
                    Open

        cellValues =
            List.foldl (\l -> (++) [ l |> String.toList |> List.map toCell ]) []
                >> List.indexedMap Tuple.pair

        land =
            AOC.gInit (List.length input)
                (input |> List.head |> Maybe.withDefault "" |> String.length)
                Open
    in
    List.foldl (\( r, row ) -> AOC.gSetRow r row) land (cellValues input)
```

## Cellular Automata

To evolve the grid of land parcels, we need to be able to count the number of a cell's eight neighbours containing a particular land type:

```elm {l}
gridNeighbours : Cell -> Location -> Land -> Int
gridNeighbours cell ( row, col ) land =
    [ AOC.gGet ( row - 1, col - 1 ) land
    , AOC.gGet ( row - 1, col ) land
    , AOC.gGet ( row - 1, col + 1 ) land
    , AOC.gGet ( row, col - 1 ) land
    , AOC.gGet ( row, col + 1 ) land
    , AOC.gGet ( row + 1, col - 1 ) land
    , AOC.gGet ( row + 1, col ) land
    , AOC.gGet ( row + 1, col + 1 ) land
    ]
        |> List.filterMap identity
        |> List.filter ((==) cell)
        |> List.length
```

We can then encode the rules that determine the next state of any cell based on its neighbourhood:

```elm {l}
nextState : Land -> Location -> Cell -> Cell
nextState land location cell =
    case cell of
        Open ->
            if gridNeighbours Trees location land >= 3 then
                Trees

            else
                Open

        Trees ->
            if gridNeighbours Yard location land >= 3 then
                Yard

            else
                Trees

        Yard ->
            if gridNeighbours Yard location land >= 1 && gridNeighbours Trees location land >= 1 then
                Yard

            else
                Open
```

Mapping the entire grid gives us the new landuse after one minute:

```elm {l}
nextLand : Land -> Land
nextLand land =
    AOC.gMapWithLocation (nextState land) land
```

Now we just have to iterate 10 times and count the number of trees and yards:

```elm {l}
score : Land -> Int
score land =
    let
        cells =
            AOC.gToList land
    in
    (List.filter ((==) Trees) cells |> List.length)
        * (List.filter ((==) Yard) cells |> List.length)
```

```elm {l r}
part1 : Int
part1 =
    AOC.iterate 10 (parse puzzleInput) nextLand |> score
```

{( part2 |}

What will the total resource value of the lumber collection area be after 1000000000 minutes?

{| part2 )}

## Approach

As with the similar [Subterranean Sustainability puzzle](d12_2018.md), the number of iterations required by the question is sufficiently large to suggest it is possible to calculate the answer without iterating every step.

Since many cellular automata transition to a stable state (or at least show regions of stability), one approach is to examine how the scores change after each iteration, and look for repeated patterns.

This can be done through visual inspection, where it becomes clear the pattern stablises after around 500 iterations.

```elm {v}
myVis : VL.Spec
myVis =
    let
        scoreSequence n land ss =
            if n == 0 then
                List.reverse ss |> List.indexedMap (\i sc -> ( toFloat i, toFloat sc ))

            else
                let
                    next =
                        nextLand land
                in
                scoreSequence (n - 1) next (score next :: ss)

        scores =
            scoreSequence 600 (parse puzzleInput) []

        data =
            VL.dataFromColumns []
                << VL.dataColumn "iteration" (VL.nums (List.map Tuple.first scores))
                << VL.dataColumn "score" (VL.nums (List.map Tuple.second scores))

        enc =
            VL.encoding
                << VL.position VL.X [ VL.pName "iteration", VL.pQuant ]
                << VL.position VL.Y [ VL.pName "score", VL.pQuant ]
    in
    VL.toVegaLite [ VL.width 600, VL.height 400, data [], enc [], VL.line [] ]
```

As we did for [Subterranean Sustainability puzzle](d12_2018.md), we can just iterate to stability, look for the next matching output to find the period of oscillation and iterate to the target number of iterations modulus that period:

```elm {l r}
part2 : Int
part2 =
    --Reaches steady state by around 500 iterations
    let
        landAt500 =
            AOC.iterate 500 (parse puzzleInput) nextLand

        scoreAt500 =
            landAt500 |> score

        period land n =
            let
                next =
                    nextLand land
            in
            if score next == scoreAt500 then
                n - 499

            else
                period next (n + 1)
    in
    AOC.iterate (modBy (period landAt500 500) (1000000000 - 500)) landAt500 nextLand
        |> score
```

---

## Reflection

Nice to have an easy day after a couple of tougher puzzles in the last few days. Much of this was very similar to [day 12](d12_2018.md), but the 2D, 3-state automata did show some visually pleasing patterns as they evolved.

It was good to be able to use some VegaLite to visualize the emergence of stability to help solve the problem (there is a chart showing above if viewing this in [litvis](https://github.com/gicentre/litvis) rather than directly on GitHub). While this approach did work, there is a risk that during the transition to stability the patterns are approximately, but not precisely repeating. A more robust, but visually less interesting approach would be to match the entire periodic sequence rather than just number from the sequence.
