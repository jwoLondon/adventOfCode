---
follows: data/d16_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 16 (2018): Chronal Classification

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/16):

{( part1|}

As you see the Elves defend their hot chocolate successfully, you go back to falling through time. This is going to become a problem.

If you're ever going to return to your own time, you need to understand how this device on your wrist works. You have a little while before you reach your next destination, and with a bit of trial and error, you manage to pull up a programming manual on the device's tiny screen.

According to the manual, the device has four registers (numbered 0 through 3) that can be manipulated by instructions containing one of 16 opcodes. The registers start with the value 0.

Every instruction consists of four values: an **opcode**, two **inputs** (named A and B), and an **output** (named C), in that order. The opcode specifies the behavior of the instruction and how the inputs are interpreted. The output, C, is always treated as a register.

In the opcode descriptions below, if something says "**value A**", it means to take the number given as A **literally**. (This is also called an "immediate" value.) If something says "**register A**", it means to use the number given as A to read from (or write to) the **register with that number**. So, if the opcode addi adds register A and value B, storing the result in register C, and the instruction addi 0 7 3 is encountered, it would add 7 to the value contained by register 0 and store the sum in register 3, never modifying registers 0, 1, or 2 in the process.

Many opcodes are similar except for how they interpret their arguments. The opcodes fall into seven general categories:

Addition:

- addr (add register) stores into register C the result of adding register A and register B.
- addi (add immediate) stores into register C the result of adding register A and value B.

Multiplication:

- mulr (multiply register) stores into register C the result of multiplying register A and register B.
- muli (multiply immediate) stores into register C the result of multiplying register A and value B.

Bitwise AND:

- banr (bitwise AND register) stores into register C the result of the bitwise AND of register A and register B.
- bani (bitwise AND immediate) stores into register C the result of the bitwise AND of register A and value B.

Bitwise OR:

- borr (bitwise OR register) stores into register C the result of the bitwise OR of register A and register B.
- bori (bitwise OR immediate) stores into register C the result of the bitwise OR of register A and value B.

Assignment:

- setr (set register) copies the contents of register A into register C. (Input B is ignored.)
- seti (set immediate) stores value A into register C. (Input B is ignored.)

Greater-than testing:

- gtir (greater-than immediate/register) sets register C to 1 if value A is greater than register B. Otherwise, register C is set to 0.
- gtri (greater-than register/immediate) sets register C to 1 if register A is greater than value B. Otherwise, register C is set to 0.
- gtrr (greater-than register/register) sets register C to 1 if register A is greater than register B. Otherwise, register C is set to 0.

Equality testing:

- eqir (equal immediate/register) sets register C to 1 if value A is equal to register B. Otherwise, register C is set to 0.
- eqri (equal register/immediate) sets register C to 1 if register A is equal to value B. Otherwise, register C is set to 0.
- eqrr (equal register/register) sets register C to 1 if register A is equal to register B. Otherwise, register C is set to 0.

Unfortunately, while the manual gives the **name** of each opcode, it doesn't seem to indicate the **number**. However, you can monitor the CPU to see the contents of the registers before and after instructions are executed to try to work them out. Each opcode has a number from 0 through 15, but the manual doesn't say which is which. For example, suppose you capture the following sample:

    Before: [3, 2, 1, 1]
    9 2 1 2
    After: [3, 2, 2, 1]

This sample shows the effect of the instruction 9 2 1 2 on the registers. Before the instruction is executed, register 0 has value 3, register 1 has value 2, and registers 2 and 3 have value 1. After the instruction is executed, register 2's value becomes 2.

The instruction itself, 9 2 1 2, means that opcode 9 was executed with A=2, B=1, and C=2. Opcode 9 could be any of the 16 opcodes listed above, but only three of them behave in a way that would cause the result shown in the sample:

- Opcode 9 could be mulr: register 2 (which has a value of 1) times register 1 (which has a value of 2) produces 2, which matches the value stored in the output register, register 2.
- Opcode 9 could be addi: register 2 (which has a value of 1) plus value 1 produces 2, which matches the value stored in the output register, register 2.
- Opcode 9 could be seti: value 2 matches the value stored in the output register, register 2; the number given for B is irrelevant

None of the other opcodes produce the result captured in the sample. Because of this, the sample above **behaves like three opcodes**.

You collect many of these samples (the first section of your puzzle input). The manual also includes a small test program (the second section of your puzzle input) - **you can ignore it for now.**

Ignoring the opcode numbers, **how many samples in your puzzle input behave like three or more opcodes?**

{| part1 )}

## Representing the Program

We will need to be able to execute the 16 different operations and see their effect on the register in order to compare with the puzzle input. Therefore our fist task is to represent those operation and the registers.

The registers can be represented as a simple dictionary with the integers 0-3 representing the keys for each register. The 16 opcodes we will identify via a custom type:

```elm {l}
type alias Registers =
    Dict Int Int


type Opcode
    = Addr
    | Addi
    | Mulr
    | Muli
    | Banr
    | Bani
    | Borr
    | Bori
    | Setr
    | Seti
    | Gtir
    | Gtri
    | Gtrr
    | Eqir
    | Eqri
    | Eqrr


opSet : List Opcode
opSet =
    [ Addr, Addi, Mulr, Muli, Banr, Bani, Borr, Bori, Setr, Seti, Gtir, Gtri, Gtrr, Eqir, Eqri, Eqrr ]
```

The operations themselves all have the same signature: they take three integer parameters and a set of register values and generate a (potentially new) set of register values. Because we will be frequently extracting and setting register values, we create a some convenience functions for doing so:

```elm {l}
type alias Operation =
    Int -> Int -> Int -> Registers -> Registers


set : Int -> Int -> Registers -> Registers
set regNum value registers =
    Dict.insert regNum value registers


get : Int -> Registers -> Int
get regNum registers =
    Dict.get regNum registers |> Maybe.withDefault -1


init : List Int -> Registers
init regValues =
    Dict.fromList (List.indexedMap Tuple.pair regValues)
```

We can create functions for each of the operations, plus an extra `noop` for responding to unknown inputs (not required if parsing input works, but a useful precaution):

```elm {l}
addr : Operation
addr a b c registers =
    set c (get a registers + get b registers) registers


addi : Operation
addi a b c registers =
    set c (get a registers + b) registers


mulr : Operation
mulr a b c registers =
    set c (get a registers * get b registers) registers


muli : Operation
muli a b c registers =
    set c (get a registers * b) registers


banr : Operation
banr a b c registers =
    set c (Bitwise.and (get a registers) (get b registers)) registers


bani : Operation
bani a b c registers =
    set c (Bitwise.and (get a registers) b) registers


borr : Operation
borr a b c registers =
    set c (Bitwise.or (get a registers) (get b registers)) registers


bori : Operation
bori a b c registers =
    set c (Bitwise.or (get a registers) b) registers


setr : Operation
setr a _ c registers =
    set c (get a registers) registers


seti : Operation
seti a _ c registers =
    set c a registers


gtir : Operation
gtir a b c registers =
    if a > get b registers then
        set c 1 registers

    else
        set c 0 registers


gtri : Operation
gtri a b c registers =
    if get a registers > b then
        set c 1 registers

    else
        set c 0 registers


gtrr : Operation
gtrr a b c registers =
    if get a registers > get b registers then
        set c 1 registers

    else
        set c 0 registers


eqir : Operation
eqir a b c registers =
    if a == get b registers then
        set c 1 registers

    else
        set c 0 registers


eqri : Operation
eqri a b c registers =
    if get a registers == b then
        set c 1 registers

    else
        set c 0 registers


eqrr : Operation
eqrr a b c registers =
    if get a registers == get b registers then
        set c 1 registers

    else
        set c 0 registers


noop : Operation
noop _ _ _ =
    identity
```

And now we can create a lookup tha retrieves the appopriate function given an opcode:

```elm {l}
getOp : Opcode -> Operation
getOp opcode =
    case opcode of
        Addr ->
            addr

        Addi ->
            addi

        Mulr ->
            mulr

        Muli ->
            muli

        Banr ->
            banr

        Bani ->
            bani

        Borr ->
            borr

        Bori ->
            bori

        Setr ->
            setr

        Seti ->
            seti

        Gtir ->
            gtir

        Gtri ->
            gtri

        Gtrr ->
            gtrr

        Eqir ->
            eqir

        Eqri ->
            eqri

        Eqrr ->
            eqrr
```

## Parsing Input

To simplify parsing, each triplet of 'before-instruction-after' lines were pre-formatted to move onto a single line. Parsing extracts the triplet and places them in a record to store the operation and a tuple of (before,after) register values:

```elm {l}
type alias Opnum =
    { num : Int
    , pA : Int
    , pB : Int
    , pC : Int
    }


type alias RegChange =
    ( List Int, List Int )


parse : List String -> List ( Opnum, RegChange )
parse =
    let
        nums =
            match "\\d+" >> List.map toInt

        parseLine line =
            case nums line of
                [ r0b, r1b, r2b, r3b, opCode, oA, oB, oC, r0a, r1a, r2a, r3a ] ->
                    Just ( Opnum opCode oA oB oC, ( [ r0b, r1b, r2b, r3b ], [ r0a, r1a, r2a, r3a ] ) )

                _ ->
                    Nothing
    in
    List.filterMap parseLine
```

## Comparing input with calculated values

To answer the querstion we need to compute the value of each 'before' register provided as input with the entire set of operations, and select only those that match the 'after' values.

Given some numbered operation and the effect it has on the 4 registers, provide a list of all the given opcodes that could produce the same result:

```elm {l}
satisfies : List Opcode -> ( Opnum, RegChange ) -> List Opcode
satisfies ops ( op, ( rBefore, rAfter ) ) =
    let
        opMatches a b c before after testOp =
            if getOp testOp a b c before == after then
                Just testOp

            else
                Nothing
    in
    List.map (opMatches op.pA op.pB op.pC (init rBefore) (init rAfter)) ops
        |> List.filterMap identity
```

```elm {l r siding}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.filter (satisfies opSet >> List.length >> flip (>=) 3)
        |> List.length
```

{( part2 |}

Using the samples you collected, work out the number of each opcode and execute the test program (the second section of your puzzle input).

**What value is contained in register 0 after executing the test program?**

{| part2 )}

There are two tasks required to answer the second part. Firstly we need to build a complete lookup between the op number and its op code. Secondly, with that lookup in place we need to run the sample program to see what effect it has on the registers.

## Building the Opcode lookup.

We need to build a dictionary that provides an opcode for each opnum. This can be found by selecting the cases where only 1 opcode satisfies a (before,after) pair. Once we have found a unique lookup, we add it to the main opnum -> opcode lookup and exclude the opcode from the set provided to compare with before/after pairs. Repeating this until there are no more opcodes left to compare gives us the full table.

```elm {l}
type alias OpLookup =
    Dict Int Opcode


buildLookup : List Opcode -> OpLookup -> List ( Opnum, RegChange ) -> OpLookup
buildLookup opcodes lookup changes =
    let
        uniquelySatisfies ops ( op, regChange ) =
            case satisfies ops ( op, regChange ) of
                [ opcode ] ->
                    Just ( op.num, opcode )

                _ ->
                    Nothing

        opsToConsider =
            opSet |> List.filter (\op -> not (List.member op (Dict.values lookup)))

        uniques =
            changes
                |> List.map (uniquelySatisfies opsToConsider)
                |> List.filterMap identity
                |> Dict.fromList

        newLookup =
            Dict.union lookup uniques
    in
    if Dict.size uniques == 0 then
        lookup

    else
        buildLookup opcodes newLookup changes
```

## Building the program

The final task is to take each instruction in the input data and convert it into a operation function, giving us a working program. Running the program in an initial set of registers initialised to 0s gives us our answer.

```elm {l}
buildProg : List (List Int) -> OpLookup -> List ( Operation, ( Int, Int, Int ) )
buildProg instructions lookup =
    let
        createOp instr =
            case instr of
                [ opNum, a, b, c ] ->
                    case Dict.get opNum lookup of
                        Just opLabel ->
                            ( getOp opLabel, ( a, b, c ) )

                        Nothing ->
                            ( noop, ( 0, 0, 0 ) )

                _ ->
                    ( noop, ( 0, 0, 0 ) )
    in
    List.map createOp instructions
```

```elm {l r siding}
part2 : Int
part2 =
    let
        run ops registers =
            List.foldl (\( op, ( a, b, c ) ) -> op a b c) registers ops

        instructions =
            progInput |> List.map (String.split " " >> List.map toInt)

        lookup =
            puzzleInput |> parse |> buildLookup opSet Dict.empty

        prog =
            buildProg instructions lookup
    in
    init [ 0, 0, 0, 0 ]
        |> run prog
        |> Dict.get 0
        |> Maybe.withDefault -1
```

## Reflection

After the previous day's monster puzzle, this one was somewhat of a relief. AoC has featured low-level / assembly simulation problems every year, and it was good to see one reappear this year. This one was quite simple, even if with a larger instruction set than we've seen in previous years. It was nice to be able to use a functional language to store the operations as functions so that running a program just becomes a simple fold.

I hope we see more variations of this before the year is out.

I took advantage of the literate programming environment of litvis by pre-formatting the input data, flattening the before-after triplets and separating it from the program data. I am increasingly attracted to the idea of 'literate data' where we are explicit about the shaping of the data before it arrives in our program (see [data/d16_2018.md](data/d16_2018.md)).
