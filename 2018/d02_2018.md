---
follows: data/d02_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2018): Inventory Management System

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/2):

{( part1|}

You stop falling through time, catch your breath, and check the screen on the device. "Destination reached. Current Year: 1518. Current Location: North Pole Utility Closet 83N10." You made it! Now, to find those anomalies.

Outside the utility closet, you hear footsteps and a voice. "...I'm not sure either. But now that so many people have chimneys, maybe he could sneak in that way?" Another voice responds, "Actually, we've been working on a new kind of suit that would let him fit through tight spaces like that. But, I heard that a few days ago, they lost the prototype fabric, the design plans, everything! Nobody on the team can even seem to remember important details of the project!"

"Wouldn't they have had enough fabric to fill several boxes in the warehouse? They'd be stored together, so the box IDs should be similar. Too bad it would take forever to search the warehouse for two similar box IDs..." They walk too far away to hear any more.

Late at night, you sneak to the warehouse - who knows what kinds of paradoxes you could cause if you were discovered - and use your fancy wrist device to quickly scan every box and produce a list of the likely candidates (your puzzle input).

To make sure you didn't miss any, you scan the likely candidate boxes again, counting the number that have an ID containing exactly two of any letter and then separately counting those with exactly three of any letter. You can multiply those two counts together to get a rudimentary checksum and compare it to what your device predicts.

For example, if you see the following box IDs:

- abcdef contains no letters that appear exactly two or three times.
- bababc contains two a and three b, so it counts for both.
- abbcde contains two b, but no letter appears exactly three times.
- abcccd contains three c, but no letter appears exactly two times.
- aabcdd contains two a and two d, but it only counts once.
- abcdee contains two e.
- ababab contains three a and three b, but it only counts once.

Of these box IDs, four of them contain a letter which appears exactly twice, and three of them contain a letter which appears exactly three times. Multiplying these together produces a checksum of 4 \* 3 = 12.

{| part1 )}

## Approach

We will need to count the number of possibly repeated letters in each box ID, which we can do by converting each string into a list of characters and building a frequency table (from [aoc.md](aoc.md))

```elm {l}
letterFreqs : String -> Dict Char Int
letterFreqs =
    String.toList >> List.foldl addToFreqTable Dict.empty
```

We can then select strings that contain a given frequency of repeated letters

```elm {l}
withRepeats : Int -> List String -> List String
withRepeats n =
    List.filter (letterFreqs >> Dict.values >> List.member n)
```

## Testing

The question provides us with a test case to check things work as expected (4 IDs with two repeated letters and 3 IDs with three repeated letters):

```elm {l r siding}
test : List (List String)
test =
    let
        input =
            [ "abcdef", "bababc", "abbcde", "abcccd", "aabcdd", "abcdee", "ababab" ]
    in
    [ withRepeats 2 input, withRepeats 3 input ]
```

And now we can apply it to the input puzzle data.

```elm {l r}
part1 : Int
part1 =
    List.length (withRepeats 2 puzzleInput) * List.length (withRepeats 3 puzzleInput)
```

{( part2 |}

Confident that your list of box IDs is complete, you're ready to find the boxes full of prototype fabric.

The boxes will have IDs which differ by exactly one character at the same position in both strings. For example, given the following box IDs:

    abcde
    fghij
    klmno
    pqrst
    fguij
    axcye
    wvxyz

The IDs abcde and axcye are close, but they differ by two characters (the second and fourth). However, the IDs fghij and fguij differ by exactly one character, the third (h and u). Those must be the correct boxes.

What letters are common between the two correct box IDs? (In the example above, this is found by removing the differing character from either ID, producing fgij.)

{| part2 )}

## Approach

To answer the question we will need to compare each string with every other one (from the set of IDs containing two or three repreated letters). We can use `pairwiseCombinations` from [aoc utilities](aoc.md) to select all such combinations.
We can then identify whether a pair of strings differs by only one character.

```elm {l}
diffByOnePairs : List String -> List ( String, String )
diffByOnePairs =
    let
        diffByOne ( strA, strB ) =
            (List.map2 (/=) (String.toList strA) (String.toList strB)
                |> List.filter identity
                |> List.length
            )
                == 1
    in
    pairwiseCombinations >> List.filter diffByOne
```

Once we have a pair of strings that differ by one character, we just need to identify their common characters:

```elm {l}
commonChrs : ( String, String ) -> String
commonChrs ( strA, strB ) =
    List.map2 Tuple.pair (String.toList strA) (String.toList strB)
        |> List.filter (\( a, b ) -> a == b)
        |> List.map Tuple.first
        |> String.fromList
```

## Testing

Again, the question provides us with a simple list of strings to test that should generate a single string `fgij`:

```elm {l r siding}
test : List String
test =
    [ "abcde", "fghij", "klmno", "pqrst", "fguij", "axcye", "wvxyz" ]
        |> diffByOnePairs
        |> List.map commonChrs
```

Now we can apply the same process to the puzzle input:

```elm {l r}
part2 : List String
part2 =
    (withRepeats 2 puzzleInput ++ withRepeats 3 puzzleInput)
        |> unique
        |> diffByOnePairs
        |> List.map commonChrs
```

## Optimisation

The approach above is an O(n^2) process (generated by all pairwise combinations).
A more efficient process could achieve the same result by sequentially storing in a set, each ID with the first character removed looking for match.
If a match is found, this is the answer. If not, try with the second character removed etc.
