---
follows: data/d15_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 15 (2018): Beverage Bandits

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/15):

{( part1|}

Having perfected their hot chocolate, the Elves have a new problem: the Goblins that live in these caves will do anything to steal it. Looks like they're here for a fight.

You scan the area, generating a map of the walls (#), open cavern (.), and starting position of every Goblin (G) and Elf (E) (your puzzle input).

Combat proceeds in **rounds**; in each round, each unit that is still alive takes a **turn**, resolving all of its actions before the next unit's turn begins. On each unit's turn, it tries to **move** into range of an enemy (if it isn't already) and then **attack** (if it is in range).

All units are very disciplined and always follow very strict combat rules. Units never move or attack diagonally, as doing so would be dishonorable. When multiple choices are equally valid, ties are broken in **reading order**: top-to-bottom, then left-to-right. For instance, the order in which units take their turns within a round is the **reading order of their starting positions** in that round, regardless of the type of unit or whether other units have moved after the round started. For example:

                     would take their
    These units:   turns in this order:
      #######           #######
      #.G.E.#           #.1.2.#
      #E.G.E#           #3.4.5#
      #.G.E.#           #.6.7.#
      #######           #######

Each unit begins its turn by identifying all possible **targets** (enemy units). If no targets remain, combat ends.

Then, the unit identifies all of the open squares (.) that are in **range** of each target; these are the squares which are **adjacent** (immediately up, down, left, or right) to any target and which aren't already occupied by a wall or another unit. Alternatively, the unit might **already** be in range of a target. If the unit is not already in range of a target, and there are no open squares which are in range of a target, the unit ends its turn.

If the unit is already in range of a target, it does not **move**, but continues its turn with an **attack**. Otherwise, since it is not in range of a target, it **moves**.

To **move**, the unit first considers the squares that are in **range** and determines **which of those squares it could reach in the fewest steps**. A step is a single movement to any **adjacent** (immediately up, down, left, or right) open (.) square. Units cannot move into walls or other units. The unit does this while considering the **current positions of units** and does not do any prediction about where units will be later. If the unit cannot reach (find an open path to) any of the squares that are in range, it ends its turn. If multiple squares are in range and **tied** for being reachable in the fewest steps, the square which is first in **reading order** is chosen. For example:

    Targets:      In range:     Reachable:    Nearest:      Chosen:
    #######       #######       #######       #######       #######
    #E..G.#       #E.?G?#       #E.@G.#       #E.!G.#       #E.+G.#
    #...#.#  -->  #.?.#?#  -->  #.@.#.#  -->  #.!.#.#  -->  #...#.#
    #.G.#G#       #?G?#G#       #@G@#G#       #!G.#G#       #.G.#G#
    #######       #######       #######       #######       #######

In the above scenario, the Elf has three targets (the three Goblins):

- Each of the Goblins has open, adjacent squares which are in range (marked with a ? on the map).
- Of those squares, four are reachable (marked @); the other two (on the right) would require moving through a wall or unit to reach.
- Three of these reachable squares are nearest, requiring the fewest steps (only 2) to reach (marked !).
- Of those, the square which is first in reading order is chosen (+).

The unit then takes a single **step** toward the chosen square along the **shortest path** to that square. If multiple steps would put the unit equally closer to its destination, the unit chooses the step which is first in reading order. (This requires knowing when there is **more than one shortest path** so that you can consider the first step of each such path.) For example:

    In range:   Nearest:    Chosen:     Distance:   Step:
    #######     #######     #######     #######     #######
    #.E...#     #.E...#     #.E...#     #4E212#     #..E..#
    #...?.# --> #...!.# --> #...+.# --> #32101# --> #.....#
    #..?G?#     #..!G.#     #...G.#     #432G2#     #...G.#
    #######     #######     #######     #######     #######

The Elf sees three squares in range of a target (?), two of which are nearest (!), and so the first in reading order is chosen (+). Under "Distance", each open square is marked with its distance from the destination square; the two squares to which the Elf could move on this turn (down and to the right) are both equally good moves and would leave the Elf 2 steps from being in range of the Goblin. Because the step which is first in reading order is chosen, the Elf moves **right** one square.

Here's a larger example of movement:

    Initially:
    #########
    #G..G..G#
    #.......#
    #.......#
    #G..E..G#
    #.......#
    #.......#
    #G..G..G#
    #########

    After 1 round:
    #########
    #.G...G.#
    #...G...#
    #...E..G#
    #.G.....#
    #.......#
    #G..G..G#
    #.......#
    #########

    After 2 rounds:
    #########
    #..G.G..#
    #...G...#
    #.G.E.G.#
    #.......#
    #G..G..G#
    #.......#
    #.......#
    #########

    After 3 rounds:
    #########
    #.......#
    #..GGG..#
    #..GEG..#
    #G..G...#
    #......G#
    #.......#
    #.......#
    #########

Once the Goblins and Elf reach the positions above, they all are either in range of a target or cannot find any square in range of a target, and so none of the units can move until a unit dies.

After moving (or if the unit began its turn in range of a target), the unit **attacks**.

To **attack**, the unit first determines **all** of the targets that are **in range** of it by being immediately **adjacent** to it. If there are no such targets, the unit ends its turn. Otherwise, the adjacent target with the **fewest hit points** is selected; in a tie, the adjacent target with the fewest hit points which is first in reading order is selected.

The unit deals damage equal to its **attack powe**r to the selected target, reducing its hit points by that amount. If this reduces its hit points to 0 or fewer, the selected target **dies**: its square becomes . and it takes no further turns.

Each **unit**, either Goblin or Elf, has 3 **attack power** and starts with 200 hit **points**.

For example, suppose the only Elf is about to attack:

           HP:            HP:

    G....  9      G....   9
    ..G..  4      ..G..   4
    ..EG.  2 -->  ..E..
    ..G..  2      ..G..   2
    ...G.  1      ...G.   1

The "HP" column shows the hit points of the Goblin to the left in the corresponding row. The Elf is in range of three targets: the Goblin above it (with 4 hit points), the Goblin to its right (with 2 hit points), and the Goblin below it (also with 2 hit points). Because three targets are in range, the ones with the lowest hit points are selected: the two Goblins with 2 hit points each (one to the right of the Elf and one below the Elf). Of those, the Goblin first in reading order (the one to the right of the Elf) is selected. The selected Goblin's hit points (2) are reduced by the Elf's attack power (3), reducing its hit points to -1, killing it.

After attacking, the unit's turn ends. Regardless of how the unit's turn ends, the next unit in the round takes its turn. If all units have taken turns in this round, the round ends, and a new round begins.

The Elves look quite outnumbered. You need to determine the **outcome** of the battle: the **number of full rounds that were completed** (not counting the round in which combat ends) multiplied by **the sum of the hit points of all remaining units** at the moment combat ends. (Combat only ends when a unit finds no targets during its turn.)

Below is an entire sample combat. Next to each map, each row's units' hit points are listed from left to right.

    Initially:
    #######
    #.G...#   G(200)
    #...EG#   E(200), G(200)
    #.#.#G#   G(200)
    #..G#E#   G(200), E(200)
    #.....#
    #######

    After 1 round:
    #######
    #..G..#   G(200)
    #...EG#   E(197), G(197)
    #.#G#G#   G(200), G(197)
    #...#E#   E(197)
    #.....#
    #######

    After 2 rounds:
    #######
    #...G.#   G(200)
    #..GEG#   G(200), E(188), G(194)
    #.#.#G#   G(194)
    #...#E#   E(194)
    #.....#
    #######

Combat ensues; eventually, the top Elf dies:

    After 23 rounds:
    #######
    #...G.#   G(200)
    #..G.G#   G(200), G(131)
    #.#.#G#   G(131)
    #...#E#   E(131)
    #.....#
    #######

    After 24 rounds:
    #######
    #..G..#   G(200)
    #...G.#   G(131)
    #.#G#G#   G(200), G(128)
    #...#E#   E(128)
    #.....#
    #######

    After 25 rounds:
    #######
    #.G...#   G(200)
    #..G..#   G(131)
    #.#.#G#   G(125)
    #..G#E#   G(200), E(125)
    #.....#
    #######

    After 26 rounds:
    #######
    #G....#   G(200)
    #.G...#   G(131)
    #.#.#G#   G(122)
    #...#E#   E(122)
    #..G..#   G(200)
    #######

    After 27 rounds:
    #######
    #G....#   G(200)
    #.G...#   G(131)
    #.#.#G#   G(119)
    #...#E#   E(119)
    #...G.#   G(200)
    #######

    After 28 rounds:
    #######
    #G....#   G(200)
    #.G...#   G(131)
    #.#.#G#   G(116)
    #...#E#   E(113)
    #....G#   G(200)
    #######

More combat ensues; eventually, the bottom Elf dies:

    After 47 rounds:
    #######
    #G....#   G(200)
    #.G...#   G(131)
    #.#.#G#   G(59)
    #...#.#
    #....G#   G(200)
    #######

Before the 48th round can finish, the top-left Goblin finds that there are no targets remaining, and so combat ends. So, the number of **full rounds** that were completed is **47**, and the sum of the hit points of all remaining units is 200+131+59+200 = **590**. From these, the **outcome** of the battle is 47 \* 590 = **27730**.

Here are a few example summarized combats:

    #######     #######
    #G..#E#     #...#E#   E(200)
    #E#E.E#     #E#...#   E(197)
    #G.##.# --> #.E##.#   E(185)
    #...#E#     #E..#E#   E(200), E(200)
    #...E.#     #.....#
    #######     #######

Combat ends after 37 full rounds
Elves win with 982 total hit points left
Outcome: 37 \* 982 = **36334**

    #######     #######
    #E..EG#     #.E.E.#   E(164), E(197)
    #.#G.E#     #.#E..#   E(200)
    #E.##E# --> #E.##.#   E(98)
    #G..#.#     #.E.#.#   E(200)
    #..E#.#     #...#.#
    #######     #######

Combat ends after 46 full rounds
Elves win with 859 total hit points left
Outcome: 46 \* 859 = **39514**

    #######     #######
    #E.G#.#     #G.G#.#   G(200), G(98)
    #.#G..#     #.#G..#   G(200)
    #G.#.G# --> #..#..#
    #G..#.#     #...#G#   G(95)
    #...E.#     #...G.#   G(200)
    #######     #######

Combat ends after 35 full rounds
Goblins win with 793 total hit points left
Outcome: 35 \* 793 = **27755**

    #######     #######
    #.E...#     #.....#
    #.#..G#     #.#G..#   G(200)
    #.###.# --> #.###.#
    #E#G#G#     #.#.#.#
    #...#G#     #G.G#G#   G(98), G(38), G(200)
    #######     #######

Combat ends after 54 full rounds
Goblins win with 536 total hit points left
Outcome: 54 \* 536 = **28944**

    #########     #########
    #G......#     #.G.....#   G(137)
    #.E.#...#     #G.G#...#   G(200), G(200)
    #..##..G#     #.G##...#   G(200)
    #...##..# --> #...##..#
    #...#...#     #.G.#...#   G(200)
    #.G...G.#     #.......#
    #.....G.#     #.......#
    #########     #########

Combat ends after 20 full rounds
Goblins win with 937 total hit points left
Outcome: 20 \* 937 = **18740**

**What is the outcome of the combat described in your puzzle input?**

{| part1 )}

Solving this puzzle involves three separate tasks. Firstly, to represent the state of the cave system and its Goblins and Elves. Secondly to calculate the shortest distance paths between Elves and Goblins, accounting reading-order ties. And thirdly, to encode and simulate the gaming mechanics.

## Task 1: Parsing and Data Structures

We can represent the caves as a `Grid` where cells are one of a `Wall`, `Empty`, occupied by a `Unit` (`Goblin` or `Elf`). We will also allow cells to contain a distance value for path finding operations.

```elm {l}
type alias Board =
    Grid MapCell


type alias Location =
    ( Int, Int )


type MapCell
    = Wall
    | Empty
    | Occupied Unit
    | Distance Int
```

A `Unit` will be either an `Elf` or `Goblin` which has a fixed 'attack power' and a number of 'hit points' that diminish over time during combat. To for ease of coding we will also store the location of the unit as part of a unit's state rather than having to rely on the grid to calculate the location.

```elm {l}
type UnitType
    = Elf
    | Goblin


type alias Unit =
    { id : Int
    , unitType : UnitType
    , location : Location
    , hp : Int
    , attack : Int
    }
```

Parsing input can be separated into two stages (which is helpful for debugging). The first reads the input text and creates a grid of characters representing each cavern in the cave grid.

The second stage converts each character into the relevant custom type to create the `Board` on which movement and combat takes place. We will allow the attack power of all Elf units to be set at this board creation stage (uses an attack of 3 for part one, but the ability to change the attack power is useful for part 2).

```elm {l}
parse : List String -> Grid Char
parse input =
    let
        numRows =
            input |> List.length

        numCols =
            input |> List.head |> Maybe.withDefault "" |> String.length

        caveValues =
            input
                |> List.foldl (\l -> (::) (l |> match "[#|E|G|.]" |> String.concat |> String.toList)) []
                |> List.reverse
                |> List.indexedMap Tuple.pair
    in
    List.foldl (\( r, row ) -> gSetRow r row) (gInit numRows numCols ' ') caveValues


toBoard : Int -> Grid Char -> Board
toBoard attackPower grid =
    let
        locationID ( row, col ) =
            row * gColCount grid + col
    in
    gMapWithLocation
        (\location chr ->
            case chr of
                'E' ->
                    Occupied (Unit (locationID location) Elf location 200 attackPower)

                'G' ->
                    Occupied (Unit (locationID location) Goblin location 200 3)

                '.' ->
                    Empty

                _ ->
                    Wall
        )
        grid
```

## Playing the game

### Moving to the enemy along shortest path

The game mechanics are quite complex, but at the heart is the ability for units to move towards each other along the shortest path. To keep things reasonably efficient, we can allocate a distance from source to target to all empty cells, calculated via a breadth first search.

The queue (`Fifo`) of locations represents cells yet to consider and their distance from the target. It is initialised with `(0, targetRow,targetCol)` and then expands breadth-first to empty neighbours until it reaches the given source location.

```elm {l}
distanceFrom : Fifo ( Int, Int, Int ) -> Location -> Board -> Board
distanceFrom locations srcLocation distanceBoard =
    let
        ( maybeLoc, remaining ) =
            Fifo.remove locations
    in
    case maybeLoc of
        Nothing ->
            distanceBoard

        Just ( d, r, c ) ->
            let
                adj =
                    emptyNeighbours ( r, c ) distanceBoard

                newBoard =
                    List.foldl (\loc b -> gSet loc (Distance (d + 1)) b) (gSet ( r, c ) (Distance d) distanceBoard) adj

                newLocations =
                    List.foldl (\( row, col ) locs -> Fifo.insert ( d + 1, row, col ) locs) remaining adj
            in
            if ( r, c ) == srcLocation then
                -- We have reached source, so we're done
                newBoard

            else
                distanceFrom newLocations srcLocation newBoard


emptyNeighbours : Location -> Board -> List Location
emptyNeighbours ( row, col ) board =
    let
        emptyLocation location =
            case gGet location board of
                Just cell ->
                    if cell == Empty then
                        Just location

                    else
                        Nothing

                Nothing ->
                    Nothing
    in
    [ emptyLocation ( row - 1, col )
    , emptyLocation ( row, col - 1 )
    , emptyLocation ( row, col + 1 )
    , emptyLocation ( row + 1, col )
    ]
        |> List.filterMap identity
```

Once we have the distances of all cells from target to source, we can find the shortest path, resolving tied shortest paths by considering them in row-prime (reading) order in order to find the location of the first step along the path.

```elm {l}
shortestPathStep : Location -> Location -> Board -> Maybe ( Int, Int, Int )
shortestPathStep ( sRow, sCol ) ( tRow, tCol ) board =
    let
        targetDistances =
            distanceFrom (Fifo.empty |> Fifo.insert ( 0, tRow, tCol )) ( sRow, sCol ) board

        tripletAt ( r, c ) =
            case gGet ( r, c ) targetDistances of
                Just (Distance d) ->
                    Just ( d, r, c )

                _ ->
                    Nothing
    in
    [ tripletAt ( sRow - 1, sCol )
    , tripletAt ( sRow, sCol - 1 )
    , tripletAt ( sRow, sCol + 1 )
    , tripletAt ( sRow + 1, sCol )
    ]
        |> List.filterMap identity
        |> List.sort
        |> List.head


move : Unit -> Board -> ( Unit, Board )
move unit board =
    let
        targetLocations =
            inRange unit board

        -- Find first step of the shortest paths to each target and select the row-prime sorted one.
        nextLocation =
            List.foldl
                (\target sp ->
                    case shortestPathStep unit.location target board of
                        Just ( newSPDist, newSPRow, newSPCol ) ->
                            case sp of
                                Just ( spDist, spRow, spCol ) ->
                                    if newSPDist < spDist then
                                        Just ( newSPDist, newSPRow, newSPCol )

                                    else if newSPDist > spDist then
                                        sp

                                    else
                                    -- Select row-prime step when tied
                                    if
                                        ( spRow, spCol ) < ( newSPRow, newSPCol )
                                    then
                                        sp

                                    else
                                        Just ( newSPDist, newSPRow, newSPCol )

                                Nothing ->
                                    -- First successful shortest path
                                    Just ( newSPDist, newSPRow, newSPCol )

                        Nothing ->
                            -- No path to target so keep existing sp
                            sp
                )
                Nothing
                targetLocations
    in
    case nextLocation of
        Just ( _, newRow, newCol ) ->
            -- Move unit to the start of shortest path (row-prime in the case of ties)
            let
                movedUnit =
                    { unit | location = ( newRow, newCol ) }
            in
            ( movedUnit, board |> gSet unit.location Empty |> gSet ( newRow, newCol ) (Occupied movedUnit) )

        Nothing ->
            -- No moves
            ( unit, board )


inRange : Unit -> Board -> List Location
inRange unit board =
    let
        enemyLocations =
            gMap
                (\mapCell ->
                    case mapCell of
                        Occupied u ->
                            if u.unitType /= unit.unitType then
                                Just u.location

                            else
                                Nothing

                        _ ->
                            Nothing
                )
                board
                |> gToList
                |> List.filterMap identity
    in
    List.foldl (\location -> (++) (emptyNeighbours location board)) [] enemyLocations
        |> List.sort
```

### Playing each game round

In order to 'play' we need to establish how many units we have and consider them in row-prime order:

```elm {l}
getUnits : Board -> List Unit
getUnits board =
    board
        -- Flattening grid is in row-prime (reading) order
        |> gToList
        |> List.foldl
            (\mapCell units ->
                case mapCell of
                    Occupied unit ->
                        units ++ [ unit ]

                    _ ->
                        units
            )
            []
```

Before we take a turn, we need to check if all of one side has been defeated. If it has, we can calculate the score. If it has not, we take thee turn and check to see if there are still some enemies left to complete the round.

```elm {l}
play : Int -> Board -> Int
play round board =
    let
        units =
            getUnits board

        ( elves, goblins ) =
            List.partition (\unit -> unit.unitType == Elf) units
    in
    if elves == [] || goblins == [] then
        round * ((List.map .hp elves |> List.sum) + (List.map .hp goblins |> List.sum))

    else
        let
            ( completed, newBoard ) =
                takeTurn True (List.map .id units) board
        in
        if completed then
            play (round + 1) newBoard

        else
            -- One side has won, but need play out a final incomplete round to calculate hitpoints
            play round newBoard


playRound : Board -> Board
playRound board =
    let
        ( completed, newBoard ) =
            takeTurn True (List.map .id (getUnits board)) board
    in
    newBoard


takeTurn : Bool -> List Int -> Board -> ( Bool, Board )
takeTurn lastTurnCompleted ids board =
    let
        getUnit id =
            -- TODO: Could we make this a dictionary directly (i.e. not via the board)
            -- and just have the board representing freespace?
            let
                unitLookup =
                    board
                        |> gToList
                        |> List.foldl
                            (\mapCell units ->
                                case mapCell of
                                    Occupied unit ->
                                        units ++ [ ( unit.id, unit ) ]

                                    _ ->
                                        units
                            )
                            []
                        |> Dict.fromList
            in
            Dict.get id unitLookup
    in
    case ids of
        [] ->
            ( lastTurnCompleted, board )

        id :: tl ->
            let
                ( elves, goblins ) =
                    List.partition (\unit -> unit.unitType == Elf) (getUnits board)

                thisTurnCompleted =
                    elves /= [] && goblins /= []
            in
            case getUnit id of
                Just unit ->
                    case combatant board unit of
                        -- Can we fight?
                        Just enemy ->
                            takeTurn thisTurnCompleted tl (attack unit enemy board)

                        Nothing ->
                            let
                                ( unit2, board2 ) =
                                    move unit board
                            in
                            case combatant board2 unit2 of
                                -- Can we fight after moving?
                                Just enemy ->
                                    takeTurn thisTurnCompleted tl (attack unit2 enemy board2)

                                -- Have moved but sill noone to fight
                                Nothing ->
                                    takeTurn thisTurnCompleted tl board2

                Nothing ->
                    -- Unit not found, so has been killed
                    takeTurn thisTurnCompleted tl board


attack : Unit -> Unit -> Board -> Board
attack unit enemy board =
    if enemy.hp <= unit.attack then
        -- Killed enemy
        gSet enemy.location Empty board

    else
        -- Reduced enemy's HP
        gSet enemy.location (Occupied { enemy | hp = enemy.hp - unit.attack }) board


combatant : Board -> Unit -> Maybe Unit
combatant board unit =
    let
        ( row, col ) =
            unit.location

        enemyAt location =
            case gGet location board of
                Just (Occupied u) ->
                    if u.unitType /= unit.unitType then
                        Just u

                    else
                        Nothing

                _ ->
                    Nothing
    in
    -- Sort adjacent enemies in row-prime order then sort from lowest to highest hit points
    [ enemyAt ( row - 1, col )
    , enemyAt ( row, col - 1 )
    , enemyAt ( row, col + 1 )
    , enemyAt ( row + 1, col )
    ]
        |> List.filterMap identity
        |> List.sortBy .hp
        |> List.head
```

To start a new game we read in the game board (the network of caverns and Elf/Goblin placements) and start a new round.

```elm {l r}
part1 : Int
part1 =
    -- This takes some time to compute (around 20 seconds), so uncomment to perform calculation.
    --puzzleInput |> parse |> toBoard 3 |> play 0
    188576
```

{( part2 |}

According to your calculations, the Elves are going to lose badly. Surely, you won't mess up the timeline too much if you give them just a little advanced technology, right?

You need to make sure the Elves not only **win**, but also suffer **no losses**: even the death of a single Elf is unacceptable.

However, you can't go too far: larger changes will be more likely to permanently alter spacetime.

So, you need to **find the outcome** of the battle in which the Elves have the **lowest integer attack power** (at least 4) that allows them to **win without a single death**. The Goblins always have an attack power of 3.

In the first summarized example above, the lowest attack power the Elves need to win without losses is 15:

    #######       #######
    #.G...#       #..E..#   E(158)
    #...EG#       #...E.#   E(14)
    #.#.#G#  -->  #.#.#.#
    #..G#E#       #...#.#
    #.....#       #.....#
    #######       #######

Combat ends after 29 full rounds
Elves win with 172 total hit points left
Outcome: 29 \* 172 = **4988**

In the second example above, the Elves need only 4 attack power:

    #######       #######
    #E..EG#       #.E.E.#   E(200), E(23)
    #.#G.E#       #.#E..#   E(200)
    #E.##E#  -->  #E.##E#   E(125), E(200)
    #G..#.#       #.E.#.#   E(200)
    #..E#.#       #...#.#
    #######       #######

Combat ends after 33 full rounds
Elves win with 948 total hit points left
Outcome: 33 \* 948 = **31284**

In the third example above, the Elves need 15 attack power:

    #######       #######
    #E.G#.#       #.E.#.#   E(8)
    #.#G..#       #.#E..#   E(86)
    #G.#.G#  -->  #..#..#
    #G..#.#       #...#.#
    #...E.#       #.....#
    #######       #######

Combat ends after 37 full rounds
Elves win with 94 total hit points left
Outcome: 37 \* 94 = **3478**

In the fourth example above, the Elves need 12 attack power:

    #######       #######
    #.E...#       #...E.#   E(14)
    #.#..G#       #.#..E#   E(152)
    #.###.#  -->  #.###.#
    #E#G#G#       #.#.#.#
    #...#G#       #...#.#
    #######       #######

Combat ends after 39 full rounds
Elves win with 166 total hit points left
Outcome: 39 \* 166 = **6474**

In the last example above, the lone Elf needs 34 attack power:

    #########       #########
    #G......#       #.......#
    #.E.#...#       #.E.#...#   E(38)
    #..##..G#       #..##...#
    #...##..#  -->  #...##..#
    #...#...#       #...#...#
    #.G...G.#       #.......#
    #.....G.#       #.......#
    #########       #########

Combat ends after 30 full rounds
Elves win with 38 total hit points left
Outcome: 30 \* 38 = **1140**

After increasing the Elves' attack power until it is just barely enough for them to win without any Elves dying, **what is the outcome** of the combat described in your puzzle input?

{| part2 )}

## Approach

Thankfully for part 2, the game mechanics are pretty similar. We just need to terminate the game if there are any Elf deaths and try increasing the attack power by one and start again. We continue doing this until we have a game with no Elf deaths.

```elm {l}
play2 : Int -> Int -> Board -> Int
play2 nElves round board =
    let
        units =
            getUnits board

        ( elves, goblins ) =
            List.partition (\unit -> unit.unitType == Elf) units

        numStartingElves =
            if nElves == -1 then
                List.length elves

            else
                nElves
    in
    if List.length elves < numStartingElves then
        -1

    else if elves == [] || goblins == [] then
        round * ((List.map .hp elves |> List.sum) + (List.map .hp goblins |> List.sum))

    else
        let
            ( completed, newBoard ) =
                takeTurn True (List.map .id units) board
        in
        if completed then
            play2 numStartingElves (round + 1) newBoard

        else
            -- One side has won, but need play out a final incomplete round to calculate hitpoints
            play2 numStartingElves round newBoard
```

```elm {l}
tryAttack : Int -> List String -> Int
tryAttack atkPower input =
    let
        score =
            input |> parse |> toBoard atkPower |> play2 -1 0
    in
    if score == -1 then
        tryAttack (atkPower + 1) input

    else
        score
```

```elm {l r}
part2 : Int
part2 =
    -- This takes some time to compute (c. 2 minutes), so uncomment to perform calculation.
    --tryAttack 4 puzzleInput
    57112
```

## Reflection

Because of the intricacies of this puzzle, I did not have time to complete it on the day of release. My initial attempts at shortest path finding were flawed and the depth first approach too slow. One of the difficulties with this kind of problem is that once the data structures and parsing has been defined and an attempt made at coding the game mechanics, it is possible to generate credible but incorrect answers. It therefore required careful test design to ensure the path finding was correct, especially for tied 'reading order' cases. The full suite of tests, including some edge cases, are described in [d15_2018tests.md](d15_2018tests.md). Litivs's ability to 'follow' other litvis documents makes separating tests from main implementation clear and easy.
