---
follows: data/d25_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 25 (2018): Four-Dimensional Adventure

'Part One' puzzle description and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/25).

{( part1|}

How many constellations are formed by the fixed points in spacetime?

{| part1 )}

## Parsing and Data Structures

Because Elm does not allow tuples of more than three elements, we need to create a record for all four dimensions.
Parsing is simply a case of splitting input text by commas and destructuring into `Point` records. Sorting the points has the potential to reduce the amount of cluster merging/removal as points are added sequentially.

```elm {l}
type alias Point =
    { a : Int
    , b : Int
    , c : Int
    , d : Int
    }


type alias Cluster =
    List Point


parse : List String -> List Point
parse =
    let
        toPoint ss =
            case List.map AOC.toInt ss of
                [ a, b, c, d ] ->
                    Just (Point a b c d)

                _ ->
                    Nothing
    in
    List.map (String.split "," >> toPoint)
        >> List.filterMap identity
        >> List.sortBy (dist (Point 0 0 0 0))
```

## Approach

We can detect if a point is near to an existing cluster by seeing if any of the cluster's points are 3 units or fewer away:

```elm {l}
dist : Point -> Point -> Int
dist p1 p2 =
    abs (p1.a - p2.a) + abs (p1.b - p2.b) + abs (p1.c - p2.c) + abs (p1.d - p2.d)


near : Point -> Point -> Bool
near p1 p2 =
    dist p1 p2 <= 3


inCluster : Point -> Cluster -> Bool
inCluster p =
    List.any (near p)
```

As we add each new point to the existing clusters, we check to see how many (if any) clusters it is near to. If it is near to any of them, we merge the point and those near clusters, replacing the original unmerged clusters with this new merged one. If it is not near to any, we add a new singleton cluster of this point.

```elm {l}
clustersNear : Point -> List Cluster -> List Cluster
clustersNear p clusters =
    List.foldl
        (\cluster nearClusters ->
            if inCluster p cluster then
                cluster :: nearClusters

            else
                nearClusters
        )
        []
        clusters
```

```elm {l}
addToClusters : Point -> List Cluster -> List Cluster
addToClusters p clusters =
    let
        nearCls =
            clustersNear p clusters

        remove cluster =
            List.filter ((/=) cluster)
    in
    if nearCls == [] then
        [ p ] :: clusters

    else
        (p :: List.concat nearCls) :: List.foldl remove clusters nearCls
```

Once all points have been added this way, we just need to count the clusters:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.foldl addToClusters []
        |> List.length
```

---

##Â Reflection

A nice easy one for Christmas morning, more in the tradition of AoCs in previous years. My initial thoughts on approaching this were to test for all pairwise distances as each point is added and then re-compute clusters, but that could become prohibitively expensive as the number of points increased.

It is unfortunate that Elm does not allow 4-tuples as this required creating a record structure instead. Because records are not _comparable_ they cannot be stored in Sets. If we could have stored clusters as Sets, the removal of old clusters would be more efficient; instead I had to create an O(n) `remove` function to remove a matched cluster from a list (by filtering clusters that don't match).
