---
follows: data/d25_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 25 (2018): Four-Dimensional Adventure

{( part1|}

The reindeer's symptoms are getting worse, and neither you nor the white-bearded man have a solution. At least the reindeer has a warm place to rest: a small bed near where you're sitting.

As you reach down, the reindeer looks up at you, accidentally bumping a button on your wrist-mounted device with its nose in the process - a button labeled **"help"**.

"Hello, and welcome to the Time Travel Support Hotline! If you are lost in time and space, press 1. If you are trapped in a time paradox, press 2. If you need help caring for a sick reindeer, press 3. If you--"

**Beep.**

A few seconds later, you hear a new voice. "Hello; please state the nature of your reindeer." You try to describe the situation.

"Just a moment, I think I can remotely run a diagnostic scan." A beam of light projects from the device and sweeps over the reindeer a few times.

"Okay, it looks like your reindeer is very low on magical energy; it should fully recover if we can fix that. Let me check your timeline for a source.... Got one. There's actually a powerful source of magical energy about 1000 years forward from you, and at roughly your position, too! It looks like... hot chocolate? Anyway, you should be able to travel there to pick some up; just don't forget a mug! Is there anything else I can help you with today?"

You explain that your device isn't capable of going forward in time. "I... see. That's tricky. Well, according to this information, your device should have the necessary hardware to open a small portal and send some hot chocolate back to you. You'll need a list of **fixed points in spacetime**; I'm transmitting it to you now."

"You just need to align your device to the constellations of fixed points so that it can lock on to the destination and open the portal. Let me look up how much hot chocolate that breed of reindeer needs."

"It says here that your particular reindeer is-- this can't be right, it says there's only one like that in the universe! But THAT means that you're--" You disconnect the call.

The list of fixed points in spacetime (your puzzle input) is a set of four-dimensional coordinates. To align your device, acquire the hot chocolate, and save the reindeer, you just need to find the **number of constellations** of points in the list.

Two points are in the same **constellation** if their manhattan distance apart is **no more than 3** or if they can form a chain of points, each a manhattan distance no more than 3 from the last, between the two of them. (That is, if a point is close enough to a constellation, it "joins" that constellation.) For example:

     0,0,0,0
     3,0,0,0
     0,3,0,0
     0,0,3,0
     0,0,0,3
     0,0,0,6
     9,0,0,0
    12,0,0,0

In the above list, the first six points form a single constellation: 0,0,0,0 is exactly distance 3 from the next four, and the point at 0,0,0,6 is connected to the others by being 3 away from 0,0,0,3, which is already in the constellation. The bottom two points, 9,0,0,0 and 12,0,0,0 are in a separate constellation because no point is close enough to connect them to the first constellation. So, in the above list, the number of constellations is **2**. (If a point at 6,0,0,0 were present, it would connect 3,0,0,0 and 9,0,0,0, merging all of the points into a single giant constellation instead.)

In this example, the number of constellations is 4:

    -1,2,2,0
    0,0,2,-2
    0,0,0,-2
    -1,2,0,0
    -2,-2,-2,2
    3,0,2,-1
    -1,3,2,2
    -1,0,-1,0
    0,2,1,-2
    3,0,0,0

In this one, it's 3:

    1,-1,0,1
    2,0,-1,0
    3,2,-1,0
    0,0,3,1
    0,0,-1,-1
    2,3,-2,0
    -2,2,0,0
    2,-2,0,-1
    1,-1,0,-1
    3,2,0,2

Finally, in this one, it's 8:

    1,-1,-1,-2
    -2,-2,0,1
    0,2,1,3
    -2,3,-2,1
    0,2,3,-2
    -1,-1,1,-2
    0,-2,-1,0
    -2,2,3,-1
    1,2,2,0
    -1,-2,0,-2

The portly man nervously strokes his white beard. It's time to get that hot chocolate.

**How many constellations are formed by the fixed points in spacetime?**

{| part1 )}

## Parsing and Data Structures

Because Elm does not allow tuples of more than three elements, we need to create a record for all four dimensions.
Parsing is simply a case of splitting input text by commas and desctructuring into `Point` records. Sorting the points has the potential to reduce the amount of cluster merging/removal as points are added sequentially.

```elm {l}
type alias Point =
    { a : Int
    , b : Int
    , c : Int
    , d : Int
    }


type alias Cluster =
    List Point


parse : List String -> List Point
parse =
    let
        toPoint ss =
            case List.map toInt ss of
                [ a, b, c, d ] ->
                    Just (Point a b c d)

                _ ->
                    Nothing
    in
    List.map (String.split "," >> toPoint)
        >> List.filterMap identity
        >> List.sortBy (dist (Point 0 0 0 0))
```

## Approach

We can detect if a point is near to an exsiting cluster by seeing if any of the cluster's points are 3 units or fewer away:

```elm {l}
dist : Point -> Point -> Int
dist p1 p2 =
    abs (p1.a - p2.a) + abs (p1.b - p2.b) + abs (p1.c - p2.c) + abs (p1.d - p2.d)


near : Point -> Point -> Bool
near p1 p2 =
    dist p1 p2 <= 3


inCluster : Point -> Cluster -> Bool
inCluster p =
    List.any (near p)
```

As we add each new point to the existing clusters, we check to see how many (if any) clusters it is near to. If it is near to any of them, we merge the point and those near clusters, replacing the original unmerged clusters with this new merged one. If it is not near to any, we add a new singlton cluster of this point.

```elm {l}
clustersNear : Point -> List Cluster -> List Cluster
clustersNear p clusters =
    List.foldl
        (\cluster nearClusters ->
            if inCluster p cluster then
                cluster :: nearClusters

            else
                nearClusters
        )
        []
        clusters
```

```elm {l}
addToClusters : Point -> List Cluster -> List Cluster
addToClusters p clusters =
    let
        nearCls =
            clustersNear p clusters

        remove cluster =
            List.filter ((/=) cluster)
    in
    if nearCls == [] then
        [ p ] :: clusters

    else
        (p :: List.concat nearCls) :: List.foldl remove clusters nearCls
```

Once all points have been added this way, we just need to count the clusters:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.foldl addToClusters []
        |> List.length
```

##Â Reflection

A nice easy one for Christmas morning, more in the tradition of AoCs in previous years. My inital thoughts on approaching this were to test for all pairwise distances as each point is added and then re-compute clusters, but that could become prohibitively expensive as the number of points increased.

It is unfortunate that Elm does not allow 4-tuples as this required creating a record structure instead. Because records are not _comparable_ they cannot be stored in Sets. If we could have stored clusters as Sets, the removal of old clusters woud be more efficient; instead I had to create an O(n) `remove` function to remove a matched cluster from a list (by filtering clusters that don't match).

## Testing

```elm {l}
testInput1 : List String
testInput1 =
    [ "0,0,0,0"
    , "3,0,0,0"
    , "0,3,0,0"
    , "0,0,3,0"
    , "0,0,0,3"
    , "0,0,0,6"
    , "9,0,0,0"
    , "12,0,0,0"
    ]


testInput2 : List String
testInput2 =
    [ "-1,2,2,0"
    , "0,0,2,-2"
    , "0,0,0,-2"
    , "-1,2,0,0"
    , "-2,-2,-2,2"
    , "3,0,2,-1"
    , "-1,3,2,2"
    , "-1,0,-1,0"
    , "0,2,1,-2"
    , "3,0,0,0"
    ]


testInput3 : List String
testInput3 =
    [ "1,-1,0,1"
    , "2,0,-1,0"
    , "3,2,-1,0"
    , "0,0,3,1"
    , "0,0,-1,-1"
    , "2,3,-2,0"
    , "-2,2,0,0"
    , "2,-2,0,-1"
    , "1,-1,0,-1"
    , "3,2,0,2"
    ]


testInput4 : List String
testInput4 =
    [ "1,-1,-1,-2"
    , "-2,-2,0,1"
    , "0,2,1,3"
    , "-2,3,-2,1"
    , "0,2,3,-2"
    , "-1,-1,1,-2"
    , "0,-2,-1,0"
    , "-2,2,3,-1"
    , "1,2,2,0"
    , "-1,-2,0,-2"
    ]
```
