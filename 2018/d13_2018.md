---
follows: data/d13_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 13 (2018): Mine Cart Madness

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/13).

{( part1|}

What is the location of the first crash?

{| part1 )}

## Data Structures

To keep code clear, we can define a set of records and types for representing the state of the track grid and the carts that move about it.

A track component can be one of four types and we add an extra `Derailed` for debugging should a cart end up somewhere it is not supposed to be.

```elm {l}
type Track
    = Slash
    | Backslash
    | Intersection
    | Straight
    | Derailed
```

A cart has a location and a direction it is heading. We will represent location as (row,column) rather than (x,y) as the question requires us to sort carts in row-prime order. It also simplifies querying a grid, which is in row-prime order by default.

The cart also needs to keep track of the most recent intersection choice (left, right or continue), as this determines what choice to make next time. And for convenience we will also store whether or not a cart has crashed into another.

```elm {l}
type alias Cart =
    { location : Location
    , direction : Direction
    , choice : Choice
    , crashed : Bool
    }


type Choice
    = Left
    | Continue
    | Right


type alias Location =
    ( Int, Int )


type alias Direction =
    ( Int, Int )
```

## Movement

The rules for movement are dependent on the current direction of a cart and the track component at its location. Adding the direction vector to a cart's position moves it and the direction itself can be transformed by a track component. The effect of a track turn on each of the four directions of travel (Up, Right, Down, Left) can be summarised as:

| Track | Old direction | New direction |
| ----- | ------------- | ------------- |
| `/`   | U (0,-1)      | R (1,0)       |
| `/`   | R (1,0)       | U (0,-1)      |
| `/`   | D (0,1)       | L (-1,0)      |
| `/`   | L (-1,0)      | D (0,1)       |
|       |               |               |
| `!`   | U (0,-1)      | L (-1,0)      |
| `!`   | R (1,0)       | D (0,1)       |
| `!`   | D (0,1)       | R (1,0)       |
| `!`   | L (-1,0)      | U (0,-1)      |

| Track | Old direction | New direction when L | New direction when R |
| ----- | ------------- | -------------------- | -------------------- |
| `+`   | U (0,-1)      | L (-1,0)             | R (1,0)              |
| `+`   | R (1,0)       | U (0,-1)             | D (0,1)              |
| `+`   | D (0,1)       | R (1,0)              | L (-1,0)             |
| `+`   | L (-1,0)      | D (0,1)              | U (0,-1)             |

Note the effect on the (dx,dy) vector, which is

- `/`: (dx -> -dy, dy -> -dx)
- `!`: (dx -> dy, dy -> dx)
- `+`L : (dx -> dy, dy -> -dx)
- `+`R : (dx -> -dy, dy -> dx)

```elm {l}
move : Track -> Cart -> Cart
move track cart =
    let
        ( row, col ) =
            cart.location

        ( dx, dy ) =
            cart.direction
    in
    case track of
        Straight ->
            { cart | location = ( row + dy, col + dx ) }

        Slash ->
            -- Swap and reverse x and y direction
            { cart | direction = ( -dy, -dx ), location = ( row - dx, col - dy ) }

        Backslash ->
            -- Swap x and y direction
            { cart | direction = ( dy, dx ), location = ( row + dx, col + dy ) }

        Intersection ->
            case cart.choice of
                Left ->
                    { cart | direction = ( dy, -dx ), choice = Continue, location = ( row - dx, col + dy ) }

                Right ->
                    { cart | direction = ( -dy, dx ), choice = Left, location = ( row + dx, col - dy ) }

                Continue ->
                    { cart | choice = Right, location = ( row + dy, col + dx ) }

        Derailed ->
            -- For help with testing/debugging when things go off the rails.
            Debug.todo "Cart has derailed!"
```

## Parsing

In building a grid of tracks, we can assume all cells are straight tracks by default (i.e. they do not influence a cart's direction) unless the input text suggests it is one of the three other track types:

```elm {l}
parseTrack : List String -> AOC.Grid Track
parseTrack input =
    let
        numRows =
            input |> List.length

        numCols =
            input |> List.head |> Maybe.withDefault "" |> String.length

        toTrack chr =
            case chr of
                -- Repaced actual backslash characters in input to avoid escaping problems.
                '!' ->
                    Backslash

                '/' ->
                    Slash

                '+' ->
                    Intersection

                _ ->
                    Straight

        tracks =
            input |> List.indexedMap (\i s -> ( i, String.toList s |> List.map toTrack ))
    in
    List.foldl (\( r, trackRow ) g -> AOC.gSetRow r trackRow g) (AOC.gInit numRows numCols Straight) tracks
```

We can parse the input text again to search for the cart symbols, which can be in one of four directions:

```elm {l}
parseCarts : List String -> List Cart
parseCarts =
    let
        cartsIn : Int -> String -> List Cart
        cartsIn row =
            String.toList
                >> List.indexedMap
                    (\col chr ->
                        case chr of
                            '>' ->
                                Just (Cart ( row, col ) ( 1, 0 ) Left False)

                            '<' ->
                                Just (Cart ( row, col ) ( -1, 0 ) Left False)

                            'v' ->
                                Just (Cart ( row, col ) ( 0, 1 ) Left False)

                            '^' ->
                                Just (Cart ( row, col ) ( 0, -1 ) Left False)

                            _ ->
                                Nothing
                    )
                >> List.filterMap identity
    in
    List.indexedMap cartsIn >> List.concat
```

## Running the carts

Now we have our track and a list of carts ready to move. We need to be able to detect whether and where any cart collisions take place, which we can do by comparing a cart's next location with all other carts:

```elm {l}
hasCrashed : Cart -> List Cart -> Bool
hasCrashed cart carts =
    List.member cart.location (List.map .location carts)
```

We need to sequence the movement of each cart (in row-prime order) and its collision detection. First move a cart, then test it against all moved carts and all carts yet to move. As each cart is moved in turn, it is transferred from the list of unmoved carts to the list of moved ones.

```elm {l}
tick : AOC.Grid Track -> List Cart -> List Cart
tick grid carts =
    let
        moveCarts unmoved moved =
            case unmoved of
                [] ->
                    moved

                cart :: tl ->
                    let
                        movedCart =
                            case AOC.gGet cart.location grid of
                                Just track ->
                                    move track cart

                                Nothing ->
                                    move Derailed cart
                    in
                    moveCarts tl
                        ({ movedCart | crashed = hasCrashed movedCart tl || hasCrashed movedCart moved }
                            :: moved
                        )
    in
    moveCarts carts []
```

We can now run the carts until we detect a crash, re-sorting the carts into row-prime order between each time increment:

```elm {l r}
part1 : Location
part1 =
    let
        grid =
            puzzleInput |> parseTrack

        tickUntilCrash carts =
            case List.filter .crashed carts of
                [ crashedCart ] ->
                    crashedCart.location

                _ ->
                    tickUntilCrash (tick grid (List.sortBy .location carts))
    in
    puzzleInput |> parseCarts |> tickUntilCrash
```

{( part2 |}

What is the location of the last cart at the end of the first tick where it is the only cart left?

{| part2 )}

The second part of the puzzle doesn't involve too much change. We just remove any carts at any crash sites between each time increment.

```elm {l}
tick2 : AOC.Grid Track -> List Cart -> List Cart
tick2 grid carts =
    let
        moveCarts oldCarts newCarts =
            case oldCarts of
                [] ->
                    newCarts

                cart :: tl ->
                    let
                        movedCart =
                            case AOC.gGet cart.location grid of
                                Just track ->
                                    move track cart

                                Nothing ->
                                    move Derailed cart
                    in
                    if hasCrashed movedCart tl then
                        moveCarts (List.filter (\c -> c.location /= movedCart.location) tl) newCarts

                    else if hasCrashed movedCart newCarts then
                        moveCarts tl (List.filter (\c -> c.location /= movedCart.location) newCarts)

                    else
                        moveCarts tl (movedCart :: newCarts)
    in
    moveCarts carts []
```

```elm {l r}
part2 : Location
part2 =
    let
        grid =
            puzzleInput |> parseTrack

        tickUntilSingleCart carts =
            case carts of
                [] ->
                    ( -1, -1 )

                [ cart ] ->
                    cart.location

                _ ->
                    tickUntilSingleCart (tick2 grid (List.sortBy .location carts))
    in
    puzzleInput |> parseCarts |> tickUntilSingleCart
```

---

## Reflection

I enjoyed this puzzle, not least because unlike some others, I felt I knew how I wanted to code the solution right from the start. This was helped in part because I sketched out the movement/direction rules on paper before any coding. I was caught out by mixing (row,col) order with (x,y) order at one stage that led to carts shooting off the grid.

I was also caught by my editor (Atom with Prettier), reformatting the leading whitespace and backslash characters without me initially noticing. Oddly, this had no effect on part 1, but did stop part 2 from working. In the end, to be safe I replaced all whitespace in the input with `.` and `\` with `!` characters.
