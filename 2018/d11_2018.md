---
follows: data/d11_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2018): Chronal Charge

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/11)

{( part1|}

What is the X,Y coordinate of the top-left fuel cell of the 3x3 square with the largest total power?

{| part1 )}

## A Naive Approach

This is a nice problem to explore possibilities for optimisation, of which there are several. As with many of the other puzzles, the first part is reasonably tolerant of inefficient brute-force approaches.

At the heart of any process we will need to calculate the power at a given location:

```elm {l}
serial : Int
serial =
    puzzleInput


power : ( Int, Int ) -> Int
power ( x, y ) =
    modBy 10 ((((x + 10) * y) + serial) * (x + 10) // 100) - 5
```

A brute-force approach could solve part 1 by calculating the total in a 3x3 window passed over every cell (bar the two right columns and bottom rows) and keeping track of the maximum power encountered:

```elm {l r}
part1 : List Int
part1 =
    let
        windowPower x y =
            List.foldl ((+) << power) 0 (AOC.gridLocations ( x, y ) ( x + 2, y + 2 ))

        ( xAnswer, yAnswer, _ ) =
            List.foldl
                (\( x, y ) ( maxX, maxY, maxP ) ->
                    if windowPower x y > maxP then
                        ( x, y, windowPower x y )

                    else
                        ( maxX, maxY, maxP )
                )
                ( -1, -1, -99 )
                (AOC.gridLocations ( 1, 1 ) ( 298, 298 ))
    in
    [ xAnswer, yAnswer ]
```

{( part2 |}

What is the X,Y,size identifier of the square with the largest total power?

{| part2 )}

Part two requires us effectively to repeat part one but for all window sizes between 1 and 300 making the naive solution impractical. We also have the minor added complexity of having to keep track of the window size that yielded the maximum window power.

## A First attempt at optimisation

Because we are simply summing the contents of each window, as we grow it incrementally (from 1x1 up to 300x300), we only need to add to the total the new cells covered by the enlarged window. These form a `J` shape around each square window reducing _n^2_ power calculations to _2n - 1:_

```txt
1 2 3 4 5
2 2 3 4 5
3 3 3 4 5
4 4 4 4 5
5 5 5 5 5
```

```elm {l}
jCoords : ( Int, Int ) -> Int -> List ( Int, Int )
jCoords ( x0, y0 ) wSize =
    let
        xMax =
            x0 + wSize - 1

        yMax =
            y0 + wSize - 1
    in
    List.map (\y -> ( xMax, y )) (List.range y0 yMax)
        ++ List.map (\x -> ( x, yMax )) (List.range x0 (xMax - 1))
```

Keeping track of the position and window size that generated of the current maximum power:

```elm {l}
type alias MaxPower =
    { x : Int
    , y : Int
    , wSize : Int
    , power : Int
    }
```

We can scan for the maximum power at each cell by growing the window from 1x1 to 300x300, excluding cases where part of the window extends beyond the 300x300 grid:

```elm {l siding}
maxPowerAt : ( Int, Int ) -> Int -> Int -> MaxPower -> MaxPower
maxPowerAt ( x0, y0 ) currentWSize accPower currentMax =
    if x0 + currentWSize > 300 || y0 + currentWSize > 300 || currentWSize > 300 then
        currentMax

    else
        let
            newPower =
                List.foldl (\( x, y ) totalPower -> totalPower + power ( x, y )) accPower (jCoords ( x0, y0 ) currentWSize)
        in
        if newPower > currentMax.power then
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower (MaxPower x0 y0 currentWSize newPower)

        else
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower currentMax


maxPower : Int -> MaxPower
maxPower maxWSize =
    List.foldl (\pos model -> maxPowerAt pos 1 0 model) (MaxPower -1 -1 -1 -1) (AOC.gridLocations ( 1, 1 ) ( 300, 300 ))
```

This allows us to compute a solution in a practical, but still lengthy amount of time (around 60 seconds on my machine).

```elm {r}
part2Unoptimised : List Int
part2Unoptimised =
    part2
```

## Improving optimisation with a summed area table

Even with the `J` shaped additions to the window as it grows, there is still significant repeated calculation of totals as adjacent windows share almost all cells in common (the extreme case of the 299x299 window has at least 88,804 of its 89,401 cells in common with its three neighbouring windows). One way of reducing this redundancy is to store the sums of the power cells in a 300x300 [summed area table](https://en.wikipedia.org/wiki/Summed-area_table). This allows us to find the total of any arbitrary rectangle in the grid with just 4 grid queries. The grid itself can be built from a single pass through the grid cells.

A summed area table contains the total of all cells above and to the left of each grid cell. The following will calculate the summed area table for any grid of values.

```elm {l}
summedArea : AOC.Grid Int -> AOC.Grid Int
summedArea g =
    let
        left ( x, y ) grd =
            AOC.gGet ( x - 1, y ) grd |> Maybe.withDefault 0

        above ( x, y ) grd =
            AOC.gGet ( x, y - 1 ) grd |> Maybe.withDefault 0

        aboveLeft ( x, y ) grd =
            AOC.gGet ( x - 1, y - 1 ) grd |> Maybe.withDefault 0

        at ( x, y ) grd =
            AOC.gGet ( x, y ) grd |> Maybe.withDefault 0

        sumAboveLeft pos grd =
            AOC.gSet pos (at pos grd + left pos grd + above pos grd - aboveLeft pos grd) grd
    in
    List.foldl sumAboveLeft g (AOC.gridLocations ( 0, 0 ) ( AOC.gColCount g - 1, AOC.gRowCount g - 1 ))
```

To generate the grid of summed powers, first we create a grid of power values and calculate its summed area values. This will be the only time we will need to perform the power calculation.

```elm {l}
summedPowerTable : AOC.Grid Int
summedPowerTable =
    AOC.gInit 300 300 0
        |> AOC.gMapWithLocation (\pos _ -> power pos)
        |> summedArea
```

We can now quickly find the total of any window of any size within the grid, in O(1) time:

```elm {l}
sum : ( Int, Int ) -> ( Int, Int ) -> AOC.Grid Int -> Int
sum ( xMin, yMin ) ( xMax, yMax ) g =
    (AOC.gGet ( xMin - 1, yMin - 1 ) g |> Maybe.withDefault 0)
        + (AOC.gGet ( xMax, yMax ) g |> Maybe.withDefault 0)
        - (AOC.gGet ( xMin - 1, yMax ) g |> Maybe.withDefault 0)
        - (AOC.gGet ( xMax, yMin - 1 ) g |> Maybe.withDefault 0)
```

## Summed Area Table Solution

Adapting the part 2 solution to calculate grid areas using the summed area table:

```elm {l}
maxPowerAt : ( Int, Int ) -> Int -> Int -> MaxPower -> MaxPower
maxPowerAt ( x0, y0 ) currentWSize accPower currentMax =
    if x0 + currentWSize > 300 || y0 + currentWSize > 300 || currentWSize > 300 then
        currentMax

    else
        let
            newPower =
                sum ( x0, y0 ) ( x0 + currentWSize - 1, y0 + currentWSize - 1 ) summedPowerTable
        in
        if newPower > currentMax.power then
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower (MaxPower x0 y0 currentWSize newPower)

        else
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower currentMax


maxPower : Int -> MaxPower
maxPower maxWSize =
    List.foldl (\pos model -> maxPowerAt pos 1 0 model) (MaxPower -1 -1 -1 -1) (AOC.gridLocations ( 1, 1 ) ( 300, 300 ))
```

This yields a solution in around 3 seconds:

```elm {l r}
part2 : List Int
part2 =
    let
        maxCell =
            maxPower 300
    in
    [ maxCell.x, maxCell.y, maxCell.wSize ]
```

## Reflection

This puzzle felt like a classic Advent of Code challenge. Comparatively simple problem definition in part 1 that can be reasonably found with a simple brute-force algorithm. And a second part that prohibits such a solution. What I enjoyed about this one is that there are lots of different approaches to optimisation, two of which I played with. Another, that I didn't explore but would be possible is to apply [Kadane's algorithm](https://en.wikipedia.org/wiki/Maximum_subarray_problem#Kadane's_algorithm) to partition the sums in a more efficient data structure for query. There is something satisfying in "feeling the benefit" of improving an algorithm's efficiency and seeing its computation time radically reduce.
