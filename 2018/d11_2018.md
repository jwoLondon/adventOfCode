---
follows: data/d11_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2018): Chronal Charge

{( part1|}

You watch the Elves and their sleigh fade into the distance as they head toward the North Pole.

Actually, you're the one fading. The falling sensation returns.

The low fuel warning light is illuminated on your wrist-mounted device. Tapping it once causes it to project a hologram of the situation: a **300x300** grid of fuel cells and their current power levels, some negative. You're not sure what negative power means in the context of time travel, but it can't be good.

Each fuel cell has a coordinate ranging from **1 to 300** in both the X (horizontal) and Y (vertical) direction. In X,Y notation, the top-left cell is 1,1, and the top-right cell is 300,1.

The interface lets you select any 3x3 square of fuel cells. To increase your chances of getting to your destination, you decide to choose the 3x3 square with the **largest total power**.

The power level in a given fuel cell can be found through the following process:

- Find the fuel cell's **rack ID**, which is its **X coordinate plus 10**.
- Begin with a power level of the **rack ID** times the **Y coordinate**.
- Increase the power level by the value of the grid serial number (your puzzle input).
- Set the power level to itself multiplied by the **rack ID**.
- Keep only the hundreds digit of the power level (so 12**3**45 becomes 3; numbers with no hundreds digit become 0).
- **Subtract 5** from the power level.

For example, to find the power level of the fuel cell at 3,5 in a grid with serial number 8:

- The rack ID is 3 + 10 = **13**.
- The power level starts at 13 \* 5 = **65**.
- Adding the serial number produces 65 + 8 = **73**.
- Multiplying by the rack ID produces 73 \* 13 = **949**.
- The hundreds digit of **9**49 is **9**.
- Subtracting 5 produces 9 - 5 = **4**.

So, the power level of this fuel cell is **4**.

Here are some more example power levels:

- Fuel cell at 122,79, grid serial number 57: power level -5.
- Fuel cell at 217,196, grid serial number 39: power level 0.
- Fuel cell at 101,153, grid serial number 71: power level 4.

Your goal is to find the 3x3 square which has the largest total power. The square must be entirely within the 300x300 grid. Identify this square using the X,Y coordinate of its **top-left fuel cell**. For example:

For grid serial number 18, the largest total 3x3 square has a top-left corner of **33,45** (with a total power of 29); these fuel cells appear in the middle of this 5x5 region:

    -2 -4 4 4 4
    -4 4 4 4 -5
    4 3 3 4 -4
    1 1 2 4 -3
    -1 0 2 -5 -2

For grid serial number 42, the largest 3x3 square's top-left is **21,61** (with a total power of 30); they are in the middle of this region:

    -3 4 2 2 2
    -4 4 3 3 4
    -5 3 3 4 -4
    4 3 3 4 -3
    3 3 3 -5 -1

**What is the X,Y coordinate of the top-left fuel cell of the 3x3 square with the largest total power?**

{| part1 )}

## A Naive Approach

This is a nice problem to explore possiblities for optimisation, of which there are several. As with many of the other puzzles, the first part is reasonably tolerant of inefficient brute-force approaches.

At the heart of any process we will need to calculate the power at a given location:

```elm {l}
serial : Int
serial =
    puzzleInput


power : ( Int, Int ) -> Int
power ( x, y ) =
    modBy 10 ((((x + 10) * y) + serial) * (x + 10) // 100) - 5
```

A brute-force approach could solve part 1 by calculating the total in a 3x3 window passed over every cell (bar the two right columns and bottom rows) and keeping track of the maximum power encountered:

```elm {l r}
part1 : List Int
part1 =
    let
        windowPower x y =
            List.foldl ((+) << power) 0 (gridLocations ( x, y ) ( x + 2, y + 2 ))

        ( xAnswer, yAnswer, _ ) =
            List.foldl
                (\( x, y ) ( maxX, maxY, maxP ) ->
                    if windowPower x y > maxP then
                        ( x, y, windowPower x y )

                    else
                        ( maxX, maxY, maxP )
                )
                ( -1, -1, -99 )
                (gridLocations ( 1, 1 ) ( 298, 298 ))
    in
    [ xAnswer, yAnswer ]
```

{( part2 |}

You discover a dial on the side of the device; it seems to let you select a square of **any size**, not just 3x3. Sizes from 1x1 to 300x300 are supported.

Realizing this, you now must find the **square of any size with the largest total power**. Identify this square by including its size as a third parameter after the top-left coordinate: a 9x9 square with a top-left corner of 3,5 is identified as 3,5,9.

For example:

- For grid serial number 18, the largest total square (with a total power of 113) is 16x16 and has a top-left corner of 90,269, so its identifier is **90,269,16**.
- For grid serial number 42, the largest total square (with a total power of 119) is 12x12 and has a top-left corner of 232,251, so its identifier is **232,251,12**.

**What is the X,Y,size identifier of the square with the largest total power?**

{| part2 )}

Part two requires us effectively to repeat part one but for all window sizes between 1 and 300 making the naive solution impractical. We also have the minor added complexity of having to keep track of the window size that yielded the maximum window power.

## A First attempt at optimisation

Because we are simply summing the contents of each window, as we grow it incrementally (from 1x1 up to 300x300), we only need to add to the total the new cells covered by the enlarged window. These form a `J` shape around each square window reducing _n^2_ power calculations to _2n - 1:_

    1 2 3 4 5
    2 2 3 4 5
    3 3 3 4 5
    4 4 4 4 5
    5 5 5 5 5

```elm {l}
jCoords : ( Int, Int ) -> Int -> List ( Int, Int )
jCoords ( x0, y0 ) wSize =
    let
        xMax =
            x0 + wSize - 1

        yMax =
            y0 + wSize - 1
    in
    List.map (\y -> ( xMax, y )) (List.range y0 yMax)
        ++ List.map (\x -> ( x, yMax )) (List.range x0 (xMax - 1))
```

Keeping track of the position and window size that generated of the current maximum power:

```elm {l}
type alias MaxPower =
    { x : Int
    , y : Int
    , wSize : Int
    , power : Int
    }
```

We can scan for the maximum power at each cell by growing the window from 1x1 to 300x300, excluding cases where part of the window extends beyond the 300x300 grid:

```elm {l siding}
maxPowerAt : ( Int, Int ) -> Int -> Int -> MaxPower -> MaxPower
maxPowerAt ( x0, y0 ) currentWSize accPower currentMax =
    if x0 + currentWSize > 300 || y0 + currentWSize > 300 || currentWSize > 300 then
        currentMax

    else
        let
            newPower =
                List.foldl (\( x, y ) totalPower -> totalPower + power ( x, y )) accPower (jCoords ( x0, y0 ) currentWSize)
        in
        if newPower > currentMax.power then
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower (MaxPower x0 y0 currentWSize newPower)

        else
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower currentMax


maxPower : Int -> MaxPower
maxPower maxWSize =
    List.foldl (\pos model -> maxPowerAt pos 1 0 model) (MaxPower -1 -1 -1 -1) (gridLocations ( 1, 1 ) ( 300, 300 ))
```

This allows us to compute a solution in a practical, but still lengthy amount of time (around 60 seconds on my machine).

```elm {r}
part2Unoptimised : List Int
part2Unoptimised =
    part2
```

## Improving optimisation with a summed area table

Even with the `J` shaped additions to the window as it grows, there is still significant repeated calculation of totals as adjacent windows share almost all cells in common (the extreme case of the 299x299 window has at least 88,804 of its 89,401 cells in common with its three neighbouring windows). One way of reducing this redundancy is to store the sums of the power cells in a 300x300 [summed area table](https://en.wikipedia.org/wiki/Summed-area_table). This allows us to find the total of any arbitrary rectangle in the grid with just 4 grid queries. The grid itself can be built from a single pass through the grid cells.

A summed area table contains the total of all cells above and to the left of each grid cell. The following will calculate the summed area table for any grid of values.

```elm {l}
summedArea : Grid Int -> Grid Int
summedArea g =
    let
        left ( x, y ) grd =
            gGet ( x - 1, y ) grd |> Maybe.withDefault 0

        above ( x, y ) grd =
            gGet ( x, y - 1 ) grd |> Maybe.withDefault 0

        aboveLeft ( x, y ) grd =
            gGet ( x - 1, y - 1 ) grd |> Maybe.withDefault 0

        at ( x, y ) grd =
            gGet ( x, y ) grd |> Maybe.withDefault 0

        sumAboveLeft pos grd =
            gSet pos (at pos grd + left pos grd + above pos grd - aboveLeft pos grd) grd
    in
    List.foldl sumAboveLeft g (gridLocations ( 0, 0 ) ( gColCount g - 1, gRowCount g - 1 ))
```

To generate the grid of summed powers, first we create a grid of power values and calculate its summed area values. This will be the only time we will need to perform the power calculation.

```elm {l}
summedPowerTable : Grid Int
summedPowerTable =
    gInit 300 300 0
        |> gMapWithLocation (\pos _ -> power pos)
        |> summedArea
```

We can now quickly find the total of any window of any size within the grid, in O(1) time:

```elm {l}
sum : ( Int, Int ) -> ( Int, Int ) -> Grid Int -> Int
sum ( xMin, yMin ) ( xMax, yMax ) g =
    (gGet ( xMin - 1, yMin - 1 ) g |> Maybe.withDefault 0)
        + (gGet ( xMax, yMax ) g |> Maybe.withDefault 0)
        - (gGet ( xMin - 1, yMax ) g |> Maybe.withDefault 0)
        - (gGet ( xMax, yMin - 1 ) g |> Maybe.withDefault 0)
```

## Summed Area Table Solution

Adapting the part 2 solution to calculate grid areas using the summed area table:

```elm {l}
maxPowerAt : ( Int, Int ) -> Int -> Int -> MaxPower -> MaxPower
maxPowerAt ( x0, y0 ) currentWSize accPower currentMax =
    if x0 + currentWSize > 300 || y0 + currentWSize > 300 || currentWSize > 300 then
        currentMax

    else
        let
            newPower =
                sum ( x0, y0 ) ( x0 + currentWSize - 1, y0 + currentWSize - 1 ) summedPowerTable
        in
        if newPower > currentMax.power then
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower (MaxPower x0 y0 currentWSize newPower)

        else
            maxPowerAt ( x0, y0 ) (currentWSize + 1) newPower currentMax


maxPower : Int -> MaxPower
maxPower maxWSize =
    List.foldl (\pos model -> maxPowerAt pos 1 0 model) (MaxPower -1 -1 -1 -1) (gridLocations ( 1, 1 ) ( 300, 300 ))
```

This yields a solution in around 3 seconds:

```elm {l r}
part2 : List Int
part2 =
    let
        maxCell =
            maxPower 300
    in
    [ maxCell.x, maxCell.y, maxCell.wSize ]
```

## Reflection

This puzzle felt like a classic Advent of Code challenge. Comparatively simple problem definition in part 1 that can be reasonably found with a simple brute-force algorithm. And a second part that prohibits such a solution. What I enjoyed about this one is that there are lots of different approaches to optimisation, two of which I played with. Another, that I didn't explore but would be possible is to apply [Kadane's algorithm](https://en.wikipedia.org/wiki/Maximum_subarray_problem#Kadane's_algorithm) to partition the sums in a more efficient data structure for query. There is something satisfying in "feeling the benefit" of improving an algorithm's efficiency and seeing its computation time radically reduce.
