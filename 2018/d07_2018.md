---
follows: data/d07_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2018): The Sum of Its Parts

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/7):

{( part1|}

You find yourself standing on a snow-covered coastline; apparently, you landed a little off course. The region is too hilly to see the North Pole from here, but you do spot some Elves that seem to be trying to unpack something that washed ashore. It's quite cold out, so you decide to risk creating a paradox by asking them for directions.

"Oh, are you the search party?" Somehow, you can understand whatever Elves from the year 1018 speak; you assume it's Ancient Nordic Elvish. Could the device on your wrist also be a translator? "Those clothes don't look very warm; take this." They hand you a heavy coat.

"We do need to find our way back to the North Pole, but we have higher priorities at the moment. You see, believe it or not, this box contains something that will solve all of Santa's transportation problems - at least, that's what it looks like from the pictures in the instructions." It doesn't seem like they can read whatever language it's in, but you can: "Sleigh kit. Some assembly required."

"'Sleigh'? What a wonderful name! You must help us assemble this 'sleigh' at once!" They start excitedly pulling more parts out of the box.

The instructions specify a series of steps and requirements about which steps must be finished before others can begin (your puzzle input). Each step is designated by a single letter. For example, suppose you have the following instructions:

    Step C must be finished before step A can begin.
    Step C must be finished before step F can begin.
    Step A must be finished before step B can begin.
    Step A must be finished before step D can begin.
    Step B must be finished before step E can begin.
    Step D must be finished before step E can begin.
    Step F must be finished before step E can begin.

Visually, these requirements look like this:

      -->A--->B--
     /    \      \
    C      -->D----->E
     \           /
      ---->F-----

Your first goal is to determine the order in which the steps should be completed. If more than one step is ready, choose the step which is first alphabetically. In this example, the steps would be completed as follows:

- Only **C** is available, and so it is done first.
- Next, both A and F are available. **A** is first alphabetically, so it is done next.
- Then, even though F was available earlier, steps B and D are now also available, and **B** is the first alphabetically of the three.
- After that, only D and F are available. E is not available because only some of its prerequisites are complete. Therefore, **D** is completed next.
- **F** is the only choice, so it is done next.
- Finally, **E** is completed.

So, in this example, the correct order is **CABDFE**.

**In what order should the steps in your instructions be completed?**

{| part1 )}

## Data Structure

This question may be recognisable as a [topological sort](https://en.wikipedia.org/wiki/Topological_sorting) where we are attempting to order a series of interdependent items in order of dependency and in the case of a choice of ordering, by lexicographic order.

We can represent the dependencies as a tree, encoded via a dictionary where each part is associated with a list of other parts that it depends on in order to be assembled.

```elm {l}
type alias DependencyTree =
    Dict String (Set String)
```

We can parse the input pairs and then build up the tree sequentially by folding over the parsed pairs. Note that we need to initialse the tree with all parts in the input so we capture those parts that have no dependencies (i.e. they only feature in the second part of a dependency pair).

```elm {l}
parse : List String -> List ( String, String )
parse =
    let
        instruction =
            AOC.submatches "Step (.) must be finished before step (.)"
                >> List.filterMap identity

        parseLine line =
            case instruction line of
                [ a, b ] ->
                    Just ( a, b )

                _ ->
                    Nothing
    in
    List.map parseLine >> List.filterMap identity


buildDependencyTree : List ( String, String ) -> DependencyTree
buildDependencyTree instructions =
    let
        initTree =
            List.foldl addInstruction Dict.empty instructions

        addInstruction ( step1, step2 ) =
            Dict.insert step1 Set.empty >> Dict.insert step2 Set.empty

        addDependency ( step1, step2 ) dTree =
            case Dict.get step2 dTree of
                Just dependencies ->
                    Dict.insert step2 (Set.insert step1 dependencies) dTree

                Nothing ->
                    dTree
    in
    List.foldl addDependency initTree instructions
```

## Approach

Once we have the dependency tree, we can traverse it depth first, sorting children alphabetically. As we traverse, we remove assembled parts and remove them from the dependencies of yet-to-be assembled parts.

```elm {l}
traverse : DependencyTree -> List String
traverse =
    let
        addToAssembled assembled dTree =
            if Dict.isEmpty dTree then
                assembled

            else
                let
                    ind =
                        firstIndependent dTree
                in
                addToAssembled (ind :: assembled)
                    (Dict.remove ind dTree |> removeIndependent ind)

        addIndependent part dependencies assembled =
            if Set.isEmpty dependencies then
                part :: assembled

            else
                assembled

        firstIndependent =
            Dict.foldl addIndependent [] >> List.sort >> List.head >> Maybe.withDefault ""

        removeIndependent ind =
            Dict.map (\_ v -> Set.remove ind v)
    in
    addToAssembled [] >> List.reverse
```

```elm {l r}
part1 : String
part1 =
    parse puzzleInput
        |> buildDependencyTree
        |> traverse
        |> String.concat
```

{( part2 |}

As you're about to begin construction, four of the Elves offer to help. "The sun will set soon; it'll go faster if we work together." Now, you need to account for multiple people working on steps simultaneously. If multiple steps are available, workers should still begin them in alphabetical order.

Each step takes 60 seconds plus an amount corresponding to its letter: A=1, B=2, C=3, and so on. So, step A takes 60+1=61 seconds, while step Z takes 60+26=86 seconds. No time is required between steps.

To simplify things for the example, however, suppose you only have help from one Elf (a total of two workers) and that each step takes 60 fewer seconds (so that step A takes 1 second and step Z takes 26 seconds). Then, using the same instructions as above, this is how each second would be spent:

    Second   Worker 1   Worker 2   Done
       0        C          .
       1        C          .
       2        C          .
       3        A          F       C
       4        B          F       CA
       5        B          F       CA
       6        D          F       CAB
       7        D          F       CAB
       8        D          F       CAB
       9        D          .       CABF
      10        E          .       CABFD
      11        E          .       CABFD
      12        E          .       CABFD
      13        E          .       CABFD
      14        E          .       CABFD
      15        .          .       CABFDE

Each row represents one second of time. The Second column identifies how many seconds have passed as of the beginning of that second. Each worker column shows the step that worker is currently doing (or . if they are idle). The Done column shows completed steps.

Note that the order of the steps has changed; this is because steps now take time to finish and multiple workers can begin multiple steps simultaneously.

In this example, it would take **15** seconds for two workers to complete these steps.

With **5** workers and the **60+ second** step durations described above, **how long will it take to complete all of the steps?**

{| part2 )}

## Data Structures

Adding a differing time to assemble for each part would not add much complexity to the problem were it not for the fact that now we can assemble parts in parallel.

We can still use the same dependency tree as the first part, but now we need additionally to keep track of a fixed number workers each potentially assembling different parts concurrently. Any given worker may either be working on assembling a part, in which case it will have a given number of seconds before completion; or it will be idle waiting for an opportunity to assemble a part.

```elm {l}
type alias Worker =
    { part : Maybe String
    , timeLeft : Int
    }
```

As assembly proceeds, we need to keep track of the evolving dependency tree (as we did for part 1), but also the status of the works, the parts that have been assembled and the time elapsed. We can store all of this in a single record, idiomatically named a `Model` in Elm.

```elm {l}
type alias Model =
    { workers : List Worker
    , assembled : List String
    , dTree : DependencyTree
    , time : Int
    }
```

We can create a helper function to calculate how long a given part will take to assemble (note that the ascii code for `A` is 65, so subtracting 4 from any letter effectively adds 60 + 1 for A, 2 for B etc., as defined in the question):

```elm {l}
timeToAssemble : String -> Int
timeToAssemble =
    String.toList >> List.head >> Maybe.withDefault ' ' >> Char.toCode >> AOC.flip (-) 4
```

We can find the parts ready to assemble in alphabetical order by traversing the dependency tree looking for parts that have no dependencies and ensuring that they are not already being worked on:

```elm {l}
readyToAssemble : DependencyTree -> List String -> List String
readyToAssemble tree beingWorkedOn =
    let
        addIndependent part dependencies parts =
            if Set.isEmpty dependencies && not (List.member part beingWorkedOn) then
                part :: parts

            else
                parts
    in
    Dict.foldl addIndependent [] tree |> List.sort
```

Assembly then takes two stages, each modifying the model containing the state of the entire assembly. The first stage is to allocate workers to any parts that are ready for assembly:

```elm {l}
allocateWorkers : Model -> Model
allocateWorkers m =
    let
        readyParts =
            readyToAssemble m.dTree (List.filterMap .part m.workers)

        workers =
            List.foldl
                (\worker ( parts, updatedWorkers ) ->
                    case worker.part of
                        Nothing ->
                            case parts of
                                hd :: tl ->
                                    -- A part available to assemble and a worker available to assemble it
                                    ( tl, Worker (Just hd) (timeToAssemble hd) :: updatedWorkers )

                                [] ->
                                    -- Idle worker (available but no part to work on)
                                    ( [], worker :: updatedWorkers )

                        _ ->
                            -- Worker is already busy on assembling another part
                            ( parts, worker :: updatedWorkers )
                )
                ( readyParts, [] )
                m.workers
    in
    { m | workers = Tuple.second workers }
```

The second stage to set the allocated workers assembling for one second:

```elm {l}
assemble : Model -> Model
assemble m =
    let
        ( assembled, updatedWorkers ) =
            List.foldl
                (\worker ( parts, workers ) ->
                    case worker.part of
                        -- Idle worker
                        Nothing ->
                            ( parts, worker :: workers )

                        Just part ->
                            if worker.timeLeft == 1 then
                                -- Completed assembly of part
                                ( part :: parts, Worker Nothing 0 :: workers )

                            else
                                -- Continue to work on assembly of part
                                ( parts, { worker | timeLeft = worker.timeLeft - 1 } :: workers )
                )
                ( [], [] )
                m.workers

        updatedTree =
            List.foldl (\part tree -> Dict.remove part tree) m.dTree assembled

        updatedTree2 =
            List.foldl removeIndependent updatedTree assembled

        removeIndependent : String -> DependencyTree -> DependencyTree
        removeIndependent ind dTree =
            Dict.map (\_ v -> Set.remove ind v) dTree
    in
    { m | workers = updatedWorkers, assembled = m.assembled ++ assembled, dTree = updatedTree2 }
```

Finally we repeat the allocate-work cycle until there are no more parts left in the dependency tree:

```elm {l}
buildTimedSequence : Model -> Model
buildTimedSequence model =
    let
        newModel =
            { model | time = model.time + 1 } |> allocateWorkers |> assemble
    in
    if .dTree newModel == Dict.empty then
        newModel

    else
        buildTimedSequence newModel
```

```elm {l r}
part2 : Int
part2 =
    buildTimedSequence
        (Model (List.repeat 5 (Worker Nothing 0))
            []
            (parse puzzleInput |> buildDependencyTree)
            0
        )
        |> .time
```

## Reflection

This was by far the toughest question for me so far. Part one was deceptively easy, applying a fairly standard topological sort on a dependency tree. But adding the complication of parallel workers really tested my ability to keep track of the entire state. This is the kind of problem I find much easier in object-oriented languages so it still takes time for me to think how to solve this functionally. After several abortive attempts it was only when I used an [Elm Record](https://elm-lang.org/docs/records) storing the `Model` and functions that transformed the model, that I was able to keep track of the state successfully. Given that almost every Elm program that uses the Elm Architecture uses such an approach, I really should have done this sooner.
