---
follows: data/d07_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2018): The Sum of Its Parts

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/7).

{( part1|}

In what order should the steps in your instructions be completed?

{| part1 )}

## Data Structure

This question may be recognisable as a [topological sort](https://en.wikipedia.org/wiki/Topological_sorting) where we are attempting to order a series of interdependent items in order of dependency and in the case of a choice of ordering, by lexicographic order.

We can represent the dependencies as a tree, encoded via a dictionary where each part is associated with a list of other parts that it depends on in order to be assembled.

```elm {l}
type alias DependencyTree =
    Dict String (Set String)
```

We can parse the input pairs and then build up the tree sequentially by folding over the parsed pairs. Note that we need to initialse the tree with all parts in the input so we capture those parts that have no dependencies (i.e. they only feature in the second part of a dependency pair).

```elm {l}
parse : List String -> List ( String, String )
parse =
    let
        instruction =
            AOC.submatches "Step (.) must be finished before step (.)"
                >> List.filterMap identity

        parseLine line =
            case instruction line of
                [ a, b ] ->
                    Just ( a, b )

                _ ->
                    Nothing
    in
    List.map parseLine >> List.filterMap identity


buildDependencyTree : List ( String, String ) -> DependencyTree
buildDependencyTree instructions =
    let
        initTree =
            List.foldl addInstruction Dict.empty instructions

        addInstruction ( step1, step2 ) =
            Dict.insert step1 Set.empty >> Dict.insert step2 Set.empty

        addDependency ( step1, step2 ) dTree =
            case Dict.get step2 dTree of
                Just dependencies ->
                    Dict.insert step2 (Set.insert step1 dependencies) dTree

                Nothing ->
                    dTree
    in
    List.foldl addDependency initTree instructions
```

## Approach

Once we have the dependency tree, we can traverse it depth first, sorting children alphabetically. As we traverse, we remove assembled parts and remove them from the dependencies of yet-to-be assembled parts.

```elm {l}
traverse : DependencyTree -> List String
traverse =
    let
        addToAssembled assembled dTree =
            if Dict.isEmpty dTree then
                assembled

            else
                let
                    ind =
                        firstIndependent dTree
                in
                addToAssembled (ind :: assembled)
                    (Dict.remove ind dTree |> removeIndependent ind)

        addIndependent part dependencies assembled =
            if Set.isEmpty dependencies then
                part :: assembled

            else
                assembled

        firstIndependent =
            Dict.foldl addIndependent [] >> List.sort >> List.head >> Maybe.withDefault ""

        removeIndependent ind =
            Dict.map (\_ v -> Set.remove ind v)
    in
    addToAssembled [] >> List.reverse
```

```elm {l r}
part1 : String
part1 =
    parse puzzleInput
        |> buildDependencyTree
        |> traverse
        |> String.concat
```

{( part2 |}

With **5** workers and the **60+ second** step durations described above, how long will it take to complete all of the steps?

{| part2 )}

## Data Structures

Adding a differing time to assemble for each part would not add much complexity to the problem were it not for the fact that now we can assemble parts in parallel.

We can still use the same dependency tree as the first part, but now we need additionally to keep track of a fixed number workers each potentially assembling different parts concurrently. Any given worker may either be working on assembling a part, in which case it will have a given number of seconds before completion; or it will be idle waiting for an opportunity to assemble a part.

```elm {l}
type alias Worker =
    { part : Maybe String
    , timeLeft : Int
    }
```

As assembly proceeds, we need to keep track of the evolving dependency tree (as we did for part 1), but also the status of the works, the parts that have been assembled and the time elapsed. We can store all of this in a single record, idiomatically named a `Model` in Elm.

```elm {l}
type alias Model =
    { workers : List Worker
    , assembled : List String
    , dTree : DependencyTree
    , time : Int
    }
```

We can create a helper function to calculate how long a given part will take to assemble (note that the ascii code for `A` is 65, so subtracting 4 from any letter effectively adds 60 + 1 for A, 2 for B etc., as defined in the question):

```elm {l}
timeToAssemble : String -> Int
timeToAssemble =
    String.toList >> List.head >> Maybe.withDefault ' ' >> Char.toCode >> AOC.flip (-) 4
```

We can find the parts ready to assemble in alphabetical order by traversing the dependency tree looking for parts that have no dependencies and ensuring that they are not already being worked on:

```elm {l}
readyToAssemble : DependencyTree -> List String -> List String
readyToAssemble tree beingWorkedOn =
    let
        addIndependent part dependencies parts =
            if Set.isEmpty dependencies && not (List.member part beingWorkedOn) then
                part :: parts

            else
                parts
    in
    Dict.foldl addIndependent [] tree |> List.sort
```

Assembly then takes two stages, each modifying the model containing the state of the entire assembly. The first stage is to allocate workers to any parts that are ready for assembly:

```elm {l}
allocateWorkers : Model -> Model
allocateWorkers m =
    let
        readyParts =
            readyToAssemble m.dTree (List.filterMap .part m.workers)

        workers =
            List.foldl
                (\worker ( parts, updatedWorkers ) ->
                    case worker.part of
                        Nothing ->
                            case parts of
                                hd :: tl ->
                                    -- A part available to assemble and a worker available to assemble it
                                    ( tl, Worker (Just hd) (timeToAssemble hd) :: updatedWorkers )

                                [] ->
                                    -- Idle worker (available but no part to work on)
                                    ( [], worker :: updatedWorkers )

                        _ ->
                            -- Worker is already busy on assembling another part
                            ( parts, worker :: updatedWorkers )
                )
                ( readyParts, [] )
                m.workers
    in
    { m | workers = Tuple.second workers }
```

The second stage to set the allocated workers assembling for one second:

```elm {l}
assemble : Model -> Model
assemble m =
    let
        ( assembled, updatedWorkers ) =
            List.foldl
                (\worker ( parts, workers ) ->
                    case worker.part of
                        -- Idle worker
                        Nothing ->
                            ( parts, worker :: workers )

                        Just part ->
                            if worker.timeLeft == 1 then
                                -- Completed assembly of part
                                ( part :: parts, Worker Nothing 0 :: workers )

                            else
                                -- Continue to work on assembly of part
                                ( parts, { worker | timeLeft = worker.timeLeft - 1 } :: workers )
                )
                ( [], [] )
                m.workers

        updatedTree =
            List.foldl (\part tree -> Dict.remove part tree) m.dTree assembled

        updatedTree2 =
            List.foldl removeIndependent updatedTree assembled

        removeIndependent : String -> DependencyTree -> DependencyTree
        removeIndependent ind dTree =
            Dict.map (\_ v -> Set.remove ind v) dTree
    in
    { m | workers = updatedWorkers, assembled = m.assembled ++ assembled, dTree = updatedTree2 }
```

Finally we repeat the allocate-work cycle until there are no more parts left in the dependency tree:

```elm {l}
buildTimedSequence : Model -> Model
buildTimedSequence model =
    let
        newModel =
            { model | time = model.time + 1 } |> allocateWorkers |> assemble
    in
    if .dTree newModel == Dict.empty then
        newModel

    else
        buildTimedSequence newModel
```

```elm {l r}
part2 : Int
part2 =
    buildTimedSequence
        (Model (List.repeat 5 (Worker Nothing 0))
            []
            (parse puzzleInput |> buildDependencyTree)
            0
        )
        |> .time
```

---

## Reflection

This was by far the toughest question for me so far. Part one was deceptively easy, applying a fairly standard topological sort on a dependency tree. But adding the complication of parallel workers really tested my ability to keep track of the entire state. This is the kind of problem I find much easier in object-oriented languages so it still takes time for me to think how to solve this functionally. After several abortive attempts it was only when I used an [Elm Record](https://elm-lang.org/docs/records) storing the `Model` and functions that transformed the model, that I was able to keep track of the state successfully. Given that almost every Elm program that uses the Elm Architecture uses such an approach, I really should have done this sooner.
