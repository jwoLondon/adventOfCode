---
follows: data/d01_2018.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2018): Chronal Calibration

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2018/day/1).

{( part1|}

Starting with a frequency of zero, what is the resulting frequency after all of the changes in frequency have been applied?

{| part1 )}

The first part simply requires us to sum all frequency changes:

```elm {l r}
part1 : Int
part1 =
    puzzleInput |> List.sum
```

{( part2 |}

What is the first frequency your device reaches twice?

{| part2 )}

We now need to keep track of the running totals as we work through the list, storing each total in a set. Whenever we calculate the latest total, we check the set of previous totals to see if it is a repeat. Once we have exhausted the list of frequency changes, we start again, but apply the frequency changes to the last running total.

```elm {l}
calibrate : Int -> List Int -> Set Int -> Int
calibrate total fChanges prevTotals =
    case fChanges of
        [] ->
            calibrate total puzzleInput prevTotals

        hd :: tl ->
            let
                newTotal =
                    total + hd
            in
            if Set.member newTotal prevTotals then
                newTotal

            else
                calibrate newTotal tl (Set.insert newTotal prevTotals)
```

```elm {l r}
part2 : Int
part2 =
    calibrate 0 puzzleInput Set.empty
```
