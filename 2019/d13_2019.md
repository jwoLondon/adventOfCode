---
follows: data/d13_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 13 (2019): Care Package

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/13):

{( part1|}

As you ponder the solitude of space and the ever-increasing three-hour roundtrip for messages between you and Earth, you notice that the Space Mail Indicator Light is blinking. To help keep you sane, the Elves have sent you a care package.

It's a new game for the ship's arcade cabinet! Unfortunately, the arcade is **all the way** on the other end of the ship. Surely, it won't be hard to build your own - the care package even comes with schematics.

The arcade cabinet runs Intcode software like the game the Elves sent (your puzzle input). It has a primitive screen capable of drawing square tiles on a grid. The software draws tiles to the screen with output instructions: every three output instructions specify the x position (distance from the left), y position (distance from the top), and tile id. The tile id is interpreted as follows:

- 0 is an **empty** tile. No game object appears in this tile.
- 1 is a **wall** tile. Walls are indestructible barriers.
- 2 is a **block** tile. Blocks can be broken by the ball.
- 3 is a **horizontal paddle** tile. The paddle is indestructible.
- 4 is a **ball** tile. The ball moves diagonally and bounces off objects.

For example, a sequence of output values like 1,2,3,6,5,4 would draw a horizontal paddle tile (1 tile from the left and 2 tiles from the top) and a ball tile (6 tiles from the left and 5 tiles from the top).

Start the game. **How many block tiles are on the screen when the game exits?**

{| part1 )}

## Parsing

Parsing the intcode is already handled by the [intcode computer](intcode.md). We now have to consider how to parse the output of a completed intcode program. In this case all outputs are sequences of data in _x,y, tileCode_ order. Let's first provide a representation of each tile type (`Score` is ignored here but used for part 2).

```elm {l}
type Tile
    = Empty
    | Wall
    | Block
    | Paddle
    | Ball
    | Score Int


toTile : Int -> Tile
toTile n =
    case n of
        1 ->
            Wall

        2 ->
            Block

        3 ->
            Paddle

        4 ->
            Ball

        _ ->
            Empty
```

Parsing the intcode output invoolves extracting numbers in groups of three and store them in a dictionary that relates location to tile type:

```elm {l}
type alias Tiles =
    Dict ( Int, Int ) Tile


outputTiles : Computer -> Tiles
outputTiles comp =
    comp.outputStore
        |> AOC.groupsOf 3
        |> List.filterMap AOC.tripletFromList
        |> List.foldl
            (\( x, y, tc ) tiles ->
                Dict.insert ( x, y )
                    (if x == -1 && y == 0 then
                        Score tc

                     else
                        toTile tc
                    )
                    tiles
            )
            Dict.empty
```

## Approach

Most of the work is done by parsing the intcode output into a dictionary of tiles. We just need to count the number of `Block` tiles:

```elm {l}
numBlocks : Tiles -> Int
numBlocks =
    Dict.values
        >> List.filter ((==) Block)
        >> List.length
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> initComputer []
        |> runProg
        |> outputTiles
        |> numBlocks
```

{( part2 |}

The game didn't run because you didn't put in any quarters. Unfortunately, you did not bring any quarters. Memory address 0 represents the number of quarters that have been inserted; set it to 2 to play for free.

The arcade cabinet has a joystick that can move left and right. The software reads the position of the joystick with input instructions:

- If the joystick is in the **neutral position**, provide 0.
- If the joystick is **tilted to the left**, provide -1.
- If the joystick is **tilted to the right**, provide 1.

The arcade cabinet also has a segment display capable of showing a single number that represents the player's current score. When three output instructions specify X=-1, Y=0, the third output instruction is not a tile; the value instead specifies the new score to show in the segment display. For example, a sequence of output values like -1,0,12345 would show 12345 as the player's current score.

Beat the game by breaking all the blocks. **What is your score after the last block is broken?**

{| part2 )}

## Visualizing the Game

To understand how the game works, it is useful to be able to visualize it as the game progresses. To do this, we can convert the dictionary of tiles into a grid and symbolise each grid cell. To create the grid we need to know how many rows and columns of tiles we need to represent.

```elm {l}
type alias Bounds =
    { minX : Int, minY : Int, maxX : Int, maxY : Int }


dimensions : List ( Int, Int ) -> Bounds
dimensions locations =
    let
        ( xs, ys ) =
            List.unzip locations
    in
    { minX = List.minimum xs |> Maybe.withDefault 0
    , minY = List.minimum ys |> Maybe.withDefault 0
    , maxX = List.maximum xs |> Maybe.withDefault 0
    , maxY = List.maximum ys |> Maybe.withDefault 0
    }
```

And then convert each cell in the grid into an appropriate symbol:

```elm {l}
display : Tiles -> List String
display tiles =
    let
        bounds =
            tiles |> Dict.keys |> dimensions

        symbol tile =
            case tile of
                Wall ->
                    '█'

                Block ->
                    '▓'

                Paddle ->
                    '═'

                Ball ->
                    '■'

                _ ->
                    '░'

        empty =
            AOC.gInit (1 + bounds.maxY - bounds.minY)
                (1 + bounds.maxX - bounds.minY)
                (symbol Empty)
    in
    Dict.foldl (\( x, y ) tile g -> AOC.gSet ( y, x ) (symbol tile) g) empty tiles
        |> AOC.gToLists
        |> List.map ((::) '\n')
        |> List.map String.fromList
```

{(fixed|}

```elm {m}
displayInitScreen : List String
displayInitScreen =
    puzzleInput
        |> initComputer []
        |> poke 0 2
        |> runProg
        |> outputTiles
        |> display
```

{|fixed)}

Iterating a few times we see the game follows classic 'breakout' rules: The ball destroyong blocks on contact, accumulating score, and the game continuing while the moving paddle prevents it from reaching the bottom of the grid.

## Playing the Game

To keep the game in play we can move the paddle to match the x-position of the ball and replace any tiles that have changed state resulting from the new output:

```elm {l}
followBall : ( Computer, Tiles ) -> ( Computer, Tiles )
followBall ( comp, tiles ) =
    let
        ballX =
            Dict.filter (\_ -> (==) Ball)
                >> Dict.keys
                >> List.head
                >> Maybe.withDefault ( 0, 0 )
                >> Tuple.first

        paddleX =
            Dict.filter (\_ -> (==) Paddle)
                >> Dict.keys
                >> List.head
                >> Maybe.withDefault ( 0, 0 )
                >> Tuple.first

        comp2 =
            comp
                |> clearOutput
                |> addInput (ballX tiles - paddleX tiles)
                |> runProg

        newTiles =
            comp2 |> outputTiles
    in
    ( comp2, Dict.union newTiles tiles )
```

To find the score after all bricks have been removed, we continue the game until the number of bricks left is zero:

```elm {l r}
part2 : Tile
part2 =
    let
        playUntilNoBlocks ( comp, tiles ) =
            case numBlocks tiles of
                0 ->
                    tiles

                _ ->
                    playUntilNoBlocks (followBall ( comp, tiles ))

        initComp =
            puzzleInput
                |> initComputer []
                |> poke 0 2
                |> runProg

        initTiles =
            initComp |> outputTiles
    in
    playUntilNoBlocks ( initComp, outputTiles initComp )
        |> Dict.get ( -1, 0 )
        |> Maybe.withDefault (Score 0)
```

---

## Reflection

I had little time for my initial attempt at this puzzle. Just enough to complete the first part. Later, in trying to complete part two, I initially tried hacking the intcode to insert a wall below the paddle. This resulted in a stack overflow which I mistakenly attributed to a buggy intcode interpreter. Further investigation revealed it was due to accumulating an ever larger set of outputs from the intcode on each new iteration of the game. This was avoided by clearing output on each iteration. It took a little while to realise that output from the intcode on each subsequent iteration was only those tiles that had changed since the previous iteration, so a union of the tile dictionaries on each iteration maintained a full description of the game as it progressed.

In the end I was pleased to be able to derive the answer without any reliance on hacking the intcode or peeking at its contents (e.g. to find the paddle and ball location).
