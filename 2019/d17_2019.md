---
follows: data/d17_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 17 (2019): Set and Forget

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/17):

{( part1|}

An early warning system detects an incoming solar flare and automatically activates the ship's electromagnetic shield. Unfortunately, this has cut off the Wi-Fi for many small robots that, unaware of the impending danger, are now trapped on exterior scaffolding on the unsafe side of the shield. To rescue them, you'll have to act quickly!

The only tools at your disposal are some wired cameras and a small vacuum robot currently asleep at its charging station. The video quality is poor, but the vacuum robot has a needlessly bright LED that makes it easy to spot no matter where it is.

An Intcode program, the **Aft Scaffolding Control and Information Interface** (ASCII, your puzzle input), provides access to the cameras and the vacuum robot. Currently, because the vacuum robot is asleep, you can only access the cameras.

Running the ASCII program on your Intcode computer will provide the current view of the scaffolds. This is output, purely coincidentally, as ASCII code: 35 means #, 46 means ., 10 starts a new line of output below the current one, and so on. (Within a line, characters are drawn left-to-right.)

In the camera output, # represents a scaffold and . represents open space. The vacuum robot is visible as ^, v, <, or > depending on whether it is facing up, down, left, or right respectively. When drawn like this, the vacuum robot is **always on a scaffold**; if the vacuum robot ever walks off of a scaffold and begins **tumbling through space uncontrollably**, it will instead be visible as X.

In general, the scaffold forms a path, but it sometimes loops back onto itself. For example, suppose you can see the following view from the cameras:

    ..#..........
    ..#..........
    #######...###
    #.#...#...#.#
    #############
    ..#...#...#..
    ..#####...^..

Here, the vacuum robot, ^ is facing up and sitting at one end of the scaffold near the bottom-right of the image. The scaffold continues up, loops across itself several times, and ends at the top-left of the image.

The first step is to calibrate the cameras by getting the **alignment parameters** of some well-defined points. Locate all scaffold intersections; for each, its alignment parameter is the distance between its left edge and the left edge of the view multiplied by the distance between its top edge and the top edge of the view. Here, the intersections from the above image are marked O:

    ..#..........
    ..#..........
    ##O####...###
    #.#...#...#.#
    ##O###O###O##
    ..#...#...#..
    ..#####...^..

For these intersections:

- The top-left intersection is 2 units from the left of the image and 2 units from the top of the image, so its alignment parameter is 2 \* 2 = **4**.
- The bottom-left intersection is 2 units from the left and 4 units from the top, so its alignment parameter is 2 \* 4 = **8**.
- The bottom-middle intersection is 6 from the left and 4 from the top, so its alignment parameter is **24**.
- The bottom-right intersection's alignment parameter is **40**.

To calibrate the cameras, you need the **sum of the alignment parameters**. In the above example, this is **76**.

Run your ASCII program. **What is the sum of the alignment parameters** for the scaffold intersections?

{| part1 )}

## Parsing

The scaffold grid data are generated by the intcode computer. We can convert to a Grid for easy direct manipulation.

```elm {l}
scaffolds : Grid Char
scaffolds =
    puzzleInput
        |> initComputer []
        |> runProg
        |> .outputStore
        |> List.map Char.fromCode
        |> String.fromList
        |> String.lines
        |> List.map String.toList
        |> gFromLists
```

## Approach

It is useful to be able to display the map of scaffolds along which the robot can move. For improved display, we can convert characters to unicode (non-ASCII !) symbols.

```elm {l}
display : Grid Char -> List String
display =
    gToLists
        >> List.map String.fromList
        >> List.map
            (String.replace "." "░\u{2004}"
                >> String.replace "#" "█\u{2004}"
                >> String.replace ">" "⇨"
                >> String.replace "^" "⇧"
                >> String.replace "<" "⇦"
                >> String.replace "v" "⇩"
                >> flip (++) "\n"
            )
```

{(fixed|} ^^^elm {m=(display scaffolds)}^^^ {|fixed)}

Visual inspection confirms that all intersections are isolated from each other so we can detect them by looking for 3x3 windows in the grid with a pattern of:

{(fixed|}
░ █ ░
█ █ █
░ █ ░
{|fixed)}

We can detect this by scanning the grid looking for N,S,E and W neighbours:

```elm {l}
intersections : Grid Char -> List ( Int, Int )
intersections grid =
    let
        isIntersection ( row, col ) cell =
            case cell of
                '#' ->
                    if
                        gGet ( row - 1, col ) grid
                            == Just '#'
                            && gGet ( row + 1, col ) grid
                            == Just '#'
                            && gGet ( row, col - 1 ) grid
                            == Just '#'
                            && gGet ( row, col + 1 ) grid
                            == Just '#'
                    then
                        Just ( row, col )

                    else
                        Nothing

                _ ->
                    Nothing
    in
    grid
        |> gMapWithLocation isIntersection
        |> gToList
        |> List.filterMap identity
```

From the list of intersections we can generate sum of alignments:

```elm {l r}
part1 : Int
part1 =
    scaffolds
        |> intersections
        |> List.map (\( x, y ) -> x * y)
        |> List.sum
```

{( part2 |}

Now for the tricky part: notifying all the other robots about the solar flare. The vacuum robot can do this automatically if it gets into range of a robot. However, you can't see the other robots on the camera, so you need to be thorough instead: you need to make the vacuum robot **visit every part of the scaffold at least once**.

The vacuum robot normally wanders randomly, but there isn't time for that today. Instead, you can **override its movement logic** with new rules.

Force the vacuum robot to wake up by changing the value in your ASCII program at address 0 from 1 to 2. When you do this, you will be automatically prompted for the new movement rules that the vacuum robot should use. The ASCII program will use input instructions to receive them, but they need to be provided as ASCII code; end each line of logic with a single newline, ASCII code 10.

First, you will be prompted for the **main movement routine**. The main routine may only call the **movement functions**: A, B, or C. Supply the movement functions to use as ASCII text, separating them with commas (,, ASCII code 44), and ending the list with a newline (ASCII code 10). For example, to call A twice, then alternate between B and C three times, provide the string A,A,B,C,B,C,B,C and then a newline.

Then, you will be prompted for each **movement function**. Movement functions may use L to **turn left**, R to **turn right**, or a number to move forward that many units. Movement functions may not call other movement functions. Again, separate the actions with commas and end the list with a newline. For example, to move forward 10 units, turn left, move forward 8 units, turn right, and finally move forward 6 units, provide the string 10,L,8,R,6 and then a newline.

Finally, you will be asked whether you want to see a **continuous video feed**; provide either y or n and a newline. Enabling the continuous video feed can help you see what's going on, but it also requires a significant amount of processing power, and may even cause your Intcode computer to overheat.

Due to the limited amount of memory in the vacuum robot, the ASCII definitions of the main routine and the movement functions may each contain **at most 20 characters**, not counting the newline.

For example, consider the following camera feed:

    #######...#####
    #.....#...#...#
    #.....#...#...#
    ......#...#...#
    ......#...###.#
    ......#.....#.#
    ^########...#.#
    ......#.#...#.#
    ......#########
    ........#...#..
    ....#########..
    ....#...#......
    ....#...#......
    ....#...#......
    ....#####......

In order for the vacuum robot to** visit every part of the scaffold at least once**, one path it could take is:

    R,8,R,8,R,4,R,4,R,8,L,6,L,2,R,4,R,4,R,8,R,8,R,8,L,6,L,2

Without the memory limit, you could just supply this whole string to function A and have the main routine call A once. However, you'll need to split it into smaller parts.

One approach is:

- **Main routine: A,B,C,B,A,C**
  (ASCII input: 65, 44, 66, 44, 67, 44, 66, 44, 65, 44, 67, 10)
- **Function A: R,8,R,8**
  (ASCII input: 82, 44, 56, 44, 82, 44, 56, 10)
- **Function B: R,4,R,4,R,8**
  (ASCII input: 82, 44, 52, 44, 82, 44, 52, 44, 82, 44, 56, 10)
- **Function C: L,6,L,2**
  (ASCII input: 76, 44, 54, 44, 76, 44, 50, 10)

Visually, this would break the desired path into the following parts:

    A,        B,            C,        B,            A,        C
    R,8,R,8,  R,4,R,4,R,8,  L,6,L,2,  R,4,R,4,R,8,  R,8,R,8,  L,6,L,2

    CCCCCCA...BBBBB
    C.....A...B...B
    C.....A...B...B
    ......A...B...B
    ......A...CCC.B
    ......A.....C.B
    ^AAAAAAAA...C.B
    ......A.A...C.B
    ......AAAAAA#AB
    ........A...C..
    ....BBBB#BBBB..
    ....B...A......
    ....B...A......
    ....B...A......
    ....BBBBA......

Of course, the scaffolding outside your ship is much more complex.

As the vacuum robot finds other robots and notifies them of the impending solar flare, it also can't help but leave them squeaky clean, collecting any space dust it finds. Once it finishes the programmed set of movements, assuming it hasn't drifted off into space, the cleaning robot will return to its docking station and report the amount of space dust it collected as a large, non-ASCII value in a single output instruction.

After visiting every part of the scaffold at least once, **how much dust does the vacuum robot report it has collected?**

{| part2 )}

## Approach

This is a more challenging task to automate. Visual inspection of the scaffold map suggests a single shortest route that starts from intial position of the droid and ends at the only other terminus.

This route is generated with the instructions:

[L,12, R,4,R,4, L,6, L,12, R,4, R,4, R,12, L,12, R,4, R,4, L,6, L,10, L,6, R,4, L,12, R,4, R,4, L,6, L,12, R,4, R,4, R,12, L,10, L,6, R,4, L,12, R,4, R,4, R,12, L,10, L,6, R,4, L,12, R,4, R,4, L,6]

Given the twin constraints of only 3 'functions' and each function a maximum of 20 characters, we can apply a greedy function generation process, here done manually given the simple size of the problem:

- _While instructions remain in list:_

  - _Proceed from start of list adding them to a new function until no more can be added within the 20 character limit._

  - _Substitute any instructions in the list with the new function._

This leads to the following generation of functions:

- **A = [L, 12, R, 4, R, 4, L, 6]**

  A, [L, 12, R, 4 R, 4R, 12], A, [L, 10, L, 6, R, 4], A, [L, 12, R, 4, R, 4, R, 12, L, 10, L, 6, R, 4, L, 12, R, 4, R, 4, R, 12, L, 10, L, 6, R, 4], A

- **B = [L, 12, R, 4 R, 4R, 12]**

  A, B, A, [L, 10, L, 6, R, 4], A, B, [L, 10, L, 6, R, 4], B, [L, 10, L, 6, R, 4], A

- **C = [L, 10, L, 6, R, 4]**

  A, B, A, C, A, B, C, B, C, A

Now we have the functions and their sequence, we just need to convert to ascii before finding the output of the intcode with this input.

```elm {l}
toAscii : String -> List Int
toAscii =
    String.toList
        >> List.map Char.toCode
        >> List.reverse
        >> (::) 10
        >> List.reverse
```

```elm {l r}
part2 : Int
part2 =
    let
        instrs =
            [ "A,B,A,C,A,B,C,B,C,A"
            , "L,12,R,4,R,4,L,6"
            , "L,12,R,4,R,4,R,12"
            , "L,10,L,6,R,4"
            , "N"
            ]
    in
    puzzleInput
        |> initComputer (List.concatMap toAscii instrs)
        |> poke 0 2
        |> runProg
        |> .out
```

---

## Reflection

The first of this year's puzzle where a solution via inspection is possible given the constraints of the scaffold path generated by the input. There is no guarantee that the greedy function assignment used in part two would work for all inputs, but presumably it is likely all puzzle inputs generate such a sequence amenable to a process.

It would be possible to automate part two by calculating the shortest path and then applying the greedy function assignment, but I am not sure the effort is worth it given the specifics of the puzzle input and the small size of the problem.
