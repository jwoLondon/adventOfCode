---
follows: data/d11_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2019): Space Police

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/11):

{( part1|}

On the way to Jupiter, you're pulled over by the Space Police.

"Attention, unmarked spacecraft! You are in violation of Space Law! All spacecraft must have a clearly visible registration identifier! You have 24 hours to comply or be sent to Space Jail!"

Not wanting to be sent to Space Jail, you radio back to the Elves on Earth for help. Although it takes almost three hours for their reply signal to reach you, they send instructions for how to power up the **emergency hull painting robot** and even provide a small Intcode program (your puzzle input) that will cause it to paint your ship appropriately.

There's just one problem: you don't have an emergency hull painting robot.

You'll need to build a new emergency hull painting robot. The robot needs to be able to move around on the grid of square panels on the side of your ship, detect the color of its current panel, and paint its current panel **black** or **white**. (All of the panels are currently **black**.)

The Intcode program will serve as the brain of the robot. The program uses input instructions to access the robot's camera: provide 0 if the robot is over a **black** panel or 1 if the robot is over a **white** panel. Then, the program will output two values:

- First, it will output a value indicating the **color to paint the panel** the robot is over: 0 means to paint the panel **black**, and 1 means to paint the panel **white**.
- Second, it will output a value indicating the **direction the robot should turn**: 0 means it should turn **left 90 degrees**, and 1 means it should turn **right 90 degrees**.

After the robot turns, it should always move **forward exactly one panel**. The robot starts facing **up**.

The robot will continue running for a while like this and halt when it is finished drawing. Do not restart the Intcode computer inside the robot during this process.

For example, suppose the robot is about to start running. Drawing black panels as ., white panels as #, and the robot pointing the direction it is facing (< ^ > v), the initial state and region near the robot looks like this:

    .....
    .....
    ..^..
    .....
    .....

The panel under the robot (not visible here because a ^ is shown instead) is also black, and so any input instructions at this point should be provided 0. Suppose the robot eventually outputs 1 (paint white) and then 0 (turn left). After taking these actions and moving forward one panel, the region now looks like this:

    .....
    .....
    .<#..
    .....
    .....

Input instructions should still be provided 0. Next, the robot might output 0 (paint black) and then 0 (turn left):

    .....
    .....
    ..#..
    .v...
    .....

After more outputs (1,0, 1,0):

    .....
    .....
    ..^..
    .##..
    .....

The robot is now back where it started, but because it is now on a white panel, input instructions should be provided 1. After several more outputs (0,1, 1,0, 1,0), the area looks like this:

    .....
    ..<#.
    ...#.
    .##..
    .....

Before you deploy the robot, you should probably have an estimate of the area it will cover: specifically, you need to know the **number of panels it paints at least once**, regardless of color. In the example above, the robot painted **6 panels** at least once. (It painted its starting panel twice, but that panel is still only counted once; it also never painted the panel it ended on.)

Build a new emergency hull painting robot and run the Intcode program on it. **How many panels does it paint at least once?**

{| part1 )}

## Approach

The direction the robot faces can be represented as a pair of integers describing a unit vector with dx and dy components:

```elm {l}
type alias Direction =
    ( Int, Int )
```

To keep code clear, we use types rather than integers for the robot instructions/state.

```elm {l}
type Turn
    = TurnLeft
    | TurnRight
    | NoTurn


type Colour
    = Black
    | White
    | NoColour


toTurn : Int -> Turn
toTurn n =
    case n of
        0 ->
            TurnLeft

        1 ->
            TurnRight

        _ ->
            NoTurn


toColour : Int -> Colour
toColour n =
    case n of
        0 ->
            Black

        1 ->
            White

        _ ->
            NoColour


fromColour : Colour -> Int
fromColour colour =
    case colour of
        Black ->
            0

        White ->
            1

        NoColour ->
            999
```

We can represent the robot and the panels it has visited as a record:

```elm {l}
type alias Robot =
    { computer : Computer
    , position : ( Int, Int )
    , movement : ( Int, Int )
    , panels : Dict ( Int, Int ) Colour
    }
```

This allows us to update the robot's state as it reads the panel colour and moves about the spacecraft. A left turn results in the the direction vector transformation (dx, dy) -> (dy, -dx) and a right turn (dx, dy) -> (-dy, dx):

```elm {l}
move : Int -> ( AOC.GridLocation, Direction ) -> ( AOC.GridLocation, Direction )
move tNum ( ( x, y ), ( dx, dy ) ) =
    let
        ( newDx, newDy ) =
            case toTurn tNum of
                TurnLeft ->
                    ( dy, -dx )

                TurnRight ->
                    ( -dy, dx )

                NoTurn ->
                    ( 0, 0 ) |> Debug.log "Bad turn instruction"

        newPosition =
            ( x + newDx, y + newDy )
    in
    ( newPosition, ( newDx, newDy ) )
```

At each increment in time, the robot (a) reads the colour at its location (read from dictionary); (b) runs the computer program to determine the new panel colour and direction to turn (runs its intcode program); (c) paints the panel in that new colour (inserts entry in dictionary); (d) turns and moves (updates position and direction vector).

```elm {l}
tick : Robot -> Robot
tick robot =
    let
        ( x, y ) =
            robot.position

        ( dx, dy ) =
            robot.movement

        panelColour =
            Dict.get ( x, y ) robot.panels |> Maybe.withDefault Black

        newComputer =
            robot.computer
                |> clearOutput
                |> addInput (fromColour panelColour)
                |> runProg
    in
    case newComputer.status of
        Halted ->
            { robot | computer = newComputer }

        _ ->
            let
                ( newColour, turn ) =
                    case newComputer.outputStore of
                        [ o1, o2 ] ->
                            ( toColour o1, toTurn o2 )

                        _ ->
                            ( NoColour, NoTurn ) |> Debug.log "Expecting a pair of output values"

                ( newDx, newDy ) =
                    case turn of
                        TurnLeft ->
                            ( dy, -dx )

                        TurnRight ->
                            ( -dy, dx )

                        NoTurn ->
                            ( 0, 0 ) |> Debug.log "Bad turn instruction"
            in
            { robot
                | computer = newComputer
                , position = ( x + newDx, y + newDy )
                , movement = ( newDx, newDy )
                , panels = Dict.insert ( x, y ) newColour robot.panels
            }
```

We can then just set off the robot until the program halts:

```elm {l}
runRobot robot =
    case robot.computer.status of
        Halted ->
            robot

        _ ->
            runRobot (tick robot)
```

The number of painted cells will be the size of the robot's panel dictionary (storing the location and colour of all painted panels).

```elm {l r}
part1 : Int
part1 =
    Robot (initComputer [] puzzleInput) ( 0, 0 ) ( 0, -1 ) Dict.empty
        |> runRobot
        |> .panels
        |> Dict.size
```

{( part2 |}

You're not sure what it's trying to paint, but it's definitely not a **registration identifier**. The Space Police are getting impatient.

Checking your external ship cameras again, you notice a white panel marked "emergency hull painting robot starting panel". The rest of the panels are **still black**, but it looks like the robot was expecting to **start on a white panel**, not a black one.

Based on the Space Law Space Brochure that the Space Police attached to one of your windows, a valid registration identifier is always **eight capital letters**. After starting the robot on a single **white panel** instead, **what registration identifier does it paint on your hull?**

{| part2 )}

## Approach Part Two

As the robot accumulates a dictionary of all the panel locations it has painted, to see the registration identifier, we simply convert it into a grid:

```elm {l}
toGrid : List ( Int, Int ) -> AOC.Grid Char
toGrid locations =
    let
        ( xs, ys ) =
            List.unzip locations

        minX =
            List.minimum xs |> Maybe.withDefault -99

        maxX =
            List.maximum xs |> Maybe.withDefault 99

        minY =
            List.minimum ys |> Maybe.withDefault -99

        maxY =
            List.maximum ys |> Maybe.withDefault 99

        empty =
            AOC.gInit (1 + maxY - minY) (1 + maxX - minX) '.'

        grid =
            List.foldl (\( x, y ) g -> AOC.gSet ( y - minY, x - minX ) 'â–ˆ' g) empty locations

        lines =
            grid
                |> AOC.gToList
                |> AOC.groupsOf (AOC.gColCount grid)
                |> List.map ((::) '\n')
                |> List.concat
    in
    grid
```

To view the message we can show just the white panels.

```elm {l}
part2 : List String
part2 =
    Robot (initComputer [] puzzleInput) ( 0, 0 ) ( 0, -1 ) (Dict.singleton ( 0, 0 ) White)
        |> runRobot
        |> .panels
        |> Dict.filter (\loc colour -> colour == White)
        |> Dict.keys
        |> toGrid
        |> AOC.gToLists
        |> List.map ((::) '\n' >> String.fromList)
```

{(fixed|}^^^elm{m=part2}^^^{|fixed)}

---

## Reflection

On the face of it, this should have been a fairly simple puzzle to solve. I felt I knew what needed to be done after the first reading of the question; building up a dictionary of painted panels should have yielded the correct answer. But frustratingly I was getting an incorrect count for part 1 (about a third of the correct answer).

I ended up creating a grid view of the painted panels in order to get a better view of what might be going wrong. But this still yielded a credible, but incorrect grid of painted panels. I spent many hours going over the code but still no luck. Eventually I revisited my [intcode](intcode.md) program to see if I had made some error in its implementation. It passed all the tests given in [day 9](d09_2019.md), and I couldn't see any obvious errors. Finally, I spotted that the program wasn't updating the instruction pointer when pausing for input, but only after some instructions, so difficult to spot. None of the day 8 tests were testing for paused input, so hadn't detected this problem. Once corrected, this yielded the correct answer. And part 2 was a trivial addition as I had already created the grid view of the painted panels.
