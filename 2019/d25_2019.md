---
follows: data/d25_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 25 (2019): Cryostasis

'Part One' puzzle description and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/25)

##Â Part 1

To enable ASCII communication with the droid, we need to convert ASCII into intcode and intcode back to ASCII:

```elm {l}
toAscii : String -> List Int
toAscii =
    String.toList
        >> List.map Char.toCode
        >> List.reverse
        >> (::) 10


fromAscii : List Int -> String
fromAscii =
    List.map Char.fromCode
        >> String.fromList
```

We can initialise the droid as any other intcode program:

```elm {l}
droid : Computer
droid =
    puzzleInput
        |> initComputer []
        |> runProg
```

Each round of communicating with the droid involves sending ASCII instructions and receiving the ASCII output:

```elm {l}
tick : String -> ( String, Computer ) -> ( String, Computer )
tick cmd ( _, comp ) =
    let
        droid_ =
            comp
                |> addInputs (toAscii cmd)
                |> runProg
    in
    ( droid_.outputStore |> fromAscii, clearOutput droid_ )
```

Some interactive exploration yields a map of the spaceship within which we can pick up 8 items (marked in blue), while avoiding picking up others (marked in red).

![santa's ship](../images/day25_2019Map.png)

We then move to the security zone and try dropping various combinations of objects until we are let through:

```elm {l m}
part1 : List String
part1 =
    let
        pickupAll =
            [ "south"
            , "take mutex"
            , "south"
            , "take manifold"
            , "west"
            , "west"
            , "take klein bottle"
            , "east"
            , "east"
            , "north"
            , "east"
            , "take mug"
            , "east"
            , "take polygon"
            , "north"
            , "take loom"
            , "north"
            , "take hypercube"
            , "south"
            , "south"
            , "east"
            , "east"
            , "east"
            , "take pointer"
            , "south"
            , "west"
            ]

        combos =
            [ "klein bottle"
            , "loom"
            , "mutex"
            , "pointer"
            , "polygon"
            , "hypercube"
            , "mug"
            , "manifold"
            ]
                |> List.map ((++) "drop ")
                |> AOC.combinations 4
                |> List.map (AOC.flip (++) [ "west" ])

        tryItems dropCmds =
            List.foldl tick ( "", droid ) (pickupAll ++ dropCmds) |> Tuple.first
    in
    List.map tryItems combos
        |> List.filter (not << String.contains "ejected")
```

---

## Reflection

This was a fun finale to the AoC with echos of the [Synacor challenge](https://challenge.synacor.com). Nothing too challenging, but I do like the 'magic' of revealing an adventure game from a list of numbers.
