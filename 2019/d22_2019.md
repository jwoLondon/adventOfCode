---
follows: data/d22_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 22 (2019): Slam Shuffle

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/22):

{( part1|}

There isn't much to do while you wait for the droids to repair your ship. At least you're drifting in the right direction. You decide to practice a new card shuffle you've been working on.

Digging through the ship's storage, you find a deck of **space cards**! Just like any deck of space cards, there are 10007 cards in the deck numbered 0 through 10006. The deck must be new - they're still in **factory order**, with 0 on the top, then 1, then 2, and so on, all the way through to 10006 on the bottom.

You've been practicing three different **techniques** that you use while shuffling. Suppose you have a deck of only 10 cards (numbered 0 through 9):

**To deal into new stack**, create a new stack of cards by dealing the top card of the deck onto the top of the new stack repeatedly until you run out of cards:

```txt
Top          Bottom
0 1 2 3 4 5 6 7 8 9   Your deck
                      New stack

  1 2 3 4 5 6 7 8 9   Your deck
                  0   New stack

    2 3 4 5 6 7 8 9   Your deck
                1 0   New stack

      3 4 5 6 7 8 9   Your deck
              2 1 0   New stack

Several steps later...

                  9   Your deck
  8 7 6 5 4 3 2 1 0   New stack

                      Your deck
9 8 7 6 5 4 3 2 1 0   New stack
```

Finally, pick up the new stack you've just created and use it as the deck for the next technique.

**To cut N cards**, take the top N cards off the top of the deck and move them as a single unit to the bottom of the deck, retaining their order. For example, to cut 3:

```txt
Top          Bottom
0 1 2 3 4 5 6 7 8 9   Your deck

      3 4 5 6 7 8 9   Your deck
0 1 2                 Cut cards

3 4 5 6 7 8 9         Your deck
              0 1 2   Cut cards

3 4 5 6 7 8 9 0 1 2   Your deck
```

You've also been getting pretty good at a version of this technique where N is negative! In that case, cut (the absolute value of) N cards from the bottom of the deck onto the top. For example, to cut -4:

```txt
Top          Bottom
0 1 2 3 4 5 6 7 8 9   Your deck

0 1 2 3 4 5           Your deck
            6 7 8 9   Cut cards

        0 1 2 3 4 5   Your deck
6 7 8 9               Cut cards

6 7 8 9 0 1 2 3 4 5   Your deck
```

To **deal with increment N**, start by clearing enough space on your table to lay out all of the cards individually in a long line. Deal the top card into the leftmost position. Then, move N positions to the right and deal the next card there. If you would move into a position past the end of the space on your table, wrap around and keep counting from the leftmost card again. Continue this process until you run out of cards.

For example, to deal with increment 3:

```txt
0 1 2 3 4 5 6 7 8 9   Your deck
. . . . . . . . . .   Space on table
^                     Current position

Deal the top card to the current position:

  1 2 3 4 5 6 7 8 9   Your deck
0 . . . . . . . . .   Space on table
^                     Current position

Move the current position right 3:

  1 2 3 4 5 6 7 8 9   Your deck
0 . . . . . . . . .   Space on table
      ^               Current position

Deal the top card:

    2 3 4 5 6 7 8 9   Your deck
0 . . 1 . . . . . .   Space on table
      ^               Current position

Move right 3 and deal:

      3 4 5 6 7 8 9   Your deck
0 . . 1 . . 2 . . .   Space on table
            ^         Current position

Move right 3 and deal:

        4 5 6 7 8 9   Your deck
0 . . 1 . . 2 . . 3   Space on table
                  ^   Current position

Move right 3, wrapping around, and deal:

          5 6 7 8 9   Your deck
0 . 4 1 . . 2 . . 3   Space on table
    ^                 Current position

And so on:

0 7 4 1 8 5 2 9 6 3   Space on table
```

Positions on the table which already contain cards are still counted; they're not skipped. Of course, this technique is carefully designed so it will never put two cards in the same position or leave a position empty.

Finally, collect the cards on the table so that the leftmost card ends up at the top of your deck, the card to its right ends up just below the top card, and so on, until the rightmost card ends up at the bottom of the deck.

The complete shuffle process (your puzzle input) consists of applying many of these techniques. Here are some examples that combine techniques; they all start with a **factory order** deck of 10 cards:

```txt
deal with increment 7
deal into new stack
deal into new stack
Result: 0 3 6 9 2 5 8 1 4 7
```

```txt
cut 6
deal with increment 7
deal into new stack
Result: 3 0 7 4 1 8 5 2 9 6
```

```txt
deal with increment 7
deal with increment 9
cut -2
Result: 6 3 0 7 4 1 8 5 2 9
```

```txt
deal into new stack
cut -2
deal with increment 7
cut 8
cut -4
deal with increment 7
cut 3
deal with increment 9
deal with increment 3
cut -1
Result: 9 2 5 8 1 4 7 0 3 6
```

Positions within the deck count from 0 at the top, then 1 for the card immediately below the top card, and so on to the bottom. (That is, cards start in the position matching their number.)

After shuffling your **factory order** deck of 10007 cards, **what is the position of card 2019?**

{| part1 )}

## Approach

Because the new position of each card $x$ after a shuffle is independent of the positions of any other card, we can find the effect of each shuffle type on the position of any card in the deck. And because shuffling the pack is circular, we can represent the three shuffle types with a modulo expression where $d$ is the size of the deck:

- New deal: $pos_{x} = (-x - 1) \pmod d $
- Cut at n: $pos_{x} = (x - n) \pmod d$
- Deal with inc n: $pos_{x} = nx \pmod d$

All three have the same linear structure $ax + b$, where $a$ and $b$ are (-1,-1), (1,-n) and (n,0) respectively. So the first task is to parse the input to extract the (a,b) values for each shuffle:

```elm {l}
parse : String -> ( Int, Int )
parse line =
    let
        shuffle =
            line
                |> AOC.submatches "(new)|(deal) with (increment) (\\d+)|(cut) (-*\\d+)"
                |> List.filterMap identity
    in
    case shuffle of
        _ :: [] ->
            ( -1, -1 )

        _ :: n :: [] ->
            ( 1, -1 * AOC.toInt n )

        _ :: _ :: n :: [] ->
            ( AOC.toInt n, 0 )

        _ ->
            ( 1, 0 ) |> Debug.log ("Unexpected shuffle: " ++ line)
```

We can combine all the transformations into a single one in the form $ (a . pos + b) \pmod {deckSize}$ noting we start with the identity shuffle transform (1,0). To prevent possible overflow of 32 bit integers (that becomes evident in part two of this puzzle), we create a function `multiply` that performs modular multiplication with arbitrary length integers.

```elm {l}
combinedShuffle : Int -> ( Int, Int )
combinedShuffle deckSize =
    let
        applyPermutation ( a0, b0 ) ( a1, b1 ) =
            ( multiply a1 a0 deckSize, b1 + multiply a1 b0 deckSize )
    in
    puzzleInput
        |> List.map parse
        |> List.foldr applyPermutation ( 1, 0 )
```

```elm {l}
multiply : Int -> Int -> Int -> Int
multiply a b m =
    let
        biMod n =
            case BigInt.modBy (BigInt.fromInt m) n of
                Just rem ->
                    if BigInt.lt rem (BigInt.fromInt 0) then
                        BigInt.add rem (BigInt.fromInt m)

                    else
                        rem

                Nothing ->
                    BigInt.fromInt 0
    in
    BigInt.mul (BigInt.fromInt a) (BigInt.fromInt b)
        |> biMod
        |> BigInt.toString
        |> String.toInt
        |> Maybe.withDefault 0
```

We can then simply provide a $pos$ value of 2019 as specified in the question:

```elm {l r}
part1 : Int
part1 =
    let
        ( a, b ) =
            combinedShuffle 10007
    in
    modBy 10007 (a * 2019 + b)
```

{( part2 |}

After a while, you realize your shuffling skill won't improve much more with merely a single deck of cards. You ask every 3D printer on the ship to make you some more cards while you check on the ship repairs. While reviewing the work the droids have finished so far, you think you see Halley's Comet fly past!

When you get back, you discover that the 3D printers have combined their power to create for you a single, giant, brand new, **factory order** deck of **119315717514047 space cards**.

Finally, a deck of cards worthy of shuffling!

You decide to apply your complete shuffle process (your puzzle input) to the deck **101741582076661 times in a row.**

You'll need to be careful, though - one wrong move with this many cards and you might **overflow** your entire ship!

After shuffling your new, giant, **factory order** deck that many times, **what number is on the card that ends up in position 2020?**

{| part2 )}

## Approach Part Two

The second part is effectively asking for the inverse of the approach of part one. Instead of finding out where a starting card ends up, we need to know what the starting position was of the card that ends up at a given position. Because we need to apply the shuffles a very large number of times and the size of the deck is now very large, it cannot be feasibly calculated by brute force.

However, we can quickly find the effect of applying the combined shuffle a large number of times:

Consider the first three times we apply the shuffle function:

$f(x) = ax + b$
$f(f(x)) = a(ax + b) + b = a^2x + ab + b$
$f(f(f(x))) = a(a^2x + ab + b) + b = a^3x + a^2b + ab + b$

And therefore the general pattern for repeating the shuffle $n$ times:

$f^{n}(x) = a^nx + a^{n-1}b + a^{n-2}b + ... + b$
$f^{n}(x) = a^nx + b(a^{n-1} + a^{n-2} + ... + 1)$
$f^{n}(x) = a^nx + {a^n-1 \over a-1} b$

In other words we can provide two new values $a_n$ and $b_n$ that represent the linear transform of applying $f(x) = ax + b$ repeatedly $n$ times. We can implement a function to calculate this iterated function efficiently using [modulo exponentiation](https://en.wikipedia.org/wiki/Modular_exponentiation), implemented as `AOC.powerMod`

Modulo division is represented by multiplying by its modulo inverse after calculating $a$ and $b$. We can take advantage that for this puzzle $a$ and $m$ are coprime allowing us to use [Fermat's little theorem](https://en.wikipedia.org/wiki/Fermat%27s_little_theorem) to calculate the inverse efficiently.

```elm {l}
modInverse : Int -> Int -> Int
modInverse a m =
    case AOC.highestCommonFactor a m of
        1 ->
            AOC.powerMod a (m - 2) m

        _ ->
            0
```

```elm {l}
iteratedShuffle : Int -> Int -> ( Int, Int ) -> ( Int, Int )
iteratedShuffle n deckSize ( a, b ) =
    let
        aPrime =
            AOC.powerMod a n deckSize

        biDeckSize =
            BigInt.fromInt deckSize

        bPrime =
            multiply b (multiply (aPrime - 1) (modInverse (a - 1) deckSize) deckSize) deckSize
    in
    ( aPrime, modBy deckSize bPrime )
```

Once we have the new values for $a$ and $b$ representing the iterated shuffle, we simply calculate the inverse of the original part 1 equation:

If the position of card $x$ after a forward shuffle is represented by

$pos = ax + b$

The inverse, i.e. the card that ended in position $pos$, will be

$x = (pos - b) / a$

```elm {l r}
part2 : Int
part2 =
    let
        deckSize =
            119315717514047

        numShuffles =
            101741582076661

        ( a, b ) =
            combinedShuffle deckSize
                |> iteratedShuffle numShuffles deckSize
    in
    multiply (modBy deckSize (2020 - b)) (modInverse a deckSize) deckSize
```

---

## Reflection

My initial approach to part 1 was as below. This worked and takes advantage of the fact we can sequentially apply one of three functions to the deck depending on the shuffle type:

```elm
parse : Int -> String -> (Int -> Int)
parse len line =
    let
        shuffle =
            line
                |> AOC.submatches "(new)|(deal) with (increment) (\\d+)|(cut) (-*\\d+)"
                |> List.filterMap identity
    in
    case shuffle of
        _ :: [] ->
            pNewDeal len

        _ :: n :: [] ->
            pCut len (AOC.toInt n)

        _ :: _ :: n :: [] ->
            pDealInc len (AOC.toInt n)

        _ ->
            identity |> Debug.log ("Unexpected shuffle: " ++ line)
```

```elm
pNewDeal : Int -> Int -> Int
pNewDeal len pos =
    modBy len (-pos - 1)


pCut : Int -> Int -> Int -> Int
pCut len n pos =
    modBy len (pos - n)


pDealInc : Int -> Int -> Int -> Int
pDealInc len n pos =
    modBy len (pos * n)
```

```elm
part1 : Int
part1 =
    puzzleInput
        |> List.map (parse 10007)
        |> List.foldl identity 2019
```

However, I had to abandon this approach for Part 2 as we needed to iterate the sequence of functions a very large number of times. This led to the approach documented in part 1 where a single affine transform was generated summarising the combined shuffles.

Part two had me stumped for a long while not because of its complexity, but the difficulty in debugging 32 bit integer overflows in modular arithmetic. The problem was that overflows can lead to credible, but incorrect answers when their modulus is taken, and the incorrect value gives little clue as to the nature of the problem.

In the end I used this [large integer calculator](https://www.boxentriq.com/code-breaking/big-number-calculator) to calculate several of the steps manually and compare with computed results. It took a long while to identify where the overflow was occurring. This led me to modifying my modular multiplication and exponentiation functions to handle arbitrary large integers (although with a modulus of 119315717514047, overflow was only 'just' over the 32 bit limit). If only Elm had the option of 64 bit integers!
