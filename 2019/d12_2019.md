---
follows: data/d12_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 12 (2019): The N-Body Problem

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/12)

## Part 1

We can parse each line of input, extracting the three digits and storing as a 3-tuple:

```elm {l}
type alias Triplets =
    List ( Int, Int, Int )


parse : List String -> Triplets
parse =
    List.map
        (AOC.submatches "([-]*\\d+)"
            >> List.filterMap identity
            >> List.filterMap String.toInt
            >> AOC.tripletFromList
        )
        >> List.filterMap identity
```

Updating the positions and velocities of each moon is straightforward if we are prepared to do so iteratively. Being a 4-body problem, this makes sense as an approach rather than attempting to solve analytically.

We can update the system at each time-step, updating both velocity and position components:

```elm
tick : ( Triplets, Triplets ) -> ( Triplets, Triplets )
tick ( ps, vs ) =
    let
        newVs =
            updateVelocities ps vs

        newPs =
            updatePositions ps newVs
    in
    ( newPs, newVs )
```

```elm {l}
tick : ( Triplets, Triplets ) -> ( Triplets, Triplets )
tick ( positions, velocities ) =
    let
        ( pxs, pys, pzs ) =
            AOC.unzip3 positions

        ( vxs, vys, vzs ) =
            AOC.unzip3 velocities

        newVs pComps vComps =
            updateVelocity pComps vComps

        newPs pComps vComps =
            updatePosition pComps (newVs pComps vComps)
    in
    ( AOC.zip3 (newPs pxs vxs) (newPs pys vys) (newPs pzs vzs)
    , AOC.zip3 (newVs pxs vxs) (newVs pys vys) (newVs pzs vzs)
    )
```

To find the gravitational effect on velocities we can compare the positions of a pair of moons to update their respective velocity components along each of the three axes:

```elm {l}
gravity : ( Int, Int ) -> ( Int, Int )
gravity ( p1, p2 ) =
    if p1 < p2 then
        ( 1, -1 )

    else if p1 > p2 then
        ( -1, 1 )

    else
        ( 0, 0 )
```

To find the net effect of gravitational pull between all four moons, we calculate the effect between each pairwise combination and add the sum of the pairwise effects to each moon's velocity components:

```elm {l}
updateVelocity : List Int -> List Int -> List Int
updateVelocity pComponents vComponents =
    let
        newVComponents ps vs =
            case List.map gravity (AOC.pairwiseCombinations pComponents) of
                [ ( a1, b1 ), ( a2, c1 ), ( a3, d1 ), ( b2, c2 ), ( b3, d2 ), ( c3, d3 ) ] ->
                    [ a1 + a2 + a3, b1 + b2 + b3, c1 + c2 + c3, d1 + d2 + d3 ]
                        |> List.map2 (+) vs

                _ ->
                    [] |> Debug.log "bad combinations"
    in
    newVComponents pComponents vComponents
```

Once we have updated velocities, we can easily update the positions of each moon:

```elm {l}
updatePosition : List Int -> List Int -> List Int
updatePosition =
    List.map2 (+)
```

Finally we can calculate the energy of the system given the set of moons' velocities and positions:

```elm {l}
energy : ( Triplets, Triplets ) -> Int
energy ( ps, vs ) =
    let
        pot =
            List.map (\( x, y, z ) -> abs x + abs y + abs z) ps

        kin =
            List.map (\( x, y, z ) -> abs x + abs y + abs z) vs
    in
    List.map2 (*) pot kin
        |> List.sum
```

```elm {l r}
part1 : Int
part1 =
    let
        ps =
            parse puzzleInput

        vs =
            List.map (always ( 0, 0, 0 )) ps
    in
    List.foldl (\i ms -> tick ms) ( ps, vs ) (List.range 1 1000)
        |> energy
```

---

## Part 2

The question suggests that the period of the cycle of combined velocity and position components will be too large to find iteratively. However, since the x, y and z components are updated independently of each other, we can find the periods of each component and find their lowest common multiple.

```elm {l r}
part2 : Int
part2 =
    let
        ( pxs, pys, pzs ) =
            puzzleInput |> parse |> AOC.unzip3

        componentTick ( ps, vs ) =
            let
                newVs =
                    updateVelocity ps vs

                newPs =
                    updatePosition ps newVs
            in
            ( newPs, newVs )

        xCycle =
            AOC.sequenceCycle 0 ( pxs, [ 0, 0, 0, 0 ] ) componentTick
                |> Maybe.withDefault ( 0, 0 )
                |> Tuple.first

        yCycle =
            AOC.sequenceCycle 0 ( pys, [ 0, 0, 0, 0 ] ) componentTick
                |> Maybe.withDefault ( 0, 0 )
                |> Tuple.first

        zCycle =
            AOC.sequenceCycle 0 ( pzs, [ 0, 0, 0, 0 ] ) componentTick
                |> Maybe.withDefault ( 0, 0 )
                |> Tuple.first
    in
    List.foldl AOC.lowestCommonMultiple 1 [ xCycle, yCycle, zCycle ]
```

---

## Reflection

A classic Advent of Code problem. Part one is reasonably straightforward that works with an iterative solution, but part two adds many orders of magnitude to the iterative process making direct calculation impossible.

While obvious a cycle detecting algorithm would be necessary it took me a little while to realise that I could find cycles in the x, y and z components of the positions and velocities completely independently and then just find the lowest common multiple. This should have been more obvious to me given that I calculated the positions/velocities for the three components separately.

Although several previous puzzles have required the detection of cycles, these have typically been with a period or offset sufficiently small to allow naive detection. For this exercise I had to implement `sequenceCycle` (added to [Aoc.elm](../src/Aoc.elm)).

The lowest common multiple algorithm isn't particularly efficient and does run the risk of overflow with these large numbers, but it was sufficient for this puzzle.
