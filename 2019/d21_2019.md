---
follows: data/d21_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 21 (2019): Springdroid Adventure

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/21)

## Part 1

To generate input to the springdroid, we need to be able to generate ASCII springscript from readable instructions by converting each character in the instructions to its ASCII code and adding a 10 (ASCII newline) at the end of each instruction.

```elm {l}
springscript : List String -> List Int
springscript =
    List.map (String.toList >> List.map Char.toCode >> AOC.flip (++) [ 10 ])
        >> List.concat
```

To view output from the springdroid, we need to be able to perform an equivalent translation of ASCII output codes back into text. This will either be a map of the last part of the hull before the droid fell through a hole (ASCII characters), or a non-ASCII code reporting the damage:

```elm {l}
droidReport : List Int -> String
droidReport sdOut =
    case List.filter ((<=) 255) sdOut of
        damage :: [] ->
            "Scanned " ++ String.fromInt damage ++ " damage."

        _ ->
            sdOut
                |> List.map Char.fromCode
                |> String.fromList
                |> String.replace "#" "█"
```

Setting off the droid is simply a matter of providing some springscript and viewing the output.

```elm {l}
activateDroid : List Int -> List Int
activateDroid instrs =
    puzzleInput
        |> initComputer instrs
        |> runProg
        |> .outputStore
```

The springscript to avoid holes: `¬A ∨ (¬C ∧ D)` (Jump if next step is a hole or hole in 3 steps and ground in 4).

We can rearrange so the logical predicates are applied in order with just a single register:

`¬A ∨ (¬C ∧ D) = ¬C ∧ D ∨ ¬A = ¬(¬D ∨ C ∧ A )`

```elm {l}
droidProg : List String
droidProg =
    [ "NOT D J"
    , "OR C J"
    , "AND A J"
    , "NOT J J"
    , "WALK"
    ]
```

```elm {r}
part1 : String
part1 =
    droidProg
        |> springscript
        |> activateDroid
        |> droidReport
```

## Part 2

## Springscript

To avoid the longer sequence of holes:

- If there are holes within 3 steps and hull 4 steps in front, then jump:
  `¬A ∨ ¬B ∨ ¬C ∧ D`
  - but only if there is hull 5 or 8 steps in front
    `∧ (E ∨ H)`

We can reduce (but not remove) the reliance the temporary register by grouping the nots (negating a group just involves `NOT J J` after the group):

`¬(A ∧ B ∧ C) ∧ D ∧ (E ∨ H)`

```elm {l}
droidProg2 : List String
droidProg2 =
    [ "OR A J"
    , "AND B J"
    , "AND C J"
    , "NOT J J"
    , "AND D J"
    , "OR E T"
    , "OR H T"
    , "AND T J"
    , "RUN"
    ]
```

```elm {r}
part2 : String
part2 =
    droidProg2
        |> springscript
        |> activateDroid
        |> droidReport
```

---

## Reflection

I think this was my favourite puzzle so far. Nicely self-contained focussing on predicate logic rather than requiring lots of coding. I think I was lucky to hit on a short solution to part A quite early one while experimenting with the droid. I found it easier to reduce the operations to working with a single register, which also gave a nice short solution.

For part B it took me much longer to arrive at a set of safe rules and I was unable to reduce them to working only with the `J` register, although I did get the rules down to 8 operations.

Looking at [this solution on reddit](https://www.reddit.com/r/adventofcode/comments/edll5a/2019_day_21_solutions/fbizudo/), it would appear that there is a 6-statement, single register solution:

```elm
[ "NOT H J"
, "OR  C J"
, "AND B J"
, "AND A J"
, "NOT J J"
, "AND D J"
, "RUN"]
```

which seems remarkably elegant. Not sure I'd have been able to come up with this though.
