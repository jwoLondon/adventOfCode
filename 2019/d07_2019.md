---
follows: data/d07_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2019): Amplification Circuit

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/7)

## Part 1

To complete this puzzle, [intcode](intcode.md) needs to be enhanced to store more than one input. A flexible solution is to provide an `inputStore` that holds of queue of input values.

We can then simple fold over computers, extracting the output from one and inserting it as the input to the next.

```elm {l m}
amplifiers : List Int -> Int
amplifiers phases =
    case phases of
        ph1 :: otherPhases ->
            let
                ampOut phase input =
                    puzzleInput
                        |> initComputer [ phase, input ]
                        |> runProg
                        |> .out
            in
            List.foldl ampOut (ampOut ph1 0) otherPhases

        _ ->
            -999 |> Debug.log "Bad phase input"
```

To find the highest signal, we simply test all permutations of the phase digits in 0 – 4 looking for the set that produces the highest output from the chained amplifiers.

```elm {l r}
part1 : Int
part1 =
    AOC.permutations [ 0, 1, 2, 3, 4 ]
        |> List.foldl (amplifiers >> max) 0
```

---

## Part 2

This requires another enhancement to [intcode](intcode.md) to allow execution of a computer to pause when waiting for input.

Passing signals through the looped amplifiers requires running each amplifier when it receives an input from the previous one, passing its output to the next until a halt state in the last amplifier is generated.

```elm {l}
generateSignal : List Int -> Int
generateSignal phases =
    case phases of
        ph1 :: otherPhases ->
            let
                createAmp phase input =
                    puzzleInput |> initComputer [ phase, input ] |> runProg

                loopedAmps =
                    otherPhases
                        |> AOC.scanl (\p amp -> createAmp p amp.out) (createAmp ph1 0)
                        |> List.indexedMap Tuple.pair
                        |> AOC.rotateList

                runCircuit amps =
                    case amps of
                        ( idPrev, ampPrev ) :: ( id, amp ) :: others ->
                            if amp.status == Halted && id == List.length phases - 1 then
                                amp.out

                            else
                                runCircuit
                                    (( id, runProg { amp | inputStore = [ ampPrev.out ] } )
                                        :: others
                                        ++ [ ( idPrev, ampPrev ) ]
                                    )

                        _ ->
                            -999 |> Debug.log "Need at least two amps to form feedback"
            in
            runCircuit loopedAmps

        _ ->
            -999 |> Debug.log "Bad phase input"
```

As with part one, we find the maximum signal by trying all permutations of the new phase digits 5 – 9.

```elm {l r}
part2 : Int
part2 =
    AOC.permutations [ 5, 6, 7, 8, 9 ]
        |> List.foldl (generateSignal >> max) 0
```

---

## Reflection

My favourite puzzle so far this year. It took me a little while to understand what was wanted in part 2, particularly the realisation that we need to pause the running of a program before it halts. Most directly whenever a computer generates an output, but more generally, when it is awaiting an input.

This solution could in theory take any number of amplifiers rather than hard-code the 5 specified in the question. Whether this abstraction will be any use in future puzzles, we wait to see.

What made this most satisfying was the refactoring once I had coded up a working solution to make a cleaner, shorter, simpler set of functions. Looking back on the final code, it still surprises me how what appears to be a complex procedure can be coded so briefly, even in a somewhat verbose language like Elm.
