---
follows: data/d06_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2019): Universal Orbit Map

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/6)

## Part 1

Each line of input represents a graph edge between two planets. In parsing them we swap the planets in each pair to create a (childPlanet, parentPlanet) order:

```elm {l}
toEdges : List String -> List ( String, String )
toEdges =
    List.map (\s -> s |> String.split ")")
        >> List.map
            (\s ->
                ( s |> List.drop 1 |> List.head |> Maybe.withDefault ""
                , s |> List.head |> Maybe.withDefault ""
                )
            )
```

We can use Elm's ready-made [elm-graph](https://package.elm-lang.org/packages/drathier/elm-graph/latest/Graph) package to build the graph from a list of edges:

```elm {l}
orbits : Graph String String Int
orbits =
    puzzleInput
        |> toEdges
        |> List.foldl Graph.Pair.insertEdge Graph.empty
```

In order to count the indirect orbits, we need to accumulate the direct orbits for each planet and the direct orbits of each of their orbiting planets. We can apply a topological sorting of the planets so that we accumulate from the edge of the tree structure towards the root.

```elm {l}
sortedPlanets : List String
sortedPlanets =
    orbits
        |> Graph.topologicalSort
        |> Maybe.withDefault []
```

We can build up a dictionary of number of indirect orbits for each planet as we process each planet in topological order.

```elm {l r}
part1 : Int
part1 =
    let
        upstream planet numOrbits =
            Graph.outgoing planet orbits
                |> Set.map (\p -> Dict.get p numOrbits |> Maybe.withDefault 0)
                |> Set.foldl (+) 1

        accumulate planet numOrbits =
            Dict.insert planet (upstream planet numOrbits) numOrbits
    in
    List.foldr accumulate Dict.empty sortedPlanets
        |> Dict.values
        |> List.map (\o -> o - 1)
        |> List.sum
```

---

## Part 2

Because the graph structure is a tree (no cycles and each plant has at most one parent it orbits), we can find the distance between any two nodes efficiently. Find the pathway for each of the two planets to the root (COM), remove the common set of planets and count the number of planets remaining.

The set of planets between any one and the root is just found by recursively navigating to the parent until we reach a COM that has no parent:

```elm {l}
planetsToRoot : String -> Set String -> Set String
planetsToRoot planet planets =
    case Graph.outgoing planet orbits |> Set.toList |> List.head of
        Just p ->
            planetsToRoot p (Set.insert p planets)

        Nothing ->
            planets
```

Then we just do some basic set intersection and differencing to find the two sets of unique planets:

```elm {l r}
part2 : Int
part2 =
    let
        sanPlanets =
            planetsToRoot "SAN" Set.empty

        youPlanets =
            planetsToRoot "YOU" Set.empty

        commonPlanets =
            Set.intersect sanPlanets youPlanets
    in
    Set.size (Set.diff youPlanets commonPlanets)
        + Set.size (Set.diff sanPlanets commonPlanets)
```

---

## Reflection

There have been many graph searching puzzles in AoC over the years and I have always created my own data structures to represent them. But bored with reinventing wheels, I decided this time to use one of Elm's packages for graph representation. This has the benefit of having an in-built topological sort which can be applied to the orbital dependencies.

Even though general graphs are not structures I work with very often, I do work with trees, so the solution to part two was straightforward. I initially assumed we would have to keep track of the distance along the path from planet to root, but simply counting the number of planets is sufficient so this simplifies the solution.

Overall, a nice introduction to graph searching without having to worry about coding breadth first searches and the like. No doubt we will have some of those in future puzzles.
