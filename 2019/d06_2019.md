---
follows: data/d06_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2019): Universal Orbit Map

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/6):

{( part1|}

You've landed at the Universal Orbit Map facility on Mercury. Because navigation in space often involves transferring between orbits, the orbit maps here are useful for finding efficient routes between, for example, you and Santa. You download a map of the local orbits (your puzzle input).

Except for the universal Center of Mass (COM), every object in space is in orbit around exactly one other object. An orbit looks roughly like this:

                      \
                       \
                        |
                        |
    AAA--> o            o <--BBB
                        |
                        |
                       /
                      /

In this diagram, the object BBB is in orbit around AAA. The path that BBB takes around AAA (drawn with lines) is only partly shown. In the map data, this orbital relationship is written AAA)BBB, which means "BBB is in orbit around AAA".

Before you use your map data to plot a course, you need to make sure it wasn't corrupted during the download. To verify maps, the Universal Orbit Map facility uses **orbit count checksums** - the total number of direct orbits (like the one shown above) and **indirect orbits**.

Whenever A orbits B and B orbits C, then A **indirectly orbits** C. This chain can be any number of objects long: if A orbits B, B orbits C, and C orbits D, then A indirectly orbits D.

For example, suppose you have the following map:

    COM)B
    B)C
    C)D
    D)E
    E)F
    B)G
    G)H
    D)I
    E)J
    J)K
    K)L

Visually, the above map of orbits looks like this:

            G - H       J - K - L
           /           /
    COM - B - C - D - E - F
                   \
                    I

In this visual representation, when two objects are connected by a line, the one on the right directly orbits the one on the left.

Here, we can count the total number of orbits as follows:

- D directly orbits C and indirectly orbits B and COM, a total of 3 orbits.
- L directly orbits K and indirectly orbits J, E, D, C, B, and COM, a total of 7 orbits.
- COM orbits nothing.

The total number of direct and indirect orbits in this example is **42**.

**What is the total number of direct and indirect orbits** in your map data?

{| part1 )}

## Parsing

Each line of input represents a graph edge between two planets. In parsing them we swap the planets in each pair to create a (childPlanet, parentPlanet) order:

```elm {l}
toEdges : List String -> List ( String, String )
toEdges =
    List.map (\s -> s |> String.split ")")
        >> List.map
            (\s ->
                ( s |> List.drop 1 |> List.head |> Maybe.withDefault ""
                , s |> List.head |> Maybe.withDefault ""
                )
            )
```

## Approach

We can use Elm's ready-made [elm-graph](https://package.elm-lang.org/packages/drathier/elm-graph/latest/Graph) package to build the graph from a list of edges:

```elm {l}
orbits : Graph String String Int
orbits =
    puzzleInput
        |> toEdges
        |> List.foldl Graph.Pair.insertEdge Graph.empty
```

In order to count the indirect orbits, we need to accumulate the direct orbits for each planet and the direct orbits of each of their orbiting planets. We can apply a topological sorting of the planets so that we accumulate from the edge of the tree structure towards the root.

```elm {l}
sortedPlanets : List String
sortedPlanets =
    orbits
        |> Graph.topologicalSort
        |> Maybe.withDefault []
```

We can build up a dictionary of number of indirect orbits for each planet as we process each planet in topological order.

```elm {l r}
part1 : Int
part1 =
    let
        upstream planet numOrbits =
            Graph.outgoing planet orbits
                |> Set.map (\p -> Dict.get p numOrbits |> Maybe.withDefault 0)
                |> Set.foldl (+) 1

        accumulate planet numOrbits =
            Dict.insert planet (upstream planet numOrbits) numOrbits
    in
    List.foldr accumulate Dict.empty sortedPlanets
        |> Dict.values
        |> List.map (\o -> o - 1)
        |> List.sum
```

{( part2 |}

Now, you just need to figure out how many **orbital transfers** you (YOU) need to take to get to Santa (SAN).

You start at the object YOU are orbiting; your destination is the object SAN is orbiting. An orbital transfer lets you move from any object to an object orbiting or orbited by that object.

For example, suppose you have the following map:

    COM)B
    B)C
    C)D
    D)E
    E)F
    B)G
    G)H
    D)I
    E)J
    J)K
    K)L
    K)YOU
    I)SAN

Visually, the above map of orbits looks like this:

                              YOU
                             /
            G - H       J - K - L
           /           /
    COM - B - C - D - E - F
                   \
                    I - SAN

In this example, YOU are in orbit around K, and SAN is in orbit around I. To move from K to I, a minimum of 4 orbital transfers are required:

- K to J
- J to E
- E to D
- D to I

Afterward, the map of orbits looks like this:

            G - H       J - K - L
           /           /
    COM - B - C - D - E - F
                   \
                    I - SAN
                     \
                      YOU

**What is the minimum number of orbital transfers required** to move from the object YOU are orbiting to the object SAN is orbiting? (Between the objects they are orbiting - **not** between YOU and SAN.)

{| part2 )}

## Approach Part Two

Because the graph structure is a tree (no cycles and each plant has at most one parent it orbits), we can find the distance between any two nodes efficiently. Find the pathway for each of the two planets to the root (COM), remove the common set of planets and count the number of planets remaining.

The set of planets between any one and the root is just found by recursively navigating to the parent until we reach a COM that has no parent:

```elm {l}
planetsToRoot : String -> Set String -> Set String
planetsToRoot planet planets =
    case Graph.outgoing planet orbits |> Set.toList |> List.head of
        Just p ->
            planetsToRoot p (Set.insert p planets)

        Nothing ->
            planets
```

Then we just do some basic set intersection and differencing to find the two sets of unique planets:

```elm {l r}
part2 : Int
part2 =
    let
        sanPlanets =
            planetsToRoot "SAN" Set.empty

        youPlanets =
            planetsToRoot "YOU" Set.empty

        commonPlanets =
            Set.intersect sanPlanets youPlanets
    in
    Set.size (Set.diff youPlanets commonPlanets)
        + Set.size (Set.diff sanPlanets commonPlanets)
```

---

## Reflection

There have been many graph searching puzzles in AoC over the years and I have always created my own data structures to represent them. But bored with reinventing wheels, I decided this time to use one of Elm's packages for graph representation. This has the benefit of having an in-built topological sort which can be applied to the orbital dependencies.

Even though general graphs are not structures I work with very often, I do work with trees, so the solution to part two was straightforward. I initially assumed we would have to keep track of the distance along the path from planet to root, but simply counting the number of planets is sufficient so this simplifies the solution.

Overall, a nice introduction to graph searching without having to worry about coding breadth first searches and the like. No doubt we will have some of those in future puzzles.
