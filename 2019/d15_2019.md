---
follows: data/d15_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 15 (2019): Oxygen System

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/15):

{( part1|}

Out here in deep space, many things can go wrong. Fortunately, many of those things have indicator lights. Unfortunately, one of those lights is lit: the oxygen system for part of the ship has failed!

According to the readouts, the oxygen system must have failed days ago after a rupture in oxygen tank two; that section of the ship was automatically sealed once oxygen levels went dangerously low. A single remotely-operated **repair droid** is your only option for fixing the oxygen system.

The Elves' care package included an Intcode program (your puzzle input) that you can use to remotely control the repair droid. By running that program, you can direct the repair droid to the oxygen system and fix the problem.

The remote control program executes the following steps in a loop forever:

- Accept a **movement command** via an input instruction.
- Send the movement command to the repair droid.
- Wait for the repair droid to finish the movement operation.
- Report on the **status** of the repair droid via an output instruction.

Only four **movement commands** are understood: north (1), south (2), west (3), and east (4). Any other command is invalid. The movements differ in direction, but not in distance: in a long enough east-west hallway, a series of commands like 4,4,4,4,3,3,3,3 would leave the repair droid back where it started.

The repair droid can reply with any of the following **status** codes:

- 0: The repair droid hit a wall. Its position has not changed.
- 1: The repair droid has moved one step in the requested direction.
- 2: The repair droid has moved one step in the requested direction; its new position is the location of the oxygen system.

You don't know anything about the area around the repair droid, but you can figure it out by watching the status codes.

For example, we can draw the area using D for the droid, # for walls, . for locations the droid can traverse, and ▒ for unexplored locations. Then, the initial state looks like this:

    ▒▒▒▒▒
    ▒▒▒▒▒
    ▒▒D▒▒
    ▒▒▒▒▒
    ▒▒▒▒▒

To make the droid go north, send it 1. If it replies with 0, you know that location is a wall and that the droid didn't move:

    ▒▒▒▒▒
    ▒▒#▒▒
    ▒▒D▒▒
    ▒▒▒▒▒
    ▒▒▒▒▒

To move east, send 4; a reply of 1 means the movement was successful:

    ▒▒▒▒▒
    ▒▒#▒▒
    ▒▒.D▒
    ▒▒▒▒▒
    ▒▒▒▒▒

Then, perhaps attempts to move north (1), south (2), and east (4) are all met with replies of 0:

    ▒▒▒▒▒
    ▒▒##▒
    ▒▒.D#
    ▒▒▒#▒
    ▒▒▒▒▒

Now, you know the repair droid is in a dead end. Backtrack with 3 (which you already know will get a reply of 1 because you already know that location is open):

    ▒▒▒▒▒
    ▒▒##▒
    ▒▒D.#
    ▒▒▒#▒
    ▒▒▒▒▒

Then, perhaps west (3) gets a reply of 0, south (2) gets a reply of 1, south again (2) gets a reply of 0, and then west (3) gets a reply of 2:

    ▒▒▒▒▒
    ▒▒##▒
    ▒▒..#
    ▒D.#▒
    ▒▒#▒▒

Now, because of the reply of 2, you know you've found the **oxygen system**! In this example, it was only 2 moves away from the repair droid's starting position.

**What is the fewest number of movement commands** required to move the repair droid from its starting position to the location of the oxygen system?

{| part1 )}

## Approach

Let's first represent the possible droid input and output types. As we will need to find the number of moves required to reach the oxygen, we can store that as part of the `Oxygen` custom type variant.

```elm {l}
type MoveCommand
    = North
    | East
    | South
    | West


cmdCode : MoveCommand -> Int
cmdCode mvCmd =
    case mvCmd of
        North ->
            1

        South ->
            2

        West ->
            3

        East ->
            4


cmdVector : MoveCommand -> ( Int, Int )
cmdVector mvCmd =
    case mvCmd of
        North ->
            ( 0, 1 )

        South ->
            ( 0, -1 )

        West ->
            ( -1, 0 )

        East ->
            ( 1, 0 )


type DroidStatus
    = Blocked
    | Moved
    | MovedToOxygen


statusFromCode : Int -> DroidStatus
statusFromCode code =
    case code of
        1 ->
            Moved

        2 ->
            MovedToOxygen

        _ ->
            Blocked


type ShipCell
    = Wall
    | Passage
    | Oxygen Int


type alias ShipMap =
    Dict ( Int, Int ) ShipCell
```

The IntCode computer only outputs the most recent move attempt (`Blocked`, `Moved` etc.); it does not store its location or history. To perform standard maze searching we need to keep track of location, distance from start as well as allow multiple states to be stored for backtracking or breadth first searching. So like [day 11](#d11_2019.md), it makes sense to store all the additional information externally to an intcode instance in a record:

```elm {l}
type alias Droid =
    { computer : Computer
    , position : ( Int, Int )
    , moves : Int
    }
```

To move the droid, we provide a move command, run its computer and update the location of the reported ship cell after attempting to move:

```elm {l}
move : MoveCommand -> Droid -> Droid
move mvCmd droid =
    let
        newComp =
            droid.computer
                |> addInput (cmdCode mvCmd)
                |> runProg

        ( x, y ) =
            droid.position

        ( dx, dy ) =
            cmdVector mvCmd

        ( newX, newY, newMoves ) =
            ( x + dx, y + dy, droid.moves + 1 )
    in
    { droid | computer = newComp, position = ( newX, newY ), moves = newMoves }
```

To build the map of ship cells we can perform a breadth first search from the start point, storing a queue of locally adjacent passages. Once the queue is empty, we have explored all possible locations accessible from the start point.

```elm {l}
updateMap : Droid -> MoveCommand -> ( Deque Droid, ShipMap ) -> ( Deque Droid, ShipMap )
updateMap droid mvCmd ( todo, shipMap ) =
    let
        newDroid =
            move mvCmd droid

        scanShip m c =
            case c.out |> statusFromCode of
                Blocked ->
                    Wall

                Moved ->
                    Passage

                MovedToOxygen ->
                    Oxygen m
    in
    case scanShip newDroid.moves newDroid.computer of
        Passage ->
            if Dict.member newDroid.position shipMap then
                ( todo, Dict.insert newDroid.position Passage shipMap )

            else
                ( Deque.pushBack newDroid todo, Dict.insert newDroid.position Passage shipMap )

        Oxygen m ->
            ( todo, Dict.insert newDroid.position (Oxygen m) shipMap )

        Wall ->
            ( todo, Dict.insert newDroid.position Wall shipMap )


explore : Deque Droid -> ShipMap -> ShipMap
explore todo shipMap =
    case Deque.popFront todo of
        ( Nothing, _ ) ->
            shipMap

        ( Just droid, todo2 ) ->
            let
                ( todo3, newShipMap ) =
                    List.foldl (updateMap droid) ( todo2, shipMap ) [ North, South, West, East ]
            in
            explore todo3 newShipMap
```

Set off the droid exploring the ship

```elm {l}
ship : ShipMap
ship =
    let
        comp =
            puzzleInput |> initComputer [] |> runProg
    in
    Dict.singleton ( 0, 0 ) Passage
        |> explore (Deque.singleton (Droid comp ( 0, 0 ) 0))
```

```elm {l r}
part1 : List Int
part1 =
    ship
        |> Dict.values
        |> List.filterMap
            (\c ->
                case c of
                    Oxygen m ->
                        Just m

                    _ ->
                        Nothing
            )
```

## Visualizing the Ship

While it is not necessary in order to compute the answer, it is helpful to be able to visualize the maze of passageways discovered by the droid. To do this, we can convert the map of ship cells into a grid and symbolise each location:

```elm {l}
symbol : ShipCell -> String
symbol tile =
    case tile of
        Passage ->
            "░"

        Wall ->
            "█"

        Oxygen _ ->
            "O"


type alias Bounds =
    { minX : Int
    , minY : Int
    , maxX : Int
    , maxY : Int
    }


dimensions : List ( Int, Int ) -> Bounds
dimensions locations =
    let
        xs =
            locations |> List.map Tuple.first

        ys =
            locations |> List.map Tuple.second
    in
    Bounds (List.minimum xs |> Maybe.withDefault 99999)
        (List.minimum ys |> Maybe.withDefault 99999)
        (List.maximum xs |> Maybe.withDefault -99999)
        (List.maximum ys |> Maybe.withDefault -99999)


display : ShipMap -> List String
display shipLocations =
    let
        bounds =
            shipLocations |> Dict.keys |> dimensions

        empty =
            AOC.gInit (1 + bounds.maxY - bounds.minY) (1 + bounds.maxX - bounds.minX) (symbol Wall)

        grid =
            Dict.foldl (\( x, y ) tile g -> AOC.gSet ( y - bounds.minY, x - bounds.minX ) (symbol tile) g) empty shipLocations
    in
    grid
        |> AOC.gToList
        |> AOC.groupsOf (AOC.gColCount grid)
        |> List.map ((::) "\n")
        |> List.concat
```

{(fixed|} ^^^elm {m=(display ship)}^^^ {|fixed)}

{( part2 |}

You quickly repair the oxygen system; oxygen gradually fills the area.

Oxygen starts in the location containing the repaired oxygen system. It takes **one minute** for oxygen to spread to all open locations that are adjacent to a location that already contains oxygen. Diagonal locations are not adjacent.

In the example above, suppose you've used the droid to explore the area fully and have the following map (where locations that currently contain oxygen are marked O):

     ##
    #..##
    #.#..#
    #.O.#
     ###

Initially, the only location which contains oxygen is the location of the repaired oxygen system. However, after one minute, the oxygen spreads to all open (.) locations that are adjacent to a location containing oxygen:

     ##
    #..##
    #.#..#
    #OOO#
     ###

After a total of two minutes, the map looks like this:

     ##
    #..##
    #O#O.#
    #OOO#
     ###

After a total of three minutes:

     ##
    #O.##
    #O#OO#
    #OOO#
     ###

And finally, the whole region is full of oxygen after a total of four minutes:

     ##
    #OO##
    #O#OO#
    #OOO#
     ###

So, in this example, all locations contain oxygen after **4** minutes.

Use the repair droid to get a complete map of the area. **How many minutes will it take to fill with oxygen?**

{| part2 )}

## Approach Part Two

Using the shipMap created in part 1, we can extract just the locations of passageways and then perform another breadth first search starting from the Oxygen location. This should be faster than part one as we are not relying on the intcode computer to sense passageways.

Keeping track of the number of moves in the third element of each `todo` tuple, the final location should represent the diameter of the passage graph and the number of minutes to propagate the oxygen supply to its furthest point.

```elm {l}
oxygenate : Set ( Int, Int ) -> Deque ( Int, Int, Int ) -> Int -> Int
oxygenate passages todo minutes =
    case Deque.popFront todo of
        ( Nothing, _ ) ->
            minutes

        ( Just ( x, y, t ), todo2 ) ->
            let
                passagesNew =
                    Set.remove ( x, y ) passages

                propagate ( dx, dy ) todo3 =
                    if Set.member ( x + dx, y + dy ) passagesNew then
                        Deque.pushBack ( x + dx, y + dy, t + 1 ) todo3

                    else
                        todo3
            in
            oxygenate passagesNew (List.foldl propagate todo2 (List.map cmdVector [ North, South, West, East ])) t
```

```elm {l r}
part2 : Int
part2 =
    let
        todo =
            ship
                |> Dict.filter
                    (\loc c ->
                        case c of
                            Oxygen _ ->
                                True

                            _ ->
                                False
                    )
                |> Dict.keys
                |> List.map (\( x, y ) -> ( x, y, 0 ))
                |> Deque.fromList

        passages =
            ship
                |> Dict.filter (\_ -> (==) Passage)
                |> Dict.keys
                |> Set.fromList
    in
    oxygenate passages todo 0
```

---

## Reflection

Another of the puzzles where I ran out of time in December so left to tackle later in the year. When I first tacked it in December I was not fully confident that my intcode computer was bug free, so when I was failing to navigate the passageways correctly, I wasn't sure if the problem was in my graph traversal or the intcode Droid messages.

My initial (December) approach was to allow the Droid to backtrack when it reached a dead-end, but this was unnecessarily complicated. A simpler and more reliable solution was to encapsulate the droid in an Elm record (just as [Day 11](d11_2019.md)) and perform a standard breadth first search.

Having done this successfully, Part 2 was similar BFS, this time starting from the Oxygen supply. Having visualised the passageways, it was helpful to see that passageways were fully connected and all one cell wide. It also helped in some debugging when I was failing to add some neighbours to the `todo` list at passageway junctions.
