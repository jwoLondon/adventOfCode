---
follows: data/d16_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 16 (2019): Flawed Frequency Transmission

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/16)

## Part 1

Convert a string of digits into a list of integers.

```elm {l}
parse : String -> List Int
parse =
    String.toList
        >> List.filterMap (String.fromChar >> String.toInt)
```

We can find the repeating pattern multipliers by repeating each digit of the base pattern 'position' times, For cases where the resulting list is shorter than the input length, we repeat the entire sequence sufficient times to at least match the input length.

In the worst case, this leads to generation of a list just under 4 times the length of the input before we extract the coefficients.

```elm {l}
coeffs : Int -> Int -> List Int
coeffs position len =
    let
        repeatedPattern =
            List.concatMap (List.repeat position) [ 0, 1, 0, -1 ]
    in
    List.repeat (1 + len // List.length repeatedPattern) repeatedPattern
        |> List.concat
        |> List.drop 1
        |> List.take len
```

```elm {l}
phase : List Int -> List Int
phase input =
    let
        row pos =
            List.map2 (*) input (coeffs pos (List.length input))
                |> List.sum
                |> remainderBy 10
                |> abs
    in
    List.map row (List.range 1 (List.length input))
```

```elm {l r}
part1 : String
part1 =
    AOC.iterate 100 (parse puzzleInput) phase
        |> List.take 8
        |> List.map String.fromInt
        |> String.concat
```

---

## Part 2

It is clear from the vastly increased signal length, that for part 2 we cannot simply iterate the part 1 solution.

Looking for patterns that might give a clue for a more efficient algorithm, we can show the base pattern used as a multiplier for the digit at each position in the signal.

| position | 1   | 2   | 3   | 4   | 5   | 6   | 7   | 8   | 9   | 10  | 11  | 12  | 13  | 14  | 15  | 16  | 17  | 18  |
| -------- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- |
| **1**    | 1   | 0   | -1  | 0   | 1   | 0   | -1  | 0   | 1   | 0   | -1  | 0   | 1   | 0   | -1  | 0   | 1   | 0   |
| **2**    | 0   | 1   | 1   | 0   | 0   | -1  | -1  | 0   | 0   | 1   | 1   | 0   | 0   | -1  | -1  | 0   | 0   | 1   |
| **3**    | 0   | 0   | 1   | 1   | 1   | 0   | 0   | 0   | -1  | -1  | -1  | 0   | 0   | 0   | 1   | 1   | 1   | 0   |
| **4**    | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 0   | 0   | 0   | 0   | -1  | -1  | -1  | -1  | 0   | 0   | 0   |
| **5**    | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 0   | 0   | 0   | 0   | 0   | -1  | -1  | -1  | -1  |
| **6**    | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 0   | 0   | 0   | 0   | 0   | 0   | -1  |
| **7**    | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 0   | 0   | 0   | 0   | 0   |
| **8**    | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 0   | 0   | 0   |
| **9**    | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 0   | 0   |
| **10**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   |
| **11**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   | 1   |
| **12**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   | 1   |
| **13**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   | 1   |
| **14**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   | 1   |
| **15**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   | 1   |
| **16**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   | 1   |
| **17**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   | 1   |
| **18**   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 0   | 1   |

We can note a couple of useful properties from this example:

- The new signal at any given position is independent of any digits in earlier positions (all 0s). This means for a a given offset, we can ignore any digits prior to that position.
- The last digit of the new signal (18th in example above) will just be the last digit of the input signal (all other multipliers are 0). For the penultimate digit it is the sum of the last two digits, the third from last the sum of the last three digits etc. This pattern holds for the final 9 digits of an 18 digit sequence.

And so it appears to be the case that for any offset that falls in the second half of the sequence we have an efficiently computable solution. We can check whether for any given input signal repeated 10000 times, the offset position (first seven digits of signal) is in the second half of the sequence:

```elm {l}
offset : String -> Int
offset =
    String.left 7 >> String.toInt >> Maybe.withDefault 0


inSecondHalf : String -> Bool
inSecondHalf signal =
    offset signal >= (10000 * String.length signal // 2)
```

Part 2 example 1: ^^^elm {m=(inSecondHalf testInput5)}^^^

Part 2 example 2: ^^^elm {m=(inSecondHalf testInput6)}^^^
Part 2 example 3: ^^^elm {m=(inSecondHalf testInput7)}^^^
Puzzle input: ^^^elm {m=(inSecondHalf puzzleInput)}^^^

We can fold and accumulate totals to find the new signals for each iteration:

```elm {l}
phase2 : List Int -> List Int
phase2 signal =
    AOC.scanl (\n -> (+) n >> modBy 10) 0 (signal |> List.reverse)
        |> List.drop 1
        |> List.reverse
```

Iterating 100 times with this new approach to calculating the modified signal should give us the correct answer but applying it to the entire input signal is wasteful as we are only interested in the digits from the offset (indicated by first 7 digits of input) to the end of the list. So to we calculate the initial truncated signal from offset to end:

```elm {l}
initSignal : String -> List Int
initSignal input =
    let
        inLen =
            String.length input

        numRepeats =
            (10001 * inLen - offset input) // inLen

        off =
            modBy (String.length input) (offset input)
    in
    input
        |> List.repeat numRepeats
        |> String.concat
        |> parse
        |> List.drop off
```

Extracting the first 8 digits from processed signal gives us the answer:

```elm {l r}
part2 : String
part2 =
    let
        input =
            initSignal puzzleInput
    in
    AOC.iterate 100 input phase2
        |> List.take 8
        |> List.map String.fromInt
        |> String.concat
```

---

## Reflection

Another puzzle where I didn't have time to complete part 2 in December.

Part one afforded a literal interpretation of the question and computes the answer in a couple of seconds. But it lulled me into thinking the optimisation required for part 2, which was clearly not computable via brute force computation of multi-thousand digit numbers, would be to look for repeated sequences in the earlier part of the signal.

After struggling to find a pattern in the output signal, I then looked just at the coefficients as laid out in the table above. This helped direct me to a more efficient solution. I spent longer than I wished debugging my code for trimming the input code as the integer division used to find the minimum number of repeats underestimated for the final test case (and puzzle input) despite working correctly for the first two tests.
