---
follows: data/d08_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2019): Space Image Format

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/8)

## Part 1

Parsing is a straightforward task of converting a long string of digits into a list of integers.

```elm {l}
parse : String -> List Int
parse =
    String.toList
        >> List.filterMap (String.fromChar >> String.toInt)
```

We need to partition the digits into layers, which themselves involve partitioning into grids which themselves involve partitioning into rows.

```elm {l}
createLayers : Int -> Int -> List Int -> List (AOC.Grid Int)
createLayers nRows nCols cells =
    let
        toRows digits rows =
            case digits of
                [] ->
                    rows

                _ ->
                    toRows (List.drop nCols digits) (rows ++ [ List.take nCols digits ])

        toGrids grids rows =
            case rows of
                [] ->
                    grids

                _ ->
                    toGrids (grids ++ [ List.take nRows rows ]) (List.drop nRows rows)

        toLayers =
            List.map
                (List.foldl (\row ( r, g ) -> ( r + 1, AOC.gSetRow r row g ))
                    ( 0, AOC.gInit nRows nCols 0 )
                    >> Tuple.second
                )
    in
    toRows cells [] |> toGrids [] |> toLayers
```

Once in a list of layers, processing cells is quite simple.

We will need to count the number of digits of a certain value in any layer:

```elm {l}
numOfDigit : Int -> AOC.Grid Int -> Int
numOfDigit n =
    AOC.gToList
        >> List.filter ((==) n)
        >> List.length
```

To find the layer with the smallest number of zeros, we simply fold over each layer, counting the number of zeros in each and keeping track of the layer with the smallest number of them.

```elm {l}
minZeroLayer : Int -> Int -> List Int -> AOC.Grid Int
minZeroLayer nRows nCols digits =
    digits
        |> createLayers nRows nCols
        |> List.foldl
            (\layer ( min0c, min0Layer ) ->
                if numOfDigit 0 layer < min0c then
                    ( numOfDigit 0 layer, layer )

                else
                    ( min0c, min0Layer )
            )
            ( nRows * nCols, AOC.gInit 0 0 0 )
        |> Tuple.second
```

```elm {l r}
part1 : Int
part1 =
    let
        nRows =
            6

        nCols =
            25

        min0 =
            puzzleInput
                |> parse
                |> minZeroLayer nRows nCols
    in
    numOfDigit 1 min0 * numOfDigit 2 min0
```

---

## Part 2

To combine multiple cell values in layers at any given 2d location, we can use [aoc.md](../aoc.md)'s `gMapWithLocation` that allows us to use row and column coordinates to map the contents of a layer. That mapping function retrieves all the digits at a given position and combines transparent (2), black (1) and white (0) colours.

```elm {l}
overlay : Int -> Int -> List (AOC.Grid Int) -> AOC.Grid Char
overlay nRows nCols layers =
    let
        symbolise digit colour =
            if digit == 2 then
                colour

            else if digit == 1 then
                '█'

            else
                '.'

        overlayCells loc =
            List.filterMap (AOC.gGet loc)
                >> List.reverse
                >> List.foldl symbolise ' '
    in
    AOC.gInit nRows nCols 2
        |> AOC.gMapWithLocation (\loc _ -> overlayCells loc layers)
```

To find the message in thee combined layers we convert it into a list of strings and display it as markdown:

```elm {l}
part2 : List String
part2 =
    let
        nRows =
            6

        nCols =
            25
    in
    puzzleInput
        |> parse
        |> createLayers nRows nCols
        |> overlay nRows nCols
        |> AOC.gToLists
        |> List.map ((::) '\n' >> String.fromList)
```

{(fixed|}^^^elm {m=part2}^^^{|fixed)}

---

## Reflection

A relatively easy puzzle that is largely about reshaping 1d lists into grid structures. I decided to use the grid functions created previously in [Aoc.elm](../src/Aoc.elm) which contain some convenience functions for keeping track of number of rows and columns and for mapping grid cell values.

I initially tripped up in part one by misremembering the question assuming I had to find the layer with the most zeros, not fewest.

Nice to be able to take advantage of litvis' markdown formatting of function output to produce a graphical depiction of the layers.
