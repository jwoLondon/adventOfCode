---
follows: data/d08_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2019): Space Image Format

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/8):

{( part1|}

The Elves' spirits are lifted when they realize you have an opportunity to reboot one of their Mars rovers, and so they are curious if you would spend a brief sojourn on Mars. You land your ship near the rover.

When you reach the rover, you discover that it's already in the process of rebooting! It's just waiting for someone to enter a BIOS password. The Elf responsible for the rover takes a picture of the password (your puzzle input) and sends it to you via the Digital Sending Network.

Unfortunately, images sent via the Digital Sending Network aren't encoded with any normal encoding; instead, they're encoded in a special Space Image Format. None of the Elves seem to remember why this is the case. They send you the instructions to decode it.

Images are sent as a series of digits that each represent the color of a single pixel. The digits fill each row of the image left-to-right, then move downward to the next row, filling rows top-to-bottom until every pixel of the image is filled.

Each image actually consists of a series of identically-sized **layers** that are filled in this way. So, the first digit corresponds to the top-left pixel of the first layer, the second digit corresponds to the pixel to the right of that on the same layer, and so on until the last digit, which corresponds to the bottom-right pixel of the last layer.

For example, given an image 3 pixels wide and 2 pixels tall, the image data 123456789012 corresponds to the following image layers:

    Layer 1: 123
             456

    Layer 2: 789
             012

The image you received is **25 pixels wide and 6 pixels tall**.

To make sure the image wasn't corrupted during transmission, the Elves would like you to find the layer that contains the **fewest 0 digits**. On that layer, what is the **number of 1 digits multiplied by the number of 2 digits?**

{| part1 )}

## Parsing

Parsing is a straightforward task of converting a long string of digits into a list of integers.

```elm {l}
parse : String -> List Int
parse =
    String.toList
        >> List.filterMap (String.fromChar >> String.toInt)
```

## Approach

We need to partition the digits into layers, which themselves involve partitioning into grids which themselves involve partitioning into rows.

```elm {l}
createLayers : Int -> Int -> List Int -> List (Grid Int)
createLayers nRows nCols cells =
    let
        toRows digits rows =
            case digits of
                [] ->
                    rows

                _ ->
                    toRows (List.drop nCols digits) (rows ++ [ List.take nCols digits ])

        toGrids grids rows =
            case rows of
                [] ->
                    grids

                _ ->
                    toGrids (grids ++ [ List.take nRows rows ]) (List.drop nRows rows)

        toLayers =
            List.map
                (List.foldl (\row ( r, g ) -> ( r + 1, gSetRow r row g ))
                    ( 0, gInit nRows nCols 0 )
                    >> Tuple.second
                )
    in
    toRows cells [] |> toGrids [] |> toLayers
```

Once in a list of layers, processing cells is quite simple.

We will need to count the number of digits of a certain value in any layer:

```elm {l}
numOfDigit : Int -> Grid Int -> Int
numOfDigit n =
    gToList
        >> List.filter ((==) n)
        >> List.length
```

To find the layer with the smallest number of zeros, we simply fold over each layer, counting the number of zeros in each and keeping track of the layer with the smallest number of them.

```elm {l}
minZeroLayer : Int -> Int -> List Int -> Grid Int
minZeroLayer nRows nCols digits =
    digits
        |> createLayers nRows nCols
        |> List.foldl
            (\layer ( min0c, min0Layer ) ->
                if numOfDigit 0 layer < min0c then
                    ( numOfDigit 0 layer, layer )

                else
                    ( min0c, min0Layer )
            )
            ( nRows * nCols, gInit 0 0 0 )
        |> Tuple.second
```

```elm {l r}
part1 : Int
part1 =
    let
        nRows =
            6

        nCols =
            25

        min0 =
            puzzleInput
                |> parse
                |> minZeroLayer nRows nCols
    in
    numOfDigit 1 min0 * numOfDigit 2 min0
```

{( part2 |}

Now you're ready to decode the image. The image is rendered by stacking the layers and aligning the pixels with the same positions in each layer. The digits indicate the color of the corresponding pixel: 0 is black, 1 is white, and 2 is transparent.

The layers are rendered with the first layer in front and the last layer in back. So, if a given position has a transparent pixel in the first and second layers, a **black** pixel in the third layer, and a white pixel in the fourth layer, the final image would have a black pixel at that position.

For example, given an image 2 pixels wide and 2 pixels tall, the image data 0222112222120000 corresponds to the following image layers:

    Layer 1: 02
             22

    Layer 2: 11
             22

    Layer 3: 22
             12

    Layer 4: 00
             00

Then, the full image can be found by determining the top visible pixel in each position:

- The top-left pixel is **black** because the top layer is 0.
- The top-right pixel is **white** because the top layer is 2 (transparent), but the second layer is 1.
- The bottom-left pixel is **white** because the top two layers are 2, but the third layer is 1.
- The bottom-right pixel is **black** because the only visible pixel in that position is 0 (from layer 4).

So, the final image looks like this:

    01
    10

**What message is produced after decoding your image?**

{| part2 )}

## Approach

To combine multiple cell values in layers at any given 2d location, we can use [aoc.md](../aoc.md)'s `gMapWithLocation` that allows us to use row and column coordinates to map the contents of a layer. That mapping function retrieves all the digits at a given position and combines transparent (2), black (1) and white (0) colours.

```elm {l}
overlay : Int -> Int -> List (Grid Int) -> Grid Char
overlay nRows nCols layers =
    let
        symbolise digit colour =
            if digit == 2 then
                colour

            else if digit == 1 then
                '█'

            else
                '▒'

        overlayCells loc =
            List.filterMap (gGet loc)
                >> List.reverse
                >> List.foldl symbolise ' '
    in
    gInit nRows nCols 2
        |> gMapWithLocation (\loc _ -> overlayCells loc layers)
```

To find the message in thee combined layer we convert it into a list of strings and display it as markdown:

```elm {l}
showLayer : Grid Char -> List String
showLayer layer =
    layer
        |> gToLists
        |> List.map
            (List.foldl (\c cs -> c :: cs) []
                >> List.reverse
                >> String.fromList
                >> (++) "\n"
            )
```

```elm {l m}
part2 : List String
part2 =
    let
        nRows =
            6

        nCols =
            25
    in
    puzzleInput
        |> parse
        |> createLayers nRows nCols
        |> overlay nRows nCols
        |> showLayer
```

## Reflection

A relatively easy puzzle that is largely about reshaping 1d lists into grid structures. I decided to use the grid functions created previously in [aoc.md](../aoc.md) which contain some convenience functions for keeping track of number of rows and columns and for mapping grid cell values.

I initially tripped up in part one by misremembering the question assuming I had to find the layer with the most zeros, not fewest.

Nice to be able to take advantage of litvis' markdown formatting of function output to produce a graphical depiction of the layers.
