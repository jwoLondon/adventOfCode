---
follows: data/d20_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 20 (2019): Donut Maze

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/20)

## Part 1

We can read the input into a grid of characters.

```elm {l}
parse : List String -> AOC.Grid Char
parse =
    List.map String.toList
        >> AOC.gFromLists
```

and confirm the parsing works by displaying the grid:

```elm {l}
display : AOC.Grid Char -> List String
display =
    AOC.gToLists
        >> List.map String.fromList
        >> (++) [ "Maze" ]
        >> List.map (String.replace " " "\u{2007}" >> String.replace "#" "█" >> AOC.flip (++) "\n")
```

One complication is identifying the portal labels that comprise two characters that may be either horizontal or vertical. If we scan the grid from left to right and top to bottom, we can ensure the two letters of each portal label will be read in the correct order. We can build up a dictionary of portal labels and the pair of locations with which they are associated (or a single location for the entrance portal AA and exit portal ZZ).

```elm {l}
type alias PortalLookup =
    Dict String (List AOC.GridLocation)


isLetter : AOC.Grid Char -> AOC.GridLocation -> Bool
isLetter g loc =
    case AOC.gGet loc g of
        Just chr ->
            chr >= 'A' && chr <= 'Z'

        Nothing ->
            False


isPassage : AOC.Grid Char -> AOC.GridLocation -> Bool
isPassage g loc =
    case AOC.gGet loc g of
        Just chr ->
            chr == '.'

        Nothing ->
            False


addPortal : AOC.Grid Char -> AOC.GridLocation -> PortalLookup -> PortalLookup
addPortal g ( row, col ) dict =
    let
        passageLocation loc =
            case AOC.gGet loc g of
                Just chr ->
                    chr == '.'

                Nothing ->
                    False

        addLocation label locs =
            if Dict.member label dict then
                Dict.update label (Maybe.map ((++) locs)) dict

            else
                Dict.insert label locs dict
    in
    if isLetter g ( row, col ) && isLetter g ( row, col + 1 ) then
        let
            name =
                [ AOC.gGet ( row, col ) g, AOC.gGet ( row, col + 1 ) g ]
                    |> List.filterMap identity
                    |> String.fromList
        in
        if passageLocation ( row, col + 2 ) then
            addLocation name [ ( row, col + 1 ) ]

        else
            addLocation name [ ( row, col ) ]

    else if isLetter g ( row, col ) && isLetter g ( row + 1, col ) then
        let
            name =
                [ AOC.gGet ( row, col ) g, AOC.gGet ( row + 1, col ) g ]
                    |> List.filterMap identity
                    |> String.fromList
        in
        if passageLocation ( row + 2, col ) then
            addLocation name [ ( row + 1, col ) ]

        else
            addLocation name [ ( row, col ) ]

    else
        dict
```

We can build the dictionary of portal label to locations by folding over each cell in the maze looking for labels. A portal may have a single location (`AA` and `ZZ`) or a pair of locations ordered by [ _outerLocation_, _innerLocation_ ]:

```elm {l}
portals : PortalLookup
portals =
    let
        ps =
            List.foldl (addPortal maze)
                Dict.empty
                (AOC.gridLocations ( 0, 0 ) ( AOC.gRowCount maze, AOC.gColCount maze ))

        ( nRows, nCols ) =
            Dict.values ps
                |> List.concat
                |> List.foldl (\( row, col ) ( mRow, mCol ) -> ( max row mRow, max col mCol )) ( 0, 0 )

        distanceFromEdge ( row, col ) =
            min (min row (nRows - row)) (min col (nCols - col))
    in
    Dict.map (\_ -> List.sortBy distanceFromEdge) ps
```

When we navigate the maze and come across a part of portal label we need to know its name. We can potentially approach a portal label from one of four directions:

```elm {l}
portalName : AOC.Grid Char -> AOC.GridLocation -> String
portalName g ( row, col ) =
    if isLetter g ( row, col ) && isLetter g ( row, col + 1 ) then
        [ AOC.gGet ( row, col ) g, AOC.gGet ( row, col + 1 ) g ]
            |> List.filterMap identity
            |> String.fromList

    else if isLetter g ( row, col ) && isLetter g ( row, col - 1 ) then
        [ AOC.gGet ( row, col - 1 ) g, AOC.gGet ( row, col ) g ]
            |> List.filterMap identity
            |> String.fromList

    else if isLetter g ( row, col ) && isLetter g ( row + 1, col ) then
        [ AOC.gGet ( row, col ) g, AOC.gGet ( row + 1, col ) g ]
            |> List.filterMap identity
            |> String.fromList

    else if isLetter g ( row, col ) && isLetter g ( row - 1, col ) then
        [ AOC.gGet ( row - 1, col ) g, AOC.gGet ( row, col ) g ]
            |> List.filterMap identity
            |> String.fromList

    else
        ""
```

We can then teleport to the other end of a teleport pair:

```elm {l}
teleport : String -> AOC.GridLocation -> AOC.GridLocation
teleport label loc =
    case Dict.get label portals of
        Just [ l0 ] ->
            loc

        Just [ l1, l2 ] ->
            if loc == l1 then
                l2

            else
                l1

        _ ->
            loc |> Debug.log "Unexpected portal location"
```

The main navigation through the maze is a breadth-first search of available unvisited locations, teleporting if we arrive at a teleport location.

```elm {l}
findExit : Deque ( AOC.GridLocation, Int ) -> List AOC.GridLocation -> Int
findExit todo visited =
    case Deque.popFront todo of
        ( Nothing, _ ) ->
            -1 |> Debug.log "No solution"

        ( Just ( ( row, col ), steps ), todo2 ) ->
            if ( row, col ) == exitLocation then
                steps

            else if List.member ( row, col ) visited then
                findExit todo2 visited

            else if isPassage maze ( row, col ) then
                -- Proceed along passageway
                findExit
                    (todo2
                        |> Deque.pushBack ( ( row - 1, col ), steps + 1 )
                        |> Deque.pushBack ( ( row + 1, col ), steps + 1 )
                        |> Deque.pushBack ( ( row, col - 1 ), steps + 1 )
                        |> Deque.pushBack ( ( row, col + 1 ), steps + 1 )
                    )
                    (( row, col ) :: visited)

            else if isLetter maze ( row, col ) then
                -- Teleport
                let
                    ( tRow, tCol ) =
                        teleport (portalName maze ( row, col )) ( row, col )
                in
                findExit
                    (todo2
                        |> Deque.pushBack ( ( tRow - 1, tCol ), steps )
                        |> Deque.pushBack ( ( tRow + 1, tCol ), steps )
                        |> Deque.pushBack ( ( tRow, tCol - 1 ), steps )
                        |> Deque.pushBack ( ( tRow, tCol + 1 ), steps )
                    )
                    (( row, col ) :: visited)

            else
                --Wall
                findExit todo2 visited
```

{(fixed|} ^^^elm m=(display maze)^^^ {|fixed)}

```elm {l}
maze : AOC.Grid Char
maze =
    -- parse test3Input
    parse puzzleInput


startLocation : AOC.GridLocation
startLocation =
    case Dict.get "AA" portals of
        Just locs ->
            List.head locs |> Maybe.withDefault ( -1, -1 )

        Nothing ->
            ( -1, -1 ) |> Debug.log "No entrance found"


exitLocation : AOC.GridLocation
exitLocation =
    case Dict.get "ZZ" portals of
        Just locs ->
            List.head locs |> Maybe.withDefault ( -1, -1 )

        Nothing ->
            ( -1, -1 ) |> Debug.log "No exit found"
```

```elm {l r}
part1 : Int
part1 =
    --findExit (Deque.singleton ( startLocation, -1 )) []
    1234567
```

---

## Part 2

We need to perform a similar BFS through the maze, but now the portal rules have changed and we should store the depth of the maze as part of the search path.

```elm {l}
type alias Loc3d =
    ( Int, Int, Int )


level0 : AOC.GridLocation -> Loc3d
level0 ( row, col ) =
    ( row, col, 0 )
```

We use the fact that the two locations of portals (other than `AA` and `ZZ`) are stored in outer-inner order, so if location matches the first portal location we come up a level and if it matches the second we go down a level:

```elm {l}
teleport2 : String -> Loc3d -> Loc3d
teleport2 label ( row, col, depth ) =
    case Dict.get label portals of
        Just [ l0 ] ->
            -- Start and end portals do not change level
            ( row, col, depth )

        Just [ ( row1, col1 ), ( row2, col2 ) ] ->
            if ( row, col ) == ( row1, col1 ) then
                if depth == 0 then
                    -- Outer portals at level 0 ignored
                    ( row, col, depth )

                else
                    -- Jump back a level
                    ( row2, col2, depth - 1 )

            else
                -- Dive down a level
                ( row1, col1, depth + 1 )

        _ ->
            ( row, col, depth ) |> Debug.log "Unexpected portal location"
```

Now we just search including the maze depth for each location:

```elm {l}
maxDepth : Int
maxDepth =
    21


findExit2 : Deque ( Loc3d, Int ) -> List Loc3d -> Int
findExit2 todo visited =
    case Deque.popFront todo of
        ( Nothing, _ ) ->
            -1 |> Debug.log "No solution"

        ( Just ( ( row, col, depth ), steps ), todo2 ) ->
            if ( row, col, depth ) == level0 exitLocation then
                steps

            else if List.member ( row, col, depth ) visited then
                findExit2 todo2 visited

            else if isPassage maze ( row, col ) then
                -- Proceed along passageway
                findExit2
                    (todo2
                        |> Deque.pushBack ( ( row - 1, col, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row + 1, col, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row, col - 1, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row, col + 1, depth ), steps + 1 )
                    )
                    (( row, col, depth ) :: visited)

            else if isLetter maze ( row, col ) then
                -- Teleport
                let
                    ( tRow, tCol, tDepth ) =
                        teleport2 (portalName maze ( row, col )) ( row, col, depth )

                    addPassage r c td =
                        if isPassage maze ( r, c ) then
                            Deque.pushBack ( ( r, c, tDepth ), steps ) td

                        else
                            td

                    -- _ =
                    --     Debug.log "Teleporting from / to" ( portalName maze ( row, col ), ( row, col, depth ), ( tRow, tCol, tDepth ) )
                in
                if depth < maxDepth then
                    findExit2
                        (todo2
                            |> addPassage (tRow - 1) tCol
                            |> addPassage (tRow + 1) tCol
                            |> addPassage tRow (tCol - 1)
                            |> addPassage tRow (tCol + 1)
                        )
                        (( row, col, depth ) :: visited)

                else
                    findExit2 todo2 visited

            else
                --Wall
                findExit2 todo2 visited
```

```elm
findExit2Old : Deque ( Loc3d, Int ) -> List Loc3d -> Int
findExit2Old todo visited =
    case Deque.popFront todo of
        ( Nothing, _ ) ->
            -1 |> Debug.log "No solution"

        ( Just ( ( row, col, depth ), steps ), todo2 ) ->
            if steps > 400 then
                steps |> Debug.log "step limit reached"

            else if ( row, col, depth ) == level0 exitLocation then
                steps

            else if List.member ( row, col, depth ) visited then
                findExit2 todo2 visited

            else if isPassage maze ( row, col ) then
                -- Proceed along passageway
                findExit2
                    (todo2
                        |> Deque.pushBack ( ( row - 1, col, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row + 1, col, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row, col - 1, depth ), steps + 1 )
                        |> Deque.pushBack ( ( row, col + 1, depth ), steps + 1 )
                    )
                    (( row, col, depth ) :: visited)

            else if isLetter maze ( row, col ) then
                -- Teleport
                let
                    ( tRow, tCol, tDepth ) =
                        teleport2 (portalName maze ( row, col )) ( row, col, depth )

                    addPassage r c td =
                        if isPassage maze ( r, c ) then
                            Deque.pushBack ( ( r, c, tDepth ), steps ) td

                        else
                            td

                    -- _ =
                    --     Debug.log "Teleporting from / to" ( portalName maze ( row, col ), ( row, col, depth ), ( tRow, tCol, tDepth ) )
                in
                findExit2
                    (todo2
                        |> addPassage (tRow - 1) tCol
                        |> addPassage (tRow + 1) tCol
                        |> addPassage tRow (tCol - 1)
                        |> addPassage tRow (tCol + 1)
                    )
                    (( row, col, depth ) :: visited)

            else
                --Wall
                findExit2 todo2 visited
```

```elm {l r}
part2 : Int
part2 =
    findExit2 (Deque.singleton ( level0 startLocation, -1 )) []
```

---

## Reflection
