---
follows: data/d23_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 23 (2019): Category Six

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/23):

{( part1|}

The droids have finished repairing as much of the ship as they can. Their report indicates that this was a **Category 6 disaster** - not because it was that bad, but because it destroyed the stockpile of Category 6 network cables as well as most of the ship's network infrastructure.

You'll need to rebuild the network from scratch.

The computers on the network are standard Intcode computers that communicate by sending **packets** to each other. There are 50 of them in total, each running a copy of the same **Network Interface Controller** (NIC) software (your puzzle input). The computers have network addresses 0 through 49; when each computer boots up, it will request its network address via a single input instruction. Be sure to give each computer a unique network address.

Once a computer has received its network address, it will begin doing work and communicating over the network by sending and receiving packets. All packets contain **two values** named X and Y. Packets sent to a computer are queued by the recipient and read in the order they are received.

To **send** a packet to another computer, the NIC will use **three output instructions** that provide the **destination address** of the packet followed by its X and Y values. For example, three output instructions that provide the values 10, 20, 30 would send a packet with X=20 and Y=30 to the computer with address 10.

To **receive** a packet from another computer, the NIC will use an input **instruction**. If the incoming packet queue is **empty**, provide -1. Otherwise, provide the X value of the next packet; the computer will then use a second input instruction to receive the Y value for the same packet. Once both values of the packet are read in this way, the packet is removed from the queue.

Note that these input and output instructions never block. Specifically, output instructions do not wait for the sent packet to be received - the computer might send multiple packets before receiving any. Similarly, input instructions do not wait for a packet to arrive - if no packet is waiting, input instructions should receive -1.

Boot up all 50 computers and attach them to your network. **What is the Y value of the first packet sent to address 255?**

{| part1 )}

## Approach

Create a dictionary of 50 NIC computers, each referenced by its network address (0-49), a queue of packets that have yet to be processed and the result of the last packet sent to address 255 (which we learn in part 2 is called a NAT).

```elm {l}
type alias Network =
    { nics : Dict Int Computer
    , packetQ : Deque (List Int)
    , nat : Maybe ( Int, Int )
    }


initNetwork : Network
initNetwork =
    { nics =
        List.foldl
            (\addr ->
                Dict.insert addr (puzzleInput |> initComputer [ addr ] |> runProg)
            )
            Dict.empty
            (List.range 0 49)
    , packetQ = Deque.empty
    , nat = Nothing
    }
```

When any computer generates output, we extract it and add it to the packet queue:

```elm {l}
extractPackets : Network -> Deque (List Int)
extractPackets net =
    let
        addToQueue nic queue =
            List.foldl Deque.pushBack queue (AOC.groupsOf 3 nic.outputStore)
    in
    List.foldl addToQueue net.packetQ (Dict.values net.nics)
```

We can extract the oldest packet and send it to the relevant computer:

```elm {l}
sendPacket : Network -> Network
sendPacket net =
    case Deque.popFront net.packetQ of
        ( Just [ addr, x, y ], q ) ->
            case Dict.get addr net.nics of
                Just nic ->
                    { net | nics = Dict.insert addr (addInputs [ y, x ] nic) net.nics, packetQ = q }

                Nothing ->
                    { net | nat = Just ( x, y ), packetQ = q }

        _ ->
            net |> Debug.log "Empty packet store"
```

Every cycle we transfer outputs to inputs via the packet queue.

```elm {l}
tick : Network -> Network
tick net =
    let
        newNICs =
            Dict.map (\_ -> addInput -1 >> runProg) net.nics

        q =
            extractPackets { net | nics = newNICs }
    in
    sendPacket { net | nics = Dict.map (\_ -> clearOutput) newNICs, packetQ = q }
```

We can now just run the computer network for some cycles until we receive a packet for the NAT at address 255:

```elm {l r}
part1 : Int
part1 =
    let
        runUntil255 net =
            case net.nat of
                Just ( x, y ) ->
                    y

                Nothing ->
                    runUntil255 (tick net)
    in
    runUntil255 initNetwork
```

{( part2 |}

Packets sent to address 255 are handled by a device called a NAT (Not Always Transmitting). The NAT is responsible for managing power consumption of the network by blocking certain packets and watching for idle periods in the computers.

If a packet would be sent to address 255, the NAT receives it instead. The NAT remembers only the **last** packet it receives; that is, the data in each packet it receives overwrites the NAT's packet memory with the new packet's X and Y values.

The NAT also monitors all computers on the network. If all computers have **empty incoming packet queues** and are **continuously trying to receive packets** without sending packets, the network is considered **idle**.

Once the network is idle, the NAT sends **only the last packet it received** to address 0; this will cause the computers on the network to resume activity. In this way, the NAT can throttle power consumption of the network when the ship needs power in other areas.

Monitor packets released to the computer at address 0 by the NAT. **What is the first Y value delivered by the NAT to the computer at address 0 twice in a row?**

{| part2 )}

## Approach Part Two

We need to be able to detect when all computers in the network are idle, which is when there is noting in the packet queue and no inputs pending in any of the computers. This allows us to run the network until it becomes idle:

```elm {l}
runUntilIdle : Network -> Network
runUntilIdle net =
    if (Dict.values net.nics |> List.concatMap .inputStore) == [] && Deque.isEmpty net.packetQ then
        net

    else
        runUntilIdle (tick net)
```

At which point, we will need to pass the NAT packet to NIC 0:

```elm {l}
natToNic0 : Network -> Network
natToNic0 net =
    let
        natPacket network =
            case network.nat of
                Just ( x, y ) ->
                    [ y, x ]

                Nothing ->
                    []
    in
    case Dict.get 0 net.nics of
        Just nic0 ->
            { net | nics = Dict.insert 0 (addInputs (natPacket net) nic0) net.nics }

        Nothing ->
            net
```

Now we keep running the network to the next idle point, halting when the NAT packet's y-value is the same as the previous one.

```elm {l}
tick2 : Network -> Network
tick2 =
    runUntilIdle >> natToNic0
```

```elm {l r}
part2 : Int
part2 =
    let
        runUntilRepeat ( net, lastNat ) =
            case net.nat of
                Just ( _, y ) ->
                    if y == lastNat then
                        y

                    else
                        runUntilRepeat ( tick2 net, y )

                Nothing ->
                    -1 |> Debug.log "Unexpected halt with nothing in the NAT"
    in
    runUntilRepeat ( tick2 (tick initNetwork), -1 )
```

---

## Reflection

My initial approach to this for part 1 was to create a dictionary of packets, using the computer id as the key. This worked for part 1, but not for part 2 as doing so lost the original order of packet generation. It was necessary to preserve that order because address 255 (the NAT) would receive many packets, but only the last one was to be used.

The final approach adopted was to create a single queue of packets, which therefore preserved packet generation order.
