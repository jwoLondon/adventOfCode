---
follows: data/d02_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2019): 1202 Program Alarm

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/2)

## Part 1

We can use the [intCode assembler](intcode.md) to solve the problem (for the original code before separating the intcode functions, see [d02_2019Standalone.md](d02_2019Standalone.md)).

```elm {l}
nounVerbProg : Int -> Int -> Computer
nounVerbProg noun verb =
    puzzleInput
        |> initComputer [ 0 ]
        |> poke 1 noun
        |> poke 2 verb
        |> runProg
```

```elm {l r}
part1 : Int
part1 =
    nounVerbProg 9 10
        |> read Immediate 0
```

---

## Part 2

The brute force approach would be to try all combinations of noun and verb between 0 and 99 each until we get the desired output. In this case, it is fast enough to suffice:

```elm {l}
target : Int
target =
    19690720
```

```elm {l r}
part2 : List Int
part2 =
    let
        mem =
            puzzleInput |> initComputer [ 0 ]
    in
    AOC.gridLocations ( 0, 0 ) ( 99, 99 )
        |> List.map
            (\( n, v ) ->
                if (nounVerbProg n v |> read Immediate 0) == target then
                    Just (100 * n + v)

                else
                    Nothing
            )
        |> List.filterMap identity
```

---

## Reflection

I do like this kind of puzzle, and great to see hints that _Intcode_ will be developed in later puzzles.

I wasn't entirely happy with a brute force approach to part 2. There was always the risk that certain combinations of noun and verb could lead to non halting programs, or ones that did not halt in a practical amount of time. In the end this wasn't a problem, but as an experiment I wanted to see the relation between inputs and output.

Here are the values for (0,0) (0,1) (0,2), (1,0), (1,1), (1,2), (2,0), (2,1) and (2,2):

```elm {r}
outputs : List ( Int, Int, Int )
outputs =
    let
        mem =
            puzzleInput |> initComputer [ 0 ]
    in
    AOC.gridLocations ( 0, 0 ) ( 2, 2 )
        |> List.map (\( n, v ) -> ( n, v, nounVerbProg n v |> read Immediate 0 ))
```

This suggests a linear relationship in the form:

```txt
out = dx * noun + dy * verb + x0y0 ..................................... (1)
```

where dx is the difference in output between two consecutive noun values, dy the difference in output between two consecutive verb values and x0y0 is the output at (noun=0, verb=0).

In the case of the puzzle input:

```txt
dx = 567476 - 337076 = 230400
dy = 337077 - 337076 = 1
x0y0 = 337076
```

Because dx is more than 99 times as large as dy, there can only be one noun value that could generate a result within 99.dy of the target:

```txt
noun = floor ((target - x0y0) / dx)
```

Using this, we can find the verb value by plugging back into (1). Assuming all puzzle inputs generate a planar relationship between (noun,verb) and output and that dx > 99\*dy, we have a general algebraic solution:

```elm {l r}
part2Algebraic : Int
part2Algebraic =
    let
        ( x0y0, x0y1, x1y0 ) =
            [ ( 0, 0 ), ( 0, 1 ), ( 1, 0 ) ]
                |> List.map (\( n, v ) -> ( n, v, nounVerbProg n v |> read Immediate 0 ))
                |> List.map AOC.tripletThird
                |> AOC.tripletFromList
                |> Maybe.withDefault ( 0, 0, 0 )

        dx =
            x1y0 - x0y0

        dy =
            x0y1 - x0y0

        noun =
            (target - x0y0) // dx

        verb =
            (target - x0y0 - dx * noun) // dy
    in
    noun * 100 + verb
```
