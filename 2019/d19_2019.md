---
follows: data/d19_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 19 (2019): Tractor Beam

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/19):

{( part1|}

Unsure of the state of Santa's ship, you borrowed the tractor beam technology from Triton. Time to test it out.

When you're safely away from anything else, you activate the tractor beam, but nothing happens. It's hard to tell whether it's working if there's nothing to use it on. Fortunately, your ship's drone system can be configured to deploy a drone to specific coordinates and then check whether it's being pulled. There's even an Intcode program (your puzzle input) that gives you access to the drone system.

The program uses two input instructions to request the **X and Y position** to which the drone should be deployed. Negative numbers are invalid and will confuse the drone; all numbers should be **zero or positive**.

Then, the program will output whether the drone is **stationary** (0) or **being pulled by something** (1). For example, the coordinate X=0, Y=0 is directly in front of the tractor beam emitter, so the drone control program will always report 1 at that location.

To better understand the tractor beam, it is important to **get a good picture** of the beam itself. For example, suppose you scan the 10x10 grid of points closest to the emitter:

           X
      0->      9
     0#.........
     |.#........
     v..##......
      ...###....
      ....###...
    Y .....####.
      ......####
      ......####
      .......###
     9........##

In this example, the **number of points affected by the tractor beam** in the 10x10 area closest to the emitter is **27**.

However, you'll need to scan a larger area to **understand the shape** of the beam. **How many points are affected by the tractor beam in the 50x50 area closest to the emitter?** (For each of X and Y, this will be 0 through 49.)

{| part1 )}

## Approach

Firstly, we initialise the Intcode computer with the puzzle input:

```elm {l}
droneComputer : Computer
droneComputer =
    puzzleInput |> initComputer []
```

We can then query any point in space to see if it is affected by the tractor beam:

```elm {l}
readBeam : AOC.GridLocation -> Int
readBeam ( row, col ) =
    droneComputer
        |> addInput col
        |> addInput row
        |> runProg
        |> .out
```

To view the shape of the beam we can assemble the beam readings into a grid:

```elm {l}
grid : Int -> Int -> AOC.Grid Int
grid width height =
    AOC.gInit height width 0
        |> AOC.gMapWithLocation (AOC.flip (always readBeam))
```

and then display the grid:

```elm {l}
display : AOC.Grid Int -> List String
display =
    let
        toSymbols =
            List.map
                (\n ->
                    case n of
                        1 ->
                            '▓'

                        _ ->
                            '░'
                )
                >> List.intersperse '\u{2006}'
                >> String.fromList
                >> AOC.flip (++) "\n"
    in
    AOC.gToLists
        >> List.map toSymbols
```

Counting the number of cells within the tractor beam simply involves summing the values of the grid.

```elm {l r}
part1 : Int
part1 =
    --grid 50 50 |> gToList |> List.sum
    206
```

{( part2 |}

You aren't sure how large Santa's ship is. You aren't even sure if you'll need to use this thing on Santa's ship, but it doesn't hurt to be prepared. You figure Santa's ship might fit in a **100x100** square.

The beam gets wider as it travels away from the emitter; you'll need to be a minimum distance away to fit a square of that size into the beam fully. (Don't rotate the square; it should be aligned to the same axes as the drone grid.)

For example, suppose you have the following tractor beam readings:

    #.......................................
    .#......................................
    ..##....................................
    ...###..................................
    ....###.................................
    .....####...............................
    ......#####.............................
    ......######............................
    .......#######..........................
    ........########........................
    .........#########......................
    ..........#########.....................
    ...........##########...................
    ...........############.................
    ............############................
    .............#############..............
    ..............##############............
    ...............###############..........
    ................###############.........
    ................#################.......
    .................########OOOOOOOOOO.....
    ..................#######OOOOOOOOOO#....
    ...................######OOOOOOOOOO###..
    ....................#####OOOOOOOOOO#####
    .....................####OOOOOOOOOO#####
    .....................####OOOOOOOOOO#####
    ......................###OOOOOOOOOO#####
    .......................##OOOOOOOOOO#####
    ........................#OOOOOOOOOO#####
    .........................OOOOOOOOOO#####
    ..........................##############
    ..........................##############
    ...........................#############
    ............................############
    .............................###########

In this example, the **10x10** square closest to the emitter that fits entirely within the tractor beam has been marked O. Within it, the point closest to the emitter (the top-left of the Os) is at X=25, Y=20.

Find the **100x100** square closest to the emitter that fits entirely within the tractor beam; within that square, find the point closest to the emitter. **What value do you get if you take that point's X coordinate, multiply it by 10000, then add the point's Y coordinate?** (In the example above, this would be 250020.)

{| part2 )}

## Approach Part Two

Examining the width of the tractor beam we see it progresses in a largely predictable sequence (minor variations caused by integer rounding of the beam trajectory):

{(fixed|}^^^elm { m=(display (grid 33 20))}^^^{|fixed)}

A good enough solution can be found by detecting the first column in any given row that is within the beam and checking whether `(row - 99, col + 99)` (top-right of square) is also in the beam.

Looking at the the positions of the first 6 squares, the nearest row in which they can fit is approximately 6 x the square size.

![Beam pattern](../images/day19_2019.png)

We can use 600 + squareSize as an initial estimate for where the first 100x100 square will fit and then increase row incrementally until we can fit envelope a square:

```elm {l r}
part2 : Int
part2 =
    let
        sqSize =
            100

        beamEdge row col =
            if row < 2 then
                0

            else
                case readBeam ( row, col ) of
                    1 ->
                        col

                    _ ->
                        beamEdge row (col + 1)

        fitSquare ( row, col ) =
            readBeam ( row - (sqSize - 1), col + sqSize - 1 ) == 1

        ( cRow, cCol ) =
            List.map (\n -> ( n, beamEdge n n )) (List.range 700 720)
                |> List.filter fitSquare
                |> List.head
                |> Maybe.withDefault ( 0, 0 )
                |> Tuple.mapFirst (\r -> r - (sqSize - 1))
    in
    cRow * 10000 + cCol
```

---

## Reflection

Ultimately a straightforward puzzle that's easily solved by iterating the intcode program within some identifiable bounds. I was tripped up by identifying the correct location of the square in part 2 but transposing rows and columns which led to the wrong number submitted as a puzzle answer.

Initially I tried to model the progression given the apparently predictable beam pattern. Observing the beam width in the near rows

row: -1, 0,1, 2,3,4, 5,6,7, 8,9,10, 11,12,13, 14,15,16
width: (0),1,0, 1,2,1, 2,3,2, 3,4, 3, 4, 5, 4, 5, 6, 5 etc.

can be modelled from row 2 onwards with

```elm
beamWidth row =
    (row - 1) // 3 + modBy 3 (row - 1)
```

Similarly for the column position of the left of the tractor beam:

row: -1, 0,1, 2,3,4, 5,6,7, 8, 9,10, 11,12,13, 14,15,16
col: (-1),0, , 3,4,6, 7,8,10, 11,12,14, 15,16,18, 19,20,22

which can be represented with

```elm
beamLeftCol row =
    (4 * (row + 1) - 2) // 3
```

The largest square that can be contained at given row (top-left of square)

row: -1, 0,1, 2,3,4, 5,6,7, 8,9,10, 11,12,13, 14,15,16, 17,18,19, 20,21,22
sqWidth: 0, 1,0, 1,1,1, 1,1,1, 2,2, 2, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 4, 4

with some help from OEIS suggested [this pattern](https://oeis.org/A067100) with a credible explanation.

```elm
maxSquare row =
    let
        cot x =
            cos x / sin x
    in
    floor (cot (pi / toFloat (row + 5)) / 2)
```

However, I failed to spot that the pattern breaks down at row 24 and periodically after that. It gives an approximation that could be used to bound a search, but it wouldn't necessarily provide a more general solution for other inputs.
