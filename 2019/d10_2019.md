---
follows: data/d10_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2019): Monitoring Station

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/10):

{( part1|}

You fly into the asteroid belt and reach the Ceres monitoring station. The Elves here have an emergency: they're having trouble tracking all of the asteroids and can't be sure they're safe.

The Elves would like to build a new monitoring station in a nearby area of space; they hand you a map of all of the asteroids in that region (your puzzle input).

The map indicates whether each position is empty (.) or contains an asteroid (#). The asteroids are much smaller than they appear on the map, and every asteroid is exactly in the center of its marked position. The asteroids can be described with X,Y coordinates where X is the distance from the left edge and Y is the distance from the top edge (so the top-left corner is 0,0 and the position immediately to its right is 1,0).

Your job is to figure out which asteroid would be the best place to build a **new monitoring station**. A monitoring station can **detect** any asteroid to which it has **direct line of sight** - that is, there cannot be another asteroid **exactly** between them. This line of sight can be at any angle, not just lines aligned to the grid or diagonally. The **best** location is the asteroid that can **detect** the largest number of other asteroids.

For example, consider the following map:

```txt
.#..#
.....
#####
....#
...##
```

The best location for a new monitoring station on this map is the highlighted asteroid at 3,4 because it can detect 8 asteroids, more than any other location. (The only asteroid it cannot detect is the one at 1,0; its view of this asteroid is blocked by the asteroid at 2,2.) All other asteroids are worse locations; they can detect 7 or fewer other asteroids. Here is the number of other asteroids a monitoring station on each asteroid could detect:

```txt
.7..7
.....
67775
....7
...87
```

Here is an asteroid (#) and some examples of the ways its line of sight might be blocked. If there were another asteroid at the location of a capital letter, the locations marked with the corresponding lowercase letter would be blocked and could not be detected:

```txt
#.........
...A......
...B..a...
.EDCG....a
..F.c.b...
.....c....
..efd.c.gb
.......c..
....f...c.
...e..d..c
```

Here are some larger examples:

- Best is 5,8 with 33 other asteroids detected:

```txt
......#.#.
#..#.#....
..#######.
.#.#.###..
.#..#.....
..#....#.#
#..#....#.
.##.#..###
##...#..#.
.#....####
```

- Best is 1,2 with 35 other asteroids detected:

```txt
#.#...#.#.
.###....#.
.#....#...
##.#.#.#.#
....#.#.#.
.##..###.#
..#...##..
..##....##
......#...
.####.###.
```

- Best is 6,3 with 41 other asteroids detected:

```txt
.#..#..###
####.###.#
....###.#.
..###.##.#
##.##.#.#.
....###..#
..#.#..#.#
#..#.#.###
.##...##.#
.....#.#..
```

- Best is 11,13 with 210 other asteroids detected:

```txt
.#..##.###...#######
##.############..##.
.#.######.########.#
.###.#######.####.#.
#####.##.#.##.###.##
..#####..#.#########
####################
#.####....###.#.#.##
##.#################
#####.##.###..####..
..######..##.#######
####.##.####...##..#
.#####..#.######.###
##...#.##########...
#.##########.#######
.####.#.###.###.#.##
....##.##.###..#####
.#.#.###########.###
#.#.#.#####.####.###
###.##.####.##.#..##
```

Find the best location for a new monitoring station. **How many other asteroids can be detected from that location?**

{| part1 )}

## Parsing

For easy manipulation by position, convert the input list into a grid:

```elm {l}
parse : List String -> AOC.Grid Char
parse =
    List.map String.toList
        >> AOC.gFromLists
```

From which we can identify asteroid locations easily:

```elm {l}
asteroidLocations : AOC.Grid Char -> List AOC.GridLocation
asteroidLocations =
    AOC.gMapWithLocation
        (\loc z ->
            if z == '#' then
                Just loc

            else
                Nothing
        )
        >> AOC.gToList
        >> List.filterMap identity
```

## Approach

We can consider asteroids from nearest to furthest in case any near ones block the view to more distant ones. Given an origin asteroid, we can find the locations of all the others, sorted by their distance from it.

```elm {l}
otherAsteroids : AOC.GridLocation -> AOC.Grid Char -> List AOC.GridLocation
otherAsteroids ( r0, c0 ) =
    asteroidLocations
        >> List.filter ((/=) ( r0, c0 ))
        >> List.sortBy (\( r, c ) -> abs (r - r0) + abs (c - c0))
```

For any given cell and a blocking one, we can find all cells that would be blocked by it, which will allow us to identify those asteroids obscured by a nearer one. We do this by finding the vector from a a given location to a blocking location and then convert it to its smallest integer vector components. For example a vector of (4,4) would be reduced to (1,1) and a vector of (6,4) would be reduced to (3,2).

```elm {l}
blocked : Int -> Int -> AOC.GridLocation -> AOC.GridLocation -> List AOC.GridLocation
blocked nRows nCols ( row0, col0 ) ( rowB, colB ) =
    if row0 == rowB && col0 == colB then
        []

    else
        let
            maxDist =
                max nRows nCols

            hcf =
                AOC.highestCommonFactor (rowB - row0) (colB - col0)

            dRow =
                (rowB - row0) // hcf

            dCol =
                (colB - col0) // hcf
        in
        List.map (\d -> ( rowB + d * dRow, colB + d * dCol )) (List.range 1 maxDist)
            |> List.filter (\( r, c ) -> r >= 0 && r < nRows && c >= 0 && c < nCols)
```

Then we can find the asteroids in direct line of sight by removing all the asteroids that are blocked by a nearer one:

```elm {l}
directView : AOC.GridLocation -> AOC.Grid Char -> AOC.Grid Char
directView origin grid =
    let
        nRows =
            AOC.gRowCount grid

        nCols =
            AOC.gColCount grid

        blockedLocations r c =
            blocked nRows nCols origin ( r, c )

        removeBlocked sortedLocations visible =
            case sortedLocations of
                [] ->
                    visible

                loc :: tl ->
                    let
                        invisible =
                            blocked nRows nCols origin loc
                    in
                    removeBlocked (List.filter (\l -> not (List.member l invisible)) tl)
                        (loc :: visible)
    in
    List.foldl (\loc -> AOC.gSet loc '#')
        (AOC.gInit nRows nCols '.')
        (removeBlocked (otherAsteroids origin grid) [])
```

We can find the number of asteroids visible from the optimal location and (useful for part 2) the optimal location itself.

```elm {l r}
part1 : ( Int, AOC.GridLocation )
part1 =
    let
        grid =
            puzzleInput |> parse

        numVisible g loc =
            directView loc g
                |> AOC.gToList
                |> List.filter ((==) '#')
                |> List.length
    in
    grid
        |> asteroidLocations
        |> List.map (\loc -> ( numVisible grid loc, loc ))
        |> List.sortBy Tuple.first
        |> List.reverse
        |> List.head
        |> Maybe.withDefault ( 0, ( 0, 0 ) )
```

{( part2 |}

Once you give them the coordinates, the Elves quickly deploy an Instant Monitoring Station to the location and discover the worst: there are simply too many asteroids.

The only solution is **complete vaporization by giant laser**.

Fortunately, in addition to an asteroid scanner, the new monitoring station also comes equipped with a giant rotating laser perfect for vaporizing asteroids. The laser starts by pointing **up** and always rotates **clockwise**, vaporizing any asteroid it hits.

If multiple asteroids are **exactly** in line with the station, the laser only has enough power to vaporize **one** of them before continuing its rotation. In other words, the same asteroids that can be **detected** can be vaporized, but if vaporizing one asteroid makes another one detectable, the newly-detected asteroid won't be vaporized until the laser has returned to the same position by rotating a full 360 degrees.

For example, consider the following map, where the asteroid with the new monitoring station (and laser) is marked X:

```txt
.#....#####...#..
##...##.#####..##
##...#...#.#####.
..#.....X...###..
..#.#.....#....##
```

The first nine asteroids to get vaporized, in order, would be:

```txt
.#....###24...#..
##...##.13#67..9#
##...#...5.8####.
..#.....X...###..
..#.#.....#....##
```

Note that some asteroids (the ones behind the asteroids marked 1, 5, and 7) won't have a chance to be vaporized until the next full rotation. The laser continues rotating; the next nine to be vaporized are:

```txt
.#....###.....#..
##...##...#.....#
##...#......1234.
..#.....X...5##..
..#.9.....8....76
```

The next nine to be vaporized are then:

```txt
.8....###.....#..
56...9#...#.....#
34...7...........
..2.....X....##..
..1..............
```

Finally, the laser completes its first full rotation (1 through 3), a second rotation (4 through 8), and vaporizes the last asteroid (9) partway through its third rotation:

```txt
......234.....6..
......1...5.....7
.................
........X....89..
.................
```

In the large example above (the one with the best monitoring station location at 11,13):

- The 1st asteroid to be vaporized is at 11,12.
- The 2nd asteroid to be vaporized is at 12,1.
- The 3rd asteroid to be vaporized is at 12,2.
- The 10th asteroid to be vaporized is at 12,8.
- The 20th asteroid to be vaporized is at 16,0.
- The 50th asteroid to be vaporized is at 16,9.
- The 100th asteroid to be vaporized is at 10,16.
- The 199th asteroid to be vaporized is at 9,6.
- **The 200th asteroid to be vaporized is at 8,2.**
- The 201st asteroid to be vaporized is at 10,9.
- The 299th and final asteroid to be vaporized is at 11,1.

The Elves are placing bets on which will be the **200th** asteroid to be vaporized. Win the bet by determining which asteroid that will be; **what do you get if you multiply its X coordinate by 100 and then add its Y coordinate?** (For example, 8,2 becomes **802**.)

{| part2 )}

## Approach Part Two

We need to be able to sweep the cannon a full 360 degrees moving clockwise from north. To do this we can find the vectors for each grid cell relative to cannon and order them by clockwise from north angle. This single sweep avoids having to divide the sweeps into quadrants as implied by the question.

```elm {l}
orderedLocations : AOC.GridLocation -> AOC.Grid Char -> List AOC.GridLocation
orderedLocations ( r0, c0 ) grid =
    otherAsteroids ( r0, c0 ) grid
        |> List.map (\( r, c ) -> ( r - r0, c - c0 ))
        |> List.sortBy (\( r, c ) -> -1 * atan2 (toFloat c) (toFloat r))
        |> List.map (\( r, c ) -> ( r + r0, c + c0 ))
```

To find the 200th asteroid blasted by the cannon, we identify all asteroids within direct view, order them by angle (which would be the order in which they are blasted), and find the 200th asteroid in that ordered list.

```elm {l r}
part2 : Int
part2 =
    let
        cannonLocation =
            part1 |> Tuple.second

        grid =
            parse puzzleInput

        asteroid200 =
            grid
                |> directView cannonLocation
                |> orderedLocations cannonLocation
                |> List.drop 199
                |> List.head
                |> Maybe.withDefault ( 0, 0 )
    in
    (100 * Tuple.second asteroid200) + Tuple.first asteroid200
```

---

## Reflection

This is the only puzzle this year, and in previous years, where I have used floating point arithmetic to find a solution (for part 2). Calculating the angle of each asteroid relative to the cannon location avoided having to separate the asteroids into quadrants, within which integer arithmetic could be used. This feels a little like 'cheating' as all the puzzles are designed to be solvable with integer arithmetic. But I'd argue the coding clarity from using a single sweep outweighs the preference to stick to integer representation.
