---
follows: data/d03_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2019): Crossed Wires

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/3):

{( part1|}

The gravity assist was successful, and you're well on your way to the Venus refuelling station. During the rush back on Earth, the fuel management system wasn't completely installed, so that's next on the priority list.

Opening the front panel reveals a jumble of wires. Specifically, **two wires** are connected to a central port and extend outward on a grid. You trace the path each wire takes as it leaves the central port, one wire per line of text (your puzzle input).

The wires twist and turn, but the two wires occasionally cross paths. To fix the circuit, you need to **find the intersection point closest to the central port**. Because the wires are on a grid, use the Manhattan distance for this measurement. While the wires do technically cross right at the central port where they both start, this point does not count, nor does a wire count as crossing with itself.

For example, if the first wire's path is R8,U5,L5,D3, then starting from the central port (o), it goes right 8, up 5, left 5, and finally down 3:

    ...........
    ...........
    ...........
    ....+----+.
    ....|....|.
    ....|....|.
    ....|....|.
    .........|.
    .o-------+.
    ...........

Then, if the second wire's path is U7,R6,D4,L4, it goes up 7, right 6, down 4, and left 4:

    ...........
    .+-----+...
    .|.....|...
    .|..+--X-+.
    .|..|..|.|.
    .|.-X--+.|.
    .|..|....|.
    .|.......|.
    .o-------+.
    ...........

These wires cross at two locations (marked X), but the lower-left one is closer to the central port: its distance is 3 + 3 = 6.

Here are a few more examples:

- R75,D30,R83,U83,L12,D49,R71,U7,L72
  U62,R66,U55,R34,D71,R55,D58,R83 = distance 159

- R98,U47,R26,D63,R33,U87,L62,D20,R33,U53,R51
  U98,R91,D20,R16,D67,R40,U7,R15,U6,R7 = distance 135

**What is the Manhattan distance** from the central port to the closest intersection?

{| part1 )}

## Parsing

The input consists of two lines each containing a list movement instructions, `"R75"` etc. We can separate the direction and distance components of each movement instruction, `('R', 75)` and then separate the two wires' instruction lists:

```elm {l}
type alias Instruction =
    ( Char, Int )


wireInstructions : List (List Instruction)
wireInstructions =
    let
        extractDistDir =
            String.uncons
                >> Maybe.withDefault ( ' ', "" )
                >> Tuple.mapSecond AOC.toInt
    in
    puzzleInput
        |> List.map (List.map extractDistDir)


wire1 : List Instruction
wire1 =
    wireInstructions |> List.head |> Maybe.withDefault []


wire2 : List Instruction
wire2 =
    wireInstructions |> List.drop 1 |> List.head |> Maybe.withDefault []
```

## Approach

We will need to keep track of all grid locations of each wire in unit steps. While this could be represented on a grid, we don't know without processing both wires, how large the grid will be. It therefore makes sense instead to store these locations as a set of coordinates. We will also need to keep track of the distance along the wire (for part 2), so let's represent each grid location as 3-tuple (x, y, distance) and each wire's path as a set of locations:

```elm {l}
type alias Location =
    ( Int, Int, Int )


type alias Path =
    Set Location
```

We can build up the path of locations by moving along each straight line wire segment in unit steps, accumulating total distance travelled.

```elm {l}
addSegment : Instruction -> ( Location, Path ) -> ( Location, Path )
addSegment ( dir, lengthToGo ) ( ( x, y, d ), p ) =
    if lengthToGo == 0 then
        ( ( x, y, d ), Set.insert ( x, y, d ) p )

    else
        case dir of
            'R' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x + 1, y, d + 1 )
                    , Set.insert ( x + 1, y, d + 1 ) p
                    )

            'L' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x - 1, y, d + 1 )
                    , Set.insert ( x - 1, y, d + 1 ) p
                    )

            'U' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x, y - 1, d + 1 )
                    , Set.insert ( x, y - 1, d + 1 ) p
                    )

            'D' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x, y + 1, d + 1 )
                    , Set.insert ( x, y + 1, d + 1 ) p
                    )

            _ ->
                ( ( x, y, d ), p ) |> Debug.log "Unknown direction"
```

Then we just accumulate the segment paths by following the instructions for each wire:

```elm {l}
path : List Instruction -> Path
path =
    List.foldl addSegment ( ( 0, 0, 0 ), Set.empty )
        >> Tuple.second
```

To find the locations of intersecting wires, we simply perform a set intersection on the two paths, ignoring the distance values at each location.

```elm {l}
intersections : Path -> Path -> List ( Int, Int )
intersections p1 p2 =
    let
        stripDist ( x, y, _ ) =
            ( x, y )
    in
    Set.intersect (Set.map stripDist p1) (Set.map stripDist p2)
        |> Set.toList
```

Finally, we find the manhattan distance to the origin for each of the intersection points and extract the smallest one:

```elm {l}
closestOverlap : Path -> Path -> Int
closestOverlap p1 p2 =
    let
        manhattan ( x, y ) =
            abs x + abs y
    in
    intersections p1 p2
        |> List.map manhattan
        |> List.minimum
        |> Maybe.withDefault -1
```

```elm {l r}
part1 : Int
part1 =
    closestOverlap (path wire1) (path wire2)
```

{( part2 |}

It turns out that this circuit is very timing-sensitive; you actually need to **minimize the signal delay**.

To do this, calculate the **number of steps** each wire takes to reach each intersection; choose the intersection where the **sum of both wires' steps** is lowest. If a wire visits a position on the grid multiple times, use the steps value from the **first** time it visits that position when calculating the total value of a specific intersection.

The number of steps a wire takes is the total number of grid squares the wire has entered to get to that location, including the intersection being considered. Again consider the example from above:

    ...........
    .+-----+...
    .|.....|...
    .|..+--X-+.
    .|..|..|.|.
    .|.-X--+.|.
    .|..|....|.
    .|.......|.
    .o-------+.
    ...........

In the above example, the intersection closest to the central port is reached after 8+5+5+2 = 20 steps by the first wire and 7+6+4+3 = **20** steps by the second wire for a total of 20+20 = **40** steps.

However, the top-right intersection is better: the first wire takes only 8+5+2 = **15** and the second wire takes only 7+6+2 = 15, a total of 15+15 = **30** steps.

Here are the best steps for the extra examples from above:

- R75,D30,R83,U83,L12,D49,R71,U7,L72
  U62,R66,U55,R34,D71,R55,D58,R83 = 610 steps

- R98,U47,R26,D63,R33,U87,L62,D20,R33,U53,R51
  U98,R91,D20,R16,D67,R40,U7,R15,U6,R7 = 410 steps

**What is the fewest combined steps the wires must take to reach an intersection?**

{| part2 )}

## Approach

Now we know the intersection points, we can filter the original two sets of locations for just those at intersection points, but unlike part 1, we also keep the accumulated distance for all wires that reached an intersection.

This gives us two sets of distances for each intersection â€“ one for the first wire and one for the second. Adding the two sets of distances gives the signal delay for each intersection, from which we can select the shortest.

```elm {l r}
part2 : Int
part2 =
    let
        path1 =
            path wire1

        path2 =
            path wire2

        crossings =
            intersections path1 path2 |> Set.fromList

        distances =
            Set.filter (\( x, y, d ) -> Set.member ( x, y ) crossings)
                >> Set.toList
                >> List.map AOC.tripletThird
    in
    List.map2 (+) (distances path1) (distances path2)
        |> List.minimum
        |> Maybe.withDefault -1
```

---

## Reflection

I enjoyed this puzzle. My initial solution for part 1 did not accumulate distance (only needed for part 2), so was simpler. But rather than repeat similar, but not identical code, for part 2, I added distance to the location tuple which was ignored for part 1.

I feel there may be more efficient solutions, but this one is good enough. For part two, I was expecting to have to sort the two sets of intersection locations before adding their distances, to ensure we were adding the correct distances for each wire. But this turned out not to be necessary, perhaps because Elm's set intersection function sorts the values anyway.
