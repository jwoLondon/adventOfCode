---
follows: data/d03_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2019): Crossed Wires

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/3)

## Part 1

The input consists of two lines each containing a list movement instructions, `"R75"` etc. We can separate the direction and distance components of each movement instruction, `('R', 75)` and then separate the two wires' instruction lists:

```elm {l}
type alias Instruction =
    ( Char, Int )


wireInstructions : List (List Instruction)
wireInstructions =
    let
        extractDistDir =
            String.uncons
                >> Maybe.withDefault ( ' ', "" )
                >> Tuple.mapSecond AOC.toInt
    in
    puzzleInput
        |> List.map (List.map extractDistDir)


wire1 : List Instruction
wire1 =
    wireInstructions |> List.head |> Maybe.withDefault []


wire2 : List Instruction
wire2 =
    wireInstructions |> List.drop 1 |> List.head |> Maybe.withDefault []
```

We will need to keep track of all grid locations of each wire in unit steps. While this could be represented on a grid, we don't know without processing both wires, how large the grid will be. It therefore makes sense instead to store these locations as a set of coordinates. We will also need to keep track of the distance along the wire (for part 2), so let's represent each grid location as 3-tuple (x, y, distance) and each wire's path as a set of locations:

```elm {l}
type alias Location =
    ( Int, Int, Int )


type alias Path =
    Set Location
```

We can build up the path of locations by moving along each straight line wire segment in unit steps, accumulating total distance travelled.

```elm {l}
addSegment : Instruction -> ( Location, Path ) -> ( Location, Path )
addSegment ( dir, lengthToGo ) ( ( x, y, d ), p ) =
    if lengthToGo == 0 then
        ( ( x, y, d ), Set.insert ( x, y, d ) p )

    else
        case dir of
            'R' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x + 1, y, d + 1 )
                    , Set.insert ( x + 1, y, d + 1 ) p
                    )

            'L' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x - 1, y, d + 1 )
                    , Set.insert ( x - 1, y, d + 1 ) p
                    )

            'U' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x, y - 1, d + 1 )
                    , Set.insert ( x, y - 1, d + 1 ) p
                    )

            'D' ->
                addSegment ( dir, lengthToGo - 1 )
                    ( ( x, y + 1, d + 1 )
                    , Set.insert ( x, y + 1, d + 1 ) p
                    )

            _ ->
                ( ( x, y, d ), p ) |> Debug.log "Unknown direction"
```

Then we just accumulate the segment paths by following the instructions for each wire:

```elm {l}
path : List Instruction -> Path
path =
    List.foldl addSegment ( ( 0, 0, 0 ), Set.empty )
        >> Tuple.second
```

To find the locations of intersecting wires, we simply perform a set intersection on the two paths, ignoring the distance values at each location.

```elm {l}
intersections : Path -> Path -> List ( Int, Int )
intersections p1 p2 =
    let
        stripDist ( x, y, _ ) =
            ( x, y )
    in
    Set.intersect (Set.map stripDist p1) (Set.map stripDist p2)
        |> Set.toList
```

Finally, we find the manhattan distance to the origin for each of the intersection points and extract the smallest one:

```elm {l}
closestOverlap : Path -> Path -> Int
closestOverlap p1 p2 =
    let
        manhattan ( x, y ) =
            abs x + abs y
    in
    intersections p1 p2
        |> List.map manhattan
        |> List.minimum
        |> Maybe.withDefault -1
```

```elm {l r}
part1 : Int
part1 =
    closestOverlap (path wire1) (path wire2)
```

---

## Part 2

Now we know the intersection points, we can filter the original two sets of locations for just those at intersection points, but unlike part 1, we also keep the accumulated distance for all wires that reached an intersection.

This gives us two sets of distances for each intersection – one for the first wire and one for the second. Adding the two sets of distances gives the signal delay for each intersection, from which we can select the shortest.

```elm {l r}
part2 : Int
part2 =
    let
        path1 =
            path wire1

        path2 =
            path wire2

        crossings =
            intersections path1 path2 |> Set.fromList

        distances =
            Set.filter (\( x, y, d ) -> Set.member ( x, y ) crossings)
                >> Set.toList
                >> List.map AOC.tripletThird
    in
    List.map2 (+) (distances path1) (distances path2)
        |> List.minimum
        |> Maybe.withDefault -1
```

---

## Reflection

I enjoyed this puzzle. My initial solution for part 1 did not accumulate distance (only needed for part 2), so was simpler. But rather than repeat similar, but not identical code, for part 2, I added distance to the location tuple which was ignored for part 1.

I feel there may be more efficient solutions, but this one is good enough. For part two, I was expecting to have to sort the two sets of intersection locations before adding their distances, to ensure we were adding the correct distances for each wire. But this turned out not to be necessary, perhaps because Elm's set intersection function sorts the values anyway.
