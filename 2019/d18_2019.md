---
follows: data/d18_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 18 (2019): Many-Worlds Interpretation

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/18)

## Part 1

Parsing is a straightforward reading of the input file directly into a grid.

```elm {l}
parse : List String -> AOC.Grid Char
parse =
    List.map (String.replace "@" "1" >> String.toList) >> AOC.gFromLists
```

For debugging purposes, it is useful to be able to display the grid:

```elm {l}
display : AOC.Grid Char -> List String
display =
    AOC.gToLists
        >> List.map (List.intersperse '\u{2006}')
        >> List.map String.fromList
        >> List.map
            (String.replace "." "░"
                >> String.replace "#" "▓"
                >> String.replace "1" "●"
                >> AOC.flip (++) "\n"
            )
```

For convenience, we can store the location of the start and all key locations:

```elm {l}
keyLocations : AOC.Grid Char -> Dict Char AOC.GridLocation
keyLocations =
    AOC.gMapWithLocation
        (\loc c ->
            if (c >= '1' && c <= '4') || (c >= 'a' && c <= 'z') then
                Just ( c, loc )

            else
                Nothing
        )
        >> AOC.gToList
        >> List.filterMap identity
        >> Dict.fromList
```

Generally, navigating the grid maze is expensive, so we can represent the maze as a graph of keys with edges representing the number of steps between them along their connecting path assuming all doors are open. We will also store any intermediate doors for later navigation.

```elm {l}
adjacentKeys : Char -> AOC.Grid Char -> List ( Char, Int, List Char )
adjacentKeys key tunnels =
    let
        ( keyRow, keyCol ) =
            Dict.get key (keyLocations tunnels) |> Maybe.withDefault ( -1, -1 )

        addNeighbour grd ( r, c ) ( steps, doors ) =
            case AOC.gGet ( r, c ) grd of
                Just cell ->
                    if cell == '#' then
                        identity

                    else
                        Deque.pushBack ( r, c, ( steps, doors ) )

                Nothing ->
                    identity

        paths grd todo adjKeys =
            case Deque.popFront todo of
                ( Nothing, _ ) ->
                    adjKeys

                ( Just ( r, c, ( steps, doors ) ), todo2 ) ->
                    case AOC.gGet ( r, c ) grd of
                        Just cell ->
                            if cell == '#' then
                                paths grd todo2 adjKeys

                            else if cell >= 'A' && cell <= 'Z' then
                                paths
                                    (AOC.gSet ( r, c ) '#' grd)
                                    (todo2
                                        |> addNeighbour grd ( r - 1, c ) ( steps + 1, Char.toLower cell :: doors )
                                        |> addNeighbour grd ( r + 1, c ) ( steps + 1, Char.toLower cell :: doors )
                                        |> addNeighbour grd ( r, c - 1 ) ( steps + 1, Char.toLower cell :: doors )
                                        |> addNeighbour grd ( r, c + 1 ) ( steps + 1, Char.toLower cell :: doors )
                                    )
                                    adjKeys

                            else if cell == '.' || cell == key then
                                paths
                                    (AOC.gSet ( r, c ) '#' grd)
                                    (todo2
                                        |> addNeighbour grd ( r - 1, c ) ( steps + 1, doors )
                                        |> addNeighbour grd ( r + 1, c ) ( steps + 1, doors )
                                        |> addNeighbour grd ( r, c - 1 ) ( steps + 1, doors )
                                        |> addNeighbour grd ( r, c + 1 ) ( steps + 1, doors )
                                    )
                                    adjKeys

                            else
                                paths grd todo2 (( cell, steps, doors ) :: adjKeys)

                        Nothing ->
                            -- We should never get here
                            adjKeys
    in
    paths
        tunnels
        (Deque.singleton ( keyRow, keyCol, ( 0, [] ) ))
        []
```

To build the graph representing the tunnels we add all the edges between all keys and their adjacent neighbours. At the same time we can build a dictionary of edges that contain one or more doors between them. For clarity we will store both the graph and the dictionary of edge doors in a single record.

```elm {l}
type alias Tunnels =
    { g : AOC.SPFGraph Char
    , d : Dict ( Char, Char ) (List Char)
    }
```

```elm {l}
buildGraph : AOC.Grid Char -> Tunnels
buildGraph grid =
    let
        ( graph, dict ) =
            keyLocations grid
                |> Dict.keys
                |> List.foldl
                    (\fromKey ( g, d ) ->
                        let
                            keysAndDoors =
                                adjacentKeys fromKey grid
                        in
                        ( keysAndDoors
                            |> List.foldl
                                (\( toKey, steps, _ ) ->
                                    AOC.addDirectedEdge fromKey toKey (toFloat steps)
                                )
                                g
                        , keysAndDoors
                            |> List.foldl
                                (\( toKey, _, doors ) edgeDoors ->
                                    if doors == [] then
                                        edgeDoors

                                    else
                                        Dict.insert ( fromKey, toKey ) doors edgeDoors
                                )
                                d
                        )
                    )
                    ( Graph.empty, Dict.empty )
    in
    Tunnels graph dict
```

From this it is easy to create a sub-graph that contains all connected keys given a set of carried keys (i.e. remove edges that are blocked by a door for which we don't have a key).

```elm {l}
accessibleTunnels : Set Char -> Tunnels -> AOC.SPFGraph Char
accessibleTunnels carried tunnels =
    let
        edges =
            Graph.edges tunnels.g

        removeClosed ( k1, k2 ) g =
            case Dict.get ( k1, k2 ) tunnels.d of
                Just doors ->
                    if List.all (\door -> Set.member door carried) doors then
                        g

                    else
                        Graph.removeEdge k1 k2 g

                Nothing ->
                    g

        subGraph =
            List.foldl removeClosed tunnels.g edges
    in
    subGraph
```

Now we have a navigable graph representing the maze with any given collection of carried keys, we can more efficiently determine any key's uncollected adjacent keys and their shortest paths to each of them. We can use Dijkstra's SPF on graph to find the shortest paths.

```elm {l}
uncollectedAdjacentKeys : Char -> Set Char -> Tunnels -> Set ( Char, Int )
uncollectedAdjacentKeys fromKey carried tunnels =
    let
        gAccess =
            accessibleTunnels carried tunnels

        keyDist toKey =
            ( toKey, AOC.shortestPathCost fromKey toKey gAccess |> Maybe.withDefault 0 |> round )

        member set k =
            Set.member k set

        searchUncollected todo visited adj =
            case Set.toList todo of
                k1 :: tl ->
                    let
                        ( newTodo, newAdj ) =
                            Set.partition (member carried)
                                (Graph.outgoing k1 gAccess |> Set.filter (not << member visited))
                                |> Tuple.mapSecond (AOC.flip Set.diff (Set.map Tuple.first adj))
                    in
                    searchUncollected (Set.union newTodo (Set.fromList tl))
                        (Set.insert k1 visited)
                        (Set.union (newAdj |> Set.map keyDist) adj)

                [] ->
                    adj
    in
    searchUncollected (Set.singleton fromKey) Set.empty Set.empty
```

We can now start storing paths to sequences of keys originating at the start location. We need to keep track of three pieces of information: the latest key picked up; the keys carried to that point, including the latest key; and the number of steps taken to reach this key. We can store this in a dictionary where the id is tuple of an alphabetical list of carried keys with the current key, and the value associated with it is the number of steps taken. If in our search we come across the same set of collected keys at a key location that we have seen before, we just store the shorter of the two.

```elm {l}
type alias Paths =
    Dict ( String, Char ) Int


carriedId : List Char -> String
carriedId =
    Set.fromList >> Set.toList >> String.fromList


search : Tunnels -> List ( List Char, Int ) -> Paths -> ( List ( List Char, Int ), Paths )
search tunnels todo paths =
    case todo of
        [] ->
            ( [], paths )

        ( carried, dist ) :: tl ->
            let
                fromKey =
                    carried |> List.head |> Maybe.withDefault '1'

                toKeys =
                    uncollectedAdjacentKeys fromKey (Set.fromList carried) tunnels

                addPath ( k, d ) ( td, ps ) =
                    let
                        id =
                            carriedId (k :: carried)

                        d2 =
                            dist + d
                    in
                    case Dict.get ( id, k ) ps of
                        Nothing ->
                            ( ( k :: carried, d2 ) :: td, Dict.insert ( id, k ) d2 ps )

                        Just oldDist ->
                            if d2 < oldDist then
                                ( ( k :: carried, d2 ) :: td, Dict.insert ( id, k ) d2 ps )

                            else
                                ( td, ps )
            in
            Set.foldl addPath ( tl, paths ) toKeys


buildPaths : List ( List Char, Int ) -> Paths -> Tunnels -> Paths
buildPaths initTodo initPaths tunnels =
    let
        ( todo, paths ) =
            search tunnels initTodo initPaths
    in
    if todo == [] then
        paths

    else
        buildPaths todo paths tunnels
```

Finally we filter all pathways to just those that end up with all keys carried and select the one with the shortest distance.

```elm {l r}
part1 : Int
part1 =
    let
        grid =
            parse test5Input

        allKeys =
            Dict.keys (keyLocations grid) |> String.fromList
    in
    grid
        |> buildGraph
        |> buildPaths [ ( [ '1' ], 0 ) ] Dict.empty
        |> Dict.toList
        |> List.filter (\( ( carried, _ ), _ ) -> carried == allKeys)
        |> List.map Tuple.second
        |> List.minimum
        |> Maybe.withDefault 0
```

---

## Part 2

The problem is a relatively minor variant of part 1 where we need to manage four path searches rather than one and they all need to share a common set of collected keys. We therefore need to keep track of 4 traversals – one in each quadrant. We can do this by creating a record representing the state of each robot (the quadrant graph it navigates, list of traversals to explore and paths of shortest traversals).

```elm {l}
type alias Model =
    { tunnels : Tunnels
    , todo : List ( List Char, Int )
    , paths : Paths
    }


type alias Models =
    { m1 : Model
    , m2 : Model
    , m3 : Model
    , m4 : Model
    }
```

We can modify the centre cells of the maze and subset quadrants to create 4 grids and then 4 graphs representing each robot's traversal space.

```elm {l}
quadrantTunnels : AOC.Grid Char -> Models
quadrantTunnels g =
    let
        cRow =
            AOC.gRowCount g // 2

        cCol =
            AOC.gColCount g // 2

        gFull =
            g
                |> AOC.gSet ( cRow, cCol ) '#'
                |> AOC.gSet ( cRow - 1, cCol ) '#'
                |> AOC.gSet ( cRow + 1, cCol ) '#'
                |> AOC.gSet ( cRow, cCol - 1 ) '#'
                |> AOC.gSet ( cRow, cCol + 1 ) '#'
                |> AOC.gSet ( cRow - 1, cCol - 1 ) '1'
                |> AOC.gSet ( cRow - 1, cCol + 1 ) '2'
                |> AOC.gSet ( cRow + 1, cCol - 1 ) '3'
                |> AOC.gSet ( cRow + 1, cCol + 1 ) '4'

        g1 =
            gFull |> AOC.gToLists |> List.take (cRow + 1) |> List.map (List.take (cCol + 1))

        g2 =
            gFull |> AOC.gToLists |> List.take (cRow + 1) |> List.map (List.drop cCol)

        g3 =
            gFull |> AOC.gToLists |> List.drop cRow |> List.map (List.take (cCol + 1))

        g4 =
            gFull |> AOC.gToLists |> List.drop cRow |> List.map (List.drop cCol)
    in
    Models
        (Model (g1 |> AOC.gFromLists |> buildGraph) [ ( [ '1' ], 0 ) ] Dict.empty)
        (Model (g2 |> AOC.gFromLists |> buildGraph) [ ( [ '2' ], 0 ) ] Dict.empty)
        (Model (g3 |> AOC.gFromLists |> buildGraph) [ ( [ '3' ], 0 ) ] Dict.empty)
        (Model (g4 |> AOC.gFromLists |> buildGraph) [ ( [ '4' ], 0 ) ] Dict.empty)
```

When a robot reaches a key, it may need to pass that key to the robot that needs it (if not needed in its own quadrant) along with the cost of retrieving it, so we create a convenience function to add a key and its distance cost to a robot's todo list.

```elm {l}
addCarried : ( Char, Int ) -> Model -> Model
addCarried ( key, dist ) model =
    case List.head model.todo of
        Nothing ->
            model

        Just ( keys, d ) ->
            -- let
            --     doors =
            --         model.tunnels.g |> AOC.nodes |> List.map Tuple.first |> Set.fromList
            --     _ =
            --         Debug.log "key and doors" ( key, doors )
            -- in
            { model | todo = ( keys ++ [ key ], d + dist ) :: model.todo }
```

When the robots each provide a list of the latest list of carried keys, we can extract just the new keys and their distance costs which we will be able to share with all other robots.

```elm {l}
newKeyCosts : List ( List Char, Int ) -> List ( Char, Int )
newKeyCosts =
    List.map (\( keys, dist ) -> ( List.head keys |> Maybe.withDefault '@', dist ))
        >> List.filter (\( k, _ ) -> k >= 'a' && k <= 'z')
```

We can now modify `buildPaths` so that each robot its quadrant and then passing on any new carried keys to the other robots that they do not already have in their own quadrant.

```elm {l}
updateModels : List ( Char, Int ) -> Models -> Models
updateModels newKeys models =
    let
        keysForQ1 =
            models.m1.tunnels.g

        keysForQ2 =
            models.m2.tunnels.g

        keysForQ3 =
            models.m3.tunnels.g

        keysForQ4 =
            models.m4.tunnels.g

        foreignKeys keys =
            List.filter
                (\( k, _ ) ->
                    (not << Set.member k)
                        (keys |> AOC.nodes |> List.map Tuple.first |> Set.fromList)
                )
                newKeys
    in
    { models
        | m1 = List.foldl addCarried models.m1 (foreignKeys keysForQ1 |> Debug.log "m1 new keys")
        , m2 = List.foldl addCarried models.m2 (foreignKeys keysForQ2)
        , m3 = List.foldl addCarried models.m3 (foreignKeys keysForQ3)
        , m4 = List.foldl addCarried models.m4 (foreignKeys keysForQ4)
    }
```

```elm {l}
buildPaths2 : Int -> Models -> Paths
buildPaths2 numSteps models =
    if numSteps == 0 then
        Dict.empty

    else
        let
            ( todo1, paths1 ) =
                search models.m1.tunnels models.m1.todo models.m1.paths |> Debug.log "r 1"

            ( todo2, paths2 ) =
                search models.m2.tunnels models.m2.todo models.m2.paths |> Debug.log "r 2"

            ( todo3, paths3 ) =
                search models.m3.tunnels models.m3.todo models.m3.paths |> Debug.log "r 3"

            ( todo4, paths4 ) =
                search models.m4.tunnels models.m4.todo models.m4.paths |> Debug.log "r 4"

            allTodo =
                -- todo1 ++ todo2 ++ todo3 ++ todo4 |> newKeyCosts
                todo1 |> newKeyCosts |> Debug.log "allTodo"

            newModels =
                updateModels allTodo models

            allPaths =
                paths1
                    |> Dict.union paths2
                    |> Dict.union paths3
                    |> Dict.union paths4
        in
        if allTodo == [] then
            allPaths

        else
            buildPaths2 (numSteps - 1) newModels
```

{(fixed|}^^^elm{m=(display (parse test8Input))}^^^{| fixed )}

```elm {l r}
part2 : List ( ( String, Char ), Int )
part2 =
    let
        grid =
            parse test8Input

        allKeys =
            Dict.keys (keyLocations grid) |> String.fromList
    in
    grid
        |> quadrantTunnels
        |> buildPaths2 5
        |> Dict.toList
```

---

## Reflection

This puzzle was too much for me in my limited time in December so I revisited it later in the year. Even with the lack of time pressure, this was tough and it took me several attempts to land on a workable solution. It was satisfying to finally come up with a solution that was reasonably efficient after spending so much time on the problem. I remain in awe of those who can solve these kinds of problems so quickly.

I found visualizing the grid and its subsequent graph representation helpful in coming to a solution. Showing the grid allowed me spot that the maze was a tree and showing the graph (with Vega – see below) helped in spotting approaches for pruning the search space.

My first attempt at part one involved calculating key-dependencies (key f needs key d which needs k c etc.), but this soon proved too costly as there are many permutations of key pickup.

My second attempt tried to simplify the problem by finding the distance between all adjacent keys, recognising that the maze was as acyclic. But again this proved too costly for the full puzzle input (and example 4) where the number of key pair permutations was large and had to be recalculated many times to explore all possible pathways through the maze.

My third attempt was similar to the working solution except that I calculated the pathways by navigating the gridded maze. This was generating the correct results for the example inputs, but was taking too long for example 4 and the puzzle input (many minutes).

Finally, representing the key network as a graph led to significant speed ups. This also made it easier to use a Dijkstra SPF search to build find the shortest path between non-adjacent keys, further speeding up the solution that only stored the shortest route for a given set of collected keys at each key location. Using this method the correct result for part 1 is found in around 7 seconds.

A byproduct of tacking this problem was that I added a Dijkstra/A\* solver to the AOC module, which may be helpful for other graph searching problems.

Test 4 (the fourth example in the part 1 instructions) takes about 8 times as long to complete than the actual puzzle input. Presumably because if the 8 keys all accessible from the centre without the need for opening any doors. This suggests that some further pruning of pathways might be possible to reduce search space. But given a reasonably fast solution for the puzzle input, I've not investigated this.

### Supplementary: Vega Graph Visualization

A simple node-link representation of the keys in the maze.

<!-- ^^^elm {v=(visGraph buildGraph.g) interactive}^^^ -->

```elm {l}
visGraph : AOC.SPFGraph Char -> V.Spec
visGraph g =
    let
        nodeTable =
            V.dataFromColumns "node-data" []
                << V.dataColumn "id" (V.vStrs (g |> AOC.nodes |> List.map (Tuple.first >> String.fromChar)))
                << V.dataColumn "distToEnd" (V.vNums (g |> AOC.nodes |> List.map Tuple.second))

        edgeTable =
            V.dataFromColumns "link-data" []
                << V.dataColumn "source" (V.vStrs (g |> AOC.edges |> List.map (AOC.tripletFirst >> String.fromChar)))
                << V.dataColumn "target" (V.vStrs (g |> AOC.edges |> List.map (AOC.tripletSecond >> String.fromChar)))
                << V.dataColumn "cost" (V.vNums (g |> AOC.edges |> List.map AOC.tripletThird))

        ds =
            V.dataSource [ nodeTable [], edgeTable [] ]

        si =
            V.signals
                << V.signal "cx" [ V.siUpdate "width /2" ]
                << V.signal "cy" [ V.siUpdate "height /2" ]
                << V.signal "nodeRadius" [ V.siValue (V.vNum 20) ]
                << V.signal "nodeCharge" [ V.siValue (V.vNum -30) ]
                << V.signal "linkDistance" [ V.siValue (V.vNum 400) ]
                << V.signal "static" [ V.siValue V.vFalse ]

        mk =
            V.marks
                << V.mark V.text
                    [ V.mName "nodes"
                    , V.mFrom [ V.srData (V.str "node-data") ]
                    , V.mEncode [ V.enEnter [ V.maText [ V.vField (V.field "id") ] ] ]
                    , V.mTransform
                        [ V.trForce
                            [ V.fsIterations (V.num 300)
                            , V.fsStatic (V.booSignal "static")
                            , V.fsForces
                                [ V.foCenter (V.numSignal "cx") (V.numSignal "cy")
                                , V.foCollide (V.numSignal "nodeRadius") []
                                , V.foNBody [ V.fpStrength (V.numSignal "nodeCharge") ]
                                , V.foLink (V.str "link-data")
                                    [ V.fpDistance (V.numSignal "linkDistance")
                                    , V.fpId (V.field "datum.id")
                                    ]
                                ]
                            ]
                        ]
                    ]
                << V.mark V.path
                    [ V.mFrom [ V.srData (V.str "link-data") ]
                    , V.mInteractive V.false
                    , V.mEncode
                        [ V.enUpdate
                            [ V.maStroke [ V.vStr "#999" ]
                            , V.maStrokeWidth [ V.vNum 0.5 ]
                            ]
                        ]
                    , V.mTransform
                        [ V.trLinkPath
                            [ V.lpShape V.lsLine
                            , V.lpSourceX (V.field "datum.source.x")
                            , V.lpSourceY (V.field "datum.source.y")
                            , V.lpTargetX (V.field "datum.target.x")
                            , V.lpTargetY (V.field "datum.target.y")
                            ]
                        ]
                    ]
    in
    V.toVega
        [ V.width 600, V.height 600, V.padding 0, V.autosize [ V.asNone ], ds, si [], mk [] ]
```
