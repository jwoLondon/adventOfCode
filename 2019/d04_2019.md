---
follows: data/d04_2019.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 4 (2019): Secure Container

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2019/day/4):

{( part1|}

You arrive at the Venus fuel depot only to discover it's protected by a password. The Elves had written the password on a sticky note, but someone threw it out.

However, they do remember a few key facts about the password:

- It is a six-digit number.
- The value is within the range given in your puzzle input.
- Two adjacent digits are the same (like 22 in 122345).
- Going from left to right, the digits **never decrease**; they only ever increase or stay the same (like 111123 or 135679).

Other than the range rule, the following are true:

- 111111 meets these criteria (double 11, never decreases).
- 223450 does not meet these criteria (decreasing pair of digits 50).
- 123789 does not meet these criteria (no double).

**How many different passwords** within the range given in your puzzle input meet these criteria?

{| part1 )}

## Approach

We can represent the potential passwords in a number of ways, for example as numbers, as strings or lists of digits. Given that we will be considering digits separately, a list of them makes sense. We can use `Char` for each digit rather as we can still perform comparison operations with them.

```elm {l}
type alias Digits =
    List Char
```

We will need to make comparisons between adjacent digits in order to test for equality and potentially to increment an adjacent digit so it is no lower than its previous neighbour. We can usefully convert a list of items with the `neighbours` function from [aoc.md](../aoc.md)

A reasonably efficient approach to the problem will be for any given password, generate the next valid one in the sequence and continue to do so until we exceed the upper limit of the range of passwords to consider, and then count the number of passwords we have created. This avoids having to check the password validity of numerous invalid passwords.

The first criterion is to ensure a password has monotonically increasing digits:

```elm {l}
makeMonotonic : Digits -> Digits
makeMonotonic =
    let
        levelDigit chr ( lowest, digits ) =
            if chr <= lowest then
                ( lowest, lowest :: digits )

            else
                ( chr, chr :: digits )
    in
    List.foldl levelDigit ( '0', [] )
        >> Tuple.second
        >> List.reverse
```

Our second criterion is for the password to contain at least one pair of adjacent matching digits. We can test for this, and if necessary make the smallest possible increment to a digit to create an adjacency.

```elm {l}
hasPair : Digits -> Bool
hasPair =
    AOC.neighbours
        >> List.filter (\( a, b ) -> a == b)
        >> (/=) []


makePair : Digits -> Digits
makePair digits =
    let
        reversed =
            List.reverse digits
    in
    case List.head reversed of
        Just d ->
            [ d, d ]
                ++ List.drop 2 reversed
                |> List.reverse

        Nothing ->
            digits
```

We can now generate the next valid password after any given one by incrementing it by 1 and then making it monotonic (of not already) and making it contain an adjacent pair (if not already). Note that if we need to make a password monotonic, it will necessarily contain an adjacent pair.

```elm {l}
next : Digits -> Digits
next pwd =
    let
        incPwd =
            pwd
                |> String.fromList
                |> String.toInt
                |> Maybe.withDefault -1
                |> (+) 1
                |> String.fromInt
                |> String.toList

        monotonicPwd =
            makeMonotonic incPwd
    in
    if incPwd /= monotonicPwd || hasPair incPwd then
        monotonicPwd

    else
        makePair monotonicPwd
```

Finally, we can generate the valid passwords within the range by starting at the lower bound and finding the next valid passwords until we exceed the upper bound.

```elm {l}
validInRange : String -> Digits -> List Digits -> List Digits
validInRange upper pwd validPwds =
    let
        isValid p =
            p == makeMonotonic p && hasPair p
    in
    if String.fromList pwd > upper then
        validPwds

    else if isValid pwd then
        validInRange upper (next pwd) (pwd :: validPwds)

    else
        validInRange upper (next pwd) validPwds
```

```elm {l r}
part1 : Int
part1 =
    let
        ( lower, upper ) =
            puzzleInput
    in
    List.length (validInRange upper (String.toList lower) [])
```

{( part2 |}

An Elf just remembered one more important detail: the two adjacent matching digits **are not part of a larger group of matching digits.**

Given this additional criterion, but still ignoring the range rule, the following are now true:

- 112233 meets these criteria because the digits never decrease and all repeated digits are exactly two digits long.
- 123444 no longer meets the criteria (the repeated 44 is part of a larger group of 444).
- 111122 meets the criteria (even though 1 is repeated more than twice, it still contains a double 22).

**How many different passwords** within the range given in your puzzle input meet all of the criteria?

{| part2 )}

## Approach Part Two

This is a simple extension to the process in part 1. We just need to filter the previously valid passwords with those that contain at least one unique matching pair. We can do this by building a frequency table of pairs and filter those that have a table containing a frequency of 1.

```elm {l}
hasUniquePair : Digits -> Bool
hasUniquePair digits =
    let
        pairs =
            AOC.neighbours digits
                |> List.filter (\( a, b ) -> a == b)

        pairFreqs =
            List.foldl AOC.addToFreqTable Dict.empty pairs
    in
    List.member 1 (Dict.values pairFreqs)
```

```elm {l r}
part2 : Int
part2 =
    let
        ( lower, upper ) =
            puzzleInput
    in
    validInRange upper (String.toList lower) []
        |> List.filter hasUniquePair
        |> List.length
```

---

## Reflection

This puzzle reminded me of [day 5, 2015](../2015/d05_2015.md) and [day 11, 2105](../2015/d05_2015.md) that both involved incremental password validation.

This is the kind of puzzle that I am not naturally good at solving, so takes me a little while to find a solution. But when I do, I find it satisfying. My initial uncertainty was how best to represent the passwords. I tried as a string and using `String.uncons` to extract digits, but this wasn't much help for pairwise comparison.

My first submission of an answer was off by one as I failed to account for the fact that the lower bound of the password range may not be a valid password.
