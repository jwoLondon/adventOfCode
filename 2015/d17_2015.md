---
follows: data/d17_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 17 (2015): No Such Thing as Too Much

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/17).

{( part1|}

Filling all containers entirely, **how many different combinations of containers can exactly fit all 150 liters of eggnog?**

{| part1 )}

## Approach

This is a version of the knapsack or subset-sum problem where we need to find a combination of items that sum to a particular value. The brute force approach is to find all combinations of items (the powerset), but we can do better than that by considering the upper- and lower-bounds of the number of items to choose.

For example we know that there is no single item that can store 150 litres, so there is no need to test combinations that include choosing one item. Similar reasoning can be used to exclude a selection of 20 items as their sum is greater than 150 litres.

By sorting the items by capacity and accumulating the sum of the sorted items, we can simply calculate the lower and upper bounds of the combination choices we need to test:

```elm {l}
minNumItems : Int -> List Int -> Int
minNumItems target =
    List.sort
        >> List.reverse
        >> AOC.scanl (+) 0
        >> List.filter (\x -> x <= target)
        >> List.length


maxNumItems : Int -> List Int -> Int
maxNumItems target =
    List.sort
        >> AOC.scanl (+) 0
        >> List.filter (\x -> x <= target)
        >> List.length
        >> AOC.flip (-) 1
```

The combinations of items we need to test can then be constrained between upper and lower limits:

```elm {l}
allCombinations : Int -> Int -> List a -> List (List a)
allCombinations minK k items =
    if k == minK then
        AOC.combinations k items

    else
        allCombinations minK (k - 1) items ++ AOC.combinations k items
```

Now we can test all combinations between limits and test each to see if they hold the desired capacity:

```elm {l}
knapsacks : Int -> Int -> Int -> List Int -> List (List Int)
knapsacks minK maxK capacity items =
    let
        holds cap n =
            List.sum n == cap
    in
    List.filter (holds capacity) (allCombinations minK maxK items)
```

```elm {l r}
part1 : Int
part1 =
    knapsacks (minNumItems 150 puzzleInput) (maxNumItems 150 puzzleInput) 150 puzzleInput
        |> List.length
```

{( part2 |}

Find the minimum number of containers that can exactly fit all 150 liters of eggnog. **How many different ways can you fill that number of containers and still hold exactly 150 litres?**

{| part2 )}

We can apply exactly the same approach but this time set both the lower and upper bounds of our combination choice to be same lower bound.

```elm {l r}
part2 : Int
part2 =
    knapsacks (minNumItems 150 puzzleInput) (minNumItems 150 puzzleInput) 150 puzzleInput
        |> List.length
```

It is possible that constraining the combinations to the lower bound would not have yielded any answer, in which case we would have to increment the number of items by 1 until we find an answer. But given an answer was found above, this was not necessary.
