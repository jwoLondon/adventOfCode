---
follows: data/d12_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 12 (2015): JSAbacusFramework dot io

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/12):

{( part1|}

Santa's Accounting-Elves need help balancing the books after a recent order.
Unfortunately, their accounting software uses a peculiar storage format.
That's where you come in.

They have a JSON document which contains a variety of things: arrays (`[1,2,3]`), objects (`{"a":1, "b":2}`), numbers, and strings. Your first job is to simply find all of the numbers throughout the document and add them together.

For example:

- `[1,2,3]` and `{"a":2,"b":4}` both have a sum of 6.
- `[[[3]]]` and `{"a":{"b":4},"c":-1}` both have a sum of 3.
- `{"a":[-1,1]}` and `[-1,{"a":1}]` both have a sum of 0.
- `[]` and `{}` both have a sum of 0.

You will not encounter any strings containing numbers.

What is the sum of all numbers in the document?

{| part1 )}

While we may be tempted by the question to write or use a JSON parser, it is worth examining the question to see if there is a simpler solution.
We are told that we will “not encounter any Strings containing numbers”.
This means that the only numbers to feature in the entire file will be the numbers we wish to count up.
We therefore don’t have to worry about where in the maze of nested JSON objects and arrays our numbers lurk.
We simply have to extract them from the file.
Like the magician who tries to distract us from spotting the sleight of hand, part one of this question provided us with much more detail than we needed to solve the problem.

Extracting numbers from a long String sounds like a job for regex (again!).
Rather than search for digits, it is more efficient search for characters that are not digits and also not the ‘-‘ sign (which can form part of a number).
If we split the string by those non-numeric characters, what is left is a list of numbers to sum.
If we had instead searched for digits directly, regex would not know whether, say, a 7 was the number 7 or part of a longer number 147 or 71 etc.
So splitting by non-digits will give us the groups of digits that make up the numbers we need.

```elm {l r}
part1 : Int
part1 =
    let
        toSum text =
            (+) (AOC.toInt text)
    in
    List.foldl toSum 0 (AOC.split "[^0-9-]+" puzzleInput)
```

{( part2 |}

Uh oh - the Accounting-Elves have realized that they double-counted everything red.

Ignore any object (and all of its children) which has any property with the value "red".
Do this only for objects (`{...}`), not arrays (`[...]`).

- `[1,2,3]` still has a sum of 6.
- `[1,{"c":"red","b":2},3]` now has a sum of 4, because the middle object is ignored.
- `{"d":"red","e":[1,2,3,4],"f":5}` now has a sum of 0, because the entire structure is ignored.
- `[1,"red",5]` has a sum of 6, because "red" in an array has no effect.

{| part2 )}

Unfortunately, part 2 is more complicated and our simple solution of counting numbers won’t work because some numbers - those associated with objects that contain the string ‘red’ should not be counted.
We therefore do need to extract the JSON structure this time.

Unlike most examples of dealing with JSON, in this instance we don’t know beforehand, what the structure of the JSON file will be.
We therefore need a JSON parser that can report the structure of the file as it finds it.
This is a more complex task than typical JSON parsing, but still computable.

## JSON Decoder

Here is a generic decoder (from <https://stackoverflow.com/questions/40825493/elm-decoding-unknown-json-structure>) that can decode a JSON of unknown structure, storing each element as one of the JSON types (that can themselves nest JSON types).

```elm {l}
type JsVal
    = JsString String
    | JsInt Int
    | JsFloat Float
    | JsArray (List JsVal)
    | JsObject (Dict String JsVal)
    | JsNull


jsDecoder : JD.Decoder JsVal
jsDecoder =
    JD.oneOf
        [ JD.map JsString JD.string
        , JD.map JsInt JD.int
        , JD.map JsFloat JD.float
        , JD.list (JD.lazy (\_ -> jsDecoder)) |> JD.map JsArray
        , JD.dict (JD.lazy (\_ -> jsDecoder)) |> JD.map JsObject
        , JD.null JsNull
        ]
```

## Parsing JSON values

Now we have a decoder we need to be able to extract the non-red numbers from those elements that can contain strings but are not objects:

```elm {l}
countNonRedNumbers : JsVal -> Int -> Int
countNonRedNumbers jsVal =
    case jsVal of
        JsInt num ->
            (+) num

        JsArray list ->
            (+) (List.foldl countNonRedNumbers 0 list)

        JsObject object ->
            if List.member (JsString "red") (Dict.values object) then
                identity

            else
                (+) (List.foldl countNonRedNumbers 0 (Dict.values object))

        _ ->
            identity
```

And now we can apply the counting to the containing JSON value provided as puzzle input:

```elm {l r}
part2 : Int
part2 =
    case JD.decodeString jsDecoder puzzleInput of
        Ok jsVal ->
            countNonRedNumbers jsVal 0

        Err msg ->
            0
```
