---
follows: data/d06_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2015): Probably a Fire Hazard

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/6):

{( part1|}

Because your neighbors keep defeating you in the holiday house decorating contest year after year, you've decided to deploy one million lights in a 1000x1000 grid.

Furthermore, because you've been especially nice this year, Santa has mailed you instructions on how to display the ideal lighting configuration.

Lights in your grid are numbered from 0 to 999 in each direction; the lights at each corner are at 0,0, 0,999, 999,999, and 999,0. The instructions include whether to turn on, turn off, or toggle various inclusive ranges given as coordinate pairs. Each coordinate pair represents opposite corners of a rectangle, inclusive; a coordinate pair like 0,0 through 2,2 therefore refers to 9 lights in a 3x3 square. The lights all start turned off.

To defeat your neighbors this year, all you have to do is set up your lights by doing the instructions Santa sent you in order.

For example:

- turn on 0,0 through 999,999 would turn on (or leave on) every light.
- toggle 0,0 through 999,0 would toggle the first line of 1000 lights, turning off the ones that were on, and turning on the ones that were off.
- turn off 499,499 through 500,500 would turn off (or leave off) the middle four lights.

After following the instructions, how many lights are lit?

{| part1 )}

## Data Structures

Any location within the grid of lights can be defined with two coordinates.
The grid itself can be represented as a 1d array with functions to translate from 2d location to position in the 1d array:

```elm {l}
type alias Lights =
    Array Int


gridSize : Int
gridSize =
    1000


type alias Location =
    ( Int, Int )


lightAt : Location -> Lights -> Int
lightAt ( x, y ) =
    Array.get (y * gridSize + x) >> Maybe.withDefault 0


setLight : Int -> Location -> Lights -> Lights
setLight value ( x, y ) =
    Array.set (y * gridSize + x) value


toggle : Location -> Lights -> Lights
toggle location lights =
    if lightAt location lights == 1 then
        setLight 0 location lights

    else
        setLight 1 location lights
```

We need to be able to extract all the locations within an arbitrary sub-rectangular region of the grid:

```elm {l}
getLocations : Location -> Location -> Location -> List Location -> List Location
getLocations ( left, top ) ( right, bottom ) ( x, y ) locations =
    if ( x, y ) == ( right, bottom ) then
        ( x, y ) :: locations

    else if x == right then
        getLocations ( left, top ) ( right, bottom ) ( left, y + 1 ) (( x, y ) :: locations)

    else
        getLocations ( left, top ) ( right, bottom ) ( x + 1, y ) (( x, y ) :: locations)
```

## Parsing

We can recognise that all instructions operate on rectangular areas of a grid defined by their top-left and bottom-right corners, so let's represent such an instruction as a record:

```elm {l}
type alias Instruction =
    { switch : Switch
    , tl : Location
    , br : Location
    }


type Switch
    = On
    | Off
    | Toggle
```

We can then parse input generating an `Instruction`:

```elm {l}
parse : String -> Maybe Instruction
parse instructText =
    let
        words =
            String.words instructText

        location locText =
            case String.split "," locText of
                [ a, b ] ->
                    ( toInt a, toInt b )

                _ ->
                    ( 0, 0 )
    in
    case words of
        [ "turn", "on", tl, "through", br ] ->
            Just (Instruction On (location tl) (location br))

        [ "turn", "off", tl, "through", br ] ->
            Just (Instruction Off (location tl) (location br))

        [ "toggle", tl, "through", br ] ->
            Just (Instruction Toggle (location tl) (location br))

        _ ->
            Nothing
```

We can apply any given instruction to the grid of lights:

```elm {l}
switchLights : Instruction -> Lights -> Lights
switchLights instruction lights =
    let
        locations =
            getLocations instruction.tl instruction.br instruction.tl []
    in
    case instruction.switch of
        On ->
            List.foldl (setLight 1) lights locations

        Off ->
            List.foldl (setLight 0) lights locations

        Toggle ->
            List.foldl toggle lights locations
```

And apply the full list of instructions to the grid:

```elm {l r}
part1 : Int
part1 =
    -- Uncomment to run (slow)
    -- List.filterMap parse puzzleInput
    --     |> List.foldl switchLights (Array.repeat (gridSize * gridSize) 0)
    --     |> Array.toList
    --     |> List.sum
    543903
```

{( part2 |}

You just finish implementing your winning light pattern when you realize you mistranslated Santa's message from Ancient Nordic Elvish.

The light grid you bought actually has individual brightness controls; each light can have a brightness of zero or more. The lights all start at zero.

The phrase turn on actually means that you should increase the brightness of those lights by 1.

The phrase turn off actually means that you should decrease the brightness of those lights by 1, to a minimum of zero.

The phrase toggle actually means that you should increase the brightness of those lights by 2.

What is the total brightness of all lights combined after following Santa's instructions?

For example:

- turn on 0,0 through 0,0 would increase the total brightness by 1.
- toggle 0,0 through 999,999 would increase the total brightness by 2000000.

{| part2 )}

The structure for this modified set of responses to instructions remains the same, but we just need to change the actions associated with `On`, `Off` and `Toggle`:

```elm {l}
changeLight : Int -> Location -> Lights -> Lights
changeLight change location lights =
    setLight (lightAt location lights |> (+) change |> max 0) location lights


switchLights2 : Instruction -> Lights -> Lights
switchLights2 instruction lights =
    let
        locations =
            getLocations instruction.tl instruction.br instruction.tl []
    in
    case instruction.switch of
        On ->
            List.foldl (changeLight 1) lights locations

        Off ->
            List.foldl (changeLight -1) lights locations

        Toggle ->
            List.foldl (changeLight 2) lights locations
```

```elm {l r}
part2 : Int
part2 =
    -- Uncomment to run (slow)
    -- List.filterMap parse puzzleInput
    --     |> List.foldl switchLights2 (Array.repeat (gridSize * gridSize) 0)
    --     |> Array.toList
    --     |> List.sum
    14687245
```
