---
follows: data/d20_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 20 (2015): Infinite Elves and Infinite Houses

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/20).

{( part1|}

What is the lowest house number of the house to get at least as many presents as the number in your puzzle input?

{| part1 )}

This is a fairly straightforward number theory problem as each house receives (10x) the sum of the number of factors (calculable from [aoc.md](aoc.md)) of its house number:

```elm {l}
numPresents : Int -> Int
numPresents houseNum =
    10 * (AOC.factors houseNum |> List.sum)
```

We can traverse the infinite houses, stopping only when the number of presents exceeds the puzzle input:

```elm {l}
firstHouse : Int -> Int
firstHouse houseNum =
    if numPresents houseNum >= puzzleInput then
        houseNum

    else
        firstHouse (houseNum + 1)
```

Traversing from house number 1 finds the answer, although it would be possible to reduce calculation time by finding the lower bound, using [Robin's inequality](https://en.wikipedia.org/wiki/Divisor_function#Growth_rate) as the start number (assuming house number is at least 5040).

```elm {l r}
part1 : Int
part1 =
    firstHouse 1
```

{( part2 |}

With these changes, what is the new lowest house number of the house to get at least as many presents as the number in your puzzle input?

{| part2 )}

A similar approach can be adopted here with the addition of limiting factors to those that are no more than _house number \* 50_.

```elm {l}
firstHousePt2 : Int -> Int
firstHousePt2 houseNum =
    if numPresentsPt2 houseNum >= puzzleInput then
        houseNum

    else
        firstHousePt2 (houseNum + 1)


numPresentsPt2 : Int -> Int
numPresentsPt2 houseNum =
    11 * (AOC.factors houseNum |> List.filter (\n -> houseNum <= n * 50) |> List.sum)
```

```elm {l r}
part2 : Int
part2 =
    firstHousePt2 1
```
