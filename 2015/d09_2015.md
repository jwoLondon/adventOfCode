---
follows: data/d09_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2015): All in a Single Night

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/9):

{( part1|}

Every year, Santa manages to deliver all of his presents in a single night.

This year, however, he has some new locations to visit; his elves have provided him the distances between every pair of locations. He can start and end at any two (different) locations he wants, but he must visit each location exactly once. What is the shortest distance he can travel to achieve this?

For example, given the following distances:

```
London to Dublin = 464
London to Belfast = 518
Dublin to Belfast = 141
```

The possible routes are therefore:

```
Dublin -> London -> Belfast = 982
London -> Dublin -> Belfast = 605
London -> Belfast -> Dublin = 659
Dublin -> Belfast -> London = 659
Belfast -> Dublin -> London = 605
Belfast -> London -> Dublin = 982
```

The shortest of these is London -> Dublin -> Belfast = 605, and so the answer is 605 in this example.

What is the distance of the shortest route?

{| part1 )}

This is a variation of the travelling salesperson problem but over a fully connected graph (i.e. all locations are directly connected to all others) and no requirement to return to the start location.

## Data Structure and Parsing

We can store the distances between locations in a dictionary and populate it after parsing the input:

```elm {l}
type alias DistanceTable =
    Dict ( String, String ) Int


distanceTable : DistanceTable
distanceTable =
    let
        parseLine dText =
            case String.words dText of
                [ place1, "to", place2, "=", dist ] ->
                    Dict.insert ( place1, place2 ) (AOC.toInt dist)
                        >> Dict.insert ( place2, place1 ) (AOC.toInt dist)

                _ ->
                    identity
    in
    List.foldl parseLine Dict.empty puzzleInput


distance : String -> String -> Int
distance loc1 loc2 =
    Dict.get ( loc1, loc2 ) distanceTable |> Maybe.withDefault 0
```

## Optimal Path

To find the shortest path, we will need to compare alternatives and calculate the total distance of each.
The `bestDistance` function abstracts the operation required to calculate the optimum (`op`) so that, for example, `min` or `max` could be applied.

```elm {l}
totalDist : List String -> Int -> Int
totalDist journeys total =
    case journeys of
        [] ->
            total

        loc1 :: loc2 :: tl ->
            totalDist (loc2 :: tl) (total + distance loc1 loc2)

        _ :: [] ->
            total


bestDistance : (Int -> Int -> Int) -> List (List String) -> Int -> Int
bestDistance op journeys bestDist =
    case journeys of
        [] ->
            bestDist

        hd :: tl ->
            bestDistance op tl (op bestDist (totalDist hd 0))
```

Finally we need to find all permutations of journeys between unique locations in the distance table.

```elm {l}
locations : DistanceTable -> List String
locations =
    let
        addUnique item list =
            if List.member item list then
                list

            else
                item :: list
    in
    Dict.foldl (\edge _ items -> addUnique (Tuple.first edge) items) []
```

```elm {l r}
part1 : Int
part1 =
    bestDistance min (locations distanceTable |> AOC.permutations) 99999
```

{( part2 |}

The next year, just to show off, Santa decides to take the route with the longest distance instead.

He can still start and end at any two (different) locations he wants, and he still must visit each location exactly once.

For example, given the distances above, the longest route would be 982 via (for example) Dublin -> London -> Belfast.

What is the distance of the longest route?

{| part2 )}

Almost identical to the first part, except we use the `max` rather than `min` function to calculate the optimum journey.

```elm {l r}
part2 : Int
part2 =
    bestDistance max (locations distanceTable |> AOC.permutations) 0
```
