---
follows: data/d09_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 9 (2015): All in a Single Night

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/9):

{( part1|}

What is the distance of the shortest route?

{| part1 )}

This is a variation of the travelling salesperson problem but over a fully connected graph (i.e. all locations are directly connected to all others) and no requirement to return to the start location.

## Data Structure and Parsing

We can store the distances between locations in a dictionary and populate it after parsing the input:

```elm {l}
type alias DistanceTable =
    Dict ( String, String ) Int


distanceTable : DistanceTable
distanceTable =
    let
        parseLine dText =
            case String.words dText of
                [ place1, "to", place2, "=", dist ] ->
                    Dict.insert ( place1, place2 ) (AOC.toInt dist)
                        >> Dict.insert ( place2, place1 ) (AOC.toInt dist)

                _ ->
                    identity
    in
    List.foldl parseLine Dict.empty puzzleInput


distance : String -> String -> Int
distance loc1 loc2 =
    Dict.get ( loc1, loc2 ) distanceTable |> Maybe.withDefault 0
```

## Optimal Path

To find the shortest path, we will need to compare alternatives and calculate the total distance of each.
The `bestDistance` function abstracts the operation required to calculate the optimum (`op`) so that, for example, `min` or `max` could be applied.

```elm {l}
totalDist : List String -> Int -> Int
totalDist journeys total =
    case journeys of
        [] ->
            total

        loc1 :: loc2 :: tl ->
            totalDist (loc2 :: tl) (total + distance loc1 loc2)

        _ :: [] ->
            total


bestDistance : (Int -> Int -> Int) -> List (List String) -> Int -> Int
bestDistance op journeys bestDist =
    case journeys of
        [] ->
            bestDist

        hd :: tl ->
            bestDistance op tl (op bestDist (totalDist hd 0))
```

Finally we need to find all permutations of journeys between unique locations in the distance table.

```elm {l}
locations : DistanceTable -> List String
locations =
    let
        addUnique item list =
            if List.member item list then
                list

            else
                item :: list
    in
    Dict.foldl (\edge _ items -> addUnique (Tuple.first edge) items) []
```

```elm {l r}
part1 : Int
part1 =
    bestDistance min (locations distanceTable |> AOC.permutations) 99999
```

{( part2 |}

What is the distance of the longest route?

{| part2 )}

Almost identical to the first part, except we use the `max` rather than `min` function to calculate the optimum journey.

```elm {l r}
part2 : Int
part2 =
    bestDistance max (locations distanceTable |> AOC.permutations) 0
```
