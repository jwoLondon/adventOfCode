---
follows: data/d02_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2015): I Was Told There Would Be No Math

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/2).

{( part1|}

All numbers in the elves' list are in feet. **How many total square feet of wrapping paper should they order?**

{| part1 )}

First let's extract the three dimensions of a present description as a 3-tuple.

```elm {l}
dimensions : String -> ( Int, Int, Int )
dimensions dimensionText =
    case String.split "x" dimensionText |> List.map AOC.toInt of
        [ l, w, h ] ->
            ( l, w, h )

        _ ->
            ( 0, 0, 0 )
```

Now we can easily calculate the area of paper required given the three dimensions of a present:

```elm {l}
paperArea : ( Int, Int, Int ) -> Int
paperArea ( l, w, h ) =
    2 * (l * w + w * h + h * l) + smallestArea l w h


smallestArea : Int -> Int -> Int -> Int
smallestArea l w h =
    case List.sort [ l, w, h ] of
        [ d1, d2, d3 ] ->
            d1 * d2

        _ ->
            0
```

Apply area measurement to all presents:

```elm {l r}
part1 : Int
part1 =
    puzzleInput |> List.map (dimensions >> paperArea) |> List.sum
```

{( part2 |}

**How many total feet of ribbon should they order?**

{| part2 )}

A similar process can be applied to calculate the perimeter of the smallest side:

```elm {l}
ribbonLength : ( Int, Int, Int ) -> Int
ribbonLength ( l, w, h ) =
    l * w * h + smallestPerimeter l w h


smallestPerimeter : Int -> Int -> Int -> Int
smallestPerimeter l w h =
    case List.sort [ l, w, h ] of
        [ d1, d2, d3 ] ->
            2 * (d1 + d2)

        _ ->
            0
```

Again, we apply the calculation to every present in the list:

```elm {l r}
part2 : Int
part2 =
    puzzleInput |> List.map (dimensions >> ribbonLength) |> List.sum
```
