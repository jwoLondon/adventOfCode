---
follows: data/d02_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2015): I Was Told There Would Be No Math

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/2):

{( part1|}

The elves are running low on wrapping paper, and so they need to submit an order for more. They have a list of the dimensions (length l, width w, and height h) of each present, and only want to order exactly as much as they need.

Fortunately, every present is a box (a perfect right rectangular prism), which makes calculating the required wrapping paper for each gift a little easier: find the surface area of the box, which is 2*l*w + 2*w*h + 2*h*l. The elves also need a little extra paper for each present: the area of the smallest side.

For example:

- A present with dimensions 2x3x4 requires 2*6 + 2*12 + 2\*8 = 52 square feet of wrapping paper plus 6 square feet of slack, for a total of 58 square feet.
- A present with dimensions 1x1x10 requires 2*1 + 2*10 + 2\*10 = 42 square feet of wrapping paper plus 1 square foot of slack, for a total of 43 square feet.

All numbers in the elves' list are in feet. How many total square feet of wrapping paper should they order?

{| part1 )}

First let's extract the three dimensions of a present description as a 3-tuple.

```elm {l}
dimensions : String -> ( Int, Int, Int )
dimensions dimensionText =
    case String.split "x" dimensionText |> List.map toInt of
        [ l, w, h ] ->
            ( l, w, h )

        _ ->
            ( 0, 0, 0 )
```

Now we can easily calculate the area of paper required given the three dimensions of a present:

```elm {l}
paperArea : ( Int, Int, Int ) -> Int
paperArea ( l, w, h ) =
    2 * (l * w + w * h + h * l) + smallestArea l w h


smallestArea : Int -> Int -> Int -> Int
smallestArea l w h =
    case List.sort [ l, w, h ] of
        [ d1, d2, d3 ] ->
            d1 * d2

        _ ->
            0
```

Apply area measurement to all presents:

```elm {l r}
part1 : Int
part1 =
    puzzleInput |> List.map (dimensions >> paperArea) |> List.sum
```

{( part2 |}

The elves are also running low on ribbon. Ribbon is all the same width, so they only have to worry about the length they need to order, which they would again like to be exact.

The ribbon required to wrap a present is the shortest distance around its sides, or the smallest perimeter of any one face. Each present also requires a bow made out of ribbon as well; the feet of ribbon required for the perfect bow is equal to the cubic feet of volume of the present. Don't ask how they tie the bow, though; they'll never tell.

For example:

- A present with dimensions 2x3x4 requires 2+2+3+3 = 10 feet of ribbon to wrap the present plus 2*3*4 = 24 feet of ribbon for the bow, for a total of 34 feet.
- A present with dimensions 1x1x10 requires 1+1+1+1 = 4 feet of ribbon to wrap the present plus 1*1*10 = 10 feet of ribbon for the bow, for a total of 14 feet.

How many total feet of ribbon should they order?

{| part2 )}

A similar process can be applied to calculate the perimter of the smallest side:

```elm {l}
ribbonLength : ( Int, Int, Int ) -> Int
ribbonLength ( l, w, h ) =
    l * w * h + smallestPerimeter l w h


smallestPerimeter : Int -> Int -> Int -> Int
smallestPerimeter l w h =
    case List.sort [ l, w, h ] of
        [ d1, d2, d3 ] ->
            2 * (d1 + d2)

        _ ->
            0
```

Again, we apply the calculation to every present in the list:

```elm {l r}
part2 : Int
part2 =
    puzzleInput |> List.map (dimensions >> ribbonLength) |> List.sum
```
