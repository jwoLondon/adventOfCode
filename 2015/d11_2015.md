---
follows: data/d11_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 11 (2015): Corporate Policy

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/11):

{( part1|}

Santa's previous password expired, and he needs help choosing a new one.

To help him remember his new password after the old one expires, Santa has devised a method of coming up with a password based on the previous one.
Corporate policy dictates that passwords must be exactly eight lowercase letters (for security reasons), so he finds his new password by incrementing his old password string repeatedly until it is valid.

Incrementing is just like counting with numbers: xx, xy, xz, ya, yb, and so on.
Increase the rightmost letter one step; if it was z, it wraps around to a, and repeat with the next letter to the left until one doesn't wrap around.

Unfortunately for Santa, a new Security-Elf recently started, and he has imposed some additional password requirements:

- Passwords must include one increasing straight of at least three letters, like abc, bcd, cde, and so on, up to xyz. They cannot skip letters; abd doesn't count.
- Passwords may not contain the letters i, o, or l, as these letters can be mistaken for other characters and are therefore confusing.
- Passwords must contain at least two different, non-overlapping pairs of letters, like aa, bb, or zz.

For example:

- `hijklmmn` meets the first requirement (because it contains the straight hij) but fails the second requirement requirement (because it contains i and l).
- `abbceffg` meets the third requirement (because it repeats bb and ff) but fails the first requirement.
- `abbcegjk` fails the third requirement, because it only has one double letter (bb).
- The next password after `abcdefgh` is `abcdffaa`.
- The next password after `ghijklmn` is `ghjaabcc`, because you eventually skip all the passwords that start with ghi..., since i is not allowed.

Given Santa's current password (your puzzle input), what should his next password be?

{| part1 )}

## Encoding Password Rules

We can start by representing the three password rules as predicate functions using regex to match valid passwords:

```elm {l}
isValid : String -> Bool
isValid pwd =
    let
        hasNoIOL =
            not << AOC.contains "[iol]"

        hasStraight =
            AOC.contains "(abc|bcd|cde|def|efg|fgh|pqr|qrs|rst|stu|tuv|uvw|vwx|wxy|xyz)"

        hasTwoPairs =
            AOC.contains "(.)\\1.*((?!\\1).)\\2"
    in
    hasStraight pwd && hasTwoPairs pwd && hasNoIOL pwd
```

## Incrementing a password

To find the next password we will need to increment the character(s) of the password, 'carrying' a character if the character to increment is a 'z'.

```elm {l}
nextPwd : String -> String
nextPwd pwd =
    let
        increment carry oldPwd newPwd =
            case oldPwd of
                [] ->
                    newPwd

                hd :: tl ->
                    if carry then
                        if hd == 'z' then
                            increment True tl ('a' :: newPwd)

                        else
                            increment False tl (Char.fromCode (Char.toCode hd + 1) :: newPwd)

                    else
                        increment False tl (hd :: newPwd)
    in
    String.fromList (increment True (String.toList pwd |> List.reverse) [])
```

## Validating passwords

We now just have to keep incrementing the password until we generate a valid one.

```elm {l}
nextValid : String -> String
nextValid pwd =
    if isValid pwd then
        pwd

    else
        nextValid (nextPwd pwd)
```

```elm {l r}
part1 : String
part1 =
    nextValid puzzleInput
```

{( part2 |}

Santa's password expired again. What's the next one?

{| part2 )}

```elm {l r}
part2 : String
part2 =
    nextValid (nextPwd part1)
```
