---
follows: data/d05_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2015): Doesn't He Have Intern-Elves For This?

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/5).

{( part1|}

How many strings are nice?

{| part1 )}

Let's start by breaking down the individual rules that make a string nice using regular expressions with groups.

```elm {l}
hasAtLeast3Vowels : String -> Bool
hasAtLeast3Vowels =
    AOC.contains "(.*[aeiou]){3}"


hasRepeats : String -> Bool
hasRepeats =
    AOC.contains "(.)\\1"


notProscribed : String -> Bool
notProscribed =
    not << AOC.contains "ab|cd|pq|xy"
```

Then it is easy to identify nice strings that satisfy all three constraints.

```elm {l r}
part1 : Int
part1 =
    let
        isNice text =
            hasAtLeast3Vowels text && hasRepeats text && notProscribed text
    in
    puzzleInput |> List.filter isNice |> List.length
```

{( part2 |}

How many strings are nice under these new rules?

{| part2 )}

We can specify the two new rules again with regex that use groups:

```elm {l}
hasABA : String -> Bool
hasABA =
    AOC.contains "(.).\\1"


hasRepeatPair : String -> Bool
hasRepeatPair =
    AOC.contains "(..).*\\1"
```

```elm {l r}
part2 : Int
part2 =
    let
        isNice text =
            hasABA text && hasRepeatPair text
    in
    puzzleInput |> List.filter isNice |> List.length
```
