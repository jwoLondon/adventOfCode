---
follows: data/d03_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2015): Perfectly Spherical Houses in a Vacuum

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/3).

{( part1|}

**How many houses receive at least one present?**

{| part1 )}

To keep track of the number of times each location is visited, let's store locations in a dictionary with the key being a 2d location and the value being the number of times visited.

```elm {l}
type alias Location =
    ( Int, Int )


type alias Visits =
    Dict Location Int
```

We need to be able to move locations depending on the move instruction symbol:

```elm {l}
move : Char -> Location -> Location
move instruction position =
    let
        ( x, y ) =
            position
    in
    case instruction of
        '<' ->
            ( x - 1, y )

        '>' ->
            ( x + 1, y )

        'v' ->
            ( x, y + 1 )

        '^' ->
            ( x, y - 1 )

        _ ->
            ( x, y )
```

We can build up the dictionary of visits by processing the move instructions.
This uses `addToFreqTable`, defined in the general advent of code utilities page.

```elm {l}
dropPresents : List Char -> Location -> Visits -> Visits
dropPresents instructions location visits =
    case instructions of
        [] ->
            AOC.addToFreqTable location visits

        hd :: tl ->
            dropPresents tl (move hd location) (AOC.addToFreqTable location visits)
```

The number of houses visited is simply the number of unique locations:

```elm {l r}
part1 : Int
part1 =
    Dict.empty
        |> dropPresents puzzleInput ( 0, 0 )
        |> Dict.size
```

{( part2 |}

**This year, how many houses receive at least one present?**

{| part2 )}

To separate Santa instructions from robo-Santa instructions we need to be able to extract every alternate symbol from the instruction list:

```elm {l}
thinList : List a -> List a
thinList list =
    if List.length list == 0 then
        []

    else
        List.take 1 list ++ thinList (List.drop 2 list)
```

We can then apply the same procedure as for part one, but to the thinned list twice â€“ once for the Santa instructions and once, offset by one instruction, for the robo-Santa instructions:

```elm {l r}
part2 : Int
part2 =
    Dict.empty
        |> dropPresents (puzzleInput |> thinList) ( 0, 0 )
        |> dropPresents (puzzleInput |> List.drop 1 |> thinList) ( 0, 0 )
        |> Dict.size
```
