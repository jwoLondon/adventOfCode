---
follows: data/d03_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2015): Perfectly Spherical Houses in a Vacuum

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/3):

{( part1|}

Santa is delivering presents to an infinite two-dimensional grid of houses.

He begins by delivering a present to the house at his starting location, and then an elf at the North Pole calls him via radio and tells him where to move next. Moves are always exactly one house to the north (`^`), south (`v`), east (`>`), or west (`<`). After each move, he delivers another present to the house at his new location.

However, the elf back at the north pole has had a little too much eggnog, and so his directions are a little off, and Santa ends up visiting some houses more than once. How many houses receive at least one present?

For example:

- `>` delivers presents to 2 houses: one at the starting location, and one to the east.
- `>^>v<` delivers presents to 4 houses in a square, including twice to the house at his starting/ending location.
- `>^v^v^v^v^v` delivers a bunch of presents to some very lucky children at only 2 houses.

{| part1 )}

To keep track of the number of times each location is visited, let's store locations in a dictionary with the key being a 2d location and the value being the number of times visited.

```elm {l}
type alias Location =
    ( Int, Int )


type alias Visits =
    Dict Location Int
```

We need to be able to move locations depending on the move instruction symbol:

```elm {l}
move : Char -> Location -> Location
move instruction position =
    let
        ( x, y ) =
            position
    in
    case instruction of
        '<' ->
            ( x - 1, y )

        '>' ->
            ( x + 1, y )

        'v' ->
            ( x, y + 1 )

        '^' ->
            ( x, y - 1 )

        _ ->
            ( x, y )
```

We can build up the dictionary of visits by processing the move instructions.
This uses `addToFreqTable`, defined in the general advent of code utilities page.

```elm {l}
dropPresents : List Char -> Location -> Visits -> Visits
dropPresents instructions location visits =
    case instructions of
        [] ->
            addToFreqTable location visits

        hd :: tl ->
            dropPresents tl (move hd location) (addToFreqTable location visits)
```

The number of houses visited is simply the number of unique locations:

```elm {l r}
part1 : Int
part1 =
    Dict.empty
        |> dropPresents puzzleInput ( 0, 0 )
        |> Dict.size
```

{( part2 |}

The next year, to speed up the process, Santa creates a robot version of himself, Robo-Santa, to deliver presents with him.

Santa and Robo-Santa start at the same location (delivering two presents to the same starting house), then take turns moving based on instructions from the elf, who is eggnoggedly reading from the same script as the previous year.

This year, how many houses receive at least one present?

For example:

- `^v` delivers presents to 3 houses, because Santa goes north, and then Robo-Santa goes south.
- `^>v<` now delivers presents to 3 houses, and Santa and Robo-Santa end up back where they started.
- `^v^v^v^v^v` now delivers presents to 11 houses, with Santa going one direction and Robo-Santa going the other.

{| part2 )}

To separate Santa instructions from robo-Santa instructions we need to be able to extract every alternate symbol from the instruction list:

```elm {l}
thinList : List a -> List a
thinList list =
    if List.length list == 0 then
        []

    else
        List.take 1 list ++ thinList (List.drop 2 list)
```

We can then apply the same procedure as for part one, but to the thinned list twice â€“ once for the Santa instructions and once, offset by one instruction, for the robo-Santa instructions:

```elm {l r}
part2 : Int
part2 =
    Dict.empty
        |> dropPresents (puzzleInput |> thinList) ( 0, 0 )
        |> dropPresents (puzzleInput |> List.drop 1 |> thinList) ( 0, 0 )
        |> Dict.size
```
