---
follows: data/d19_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 19 (2015): Medicine for Rudolph

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/19).

{( part1|}

How many distinct molecules can be created after all the different ways you can do one replacement on the medicine molecule?

{| part1 )}

Let's first read the puzzle input file and store the atom conversion rules. Each rule is a lookup between a named atom and a list of replacement molecules, so is conveniently stored in a dictionary.

```elm {l}
type alias ReplacementRules =
    Dict String (List String)
```

We can parse the input text with some simple regex:

```elm {l}
parse : List String -> ReplacementRules
parse input =
    let
        parseLine text rules =
            let
                append newV v =
                    case v of
                        Nothing ->
                            Just [ newV ]

                        Just val ->
                            Just (newV :: val)
            in
            case AOC.submatches "(\\w+) => (\\w+)" text of
                [ Just m0, Just molecules ] ->
                    Dict.update m0 (append molecules) rules

                _ ->
                    rules
    in
    List.foldl parseLine Dict.empty input
```

The approach to the first part of the puzzle is quite simple. We just need to apply all of the replacement rules to each of the atoms that make up the (input) medicine, store the resulting molecules in a set (to eliminate duplicates), and count the number of distinct molecules at the end of all replacements.

We can identify individual atoms in a molecule as each starts with a capital letter and is followed by zero or more lower-case letters (e.g. `H`, `Ca`, `Rn`).

```elm {l}
calibrate : String -> ReplacementRules -> List String
calibrate molecule rules =
    let
        selections =
            (AOC.match "[A-Z][a-z]*" >> AOC.selectSplit)
                >> List.map (AOC.mapTriplet String.concat identity String.concat)
    in
    generate (selections molecule) rules [] |> AOC.unique


generate : List ( String, String, String ) -> ReplacementRules -> List String -> List String
generate selections rules molecules =
    case selections of
        [] ->
            molecules

        selection :: tl ->
            generate tl rules (molecules ++ replacements selection rules)


replacements : ( String, String, String ) -> ReplacementRules -> List String
replacements ( left, atom, right ) rules =
    let
        newMolecules =
            Dict.get atom rules
                |> Maybe.withDefault []
    in
    List.map (\m -> left ++ m ++ right) newMolecules
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput |> parse |> calibrate medicine |> List.length
```

{( part2 |}

How long will it take to make the medicine? Given the available replacements and the medicine molecule in your puzzle input, what is the fewest number of steps to go from e to the medicine molecule?

{| part2 )}

## Approach

There are several approaches that could be used to solving this much harder task of generating the medicine.
The first, and most general is to enumerate all possible transformations of `e` until we get the desired molecule. Relatedly, we could find all reverse transformations away from our desired molecule until we get back to e.

Both apply a brute force approach that has worked in many other puzzles. While general, and really only a minor modification of other solutions, the problem here is that there are way too many possible pathways to calculate in a practical time.

Looking at the [puzzle input](data/d19_2015.md), it can be seen that none of the generated molecules (right hand side of the lookup rules) are subsets of any others. This allows us to employ a greedy reverse transformations, substituting the longest molecules first, until we end up with _e_, counting the iterations. This is much quicker to calculate (milliseconds) because the tree of possible transformations is radically pruned as transformations are greedily applied.

So, let's first create a 'reverse lookup', ordered by length that for any given molecule, will tell us which molecule can be directly transformed into it. The lookup dictionary is transformed into a list of tuples (`revDict`) so it can be sorted by length of molecule:

```elm {l}
type alias RevReplacementRules =
    List ( String, String )


reverseLookup : ReplacementRules -> RevReplacementRules -> RevReplacementRules
reverseLookup dict revDict =
    let
        byLength ( k1, v1 ) ( k2, v2 ) =
            compare (String.length k1) (String.length k2)

        add key values rDict =
            case values of
                [] ->
                    rDict

                hd :: tl ->
                    add key tl (( hd, key ) :: rDict)
    in
    case Dict.toList dict of
        [] ->
            revDict |> List.sortWith byLength |> List.reverse

        ( key, values ) :: tl ->
            reverseLookup (Dict.fromList tl) (add key values revDict)
```

We can traverse backwards from the medicine molecule to _e_, keeping track of the number of steps:

```elm {l}
traceback : RevReplacementRules -> String -> Int -> ( Int, String )
traceback revRules compound n =
    if compound == "e" then
        ( n, "e" )

    else
        let
            ( m, newCompound ) =
                back revRules compound 0
        in
        if newCompound == compound then
            ( n + m, compound )

        else
            traceback revRules newCompound (n + m)


back : RevReplacementRules -> String -> Int -> ( Int, String )
back revRules compound n =
    case revRules of
        [] ->
            ( n, compound )

        ( k, v ) :: tl ->
            back tl (AOC.replace k v compound) (n + List.length (AOC.match k compound))
```

```elm {l r}
part2 : Int
part2 =
    traceback (reverseLookup (parse puzzleInput) []) medicine 0
        |> Tuple.first
```
