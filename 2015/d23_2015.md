---
follows: data/d23_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 23 (2015): Opening the Turing Lock

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/23):

{( part1|}

Little Jane Marie just got her very first computer for Christmas from some unknown benefactor. It comes with instructions and an example program, but the computer itself seems to be malfunctioning. She's curious what the program does, and would like you to help her run it.

The manual explains that the computer supports two registers and six instructions (truly, it goes on to remind the reader, a state-of-the-art technology). The registers are named a and b, can hold any non-negative integer, and begin with a value of 0. The instructions are as follows:

- hlf r sets register r to **half** its current value, then continues with the next instruction.
- tpl r sets register r to **triple** its current value, then continues with the next instruction.
- inc r **increments** register r, adding 1 to it, then continues with the next instruction.
- jmp offset is a **jump**; it continues with the instruction offset away **relative to itself**.
- jie r, offset is like jmp, but only jumps if register r is **even** ("jump if even").
- jio r, offset is like jmp, but only jumps if register r is 1 ("jump if **one**", not odd).

All three jump instructions work with an **offset** relative to that instruction. The offset is always written with a prefix + or - to indicate the direction of the jump (forward or backward, respectively). For example, jmp +1 would simply continue with the next instruction, while jmp +0 would continuously jump back to itself forever.

The program exits when it tries to run an instruction beyond the ones defined.

For example, this program sets a to 2, because the jio instruction causes it to skip the tpl instruction:

    inc a
    jio a, +2
    tpl a
    inc a

**What is the value in register b when the program in your puzzle input is finished executing?**

{| part1 )}

## Data Structures

The two registers can be represented as a dictionary (allowing a greater number of registers to be used if we needed to) and a custom type to define the instruction set.

The program itself (parsed from the puzzle input data) is conveniently represented with an Array as we will need to make random-access jumps to different parts of it in response to the three jump instructions.

```elm {l}
type alias Registers =
    Dict Char Int


type Instruction
    = Hlf Char
    | Tpl Char
    | Inc Char
    | Jmp Int
    | Jie Char Int
    | Jio Char Int
```

## Parsing Input

A regex can be used to capture all possibilities of the limited instruction set. Frome this we can transform to custom types and build the program to run.

```elm {l}
parse : List String -> Array Instruction
parse =
    let
        toChar =
            String.toList >> List.head >> Maybe.withDefault 'X'

        parseLine text instructions =
            case submatches "(\\w+) ([ab]|[+-]\\d+)(?:, )?([+-]\\d+)?" text of
                [ Just "hlf", Just reg, Nothing ] ->
                    Hlf (toChar reg) :: instructions

                [ Just "tpl", Just reg, Nothing ] ->
                    Tpl (toChar reg) :: instructions

                [ Just "inc", Just reg, Nothing ] ->
                    Inc (toChar reg) :: instructions

                [ Just "jmp", Just offset, Nothing ] ->
                    Jmp (toInt offset) :: instructions

                [ Just "jie", Just reg, Just offset ] ->
                    Jie (toChar reg) (toInt offset) :: instructions

                [ Just "jio", Just reg, Just offset ] ->
                    Jio (toChar reg) (toInt offset) :: instructions

                _ ->
                    instructions
    in
    List.foldl parseLine [] >> List.reverse >> Array.fromList
```

## Approach

This is a fairly simple puzzle which we can solve by keeping a pointer to the current instruction in an array of instructions and processing the instruction at the pointer location. We continue to jump to locations in the array until an instruction takes us out of bounds (generating a `Nothing` when attempting to get a value from the array), at which point we halt and examine register b.

```elm {l}
run : Int -> Registers -> Array Instruction -> Registers
run pos registers program =
    let
        get regName =
            Dict.get regName registers |> Maybe.withDefault 0
    in
    case Array.get pos program of
        Just (Hlf reg) ->
            run (pos + 1) (Dict.insert reg (get reg // 2) registers) program

        Just (Tpl reg) ->
            run (pos + 1) (Dict.insert reg (get reg * 3) registers) program

        Just (Inc reg) ->
            run (pos + 1) (Dict.insert reg (get reg + 1) registers) program

        Just (Jmp offset) ->
            run (pos + offset) registers program

        Just (Jie reg offset) ->
            if modBy 2 (get reg) == 0 then
                run (pos + offset) registers program

            else
                run (pos + 1) registers program

        Just (Jio reg offset) ->
            if get reg == 1 then
                run (pos + offset) registers program

            else
                run (pos + 1) registers program

        Nothing ->
            registers
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> run 0 (Dict.fromList [ ( 'a', 0 ), ( 'b', 0 ) ])
        |> Dict.get 'b'
        |> Maybe.withDefault 0
```

{( part2 |}

The unknown benefactor is **very** thankful for releasi-- er, helping little Jane Marie with her computer. Definitely not to distract you, what is the value in register b after the program is finished executing if register a starts as 1 instead?

{| part2 )}

We just need to initialse the register `a` with a 1 and run the same program:

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> run 0 (Dict.fromList [ ( 'a', 1 ), ( 'b', 0 ) ])
        |> Dict.get 'b'
        |> Maybe.withDefault 0
```

## Reflection

There is something satisfying in building an assembly-language interpreter, even if it is as simple as the one specified in the question. Using custom types keeps the code clean and provides a template for future extensions to the language (assembunny code in 2016).
