---
follows: data/d14_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 14 (2015): Reindeer Olympics

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/14):

{( part1|}

This year is the Reindeer Olympics! Reindeer can fly at high speeds, but must rest occasionally to recover their energy. Santa would like to know which of his reindeer is fastest, and so he has them race.

Reindeer can only either be flying (always at their top speed) or resting (not moving at all), and always spend whole seconds in either state.

For example, suppose you have the following Reindeer:

- Comet can fly 14 km/s for 10 seconds, but then must rest for 127 seconds.
- Dancer can fly 16 km/s for 11 seconds, but then must rest for 162 seconds.

After one second, Comet has gone 14 km, while Dancer has gone 16 km. After ten seconds, Comet has gone 140 km, while Dancer has gone 160 km. On the eleventh second, Comet begins resting (staying at 140 km), and Dancer continues on for a total distance of 176 km. On the 12th second, both reindeer are resting. They continue to rest until the 138th second, when Comet flies for another ten seconds. On the 174th second, Dancer flies for another 11 seconds.

In this example, after the 1000th second, both reindeer are resting, and Comet is in the lead at 1120 km (poor Dancer has only gotten 1056 km by that point). So, in this situation, Comet would win (if the race ended at 1000 seconds).

Given the descriptions of each reindeer (in your puzzle input), after exactly 2503 seconds, what distance has the winning reindeer traveled?

{| part1 )}

## Data Structure and Parsing

We can store each reindeer's speed and endurance as a record and use regex to parse the data to populate them:

```elm {l}
type alias Reindeer =
    { speed : Int
    , duration : Int
    , rest : Int
    }


parse : List String -> List Reindeer
parse =
    let
        parseLine line =
            case submatches "(\\d+) km/s for (\\d+) seconds, but then must rest for (\\d+)" line of
                [ Just speed, Just duration, Just rest ] ->
                    (++) [ Reindeer (toInt speed) (toInt duration) (toInt rest) ]

                _ ->
                    identity
    in
    List.foldl parseLine []
```

## Running the race

Calculating how far a reindeer has moved after a given time is a simple arithmetic calculation:

```elm {l}
distanceAfter : Int -> Reindeer -> Int
distanceAfter time reindeer =
    let
        cycle =
            reindeer.duration + reindeer.rest
    in
    reindeer.speed * (reindeer.duration * (time // cycle) + min reindeer.duration (modBy cycle time))
```

And so the winner will be the reindeer that covers the greatest distance.

```elm {l r}
part1 : Int
part1 =
    parse puzzleInput
        |> List.map (distanceAfter 2503)
        |> List.maximum
        |> Maybe.withDefault -1
```

{( part2 |}

Seeing how reindeer move in bursts, Santa decides he's not pleased with the old scoring system.

Instead, at the end of each second, he awards one point to the reindeer currently in the lead. (If there are multiple reindeer tied for the lead, they each get one point.) He keeps the traditional 2503 second time limit, of course, as doing otherwise would be entirely ridiculous.

Given the example reindeer from above, after the first second, Dancer is in the lead and gets one point. He stays in the lead until several seconds into Comet's second burst: after the 140th second, Comet pulls into the lead and gets his first point. Of course, since Dancer had been in the lead for the 139 seconds before that, he has accumulated 139 points by the 140th second.

After the 1000th second, Dancer has accumulated 689 points, while poor Comet, our old champion, only has 312. So, with the new scoring system, Dancer would win (if the race ended at 1000 seconds).

Again given the descriptions of each reindeer (in your puzzle input), after exactly 2503 seconds, how many points does the winning reindeer have?

{| part2 )}

To use the modified rules, we need to replace `distanceAfter` with a function to calculate the points after a given time:

```elm {l}
pointsAt : Int -> List Int -> List Int
pointsAt time distances =
    let
        points d =
            if List.maximum distances == Just d then
                1

            else
                0
    in
    List.map points distances


pointsAfter : Int -> List Reindeer -> List Int
pointsAfter maxTime reindeer =
    let
        pa time tMax points deer =
            let
                distances =
                    List.map (distanceAfter time) deer

                newPoints =
                    List.map2 (+) points (pointsAt time distances)
            in
            if time == tMax then
                newPoints

            else
                pa (time + 1) tMax newPoints deer
    in
    pa 1 maxTime (List.repeat (List.length reindeer) 0) reindeer
```

```elm {l r}
part2 : Int
part2 =
    parse puzzleInput
        |> pointsAfter 2503
        |> List.maximum
        |> Maybe.withDefault -1
```
