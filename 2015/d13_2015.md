---
follows: data/d13_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 13 (2015): Knights of the Dinner Table

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/13).

{( part1|}

What is the total change in happiness for the optimal seating arrangement of the actual guest list?

{| part1 )}

## Approach

This is a similar to the [Day 9, 2015 puzzle](d09_2015.md) where the lowest cost of connecting a set of items is to be found. The variation is that 'edges' are directed (a so-called 'digraph' where the cost of A -> B is not necessarily the same as the cost of B -> A) and we have to connect back to the start.

Because the graph is directed, the number of permutations we need to test is greater than the previous case, but in principle we can apply the same approach of finding the happiness of every permutation of guests and selecting the one with greatest happiness.

## Data Structure and Parsing

As with the [Day 9, 2015 puzzle](d09_2015.md), we can store connections in a dictionary and populate it by parsing the input text.

```elm {l}
type alias NeighbourTable =
    Dict ( String, String ) Int


neighbourTable : NeighbourTable
neighbourTable =
    let
        tokens =
            String.slice 0 -1
                >> AOC.replace " would lose " ",-"
                >> AOC.replace " would gain " ","
                >> AOC.replace " happiness units by sitting next to " ","
                >> String.split ","

        entry strs =
            case strs of
                [ p1, hText, p2 ] ->
                    Dict.insert ( p1, p2 ) (AOC.toInt hText)

                _ ->
                    identity
    in
    puzzleInput
        |> List.map tokens
        |> List.foldl entry Dict.empty


happiness : String -> String -> Int
happiness p1 p2 =
    (Dict.get ( p1, p2 ) neighbourTable |> Maybe.withDefault 0)
        + (Dict.get ( p2, p1 ) neighbourTable |> Maybe.withDefault 0)
```

## Permuting Seating Plans

Similarly, to permute possible seating plans, we need to identify the list of people in our edge table:

```elm {l}
people : List String
people =
    let
        addUnique item list =
            if List.member item list then
                list

            else
                item :: list
    in
    Dict.foldl (\edge _ items -> addUnique (Tuple.first edge) items) [] neighbourTable
```

And we need to be able to calculate the total happiness of any given arrangement:

```elm {l}
maxHappiness : Int -> List (List String) -> Int
maxHappiness maxH seatingPlans =
    let
        totalHappiness seatingPlan total =
            case seatingPlan of
                [] ->
                    total

                p1 :: p2 :: tl ->
                    totalHappiness (p2 :: tl) (total + happiness p1 p2)

                _ :: [] ->
                    total
    in
    case seatingPlans of
        [] ->
            maxH

        hd :: tl ->
            maxHappiness (max maxH (totalHappiness hd 0)) tl
```

Now we can calculate all permutations of seating plans, make each of them cyclical and find the one with maximum happiness.

```elm {l r}
part1 : Int
part1 =
    people
        |> AOC.permutations
        |> AOC.makeCycle
        |> maxHappiness 0
```

{( part2 |}

What is the total change in happiness for the optimal seating arrangement that actually includes yourself?

{| part2 )}

Very similar to the first part, but we insert an extra 'happiness-neutral' person in the guest list:

```elm {l r}
part2 : Int
part2 =
    ("me" :: people)
        |> AOC.permutations
        |> AOC.makeCycle
        |> maxHappiness 0
```
