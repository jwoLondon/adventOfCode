---
follows: data/d08_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2015): Matchsticks

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/8).

{( part1|}

Disregarding the whitespace in the file, **what is the number of characters of code for string literals minus the number of characters in memory for the values of the strings in total for the entire file?**

{| part1 )}

## Approach

Parsing the input can be handled with regex. The mischievous fun in this question is that the patterns we are looking for are those that exist within the regex language itself as well as within Elm. There are therefore several levels of nested 'escape' characters (hence the 'matchsticks' of the title).

The question simply asks for the length of the decoded string not the string itself, which simplifies the problem as we donâ€™t have to worry about the contents of an escaped character sequence.

To avoid complications we first apply a search and replace to the input text replacing `"` with `~` and `\` with `/`.
This avoid unecessary escaping within our Elm code.

## Character conversion and counting

Because all sequences are encased in a pair of quotation marks, we can simply strip the first and last characters from a sequence (`shrink`). Because we only need to count the number of decoded characters we don't need to preserve symbols, just place holders, which we can represent with an `_`.

```elm {l}
shrink : String -> String
shrink =
    String.slice 1 -1


decode : String -> String
decode =
    AOC.replace "(/~)|(//)|(/x..)" "_" << shrink


numChars : List String -> Int
numChars =
    List.foldl ((+) << String.length) 0
```

We can then simply count the number of characters in the original and decoded text:

```elm {l r}
part1 : Int
part1 =
    numChars puzzleInput - numChars (List.map decode puzzleInput)
```

{( part2 |}

Your task is to find the total number of characters to represent the newly encoded strings minus the number of characters of code in each original string literal.

{| part2 )}

We now need to create an 'encoding' function that adds symbols equivalent to escape characters and encases the entire encoded string in two symbols representing outer quotations.

```elm {l}
encase : String -> String
encase text =
    "~" ++ text ++ "~"


encode : String -> String
encode =
    encase << AOC.replace "(~)|(/)" "__"
```

```elm {l r}
part2 : Int
part2 =
    numChars (List.map encode puzzleInput) - numChars puzzleInput
```
