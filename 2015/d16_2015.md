---
follows: data/d16_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 16 (2015): Aunt Sue

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/16):

{( part1|}

Your Aunt Sue has given you a wonderful gift, and you'd like to send her a thank you card. However, there's a small problem: she signed it "From, Aunt Sue".

You have 500 Aunts named "Sue".

So, to avoid sending the card to the wrong person, you need to figure out which Aunt Sue (which you conveniently number 1 to 500, for sanity) gave you the gift. You open the present and, as luck would have it, good ol' Aunt Sue got you a My First Crime Scene Analysis Machine! Just what you wanted. Or needed, as the case may be.

The My First Crime Scene Analysis Machine (MFCSAM for short) can detect a few specific compounds in a given sample, as well as how many distinct kinds of those compounds there are. According to the instructions, these are what the MFCSAM can detect:

- children, by human DNA age analysis.
- cats. It doesn't differentiate individual breeds.
- Several seemingly random breeds of dog: samoyeds, pomeranians, akitas, and vizslas.
- goldfish. No other kinds of fish.
- trees, all in one group.
- cars, presumably by exhaust or gasoline or something.
- perfumes, which is handy, since many of your Aunts Sue wear a few kinds.

In fact, many of your Aunts Sue have many of these. You put the wrapping from the gift into the MFCSAM. It beeps inquisitively at you a few times and then prints out a message on ticker tape:

```
children: 3
cats: 7
samoyeds: 2
pomeranians: 3
akitas: 0
vizslas: 0
goldfish: 5
trees: 3
cars: 2
perfumes: 1
```

You make a list of the things you can remember about each Aunt Sue. Things missing from your list aren't zero - you simply don't remember the value.

What is the number of the Sue that got you the gift?

{| part1 )}

## Data structures

Each 'Sue' has a name and a series of characteristics you remember about her. Each 'characterstic' comprises a thing (e.g. goldfish) and a count of the number of those things. So let's provide a data structure for capturing that information:

```elm {l}
type alias Sue =
    { name : String
    , characteristics : Dict String Int
    }
```

We can now represent the Sue detected by My First Crime Scene Analysis Machine:

```elm {l}
donor : Sue
donor =
    [ ( "children", 3 ), ( "cats", 7 ), ( "samoyeds", 2 ), ( "pomeranians", 3 ), ( "akitas", 0 ), ( "vizslas", 0 ), ( "goldfish", 5 ), ( "trees", 3 ), ( "cars", 2 ), ( "perfumes", 1 ) ]
        |> List.foldl (\( thing, count ) -> Dict.insert thing count) Dict.empty
        |> Sue "donor"
```

In order to detect whether any given Sue is the donor Sue, we can compare known characteristics:

```elm {l}
couldBe : Sue -> Sue -> Bool
couldBe sueA sueB =
    let
        couldMatch characteristics characteristic count =
            case Dict.get characteristic characteristics of
                Nothing ->
                    True

                Just numItems ->
                    numItems == count

        matched =
            Dict.filter (couldMatch sueB.characteristics) sueA.characteristics
    in
    Dict.size matched == Dict.size sueA.characteristics
```

We can now parse the input, extracting the Sue characteristics and checking to see if each could be the donor:

```elm {l}
parse : List String -> List Sue
parse =
    let
        regex =
            "(Sue \\d+): (\\w+): (\\d+)[,] (\\w+): (\\d+)[,] (\\w+): (\\d+)"

        parseLine text sues =
            case submatches regex text of
                [ Just name, Just t1, Just n1, Just t2, Just n2, Just t3, Just n3 ] ->
                    addSue name [ ( t1, toInt n1 ), ( t2, toInt n2 ), ( t3, toInt n3 ) ] :: sues

                _ ->
                    sues
    in
    List.foldl parseLine []


addSue : String -> List ( String, Int ) -> Sue
addSue name =
    List.foldl (\( thing, count ) -> Dict.insert thing count) Dict.empty
        >> Sue name
```

```elm {l r}
part1 : List String
part1 =
    puzzleInput
        |> parse
        |> List.filter (couldBe donor)
        |> List.map .name
```

{( part2 |}

As you're about to send the thank you note, something in the MFCSAM's instructions catches your eye. Apparently, it has an outdated retroencabulator, and so the output from the machine isn't exact values - some of them indicate ranges.

In particular, the cats and trees readings indicates that there are greater than that many (due to the unpredictable nuclear decay of cat dander and tree pollen), while the pomeranians and goldfish readings indicate that there are fewer than that many (due to the modial interaction of magnetoreluctance).

What is the number of the real Aunt Sue?

{| part2 )}

The only different for the second part is that we have to use a modified `couldBe` to account for the new rules:

```elm {l}
couldBe2 : Sue -> Sue -> Bool
couldBe2 sueA sueB =
    let
        couldMatch characteristics characteristic count =
            case Dict.get characteristic characteristics of
                Nothing ->
                    True

                Just numItems ->
                    case characteristic of
                        "cat" ->
                            numItems > count

                        "trees" ->
                            numItems > count

                        "pomeranians" ->
                            numItems < count

                        "goldfish" ->
                            numItems < count

                        _ ->
                            numItems == count

        matched =
            Dict.filter (couldMatch sueB.characteristics) sueA.characteristics
    in
    Dict.size matched == Dict.size sueA.characteristics
```

```elm {l r}
part2 : List String
part2 =
    puzzleInput
        |> parse
        |> List.filter (couldBe2 donor)
        |> List.map .name
```
