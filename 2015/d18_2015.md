---
follows: data/d18_2015.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 18 (2015): Like a GIF For Your Yard

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2015/day/18):

{( part1|}

After the million lights incident, the fire code has gotten stricter: now, at most ten thousand lights are allowed. You arrange them in a 100x100 grid.

Never one to let you down, Santa again mails you instructions on the ideal lighting configuration. With so few lights, he says, you'll have to resort to animation.

Start by setting your lights to the included initial configuration (your puzzle input). A # means "on", and a . means "off".

Then, animate your grid in steps, where each step decides the next configuration based on the current one. Each light's next state (either on or off) depends on its current state and the current states of the eight lights adjacent to it (including diagonals). Lights on the edge of the grid might have fewer than eight neighbors; the missing ones always count as "off".

For example, in a simplified 6x6 grid, the light marked A has the neighbors numbered 1 through 8, and the light marked B, which is on an edge, only has the neighbors marked 1 through 5:

```
1B5...
234...
......
..123.
..8A4.
..765.
```

The state a light should have next is based on its current state (on or off) plus the number of neighbors that are on:

- A light which is on stays on when 2 or 3 neighbors are on, and turns off otherwise.
- A light which is off turns on if exactly 3 neighbors are on, and stays off otherwise.

All of the lights update simultaneously; they all consider the same current state before moving to the next.

Here's a few steps from an example configuration of another 6x6 grid:

Initial state:

```
.#.#.#
...##.
#....#
..#...
#.#..#
####..
```

After 1 step:

```
..##..
..##.#
...##.
......
#.....
#.##..
```

After 2 steps:

```
..###.
......
..###.
......
.#....
.#....
```

After 3 steps:

```
...#..
......
...#..
..##..
......
......
```

After 4 steps:

```
......
......
..##..
..##..
......
......
```

After 4 steps, this example has four lights on.

In your grid of 100x100 lights, given your initial configuration, how many lights are on after 100 steps?

{| part1 )}

## Data Structures and Parsing

As with the similar [day 6, 2015 puzzle](d06_2015.md), we can represent lights as an array allowing us to query the state of the light at any arbitrary location. The only change is that we will be considering the neighbours of bordering cells (which are assumed to be 'off'), so we make minor modification to `lightAt`.

```elm {l}
type alias Lights =
    Array Int


type alias Location =
    ( Int, Int )


parse : List Char -> Lights
parse =
    let
        digits chr =
            case chr of
                '#' ->
                    1

                _ ->
                    0
    in
    List.map digits >> Array.fromList


gridSize : Int
gridSize =
    100


lightAt : Location -> Lights -> Int
lightAt ( x, y ) =
    if (x < 0) || (y < 0) || (x >= gridSize) || (y >= gridSize) then
        always 0

    else
        Array.get (y * gridSize + x) >> Maybe.withDefault 0


setLight : Location -> Int -> Lights -> Lights
setLight ( x, y ) value =
    Array.set (y * gridSize + x) value
```

## Celular Automata

The puzzle requires us to implement a [Game of Life](https://en.wikipedia.org/wiki/Conway's_Game_of_Life), applying simple rules to the on/off status of each light depending on the number of its neighbours that are on.
We could provide a simple function that reports the number of neighbouring lights that are on:

```elm {l siding}
numNeighbours : Location -> Lights -> Int
numNeighbours ( x, y ) lights =
    lightAt ( x - 1, y - 1 ) lights
        + lightAt ( x, y - 1 ) lights
        + lightAt ( x + 1, y - 1 ) lights
        + lightAt ( x - 1, y ) lights
        + lightAt ( x + 1, y ) lights
        + lightAt ( x - 1, y + 1 ) lights
        + lightAt ( x, y + 1 ) lights
        + lightAt ( x + 1, y + 1 ) lights
```

In anticipation of the second part of the puzzle, we can create a modified version of the function that takes as its first parameter, the light querying function.
This will allow us to change how we test for light state by providing a different function should we need to:

```elm {l}
numNeighbours : (Location -> Lights -> Int) -> Location -> Lights -> Int
numNeighbours lightFn ( x, y ) lights =
    lightFn ( x - 1, y - 1 ) lights
        + lightFn ( x, y - 1 ) lights
        + lightFn ( x + 1, y - 1 ) lights
        + lightFn ( x - 1, y ) lights
        + lightFn ( x + 1, y ) lights
        + lightFn ( x - 1, y + 1 ) lights
        + lightFn ( x, y + 1 ) lights
        + lightFn ( x + 1, y + 1 ) lights
```

This makes it easy to apply the 'Game of Life' rules that determines the state of a light dependent on its neighbour count:

```elm {l}
evolvedCell : (Location -> Lights -> Int) -> Location -> Lights -> Int
evolvedCell lightFn location lights =
    let
        neighbours =
            numNeighbours lightFn location lights
    in
    if lightFn location lights == 1 then
        if neighbours == 2 || neighbours == 3 then
            1

        else
            0

    else if neighbours == 3 then
        1

    else
        0
```

This allows us to evolve all the cells in the entire grid.
We can recursively update the position of the cell to evolve until we have reached the final cell in the bottom-right corner:

```elm {l}
evolvedGrid : (Location -> Lights -> Int) -> Location -> Lights -> Lights -> Lights
evolvedGrid lightFn ( x, y ) prevLights newLights =
    let
        nextLocation ( col, row ) =
            if col < gridSize - 1 then
                ( col + 1, row )

            else
                ( 0, row + 1 )
    in
    if x >= gridSize || y >= gridSize then
        newLights

    else
        evolvedGrid lightFn
            (nextLocation ( x, y ))
            prevLights
            (setLight ( x, y ) (evolvedCell lightFn ( x, y ) prevLights) newLights)
```

Finally, we can evolve the grid a fixed number of times to solve the puzzle:

```elm {l}
evolve : (Location -> Lights -> Int) -> Int -> Lights -> Lights
evolve lightFn numEvolutions lights =
    let
        emptyGrid =
            Array.repeat (gridSize * gridSize) 0
    in
    if numEvolutions == 0 then
        lights

    else
        evolve lightFn (numEvolutions - 1) (evolvedGrid lightFn ( 0, 0 ) lights emptyGrid)
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput |> parse |> evolve lightAt 100 |> Array.toList |> List.sum
```

{( part2 |}

You flip the instructions over; Santa goes on to point out that this is all just an implementation of Conway's Game of Life. At least, it was, until you notice that something's wrong with the grid of lights you bought: four lights, one in each corner, are stuck on and can't be turned off. The example above will actually run like this:

Initial state:

```
##.#.#
...##.
#....#
..#...
#.#..#
####.#
```

After 1 step:

```
#.##.#
####.#
...##.
......
#...#.
#.####
```

After 2 steps:

```
#..#.#
#....#
.#.##.
...##.
.#..##
##.###
```

After 3 steps:

```
#...##
####.#
..##.#
......
##....
####.#
```

After 4 steps:

```
#.####
#....#
...#..
.##...
#.....
#.#..#
```

After 5 steps:

```
##.###
.##..#
.##...
.##...
#.#...
##...#
```

After 5 steps, this example now has 17 lights on.

In your grid of 100x100 lights, given your initial configuration, but with the four corners always in the on state, how many lights are on after 100 steps?

{| part2 )}

One way of minimising the code changes required for the second part is to modify `lightAt` so that if one of the corner light positions is queried, it always returns an 'on':

```elm {l}
lightAt2 : Location -> Lights -> Int
lightAt2 ( x, y ) =
    if (x < 0) || (y < 0) || (x >= gridSize) || (y >= gridSize) then
        always 0

    else if x == 0 && (y == 0 || y == gridSize - 1) then
        always 1

    else if x == gridSize - 1 && (y == 0 || y == gridSize - 1) then
        always 1

    else
        Array.get (y * gridSize + x) >> Maybe.withDefault 0
```

The final query of the lights array simply sums the values to count the number of on switches, so we must also ensure that the corner values are all set to 'on':

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> evolve lightAt2 100
        |> setLight ( 0, 0 ) 1
        |> setLight ( 0, gridSize - 1 ) 1
        |> setLight ( gridSize - 1, 0 ) 1
        |> setLight ( gridSize - 1, gridSize - 1 ) 1
        |> Array.toList
        |> List.sum
```
