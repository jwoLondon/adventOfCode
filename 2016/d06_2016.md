---
follows: data/d06_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 6 (2016): Signals and Noise

{( part1|}

Something is jamming your communications with Santa. Fortunately, your signal is only partially jammed, and protocol in situations like this is to switch to a simple repetition code to get the message through.

In this model, the same message is sent repeatedly. You've recorded the repeating message signal (your puzzle input), but the data seems quite corrupted - almost too badly to recover. **Almost**.

All you need to do is figure out which character is most frequent for each position. For example, suppose you had recorded the following messages:

eedadn
drvtee
eandsr
raavrd
atevrs
tsrnev
sdttsa
rasrtv
nssdts
ntnada
svetve
tesnvt
vntsnd
vrdear
dvrsen
enarar
The most common character in the first column is e; in the second, a; in the third, s, and so on. Combining these characters returns the error-corrected message, easter.

Given the recording in your puzzle input, **what is the error-corrected version** of the message being sent?

{| part1 )}

##Â Approach

As we can process columns independently we can simply build a letter frequency table column at a time. We can transpose the original list of strings to process columns as lists of characters and use the AoC built-in mode function for finding the most common letter in each.

```elm {l r}
part1 : String
part1 =
    puzzleInput
        |> List.map String.toList
        |> transpose
        |> List.map mode
        |> List.filterMap identity
        |> String.fromList
```

{( part2 |}

Of course, that **would** be the message - if you hadn't agreed to use a **modified repetition code** instead.

In this modified code, the sender instead transmits what looks like random data, but for each character, the character they actually want to send is **slightly less likely** than the others. Even after signal-jamming noise, you can look at the letter distributions in each column and choose the **least common** letter to reconstruct the original message.

In the above example, the least common character in the first column is a; in the second, d, and so on. Repeating this process for the remaining characters produces the original message, advent.

Given the recording in your puzzle input and this new decoding methodology, **what is the original message** that Santa is trying to send?

{| part2 )}

Almost identical to the first part except that we need to find the least common letter so need to adapt the `mode` function to do this.

```elm {l}
leastCommon : List comparable -> Maybe comparable
leastCommon =
    List.foldl addToFreqTable Dict.empty
        >> Dict.toList
        >> List.map (\( a, b ) -> ( b, a ))
        >> List.sort
        >> List.map Tuple.second
        >> List.head
```

```elm {l r}
part2 : String
part2 =
    puzzleInput
        |> List.map String.toList
        |> transpose
        |> List.map leastCommon
        |> List.filterMap identity
        |> String.fromList
```

## Reflection

A nice simple puzzle that ecourages thinking about elegant solutions. Initially I was tempted to create a single frequency table for storing all coloumns in the puzzle input, but it soon became clear this was not necessary nor particularly elegant. Reusing existing `transpose` and `mode` functions keeps code clear and compact.
