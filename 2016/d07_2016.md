---
follows: data/d07_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2016): Internet Protocol Version 7

{( part1|}

While snooping around the local network of EBHQ, you compile a list of IP addresses (they're IPv7, of course; IPv6 is much too limited). You'd like to figure out which IPs support TLS (transport-layer snooping).

An IP supports TLS if it has an Autonomous Bridge Bypass Annotation, or **ABBA**. An ABBA is any four-character sequence which consists of a pair of two different characters followed by the reverse of that pair, such as xyyx or abba. However, the IP also must not have an ABBA within any hypernet sequences, which are contained by **square brackets**.

For example:

- abba[mnop]qrst supports TLS (abba outside square brackets).
- abcd[bddb]xyyx does **not** support TLS (bddb is within square brackets, even though xyyx is outside square brackets).
- aaaa[qwer]tyui does **not** support TLS (aaaa is invalid; the interior characters must be different).
- ioxxoj[asdfgh]zxcvbn supports TLS (oxxo is outside square brackets, even though it's within a larger string).

**How many IPs** in your puzzle input support TLS?

{| part1 )}

## Approach

This looks like a problem well suited to using regular expressions. These can be used for (a) detecting hypernet and supernet components of each message (within and outside square brackets); (b) the ABBA sequences.

- Hypernets: match a non-captured open square bracket followed by a captured group of at least one word character that is not followed by another open square brackets, and is finally followed by a non-captured close square bracket.
- Supernets: one or more word characters that are not followed by an unmatched closing square bracket.
- ABBA: Match a character followed by one that is not the same followed by the second match and then the first.

```elm {l}
hypernets : String -> List String
hypernets =
    submatches "(?:\\[)(\\w*)(?:\\])" >> List.filterMap identity


supernets : String -> List String
supernets =
    match "\\w+(?![^\\[]*\\])"


hasABBA : String -> Bool
hasABBA =
    contains "(.)(?!\\1)(.)\\2\\1"
```

### Testing

We can test the super/hyper net matching against the provided examples and a few more to check for a wider range of edge cases.

```elm {l}
testMessages : List String
testMessages =
    [ "abba[mnop]qrst"
    , "abcd[bddb]xyyx"
    , "aaaa[qwer]tyui"
    , "ioxxoj[asdfgh]zxcvbn"
    , ""
    , "abba"
    , "[bccb]"
    , "[]cddc[]"
    , "[]ddddeeddddd[]"
    , "[]ef[]fe[]"
    , "[fgf]fggf[fgf]"
    , "[[ghg]ghhg[ghg]]"
    ]
```

```elm {l}
test : (String -> List String) -> List String -> List String
test fn =
    List.map fn
        >> List.map (List.intersperse " " >> String.concat)
        >> List.intersperse "\n"
```

#### Hypernets

^^^elm {m=(test hypernets testMessages)}^^^

#### Supernets

^^^elm {m=(test supernets testMessages)}^^^

```elm {l r}
part1 : Int
part1 =
    let
        isTLS address =
            (not << List.any hasABBA) (hypernets address) && List.any hasABBA (supernets address)
    in
    puzzleInput
        |> List.filter isTLS
        |> List.length
```

{( part2 |}

You would also like to know which IPs support SSL (super-secret listening).

An IP supports **SSL** if it has an Area-Broadcast Accessor, or **ABA**, anywhere in the supernet sequences (outside any square bracketed sections), and a corresponding Byte Allocation Block, or **BAB**, anywhere in the hypernet sequences. An ABA is any three-character sequence which consists of the same character twice with a different character between them, such as xyx or aba. A corresponding BAB is the same characters but in reversed positions: yxy and bab, respectively.

For example:

- aba[bab]xyz supports SSL (aba outside square brackets with corresponding bab within square brackets).
- xyx[xyx]xyx does **not** support SSL (xyx, but no corresponding yxy).
- aaa[kek]eke supports SSL (eke in supernet with corresponding kek in hypernet; the aaa sequence is not related, because the interior character must be different).
- zazbz[bzb]cdb supports SSL (zaz has no corresponding aza, but zbz has a corresponding bzb, even though zaz and zbz overlap).

**How many IPs** in your puzzle input support SSL?

{| part2 )}

## Approach

We can similarly use regular expressions to identify ABA and BAB sequences. But this time we need to detect if we have a paired ABA - BAB within the two elements of the sequence. We can identify the 'AB' part of all 'ABAs' in a sequnce and then see if there is any common two-letter strings between supernet AB(A)s and the reversed hypernet AB(A)

```elm {l}
abInABA : String -> List String
abInABA =
    Regex.find
        (Regex.fromString "(.)(?=(?!\\1)(.)(?=\\1))" |> Maybe.withDefault Regex.never)
        >> List.map (.submatches >> List.filterMap identity >> String.concat)


intersects : List comparable -> List comparable -> Bool
intersects xs ys =
    (not << Set.isEmpty) (Set.intersect (Set.fromList xs) (Set.fromList ys))


isSSL : String -> Bool
isSSL msg =
    intersects
        (supernets msg |> List.concatMap abInABA)
        (hypernets msg |> List.concatMap abInABA |> List.map String.reverse)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.filter isSSL
        |> List.length
```

## Reflection

Although it can be quite cryptic, I find regex easier to work with than the Elm parser. This problem cannot be entirely solved with regex though as for part two we need to compare a match in a supernet with a reversed match in the hypernet â€“ something not easily solved with seqential processing of regular expressions.
