---
follows: data/d07_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 7 (2016): Internet Protocol Version 7

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/7).

{( part1|}

**How many IPs** in your puzzle input support TLS?

{| part1 )}

This looks like a problem well suited to using regular expressions. These can be used for (a) detecting hypernet and supernet components of each message (within and outside square brackets); (b) the ABBA sequences.

- Hypernets: match a non-captured open square bracket followed by a captured group of at least one word character that is not followed by another open square brackets, and is finally followed by a non-captured close square bracket.
- Supernets: one or more word characters that are not followed by an unmatched closing square bracket.
- ABBA: Match a character followed by one that is not the same followed by the second match and then the first.

```elm {l}
hypernets : String -> List String
hypernets =
    AOC.submatches "(?:\\[)(\\w*)(?:\\])" >> List.filterMap identity


supernets : String -> List String
supernets =
    AOC.match "\\w+(?![^\\[]*\\])"


hasABBA : String -> Bool
hasABBA =
    AOC.contains "(.)(?!\\1)(.)\\2\\1"
```

## Testing

We can test the super/hyper net matching against the provided examples and a few more to check for a wider range of edge cases.

```elm {l}
testMessages : List String
testMessages =
    [ "abba[mnop]qrst"
    , "abcd[bddb]xyyx"
    , "aaaa[qwer]tyui"
    , "ioxxoj[asdfgh]zxcvbn"
    , ""
    , "abba"
    , "[bccb]"
    , "[]cddc[]"
    , "[]ddddeeddddd[]"
    , "[]ef[]fe[]"
    , "[fgf]fggf[fgf]"
    , "[[ghg]ghhg[ghg]]"
    ]
```

```elm {l}
test : (String -> List String) -> List String -> List String
test fn =
    List.map fn
        >> List.map (List.intersperse " " >> String.concat)
        >> List.intersperse "\n"
```

### Hypernets

^^^elm {m=(test hypernets testMessages)}^^^

### Supernets

^^^elm {m=(test supernets testMessages)}^^^

```elm {l r}
part1 : Int
part1 =
    let
        isTLS address =
            (not << List.any hasABBA) (hypernets address) && List.any hasABBA (supernets address)
    in
    puzzleInput
        |> List.filter isTLS
        |> List.length
```

{( part2 |}

**How many IPs** in your puzzle input support SSL?

{| part2 )}

We can similarly use regular expressions to identify ABA and BAB sequences. But this time we need to detect if we have a paired ABA - BAB within the two elements of the sequence. We can identify the 'AB' part of all 'ABAs' in a sequence and then see if there is any common two-letter strings between supernet AB(A)s and the reversed hypernet AB(A)

```elm {l}
abInABA : String -> List String
abInABA =
    Regex.find
        (Regex.fromString "(.)(?=(?!\\1)(.)(?=\\1))" |> Maybe.withDefault Regex.never)
        >> List.map (.submatches >> List.filterMap identity >> String.concat)


intersects : List comparable -> List comparable -> Bool
intersects xs ys =
    (not << Set.isEmpty) (Set.intersect (Set.fromList xs) (Set.fromList ys))


isSSL : String -> Bool
isSSL msg =
    intersects
        (supernets msg |> List.concatMap abInABA)
        (hypernets msg |> List.concatMap abInABA |> List.map String.reverse)
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.filter isSSL
        |> List.length
```

---

## Reflection

Although it can be quite cryptic, I find regex easier to work with than the Elm parser. This problem cannot be entirely solved with regex though as for part two we need to compare a match in a supernet with a reversed match in the hypernet â€“ something not easily solved with sequential processing of regular expressions.
