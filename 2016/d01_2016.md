---
follows: data/d01_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 1 (2016): No Time for a Taxicab

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/1):

{( part1|}

You're airdropped near Easter Bunny Headquarters in a city somewhere. "Near", unfortunately, is as close as you can get - the instructions on the Easter Bunny Recruiting Document the Elves intercepted start here, and nobody had time to work them out further.

The Document indicates that you should start at the given coordinates (where you just landed) and face North. Then, follow the provided sequence: either turn left (L) or right (R) 90 degrees, then walk forward the given number of blocks, ending at a new intersection.

There's no time to follow such ridiculous instructions on foot, though, so you take a moment and work out the destination. Given that you can only walk on the street grid of the city, how far is the shortest path to the destination?

For example:

- Following R2, L3 leaves you 2 blocks East and 3 blocks North, or 5 blocks away.
- R2, R2, R2 leaves you 2 blocks due South of your starting position, which is 2 blocks away.
- R5, L5, R5, R3 leaves you 12 blocks away.

**How many blocks away** is Easter Bunny HQ?

{| part1 )}

## Approach

A simple start would be to create a tuple for storing any grid location and direction of travel:

```elm {l}
type alias Taxi =
    -- (row,col,dir)
    ( Int, Int, Int )
```

The (Manhattan) distance from the start of any location will the sum of its absolute coordinates:

```elm {l}
distanceFromStart : Taxi -> Int
distanceFromStart ( x, y, _ ) =
    abs x + abs y
```

We also need to 'decode' the movement instructions. We can simplify things by representing movements following a left turn as negative numbers and movements following a right turn as positive. This allows us to store the entire set of instructions as a single list of integers:

```elm {l}
parse : String -> List Int
parse =
    let
        toMovement instr =
            case String.toList instr of
                'L' :: cs ->
                    -1 * (String.fromList cs |> toInt)

                'R' :: cs ->
                    String.fromList cs |> toInt

                _ ->
                    0
    in
    match "([LR]\\d+)" >> List.map toMovement
```

We can store the current direction as a number between 0 and 3 where 0 is 'north', 1 is 'east', 2 is 'south' and 3 is 'west'. So changing direction just involves adding 1 when turning right and subtracting 1 when turning left and then taking the modulus of 4:

```elm {l}
rotate : Int -> Int -> Int
rotate instr dir =
    if instr < 0 then
        modBy 4 (dir + 1)

    else
        modBy 4 (dir + 3)
```

We can move a taxi from any location to the next as a function of the number of steps to move (remembering that a negative value indicates a left turn and postive value a right turn)

```elm {l}
move : Int -> Taxi -> Taxi
move instr ( r, c, dir ) =
    case rotate instr dir of
        0 ->
            ( r, c - abs instr, 0 )

        1 ->
            ( r - abs instr, c, 1 )

        2 ->
            ( r, c + abs instr, 2 )

        _ ->
            ( r + abs instr, c, 3 )
```

Bringing it all together:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> List.foldl move ( 0, 0, 0 )
        |> distanceFromStart
```

{( part2 |}

Then, you notice the instructions continue on the back of the Recruiting Document. Easter Bunny HQ is actually at the first location you visit twice.

For example, if your instructions are R8, R4, R4, R8, the first location you visit twice is 4 blocks away, due East.

How many blocks away is the **first location you visit twice**?

{| part2 )}

We will need to keep track of previous visited locations in order to know if we visit one for a second time. Additionally, we need to know the locations of the intermeidate positions between each new instruction. So let's first modify `move` so it stores all locations that result from each instruction:

```elm {l}
moveInc : Int -> Taxi -> List ( Int, Int )
moveInc instr ( r, c, dir ) =
    case rotate instr dir of
        0 ->
            List.map (\n -> ( r, c - n )) (List.range 1 (abs instr))

        1 ->
            List.map (\n -> ( r - n, c )) (List.range 1 (abs instr))

        2 ->
            List.map (\n -> ( r, c + n )) (List.range 1 (abs instr))

        _ ->
            List.map (\n -> ( r + n, c )) (List.range 1 (abs instr))
```

Now we can keep track of these locations and stop if we find a repeat. As we need to terminate on a matched location rather than after all instructions have been processed, we use a recursive function rather than folding as we did in the first part. We can take advantage of the fact that after the first move, which can never involve a repeat visit, the list of previously visited cells will have a length of at least 2 (e.g. R1 would result in [(0,0),(0,1)]. We can therefore use a list with a single location to indicate the location of the first repeat.

```elm {l}
checkRepeats : List ( Int, Int ) -> List ( Int, Int ) -> List ( Int, Int )
checkRepeats newLocations oldLocations =
    case newLocations of
        [] ->
            oldLocations

        hd :: tl ->
            if List.member hd oldLocations then
                -- Revisited location
                [ hd ]

            else
                checkRepeats tl (hd :: oldLocations)
```

Finally we process all the instructions, checking the locations added by each new instriction for a repeat and terminating if one is found.

```elm {l}
makeMoves : Taxi -> List ( Int, Int ) -> List Int -> Int
makeMoves taxi visited instructions =
    case instructions of
        [] ->
            -1

        instr :: tl ->
            case checkRepeats (moveInc instr taxi) visited of
                [ ( x, y ) ] ->
                    distanceFromStart ( x, y, 0 )

                locs ->
                    makeMoves (move instr taxi) locs tl
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> makeMoves ( 0, 0, 0 ) [ ( 0, 0 ) ]
```

## Reflection

For an opening puzzle, this one is a little more involved than most. Even part one, which is a simpler version of [d03_2015](d03_2015.md) in that it does not require a dictionary to represent previous visits, required functions to turn instructions into new locations depending on in current direction and location.

90 degree rotations on a grid might suggest multiplying complex numbers, but to be concise this would require a complex number package which does not come as stanadard with Elm. While writing one isn't too hard, this would be an overengineered solution for this problem.

It would also be possible to break down the instruction to postion functions into _scale_ and _translate_ functions along with the existing _rotate_. This would make it clearer that the puzzle is really just the application fo a series of affine transformations.
