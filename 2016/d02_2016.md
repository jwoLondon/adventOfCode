---
follows: data/d02_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 2 (2016): Bathroom Security

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/2):

{( part1|}

You arrive at **Easter Bunny Headquarters** under cover of darkness. However, you left in such a rush that you forgot to use the bathroom! Fancy office buildings like this one usually have keypad locks on their bathrooms, so you search the front desk for the code.

"In order to improve security," the document you find says, "bathroom codes will no longer be written down. Instead, please memorize and follow the procedure below to access the bathrooms."

The document goes on to explain that each button to be pressed can be found by starting on the previous button and moving to adjacent buttons on the keypad: U moves up, D moves down, L moves left, and R moves right. Each line of instructions corresponds to one button, starting at the previous button (or, for the first line, **the "5" button**); press whatever button you're on at the end of each line. If a move doesn't lead to a button, ignore it.

You can't hold it much longer, so you decide to figure out the code as you walk to the bathroom. You picture a keypad like this:

    1 2 3
    4 5 6
    7 8 9

Suppose your instructions are:

    ULL
    RRDDD
    LURDL
    UUUUD

- You start at "5" and move up (to "2"), left (to "1"), and left (you can't, and stay on "1"), so the first button is 1.
- Starting from the previous button ("1"), you move right twice (to "3") and then down three times (stopping at "9" after two moves and ignoring the third), ending up with 9.
- Continuing from "9", you move left, up, right, down, and left, ending with 8.
- Finally, you move up four times (stopping at "2"), then down once, ending with 5.

So, in this example, the bathroom code is 1985.

Your puzzle input is the instructions from the document you found at the front desk. What is the **bathroom code**?

{| part1 )}

## Approach

This is a nice example of a simple [finite state machine](https://en.wikipedia.org/wiki/Finite-state_machine) (FSM), where that current state is the last button pressed (or button 5 at the start) and transitions to new states (buttons) are determined by the `U`, `D`, `L` and `R` instructions.

We can represent the FSM as a dictionary where the key is the state and the value is a record storing the possible transitions to new button states. For edge buttons where some instructions have no effect, we simply store the same state associated with the ignored instruction.

```elm {l}
type alias Transition comparable =
    { up : comparable
    , down : comparable
    , left : comparable
    , right : comparable
    }


type alias Pad comparable =
    Dict comparable (Transition comparable)


pad1 : Pad Int
pad1 =
    Dict.empty
        |> Dict.insert 1 (Transition 1 4 1 2)
        |> Dict.insert 2 (Transition 2 5 1 3)
        |> Dict.insert 3 (Transition 3 6 2 3)
        |> Dict.insert 4 (Transition 1 7 4 5)
        |> Dict.insert 5 (Transition 2 8 4 6)
        |> Dict.insert 6 (Transition 3 9 5 6)
        |> Dict.insert 7 (Transition 4 7 7 8)
        |> Dict.insert 8 (Transition 5 8 7 9)
        |> Dict.insert 9 (Transition 6 9 8 9)
```

Transitions from one state to the next are determined by the instructions:

```elm {l}
transition : Pad comparable -> Char -> comparable -> comparable
transition fsm dir state =
    let
        tr =
            Dict.get state fsm |> Maybe.withDefault (Transition state state state state)
    in
    case dir of
        'U' ->
            tr.up

        'D' ->
            tr.down

        'L' ->
            tr.left

        'R' ->
            tr.right

        _ ->
            state
```

To find the next button after a sequence of instructions we just fold over the instruction list:

```elm {l}
button : Pad comparable -> String -> comparable -> comparable
button pad dirs prevKey =
    List.foldl (transition pad) prevKey (String.toList dirs)
```

And to find the final sequence of button presses, we just fold over the list of instruction lists recording intermediate results with `scanl`, dropping the first button as this is our initial state (button 5) and not pressed as part of the sequence.

```elm {l r}
part1 : List Int
part1 =
    puzzleInput
        |> scanl (button pad1) 5
        |> List.drop 1
```

{( part2 |}

You finally arrive at the bathroom (it's a several minute walk from the lobby so visitors can behold the many fancy conference rooms and water coolers on this floor) and go to punch in the code. Much to your bladder's dismay, the keypad is not at all like you imagined it. Instead, you are confronted with the result of hundreds of man-hours of bathroom-keypad-design meetings:

        1
      2 3 4
    5 6 7 8 9
      A B C
        D

You still start at "5" and stop when you're at an edge, but given the same instructions as above, the outcome is very different:

- You start at "5" and don't move at all (up and left are both edges), ending at 5.
- Continuing from "5", you move right twice and down three times (through "6", "7", "B", "D", "D"), ending at D.
- Then, from "D", you move five more times (through "D", "B", "C", "C", "B"), ending at B.
- Finally, after five more moves, you end at 3.

So, given the actual keypad layout, the code would be 5DB3.

Using the same instructions in your puzzle input, what is the correct **bathroom code**?

{| part2 )}

This is pretty much the same as part 1 except that we have a slightly different FSM for the new keypad arrangement. We need to store states as `Char` rather than `Int` as we have both letters and numbers.

```elm {l}
pad2 : Pad Char
pad2 =
    Dict.empty
        |> Dict.insert '1' (Transition '1' '3' '1' '1')
        |> Dict.insert '2' (Transition '2' '6' '2' '3')
        |> Dict.insert '3' (Transition '1' '7' '2' '4')
        |> Dict.insert '4' (Transition '4' '8' '3' '4')
        |> Dict.insert '5' (Transition '5' '5' '5' '6')
        |> Dict.insert '6' (Transition '2' 'A' '5' '7')
        |> Dict.insert '7' (Transition '3' 'B' '6' '8')
        |> Dict.insert '8' (Transition '4' 'C' '7' '9')
        |> Dict.insert '9' (Transition '9' '9' '8' '9')
        |> Dict.insert 'A' (Transition '6' 'A' 'A' 'B')
        |> Dict.insert 'B' (Transition '7' 'D' 'A' 'C')
        |> Dict.insert 'C' (Transition '8' 'C' 'B' 'C')
        |> Dict.insert 'D' (Transition 'B' 'D' 'D' 'D')
```

```elm {l r}
part2 : String
part2 =
    puzzleInput
        |> scanl (button pad2) '5'
        |> List.drop 1
        |> String.fromList
```

## Reflection

I enjoyed the elegance of the solution to this problem. The initial code for part one hard coded the states as integers but as revealed in part 2, this needed to be abstracted to `comparable` types to allow both integers and characters to be represented.

Much of the effort involved constructing the correct transitions based on the keypad arrangement. It would be interesting to create a function that given a keypad grid, constructed the FSM automatically based on the grid positions.
