---
follows: data/d05_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 5 (2016): How About a Nice Game of Chess?

{( part1|}

You are faced with a security door designed by Easter Bunny engineers that seem to have acquired most of their security knowledge by watching hacking movies.

The **eight-character password** for the door is generated one character at a time by finding the MD5 hash of some Door ID (your puzzle input) and an increasing integer index (starting with 0).

A hash indicates the **next character** in the password if its hexadecimal representation starts with **five zeroes**. If it does, the sixth character in the hash is the next character of the password.

For example, if the Door ID is abc:

- The first index which produces a hash that starts with five zeroes is 3231929, which we find by hashing abc3231929; the sixth character of the hash, and thus the first character of the password, is 1.
- 5017308 produces the next interesting hash, which starts with 000008f82..., so the second character of the password is 8.
- The third time a hash starts with five zeroes is for abc5278568, discovering the character f.

In this example, after continuing this search a total of eight times, the password is 18f47a30.

Given the actual Door ID, **what is the password**?

{| part1 )}

To solve this puzzle we need an MD5 hashing package. MD5Fast (see [src/MD5Fast.elm](src/MD5Fast.elm)) does this although it is much slower than the built-in Java MD5 hashing.

To start, we need to find zero hashes by incrementing possible values until at 5 zeros are found in the first 5 characters of the hashed value:

```elm {l}
findNextZeroHash : String -> Int -> Int
findNextZeroHash key index =
    let
        hash =
            key ++ String.fromInt index |> MD5.hex
    in
    if String.left 5 hash == "00000" then
        index

    else
        findNextZeroHash key (index + 1)
```

```elm {l}
password : Int -> List Char -> String -> List Char
password index pwd key =
    if List.length pwd >= 8 then
        pwd

    else
        let
            zIndex =
                findNextZeroHash key index
        in
        password (zIndex + 1) (pwd ++ pwdDigit key zIndex) key


pwdDigit : String -> Int -> List Char
pwdDigit key index =
    MD5.hex (key ++ String.fromInt index)
        |> String.slice 5 6
        |> String.toList
```

```elm {l m}
part1 : String
part1 =
    -- password 1 [] puzzleInput |>
    -- Commented out to speed up display of formatted output (uncoment to recalculate)
    [ '8', '0', '1', 'b', '5', '6', 'a', '7' ]
        |> String.fromList
```

{( part2 |}

As the door slides open, you are presented with a second door that uses a slightly more inspired security mechanism. Clearly unimpressed by the last version (in what movie is the password decrypted **in order**?!), the Easter Bunny engineers have worked out a better solution.

Instead of simply filling in the password from left to right, the hash now also indicates the **position** within the password to fill. You still look for hashes that begin with five zeroes; however, now, the **sixth** character represents the position (0-7), and the **seventh** character is the character to put in that position.

A hash result of 000001f means that f is the **second** character in the password. Use only the **first result** for each position, and ignore invalid positions.

For example, if the Door ID is abc:

- The first interesting hash is from abc3231929, which produces 0000015...; so, 5 goes in position 1: -5------.
- In the previous method, 5017308 produced an interesting hash; however, it is ignored, because it specifies an invalid position (8).
- The second interesting hash is at index 5357525, which produces 000004e...; so, e goes in position 4: -5--e---.

You almost choke on your popcorn as the final character falls into place, producing the password 05ace8e3.

Given the actual Door ID and this new method, **what is the password**?

{| part2 )}

## Approach

This is largely similar to part 1 except that we need to keep a record of which positions have been found so as not to overwrite them with new hashes. We can use a dictionary to store completed positions and only update it if a given position is absent from the dictionary. The task is complete when the size of dictionary is 8.

```elm {l}
addDigit : ( Int, Char ) -> Dict Int Char -> Dict Int Char
addDigit ( pos, chr ) pwd =
    if Dict.member pos pwd then
        pwd

    else
        Dict.insert pos chr pwd
```

```elm {l}
pwdDigit2 : String -> Int -> ( Maybe Int, Char )
pwdDigit2 key index =
    let
        chrs =
            MD5.hex (key ++ String.fromInt index)
                |> String.slice 5 7
                |> String.toList
    in
    case chrs of
        [ pos, chr ] ->
            if pos >= '0' && pos < '8' then
                ( pos |> String.fromChar |> String.toInt, chr )

            else
                ( Nothing, ' ' )

        _ ->
            ( Nothing, ' ' )
```

```elm {l}
password2 : Int -> Dict Int Char -> String -> Dict Int Char
password2 index pwd key =
    if Dict.size pwd >= 8 then
        pwd

    else
        let
            zIndex =
                findNextZeroHash key index
        in
        case pwdDigit2 key zIndex of
            ( Just pos, chr ) ->
                password2 (zIndex + 1) (addDigit ( pos, chr ) pwd) key

            _ ->
                password2 (zIndex + 1) pwd key
```

```elm {l r}
part2 : String
part2 =
    -- password2 1 Dict.empty puzzleInput |> Dict.values |>
    -- Commented out to speed up display of formatted output (uncoment to recalculate)
    [ '4', '2', '4', 'a', '0', '9', '7' ]
        |> String.fromList
```

## Reflection

Very similar to d04_2015, MD5 hashing puzzles are a little frustrating in Elm simply because the hashing algorithm is quite slow. The puzzle itself is pretty straightforward.
