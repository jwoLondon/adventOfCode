---
follows: data/d08_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2016): Two-Factor Authentication

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/8):

{( part1|}

You come across a door implementing what you can only assume is an implementation of two-factor authentication after a long game of requirements telephone.

To get past the door, you first swipe a keycard (no problem; there was one on a nearby desk). Then, it displays a code on a little screen, and you type that code on a keypad. Then, presumably, the door unlocks.

Unfortunately, the screen has been smashed. After a few minutes, you've taken everything apart and figured out how it works. Now you just have to work out what the screen **would** have displayed.

The magnetic strip on the card you swiped encodes a series of instructions for the screen; these instructions are your puzzle input. The screen is **50 pixels wide and 6 pixels tall**, all of which start off, and is capable of three somewhat peculiar operations:

- rect AxB turns **on** all of the pixels in a rectangle at the top-left of the screen which is A wide and B tall.
- rotate row y=A by B shifts all of the pixels in row A (0 is the top row) **right** by B pixels. Pixels that would fall off the right end appear at the left end of the row.
- rotate column x=A by B shifts all of the pixels in column A (0 is the left column) **down** by B pixels. Pixels that would fall off the bottom appear at the top of the column.

For example, here is a simple sequence on a smaller screen:

- rect 3x2 creates a small rectangle in the top-left corner:

  ###....
  ###....
  .......

- rotate column x=1 by 1 rotates the second column down by one pixel:

  #.#....
  ###....
  .#.....

- rotate row y=0 by 4 rotates the top row right by four pixels:

  ....#.#
  ###....
  .#.....

- rotate column x=1 by 1 again rotates the second column down by one pixel, causing the bottom pixel to wrap back to the top:

  .#..#.#
  #.#....
  .#.....

As you can see, this display technology is extremely powerful, and will soon dominate the tiny-code-displaying-screen market. That's what the advertisement on the back of the display tries to convince you, anyway.

There seems to be an intermediate check of the voltage used by the display: after you swipe your card, if the screen did work, **how many pixels should be lit**?

{| part1 )}

## Parsing

We need to extract the relevant operations from the text describing each rect/col, so let's start with a custom type to store the operations:

```elm {l}
type Instruction
    = Rect Int Int
    | RotateRow Int Int
    | RotateCol Int Int
```

Now we can parse the input to convert the puzzle text input into list of Instructions:

```elm {l}
parseInput : List String -> List Instruction
parseInput =
    let
        toInstruction instr =
            case instr of
                [ "t", w, h ] ->
                    Just (Rect (toInt w) (toInt h))

                [ "x", col, n ] ->
                    Just (RotateCol (toInt col) (toInt n))

                [ "y", row, n ] ->
                    Just (RotateRow (toInt row) (toInt n))

                _ ->
                    Nothing

        parseLine =
            submatches ".*(t|x|y).(\\d+)(?:x| by )(\\d+)"
                >> List.filterMap identity
                >> toInstruction
    in
    List.map parseLine >> List.filterMap identity
```

## Grid Operations

The grid of values can be represented as an AoC `Grid` of `Bool`. We will need to be able to apply the three operations to the grid:

```elm {l}
rect : Int -> Int -> Int -> Grid Bool -> Grid Bool
rect w x y grid =
    if y < 0 then
        grid

    else if x < 0 then
        rect w (w - 1) (y - 1) grid

    else
        rect w (x - 1) y (gSet ( y, x ) True grid)


rotate : Int -> List a -> List a
rotate n list =
    let
        pivot =
            modBy (List.length list) -n
    in
    List.drop pivot list ++ List.take pivot list
```

We now have the components in place to transform a grid according to an instruction:

```elm {l}
applyInstruction : Instruction -> Grid Bool -> Grid Bool
applyInstruction instr grd =
    case instr of
        Rect w h ->
            rect w (w - 1) (h - 1) grd

        RotateRow y n ->
            case gGetRow y grd of
                Just row ->
                    gSetRow y (row |> rotate n) grd

                Nothing ->
                    grd

        RotateCol x n ->
            case gGetCol x grd of
                Just col ->
                    gSetCol x (col |> rotate n) grd

                Nothing ->
                    grd
```

And now we apply the full list of instuctions determined by the puzzle input:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parseInput
        |> List.foldl applyInstruction (gInit 6 50 False)
        |> gToList
        |> List.filter identity
        |> List.length
```

{( part2 |}

You notice that the screen is only capable of displaying capital letters; in the font it uses, each letter is 5 pixels wide and 6 tall.

After you swipe your card, **what code is the screen trying to display**?

{| part2 )}

## Displaying the Grid

This just requires a simple transformation of each Boolean grid cell into a display character and a new line at the end of each grid row:

```elm {l}
display : Grid Bool -> List String
display =
    let
        toChr b =
            if b then
                '█'

            else
                '░'
    in
    gMap toChr
        >> gToLists
        >> List.map String.fromList
        >> List.intersperse "\n"
```

{(fixed|}

```elm {l m}
part2 : List String
part2 =
    puzzleInput
        |> parseInput
        |> List.foldl applyInstruction (gInit 6 50 False)
        |> display
```

{|fixed)}

## Reflection

Using the existing AoC `Grid` data structure made this a fairly simple solution to implement. It took a little while to get the recursive `rect` function working as expected, but generally this was a nice example of a functional approach to an iterative processing problem.

Part two was one of the few puzzles that required visual assessment of the answer. I guess it would be possible to build some character recognition, but without knowing in advance what the 'font' is this would be a little tedious to build. Much simpler to convert the output into a grid of characters.
