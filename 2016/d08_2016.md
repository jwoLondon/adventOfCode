---
follows: data/d08_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 8 (2016): Two-Factor Authentication

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/8).

{( part1|}

There seems to be an intermediate check of the voltage used by the display: after you swipe your card, if the screen did work, **how many pixels should be lit**?

{| part1 )}

We need to extract the relevant operations from the text describing each rect/col, so let's start with a custom type to store the operations:

```elm {l}
type Instruction
    = Rect Int Int
    | RotateRow Int Int
    | RotateCol Int Int
```

Now we can parse the input to convert the puzzle text input into list of Instructions:

```elm {l}
parseInput : List String -> List Instruction
parseInput =
    let
        toInstruction instr =
            case instr of
                [ "t", w, h ] ->
                    Just (Rect (AOC.toInt w) (AOC.toInt h))

                [ "x", col, n ] ->
                    Just (RotateCol (AOC.toInt col) (AOC.toInt n))

                [ "y", row, n ] ->
                    Just (RotateRow (AOC.toInt row) (AOC.toInt n))

                _ ->
                    Nothing

        parseLine =
            AOC.submatches ".*(t|x|y).(\\d+)(?:x| by )(\\d+)"
                >> List.filterMap identity
                >> toInstruction
    in
    List.map parseLine >> List.filterMap identity
```

## Grid Operations

The grid of values can be represented as an AoC `Grid` of `Bool`. We will need to be able to apply the three operations to the grid:

```elm {l}
rect : Int -> Int -> Int -> AOC.Grid Bool -> AOC.Grid Bool
rect w x y grid =
    if y < 0 then
        grid

    else if x < 0 then
        rect w (w - 1) (y - 1) grid

    else
        rect w (x - 1) y (AOC.gSet ( y, x ) True grid)


rotate : Int -> List a -> List a
rotate n list =
    let
        pivot =
            modBy (List.length list) -n
    in
    List.drop pivot list ++ List.take pivot list
```

We now have the components in place to transform a grid according to an instruction:

```elm {l}
applyInstruction : Instruction -> AOC.Grid Bool -> AOC.Grid Bool
applyInstruction instr grd =
    case instr of
        Rect w h ->
            rect w (w - 1) (h - 1) grd

        RotateRow y n ->
            case AOC.gGetRow y grd of
                Just row ->
                    AOC.gSetRow y (row |> rotate n) grd

                Nothing ->
                    grd

        RotateCol x n ->
            case AOC.gGetCol x grd of
                Just col ->
                    AOC.gSetCol x (col |> rotate n) grd

                Nothing ->
                    grd
```

And now we apply the full list of instructions determined by the puzzle input:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parseInput
        |> List.foldl applyInstruction (AOC.gInit 6 50 False)
        |> AOC.gToList
        |> List.filter identity
        |> List.length
```

{( part2 |}

After you swipe your card, **what code is the screen trying to display**?

{| part2 )}

## Displaying the Grid

This just requires a simple transformation of each Boolean grid cell into a display character and a new line at the end of each grid row:

```elm {l}
display : AOC.Grid Bool -> List String
display =
    let
        toChr b =
            if b then
                '█'

            else
                '░'
    in
    AOC.gMap toChr
        >> AOC.gToLists
        >> List.map String.fromList
        >> List.intersperse "\n"
```

{(fixed|}

```elm {l m}
part2 : List String
part2 =
    puzzleInput
        |> parseInput
        |> List.foldl applyInstruction (AOC.gInit 6 50 False)
        |> display
```

{|fixed)}

---

## Reflection

Using the existing AoC `Grid` data structure made this a fairly simple solution to implement. It took a little while to get the recursive `rect` function working as expected, but generally this was a nice example of a functional approach to an iterative processing problem.

Part two was one of the few puzzles that required visual assessment of the answer. I guess it would be possible to build some character recognition, but without knowing in advance what the 'font' is this would be a little tedious to build. Much simpler to convert the output into a grid of characters.
