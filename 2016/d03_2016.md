---
follows: data/d03_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 3 (2016): Squares With Three Sides

{( part1|}

Now that you can think clearly, you move deeper into the labyrinth of hallways and office furniture that makes up this part of Easter Bunny HQ. This must be a graphic design department; the walls are covered in specifications for triangles.

Or are they?

The design document gives the side lengths of each triangle it describes, but... 5 10 25? Some of these aren't triangles. You can't help but mark the impossible ones.

In a valid triangle, the sum of any two sides must be larger than the remaining side. For example, the "triangle" given above is impossible, because 5 + 10 is not larger than 25.

In your puzzle input, **how many** of the listed triangles are **possible**?

{| part1 )}

## Approach

Each triangle is conveniently stored in a 3-tuple:

```elm {l}
type alias Tri =
    ( Int, Int, Int )
```

We can extract the side lengths of each triangle with some simple parsing

```elm {l}
parse : String -> Tri
parse =
    let
        toTriplet ss =
            case ss of
                [ s1, s2, s3 ] ->
                    ( toInt s1, toInt s2, toInt s3 )

                _ ->
                    ( 0, 0, 0 )
    in
    match "(\\d+)" >> toTriplet
```

To test if a triplet could represent a real triangle, we could sort side lengths and check the sum of the shortest two is greater than the longest one, or more simply test all combinations:

```elm {l}
isTriangle : Tri -> Bool
isTriangle ( l1, l2, l3 ) =
    l1 + l2 > l3 && l1 + l3 > l2 && l2 + l3 > l1
```

And to count the number of valid triangles we just filter based on our `isTriangle` test:

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> List.map parse
        |> List.filter isTriangle
        |> List.length
```

{( part2 |}

Now that you've helpfully marked up their design documents, it occurs to you that triangles are specified in groups of three vertically. Each set of three numbers in a column specifies a triangle. Rows are unrelated.

For example, given the following specification, numbers with the same hundreds digit would be part of the same triangle:

    101 301 501
    102 302 502
    103 303 503
    201 401 601
    202 402 602
    203 403 603

In your puzzle input, and instead reading by columns, how many of the listed triangles are possible?

{| part2 )}

This is pretty much the same as part one except that we first need to transpose the input in groups of 9:

```elm {l}
transpose9 : List Tri -> List Tri
transpose9 tris =
    case tris of
        ( a1, a2, a3 ) :: ( b1, b2, b3 ) :: ( c1, c2, c3 ) :: tl ->
            ( a1, b1, c1 )
                :: ( a2, b2, c2 )
                :: ( a3, b3, c3 )
                :: transpose9 tl

        _ ->
            tris
```

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> List.map parse
        |> transpose9
        |> List.filter isTriangle
        |> List.length
```

## Reflection

A nice simple problem well suited to a functional approach and pattern matching in lists.
