---
follows: data/d10_2016.md

id: "litvis"
---

@import "../css/aoc.less"

# Day 10 (2016): Balance Bots

'Part One' and 'Part Two' puzzle descriptions and puzzle input from [Advent of Code](https://adventofcode.com/2016/day/10):

{( part1|}

Based on your instructions, **what is the number of the bot** that is responsible for comparing value-61 microchips with value-17 microchips?

{| part1 )}

We can represent the bots as a dictionary with each key storing its id (prefaced with `bot` or `output`), up to two numeric values and links to high and low keys.

```elm {l}
type alias Botnet =
    Dict String { vals : List Int, low : String, hi : String }
```

We will be adding numeric values to some bots/outputs either directly from parsing the input, or when active bots holding two values pass them on. When adding a value to a bot or output we ensure multiple values are ordered from low to high.

```elm {l}
addValue : String -> Int -> Botnet -> Botnet
addValue id val bn =
    case Dict.get id bn of
        Nothing ->
            Dict.insert id { vals = [ val ], low = "", hi = "" } bn

        Just node ->
            Dict.insert id { vals = val :: node.vals |> List.sort, low = node.low, hi = node.hi } bn
```

We build the dictionary from each line of the puzzle input, adding nodes or edges depending on the input line.

```elm {l}
parse : List String -> Botnet
parse =
    let
        parseLine =
            AOC.submatches "(\\d+)(?:.*low| goes) to (bot|output) (\\d+)(?:.*(bot|output) (\\d+))?"
                >> List.filterMap identity

        add line botnet =
            case line of
                [ val, dest, bot ] ->
                    addValue (dest ++ bot) (AOC.toInt val) botnet

                [ bot, lDest, lId, hDest, hId ] ->
                    case Dict.get ("bot" ++ bot) botnet of
                        Nothing ->
                            botnet
                                |> Dict.insert ("bot" ++ bot)
                                    { vals = [], low = lDest ++ lId, hi = hDest ++ hId }

                        Just node ->
                            botnet
                                |> Dict.insert ("bot" ++ bot)
                                    { vals = node.vals, low = lDest ++ lId, hi = hDest ++ hId }

                _ ->
                    botnet |> Debug.log "unexpected input"
    in
    List.map parseLine
        >> List.foldl add Dict.empty
```

We can propagate values from any bot that contains two values along its low and high edges

```elm {l}
propagate : ( Botnet, Botnet ) -> Botnet
propagate ( static, active ) =
    let
        passValues { vals, low, hi } =
            case vals of
                [ lVal, hVal ] ->
                    addValue low lVal >> addValue hi hVal

                _ ->
                    identity
    in
    List.foldl passValues static (Dict.values active)
```

We can continue to propagate until there are no more changes to the botnet:

```elm {l}
run : Botnet -> Botnet -> Botnet
run done todo =
    let
        ( static, active ) =
            Dict.partition (\_ v -> List.length v.vals /= 2) todo

        newActive =
            propagate ( static, active )
    in
    if active == Dict.empty then
        Dict.union static done

    else
        run (Dict.union done active) (List.foldl Dict.remove newActive (Dict.keys active))
```

To find the bot that compares two given numbers

```elm {l}
botWith : Int -> Int -> Botnet -> Maybe String
botWith n1 n2 bn =
    bn
        |> Dict.filter (\_ v -> v.vals == [ n1, n2 ])
        |> Dict.keys
        |> List.head
```

```elm {l r}
part1 : Int
part1 =
    puzzleInput
        |> parse
        |> run Dict.empty
        |> botWith 17 61
        |> Maybe.withDefault ""
        |> String.dropLeft 3
        |> AOC.toInt
```

{( part2 |}

What do you get if you **multiply together the values** of one chip in each of outputs 0, 1, and 2?

{| part2 )}

As we have a graph of fully active bots and outputs we simply filter outputs 0, 1 and 2 and find their product.

```elm {l r}
part2 : Int
part2 =
    puzzleInput
        |> parse
        |> run Dict.empty
        |> Dict.filter (\k v -> k == "output0" || k == "output1" || k == "output2")
        |> Dict.values
        |> List.filterMap (.vals >> List.head)
        |> List.product
```

---

## Reflection

While this is effectively a graph traversal puzzle, it requires search for nodes (bots or outputs) that can be anywhere in the graph, so is more efficiently represented in a dictionary for quick searching. My initial solution repeatedly propagated the values of all active bots on each iteration, which was unnecessary, so instead I partitioned the botnet into those with newly active bots and those that were either static or already active.

An alternative approach would be to use an explicit graph representation and perform a topological sort so that propagation could be applied sequentially on sorted nodes.
